.MLLIT==1
	TITLE WUMPUS

T=1
T1=2
ROOM=3
ME=4
CNT=5
A=6
P=17
WORLD=.
NTABLE=WORLD+21.		;RANDOM TABLE OF NUMBERS

X0=NTABLE+21.			;RANDOM # GEN SEED
RCOUNT=X0+1			;RANDOM # GEN COUNTER
RANDP=RCOUNT+1			;RANDOM # GEN POINTER
ARROWS=RANDP+1			;NUMBER OF ARROWS ALLOWED

PDL=ARROWS+1			;PUSH DOWN LIST

.=PDL+40






	DEFINE SENSE PROOM, HAZARD, ORWORD
	LDB ROOM, PROOM
	LDB T, HAZARD
	IORM T, ORWORD
	TERMIN

	DEFINE SENSE3 HAZARD, ORWORD
	PUSH P, ROOM
	SENSE PROOM1, HAZARD, ORWORD
	MOVE ROOM, (P)
	SENSE PROOM2, HAZARD, ORWORD
	MOVE ROOM, (P)
	SENSE PROOM3, HAZARD, ORWORD
	POP P, ROOM
	TERMIN

	DEFINE TYPE TEXT
	JRST [PUSH P,A
	MOVEI A,[ASCIZ/TEXT/]
	PUSHJ P,ATYPE
	POP P,A
	JRST .+1]
	TERMIN

	DEFINE TYP ADDRS
	JRST [PUSH P,A
	MOVEI A, ADDRS
	PUSHJ P,ATYPE
	POP P,A
	JRST .+1]
	TERMIN



;DO NOT CHANGE THE ORDER OF THESE BYTE POINTERS:

PROOM1:	360600,,WORLD(ROOM);	POINT 6, WORLD (ROOM), ^D5	;BYTE POINTER TO FIRST TUNNEL
PROOM2: 300600,,WORLD(ROOM);	POINT 6, WORLD (ROOM), ^D11	;SECOND TUNNEL
PROOM3: 220600,,WORLD(ROOM);	POINT 6, WORLD (ROOM), ^D17	;THIRD TUNNEL
PWUMP:  210100,,WORLD(ROOM);	POINT 1, WORLD (ROOM), ^D18	;BYTE POINTER TO WUMPUS BIT
PBAT:   200100,,WORLD(ROOM);	POINT 1, WORLD (ROOM), ^D19	;BAT BIT
PPIT:   170100,,WORLD(ROOM);	POINT 1, WORLD (ROOM), ^D20	;PIT BIT

SPACE:	ASCIZ/ /
CRLF:	ASCIZ/
/
EXPL:	ASCIZ/
YOU ARE A FAMOUS HUNTER DESCENDING DOWN INTO THE CAVES OF DARKNESS, LAIR
OF THE FAMOUS MAN-EATING WUMPUS.  YOU ARE EQUIPED WITH FIVE BENT
ARROWS, AND ALL YOUR SENSES.  THERE ARE TWENTY CAVES CONNECTED BY
TUNNELS, AND THERE ARE TWO OTHER KINDS OF HAZARDS (THREE EACH):
   A) SUPER-BATS WHICH IF YOU STUMBLE INTO THEIR ROOM WILL PICK
      YOU UP AND DROP YOU IN SOME RANDOM ROOM IN THE NETWORK.
   B) PITS, WHICH ARE BOTTOMLESS, AND FATAL TO FALL INTO.
BOTH ARE IMMOVABLE.
     IF YOU END UP IN THE SAME ROOM AS THE WUMPUS, YOU LOSE.  NORMALLY
THE WUMPUS DOES NOT MOVE (HAVING GORGED HIMSELF UPON A PREVIOUS HUNTER).
TWO THINGS WAKE HIM UP:
   1) WALKING INTO HIS ROOM,
   2) SHOOTING AN ARROW ANYWHERE IN THE NETWORK.
IF HE WAKES THERE IS A 50-50 CHANCE HE WILL MOVE INTO AN ADJOINING CAVE.
HE IS TOO BIG TO BE PICKED UP BY BATS, AND HAS SUCKER FEET,
SO HE DOESN'T FALL INTO THE PITS.  YOU CAN SMELL THE WUMPUS FROM ONE OR
TWO ROOMS AWAY.  YOU CAN HEAR BATS ONE ROOM AWAY, AND FEEL DRAFTS (FROM
BOTTOMLESS PITS) FROM ONE ROOM AWAY (AND TASTE THE FEAR...).
     TO SHOOT TYPE S (WITH NO CR) INSTEAD OF A MOVE, AND THEN SPECIFY
A LEGAL PATH OF LENGTH FIVE FOR THE ARROW TO FOLLOW.  IF YOU GUESS AND
LOSE THE ARROW TAKES A RANDOM PATH.  PATHS CAN BE TRUNCATED BY TYPING CR
INSTEAD OF A ROOM NUMBER.

       GOOD LUCK HUNTING!!
î/



BEG:GO:	.OPEN 1,[20,,'TTY]	;CVT LOWER TO UPPERCASE
	.VALUE
	.OPEN 2,[SIXBIT /  !TTY/]
	.VALUE
	MOVEI P, PDL
	TYPE [DIRECTIONS? (Y OR N) ]	;DOES HE WANT THE EXPL?
GETLF:	.IOT 1,6		;WAIT FOR LF
	CAIN 6,"Y		;DID HE SAY YES?
	TYP EXPL
	TYP CRLF
	PUSHJ P,NAMEF		;FIDDLE WITH NAME
	.RDTIM T,
	MOVEM T, X0		;FOR RANDOM # GEN
	HRREI T, -5
	MOVEM T, ARROWS		;SET UP FIVE BENT ARROWS
	SETZM RCOUNT
	SETZM WORLD		;RESTART THE WORLD
	MOVE T,[XWD WORLD,WORLD+1]
	BLT T, WORLD+20.
	PUSHJ P, SETNTB		;SET UP NTABLE
	PUSHJ P, RANDOM
	PUSHJ P, RANDOM
	IMULI T, 3
	PUSH P, T
	PUSHJ P, SHUFFL		;SHUFFLE NTABLE
	SOSLE (P)
	JRST .-2
	POP P,T
	MOVE T, NTABLE+20.	;MAKE NTABLE A RING
	MOVEM T, NTABLE
	PUSHJ P, FIRST2		;FIRST TWO TUNNELS PER ROOM
	PUSHJ P, LAST1		;LAST ONE
	PUSHJ P, SHUFFL		;SHUFFLE NTABLE
	PUSHJ P, SHUFFL
	PUSHJ P, IHZRDS		;ADD HAZARDS AND ME
	JRST CIRCLE


UNAME:	0
	0	;TO STOP TYPEOUT LOOP
NAMEF:	.SUSET [.RUNAM,,UNAME]

	TYPE HERE'S THE FAMOUS WUMPUS HUNTER MAJOR 

	MOVE T,[440600,,UNAME]
NAMEL:	ILDB T1,T
	JUMPE T1,EON
	ADDI T1,40
	.IOT 2,T1	;OUTPUT CHAR OF NAME
	JRST NAMEL

EON:	TYP CRLF
	TYPE [DESCENDING INTO THE "CAVES OF DARKNESS", LAIR OF THE
DEADLY MAN-EATING WUMPUS.]
	TYP CRLF
	MOVEI T, 30.*1
	.SLEEP T,
	TYPE [DOWN...]
	TYP CRLF
	MOVEI T,1*30.
	.SLEEP T,
	TYPE [DOWN...]
	TYP CRLF
	MOVEI T,1*30.
	.SLEEP T,
	TYPE [DOWN...]
	TYP CRLF
	MOVEI T,1*30.
	.SLEEP T,
	POPJ P,

SETNTB:	SETZM T			;SET UP NTABLE WITH 1-20
SETNT1:	AOS T
	MOVEM T, NTABLE(T)
	CAIGE T, 20.
	JRST SETNT1
	POPJ P,

SHUFFL:	MOVEI ME, 1		;SHUFFLE NTABLE ONCE
SHUFF1:	PUSHJ P, RANDOM		;GET RANDOM NUMBER
	CAME T, ME		;IF SAME, ONLY TWO SWAPS
	EXCH P, NTABLE (T)	;SWAP NTABLE (T) WITH NTABLE (ME)
	EXCH P, NTABLE (ME)
	EXCH P, NTABLE (T)
	AOS ME
	CAIG ME, 20.
	JRST SHUFF1
	POPJ P,

FIRST2:	MOVEI T1,1		;PUT FIRST TWO TUNNELS IN EACH ROOM
FIRSTA:	MOVE ROOM, NTABLE (T1)	;GET RING ENTRY
	MOVE T, NTABLE-1 (T1)	;GET PREVIOUS ENTRY
	DPB T, PROOM1		;PUT IN ONE DIRECTION
	EXCH T, ROOM
	DPB T, PROOM2		;PUT IN OTHER
	AOS T1
	CAIG T1, 20.
	JRST FIRSTA
	POPJ P,


LAST1:	HRREI T, -500.		;500 TRYS OR ELSE!!
	PUSH P, T
LASTA:	PUSHJ P, SHUFFL		;SHUFFLE NTABLE
	MOVEI ROOM, 1
	SETZM T
LASTA1:	DPB T, PROOM3		;ZERO THIRD TUNNELS
	AOS ROOM
	CAIG ROOM, 20.
	JRST LASTA1
	MOVEI T1, 1		;INDEX TO NTABLE
LASTB:	MOVE ROOM, NTABLE(T1)
	LDB T, PROOM3
	JUMPN T, LASTC1		;IF NTABLE(T1) HAS 3RD RM, GET NEXT T1
	MOVEI ROOM, 1		;INDEX TO WORLD
LASTC:	LDB T, PROOM3
	JUMPN T, LASTD		;IF THIRD ROOM FULL, NEXT!
	CAMN ROOM, NTABLE (T1)	;IS ROOM SAME AS NTABLE ENTRY?
	JRST LASTD		;YEP, NEXT ROOM
	LDB T, PROOM1
	CAMN T, NTABLE (T1)	;NTABLE(T1) = TUNNEL #1?
	JRST LASTD		;YEP, NEXT ROOM
	LDB T, PROOM2
	CAMN T, NTABLE(T1)	;NTABLE(T1) = TUNNEL #2?
	JRST LASTD		;YEP, NEXT ROOM
	MOVE T, NTABLE (T1)	;GET ROOM NUMBER
	DPB T, PROOM3		;PUT IT IN WORLD
	EXCH T, ROOM
	DPB T, PROOM3		;PUT IN OTHER DIRECTION
LASTC1:	AOS T1
	CAIG T1, 20.		;END OF NTABLE?
	JRST LASTB		;NOPE, GO BACK
	POP P, T		;GET RID OF COUNTER
	POPJ P,

LASTD:	AOS ROOM		;LOOK AT NEXT ROOM
	CAIG ROOM, 20.		;LOOKED AT ALL?
	JRST LASTC		;NOPE, GO BACK
	AOSGE (P)		;YEP, COUNT SHUFFLES
	JRST LASTA		;SHUFFLE AND TRY AGAIN
	TYPE (CANT FINISH TUNNELS. FOO!)
	JRST GO


IHZRDS:	PUSHJ P, SHUFFL		;SET UP HAZARDS AND ME
	PUSHJ P, SHUFFL
	SETOM T			;BIT FOR MARKING WORLD
	MOVE ROOM, NTABLE+1	;WUMPUS GOES IN FIRST ROOM
	DPB T, PWUMP
	HRREI T1, -2		;THREE BATS AND PITS
IHZRDL:	MOVE ROOM, NTABLE+4(T1)
	DPB T, PPIT
	MOVE ROOM, NTABLE+7(T1)
	DPB T, PBAT
	AOJLE T1, IHZRDL
	PUSH P,A
	MOVEI T,NTABLE
PUTME:	MOVE ROOM,(T)
	MOVEI T1,2
	MOVEM ROOM,(P)
LOOKO:	LDB ROOM,PROOM1(T1)
	LDB A,PWUMP	;IS THE WUMPUS THERE
	JUMPN A,[ AOJA T,PUTME]
	LDB A,PPIT
	JUMPN A,[ AOJA T,PUTME]
	MOVE ROOM,(P)
	SOJGE T1,LOOKO
	LDB A,PBAT
	JUMPN A,[AOJA T,PUTME]
	LDB A,PWUMP
	JUMPN A,[ AOJA T,PUTME]
	LDB A,PPIT
	JUMPN A,[ AOJA T,PUTME]
	POP P,ME
	POPJ P,


MWUMP:	PUSH P, ROOM		;MOVE THE WUMPUS (50% CHANCE)
	PUSH P, T		;TRANSPARENT TO AC'S
	PUSH P, T1
	MOVEI ROOM, 1
MWUMP1:	LDB T, PWUMP		;FIND THE WUMPUS FIRST
	JUMPN T, MWUMP2		;GOT HIM
	AOS ROOM
	CAIG ROOM, 20.
	JRST MWUMP1
	TYPE [BAD NEWS IN MWUMP.]
	JRST GO

MWUMP2:	PUSHJ P, RANDOM
	CAILE T, 10.		;50% CHANCE OF MOVING!
	JRST MWUMPX		;NOT THIS TIME
	IDIVI T, 3
	SETZM T
	DPB T, PWUMP		;GET RID OF WUMPUS
	LDB ROOM, PROOM1 (T1)	;GET RANDOM ROOM
	SETOM T
	DPB T, PWUMP		;PUT WUMPUS THERE
MWUMPX:	POP P, T1
	POP P, T
	POP P, ROOM
	POPJ P,


CIRCLE:	TYPE [YOU ARE IN ROOM ]	;MASTER LOOP
	MOVE T, ME
	PUSHJ P, DECPNT
	TYP CRLF
	MOVE ROOM, ME
	LDB T, PWUMP		;GET WUMPUS BIT
	JUMPE T, CIRCL1		;NO WUMPUS, GO ON
	PUSHJ P, MWUMP		;HE'S THERE, TO MOVE OR NOT TO MOVE?
CIRCLF:	MOVE ROOM,ME
	LDB T, PWUMP
	JUMPE T, CIRCL1		;HE MOVED, YOU'RE SAFE...
	TYPE [YOU WERE EATEN BY THE WUMPUS.]
	JRST GO

CIRCL1:	LDB T,PBAT
	JUMPE T, CIRCL2		;NO BATS, GO ON
	TYPE [BATS IN YOUR ROOM!]
	TYP CRLF
	PUSHJ P,RANDOM
	MOVE ME, T		;PUT ME SOMEWHERE RANDOM.
	JRST CIRCLE

CIRCL2:	LDB T, PPIT
	JUMPE T, CIRCL3		;NO PITS, GO ON
	TYPE (YOU FELL INTO A PIT. YOU LOSE!)
	JRST GO

CIRCL3:	SETZM T1		;SENSE ALL AROUND. T1 IS OR-WORD
	PUSHJ P, WSENSE
	JRST CIRCL4
WSENSE:	SENSE3 PWUMP, T1	;SENSE THE WORLD!!
	POPJ P,

CIRCL4:	LDB ROOM, PROOM1
	PUSHJ P, WSENSE
	MOVE ROOM, ME
	LDB ROOM, PROOM2
	PUSHJ P, WSENSE
	MOVE ROOM, ME
	LDB ROOM, PROOM3
	PUSHJ P, WSENSE
	JUMPE T1, CIRCL5	;NO SMELL OF WUMPUS, GO ON
	TYPE [I SMELL A WUMPUS.]
	TYP CRLF
CIRCL5:	SETZM T1		;CLEAR IOR BIT
	MOVE ROOM, ME
	SENSE3  PBAT, T1	;SENSE BATS
	JUMPE T1, CIRCL6	;NO BATS, GO ON
	TYPE [I HEAR SQUEEKING.]
	TYP CRLF
CIRCL6:	SETZM T1		;CLEAR IOR BIT FOR PITS
	MOVE ROOM, ME
	SENSE3 PPIT, T1
	JUMPE T1, MOVEX		;NO BATS, LET HIM MOVE.
	TYPE [I FEEL A DRAFT.]
	TYP CRLF
	JRST MOVEX		; GO ON TO MOVE ROUTINE.

;MOVE ROUTINE:  DOES WORK RELATED TO USER MOVE.

MOVEX:	TYPE [TUNNELS TO ]
	MOVE ROOM, ME
	LDB T, PROOM1
	PUSHJ P, DECPNT
	TYP SPACE
	LDB T, PROOM2
	PUSHJ P, DECPNT
	TYP SPACE
	LDB T, PROOM3
	PUSHJ P, DECPNT
	TYP CRLF
MOVE1:	TYPE [MOVE? ]		;DEMAND MOVE
	PUSHJ P, DECIN
	  JRST MOVE2		;NON-NUMBER, GO CHECK.
	SKIPE T
	CAILE T, 20.		;BETWEEN 1-20?
	JRST WHAT		;NOPE.
	MOVE T1, ME
	PUSHJ P, LEGAL		;LEGAL MOVE? (FROM T1 TO T)
	JRST NOTPOS		;NOPE, NOT POSSIBLE
	MOVE ME, T		; YEP, MAKE IT
	JRST CIRCLE		;GO BACK

MOVE2:	CAIN T,"S		;ONLY NON-NUMBER IS S
	JRST SHOOT		;TO SHOOT ARROW.
WHAT:	CAIN T,1000
	JRST MOVE1
	TYPE [WHAT??]		;NON-RECOGNIZABLE CHAR
	SKIPA
NOTPOS:	TYPE [NOT POSSIBLE!]	;...
	TYP CRLF
	JRST CIRCLE


;ROUTINE TO DETERMINE IF A TUNNEL EXISTS TWEEN T AND T1
;SKIP RETURN = YES.  TRANSPARENT TO AC'S

LEGAL:	PUSH P, ROOM
	MOVE ROOM, T
	LDB T, PROOM1
	CAMN T, T1
	JRST SLEGAL
	LDB T, PROOM2
	CAMN T, T1
	JRST SLEGAL
	LDB T, PROOM3
	CAMN T, T1
SLEGAL:	AOS  -1(P)		;YEP, 'S LEGAL
	MOVE T, ROOM		;RESTORE AC'S
	POP P, ROOM
	POPJ P,


;SHOOT ROUTINE TO HANDLE SHOOTING ARROWS.

SHOOT:	TYPE [HOOT!]
	TYP CRLF
	AOSLE ARROWS		;ARROWS LEFT?
	JRST LOSE1		;NOPE
	TYPE [FINISH WITH CR.]
	TYP CRLF
	MOVE ROOM, ME		;ARROW STARTS AT ME
	SETZM NTABLE		;TO CHECK 180 DEG BOUNCES
	HRREI CNT, -5		;FIVE ROOMS
SHOOT1:	TYPE [ROOM: ]		;GET ROOM NUMBER
	PUSHJ P, DECIN
	  JRST SHOOTX		;UNRECOGNIZABLE INPUT = JRST GO
	SKIPE T
	CAILE T, 20.
	JRST SHOOTX		;OUT OF BOUNDS = JRST GO
	MOVE T1, ROOM
	PUSHJ P, LEGAL		;TUNNEL TWEEN T AND T1?
	  JRST BOUNCE		;NOPE, HE'S FISHING, GET HIM
	CAMN T, NTABLE		;DOES HE WANT TO DO A 180?
	JRST BOUNCE		;YEP, GET HIM
	MOVEM ROOM, NTABLE	;ARROW JUST LEAVING (ROOM)
	PUSHJ P, MEWUMP		;KILL ME OR WUMPUS??
	AOJL CNT, SHOOT1	;NO, MORE ARROWS = GO BACK
SHOOTX:	PUSHJ P, MWUMP		;FINISHED, MOVE WUMPUS
	JRST CIRCLF		;GO BACK, SEE IF WUMPUS KILLS, DONT LET
				;HIM MOVE A GAIN.
BOUNCE:	TYPE (BOUNCE)		;HE TRIED TO FOOL US, RANDOM ARROW PATH.
	TYP CRLF
	PUSHJ P, RANDOM
	IDIVI T, 3
	LDB T, PROOM1(T1)	;GET RANDOM TUNNEL
	PUSHJ P, MEWUMP		;KILL ANYONE?
	AOJL CNT, BOUNCE	;CONTINUE BOUNCING
	JRST CIRCLE


MEWUMP:	CAMN ME, T		;KILL ME OR WUMPUS?
	JRST LOSE		;YEP, ME
	MOVE ROOM, T
	LDB T, PWUMP		;KILL WUMPUS?
	JUMPN T, WIN		;YEP
	POPJ P,

CONSTANTS
LOSE1:	TYPE [NO MORE ARROWS. YOU LOSE BY DEFAULT.]
	JRST GO
LOSE:	TYPE [YOU SHOT YOURSELF.]
	JRST GO

WIN:	TYPE [YOU SLEW THE WUMPUS. CONGRATULATIONS!!!]
	JRST GO


ATYPE:	PUSH P,T
	HRLI A,440700
	ILDB T,A
	JUMPE T,[POP P,T?POPJ P,]
	.IOT 2,T
	JRST .-3
;DECIN INPUTS A DECIMAL NUMBER.  IT MUST BE TERMINATED
;WITH A CR.  FIRST CHAR IF NON-NUMBER GETS RETURNED WITH POPJ.
;NORMAL RETURN IS SKIP.    ANSWER IN T.

DECIN:	.IOT 1,T
	CAIE T, 15
	JRST DECDEC
	HRRZI T,1000
	POPJ P,
DECDEC:	CAILE T,"9
	POPJ P,
	CAIGE T, "0
	POPJ P,
	MOVE T1, T
	SETZM T
	SKIPA
DECIN1:	.IOT 1,T1
	CAILE T1, "9
	JRST DECIN2
	CAIGE T1, "0
	JRST DECIN2
	SUBI T1, 60
	IMULI T, 12
	ADD T, T1
	JRST DECIN1

DECIN2:	CAIE T1, 15		; A CRLF?
	JRST DWHAT
	AOS (P)
	POPJ P,

DWHAT:	TYPE ( WHAT??)
	TYP CRLF
	JRST DECIN

;DECIMAL PRINT ROUTINE.  TAKES ARGUMENT IN T, USES T1
;DESTROYS T, T1

DECPNT:	IDIVI T, 12
	ADDI T1, 60
	PUSH P, T1
	SKIPLE T
	PUSHJ P, DECPNT
	POP P, T
	.IOT 2,T
	POPJ P,


;RANDOM NUMBER GENERATOR: USES FORMULA:
;   X[N+1]  = (  A*X[N] + C )TAKEN MOD(M)

RANDOM:	SKIPE RCOUNT		;RANDOM # GEN (RANGE = 1-20)
	JRST RAND1
	AOS RCOUNT
	MOVE T, X0
	IMUL T, MA
	ADD T, MC
	IDIV T, MM
	MOVEM T1, X0
	MOVE T, [360600,,X0]	;POINT 6, X0, 5
	MOVEM T, RANDP
RANDX:	LDB T, RANDP
	IDIVI T, 20.
	MOVE T, T1
	AOS T
	POPJ P,

RAND1:	AOS RCOUNT
	MOVEI T, 6
	CAMN T, RCOUNT
	SETZM RCOUNT
	IBP RANDP
	JRST RANDX

MA:	32768.-19.		;RANDOM # GEN CONSTANTS
MC:	262144.-11.
MM:	34359738368.-23.
X0SAVD:	5


	END BEG
