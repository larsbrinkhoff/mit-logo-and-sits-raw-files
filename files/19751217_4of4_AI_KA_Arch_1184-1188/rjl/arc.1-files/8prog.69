*0
	0
	JMP I 2
	INTER
*10
APNT,	0
BPNT,	0
CPNT,	0
IAPNT,	0
IBPNT,	0
ICPNT,	0
*20
J,	0
M,	0
IN,	0
AD1,	0
AD2,	0
AD3,	0
MARK1,	0
MARK2,	0
MARK3,	0
TMARK1,	0
TMARK2,	0
TMARK3,	0
VAL1,	0
VAL2,	0
STEPCN,	0
LFTMAR,	0
NTYPD,	0
NUMBR,	0
*100
ST,
QN,	0
N,	0
NUMCHN,	-144
NUMSCN,	-12
NUMSCS,	0
NUMHSA,	-1
NUMTIC,	-144
NUMSHT,	0
DSCALE,	1
	0
DOFSET,	0;4000
*120
NUMTHG,	ST-.
FLAGS,	0
INTEXT,	CLA CLL
	TAD INTMQ
	MQL
	TAD FLAGS
	RTF
	CLA
	TAD INTAC
	JMP I 0
INTMQ,	0
INTAC,	0
TOLCNT,	200
TOOCNT,	0
TOIPNT,	TYOBUF
TOOPNT,	TYOBUF
INPCHN,	0

PAGE
GO,	CAF
	SWAB
	KCF
	CLA
	TAD (TFL
	DCA I (MTFL
	TAD (KCF
	DCA I (TYIFLG
	TAD (200
	DCA TOLCNT
	DCA TOOCNT
	TAD (TYOBUF
	DCA TOIPNT
	TAD (TYOBUF
	DCA TOOPNT
	JMS I (CRLF
	IAC
	KIE
	ION
	JMS I (DIS
	JMP .-1
ZERO,	CLA
	TAD (-1750
	DCA IN
	CLA CLL CMA
	DCA IAPNT
	CDF+10
BCLR,	DCA I IAPNT
	TAD (4000
	DCA I IAPNT
	ISZ IN
	JMP BCLR
	CDF
	JMS I (CRLF
	JMP INTEXT
ADGET,	0
	CLA
	TAD (-3
	DCA N
	TAD (AD1-1
	DCA APNT
	TAD (MARK1-1
	DCA BPNT
	TAD (TMARK1-1
	DCA CPNT
	ADCL
	TAD (1
	ADLM
	TAD (100
	ADLE
ADLOOP,	ADST
	ADSK
	JMP .-1
	ADRB
	MQL
	MQA
	DCA I APNT
	MQA
	TAD (1000
	MQL
	DVI;XINCR
	ACL
	DCA I CPNT
	ACL
	CLL RAL
	CIA
	DCA I BPNT
	ISZ N
	JMP ADLOOP
	JMP I ADGET
PAGE
DIS,	0
	CAM
	TAD NUMCHN
	CIA
	DCA N
	TAD (2000
	SWP
	DVI;N
	ACL
	DCA XINCR
	DCA LDDPNT
	TAD NUMSHT
	DCA SHTNUM
	JMS I (ADGET
	CLA
	TAD (7000
	DCA X
	TAD NUMCHN
	DCA N
DL,	CAM;DAD;DOFSET
	CDF+10
	DAD
LDDPNT,	0
	CDF
	SPA
	JMP DDWN
	CLL
	LSR
SHTNUM,	0
	DVI;DSCALE
	DIXY
	SZL
	JMP DUP
	SWP
	DCA DTEMP
	TAD DTEMP
	AND (6000
	SZA
	JMP DUP
	TAD (7000
	TAD DTEMP
YDO,	DILY
	DCA Y
	TAD X
	DILX
	TAD XINCR
	DCA X
	TAD (-2
	DCA J
	TAD (VAL1
	DCA PT
	TAD (MARK1-1
	DCA BPNT
LKLOOP,	CLA
	TAD I BPNT
	TAD LDDPNT
	SZA
	JMP NOTHER
	TAD (-30
	DCA M
	TAD Y
	TAD (1000
	DCA I PT
	TAD Y
	TAD (-14
DMARK,	DISD
	JMP .-1
	DIXY
	DILY
	IAC
	ISZ M
	JMP DMARK
NOTHER,	ISZ PT
	ISZ J
	JMP LKLOOP
	ISZ LDDPNT
	ISZ LDDPNT
	ISZ N
	JMP DL
	JMP I DIS
DDWN,	CLA
	TAD (7000
	JMP YDO
DUP,	CLA
	TAD (777
	JMP YDO
XINCR,	0
DTEMP,	0;0
X,	0
Y,	0
PT,	0
PAGE
VALUES,	CLA
	TAD TMARK1
	JMS I (DECTYP
	JMS SPACE
	CLA
	TAD VAL1
	JMS I (DECTYP
	JMS SPACE
	CLA
	TAD TMARK2
	JMS I (DECTYP
	JMS SPACE
	CLA
	TAD VAL2
	JMS I (DECTYP
	JMS SPACE
	CLA
	TAD VAL1
	CIA
	TAD VAL2
	SPA
	CIA
	JMS I (DECTYP
	JMS I (CRLF
	JMP INTEXT
SPACE,	0
	CLA
	TAD (40
	JMS I (TYO
	JMP I SPACE
KEYBRD,	CLA
	KRS
	DCA CTEM
TYIFLG,	KCF
	TAD (CTAB-2
	DCA IAPNT
LLOOP,	ISZ APNT
	CLA
	TAD I IAPNT
	SNA
	JMP ERRC
	CIA
	TAD CTEM
	SZA
	JMP LLOOP
	TAD I IAPNT
	DCA JADDR
	TAD CTEM
	JMS I (TYO
	JMP I JADDR
ERRC,	CLA
	DCA NTYPD
	DCA NUMBR
	TAD ("?
	JMS I (TYO
	JMS I (CRLF
	JMP INTEXT
DECTYP,	0
	MQL
	TAD (NUMTAB-1
	DCA ICPNT
DECTP1,	CLA
	TAD I ICPNT
	SNA
	JMP DECTP2
	DCA DDIV
	DVI;DDIV
	SWP
	TAD (260
	JMS I (TYO
	JMP DECTP1
DECTP2,	JMP I DECTYP
DDIV,	0
NUMTAB,	1750
	144
	12
	1
	0

CRLF,	0
	CLA
	TAD (15
	JMS I (TYO
	CLA
	TAD (12
	JMS I (TYO
	JMP I CRLF
NTYPE,	0
CTEM,	0
JADDR,	0
PAGE
INTER,	DCA INTAC
	ACL
	DCA INTMQ
	GTF
	DCA FLAGS
	CLSK
	SKP
	JMP CLKBRK
	TSF
	SKP
	JMP ITYO
	KSF
	SKP
	JMP I (KEYBRD
	HLT

CLKBRK,	CLA
	CLSA
	DCA CLTEMP
	TAD CLTEMP
	AND (1
	SNA
	JMP CLKBR1
	ISZ STEPCN
	NOP
CLKBR1,	CLA
	TAD CLTEMP
	AND (2
	SNA
	JMP INTEXT
	ISZ LFTMAR
	SKP
	JMP LEFTGO
	DCA LFTMAR
	JMP INTEXT
LEFTGO,	ION
	JMP I (LEFTHT
CLTEMP,	0
TYO,	0
	DCA TYOTEM
	TAD TOLCNT
	SNA
	JMP NOTYO
	CLA
	TAD (ION
	DCA TYOION
	TAD (NOP
	SKON
	DCA TYOION
	IOF
	CLA
	TAD TOLCNT
	SNA
	JMP NOTYOI
	TAD (-1
	DCA TOLCNT
	TAD TYOTEM
	DCA I TOIPNT
MTFL,	TFL
	TAD (NOP
	DCA MTFL
	CLA
	ISZ TOOCNT
	TAD TOIPNT
	IAC
	AND (177
	TAD (TYOBUF
	DCA TOIPNT
TYOION,	0
	JMP I TYO
NOTYOI,	ION
NOTYO,	CLA
	TAD TYOTEM
	SKON
	JMP I TYO
	JMP TYO+1
TYOTEM,	0

ITYO,	CLA
	TAD TOOCNT
	SNA
	JMP ITYO1
	TAD (-1
	DCA TOOCNT
	TAD I TOOPNT
	TLS
	CLA
	TAD TOOPNT
	IAC
	AND (177
	TAD (TYOBUF
	DCA TOOPNT
	ISZ TOLCNT
	JMP INTEXT
ITYO1,	TCF
	TAD (TFL
	DCA MTFL
	JMP INTEXT
PAGE
TYOBUF,	0
*.+177
NUMI,	ISZ NTYPD
	CLA
	TAD NUMBR
	MQL
	TAD I (CTEM
	AND (17
	MUY;D10
	SZA
	JMP I (ERRC
	ACL
	DCA NUMBR
	JMP INTEXT

D10,	12

PAGE
GOSCAN,	CLA
	TAD NUMSCN
	DCA NS
	DCA NTYPD
GLOOP,	CLA CLL CMA
	CLZE
	DCA LFTMAR
	TAD (12
	CLDE
	ION
	JMS I (DIS
	JMP .-1

LEFTHT,	CLA
	TAD (2
	CLZE
	CLA
	TAD (11
	CLDE
	CLA
	ADCL
	TAD INPCHN
	ADLM
	TAD NUMCHN
	DCA NC
	DCA CHP1
	DCA CHP2
CLOOP,	CLA
	TAD NUMTIC
	DCA STEPCN
	TAD NUMSCS
	DCA SAMC
	TAD NUMHSA
	DCA HSAMC
	CDF+10
	CAM;DAD
CHP1,	0
	CDF
	DST;CCHN
	ION
	NOP
	ADST
SLOOP,	ADSK
	JMP .-1
	ADRB
	ADST
	ASR;14
	DAD;CCHN
	DST;CCHN
	ISZ SAMC
	JMP SLOOP
	ISZ HSAMC
	JMP SLOOP
	CDF+10
	DST
CHP2,	0
	CDF
WLOOP,	CLA
	TAD STEPCN
	SPA
	JMP WLOOP
	SZA
	JMP TMOUT	/WHOOPS
	ISZ CHP1
	ISZ CHP2
	ISZ CHP1
	ISZ CHP2
	ISZ NC
	JMP CLOOP
	JMS I (FIXIT
	ISZ NS
	JMP GLOOP
	CLA CMA
	CLZE
	JMS I (DIS
	JMP .-1
TMOUT,	HLT
	JMP I (GO
NS,	0
NC,	0
SAMC,	0
SOFF,	0;0
CCHN,	0;0
HSAMC,	0
PAGE
ADJUST,	ION
	CLA CMA
	CLZE
	ADCL
	CLA
	TAD (3
	CLDE
	CLA
	TAD INPCHN
	ADLM
	ADST
XAD2,	CLA
	CLSA
	DCA N
	TAD N
	DIXY
	AND (2
	SNA
	JMP XAD1
	TAD (5
	JMS I (TYO
XAD1,	CLA
	TAD N
	AND (1
	SZA
	ISZ APNT
	NOP
	CLA
	TAD APNT
	DILX
	ADSK
	JMP .-1
	ADRB
	ADST
	DILY
	JMP XAD2


KALIED,	ION
	TAD HPOS
	JMS SCALE
	CMA
	TAD VPOS
	DCA VPOS
	TAD VPOS
	DILX
	JMS SCALE
	TAD HPOS
	DILY
	DISD
	JMP .-1
	DIXY
	DCA HPOS
	JMP KALIED
SCALE,	0
	DCA PLACE
	OSR
	CIA
	DCA Q
	TAD PLACE
CHANGE,	CLL
	SPA
	CML
	RAR
	ISZ Q
	JMP CHANGE
	JMP I SCALE
Q,	0
HPOS,	0
VPOS,	0
SETSCL,	TAD (DSCALE
	JMP COMSE1
SETDOL,	CMA
SETDOH,	TAD (DOFSET+1
	JMP COMSE1
SETSHT,	TAD (NUMSHT
COMSE1,	DCA PLACE
	TAD NTYPD
	SNA
	JMP CM1TYP
	CLA
	TAD NUMBR
	JMP COMSE2
CM1TYP,	TAD I PLACE
	JMP COMTYP+2
SETTIC,	TAD (NUMTIC
	JMP COMSET
SETSCS,	TAD (NUMSCS
	JMP COMSET
SETSCN,	TAD (NUMSCN
	JMP COMSET
SETHSA,	TAD (NUMHSA
	JMP COMSET
SETCHN,	TAD (NUMCHN
COMSET,	DCA PLACE
	TAD NTYPD
	SNA
	JMP COMTYP
	CLA
	TAD NUMBR
	CIA
COMSE2,	DCA I PLACE
	DCA NTYPD
	DCA NUMBR
	JMS I (CRLF
	JMP INTEXT
COMTYP,	TAD I PLACE
	CIA
	JMS I (DECTYP
	JMS I (CRLF
	JMP INTEXT
PLACE,	0
PAGE
PUNCH,	ION
	HLT
	JMS FEED
	CLA
	TAD (ST
	DCA PPNT
	TAD NUMTHG
	STL
	RAR
	DCA QN
	TAD (NOP
	DCA FLD
	JMS PUNEM
	CLA
	DCA PPNT
	TAD NUMCHN
	DCA QN
	TAD (CDF+10
	DCA FLD
	JMS PUNEM
	JMS FEED
	JMS I (DIS
	JMP .-1
FEED,	0
	CLA
	TAD (-100
	DCA QN
	CLA
	JMS DTYO
	ISZ QN
	JMP .-3
	CLA CMA
	JMS DTYO
	JMP I FEED
DTYO,	0
	DCA QJ
	JMS I (DIS
	CLA
	TAD TOLCNT
	SNA
	JMP .-4
	CLA
	TAD QJ
	JMS I (TYO
	JMP I DTYO
PUNEM,	0
FLD,	0
	CAM DAD
PPNT,	0
	CDF
	DST;PNUM
	LSR;4
	JMS DTYO
	CAM DAD;PNUM
	SHL;4
	JMS DTYO
	CLA
	TAD PNUM
	JMS DTYO
	ISZ PPNT
	ISZ PPNT
	ISZ QN
	JMP FLD
	JMP I PUNEM
PNUM,	0;0
QJ,	0
PAGE
READ,	JMS I (CRLF
	TSF;JMP .-1
	CAF;SWAB
	KCC
	TAD NUMTHG
	DCA QN
	TAD (ST
	DCA RPNT
	TAD (NOP
	DCA RFLD
	TAD (NOP
	DCA RDISP
	JMS READC
	IAC
	AND (377
	SZA
	JMP .-4
READF1,	NOP
	JMS READEM
	CLA
	TAD NUMCHN
	DCA QN
	TAD (CDF+10
	DCA RFLD
	DCA RPNT
	TAD (JMS I (DIS
	DCA RDISP
	JMS READEM
	JMP I (GO
READEM,	0
	CAM
	DCA RED
	JMS READC
READF2,	NOP
	SHL;4
	DCA RED+1
	JMS READC
READF3,	NOP
	CLL
	LSR;4
	DAD;RED
	DST;RED
	JMS READC
READF4,	NOP
	CLL
	LSR;14
	DAD;RED
RFLD,	0
	DST
RPNT,	0
	CDF
	ISZ RPNT
	ISZ RPNT
	ISZ QN
	JMP READEM+1
	JMP I READEM
RED,	0;0
READC,	0
RDISP,	0
	KSF
	JMP RDISP
	KRB
READF5,	NOP
	JMP I READC
PAGE
FIXUP,	JMS FIXIT
	JMP INTEXT

OFFSET,	JMS FIXIT
	JMS I (DIS
	KSF
	SKP
	JMP I (GO
	CLA
	DCA OPNT
	DCA OPNT1
	TAD NUMCHN
	DCA IN
OLOOP,	CAM DAD;ONE
	CDF+10
	DAD
OPNT,	0
	DST
OPNT1,	0
	CDF
	ISZ OPNT;ISZ OPNT
	ISZ OPNT1;ISZ OPNT1
	ISZ IN
	JMP OLOOP
	JMP OFFSET
FIXIT,	0
	CAM
	DCA CMPNT
	TAD NUMCHN
	DCA IN
	DST;UPPER
	CLL CMA CLA RAR
	DST;LOWER
CMLOOP,	CDF+10
	CAM DAD
CMPNT,	0
	CDF
	CLL
	LSR;1
	DST;CURVAL
	DCM
	DAD;UPPER
	SMA
	JMP CMP1
	CAM DAD;CURVAL
	DST;UPPER
CMP1,	CAM DAD;LOWER
	DCM
	DAD;CURVAL
	SMA
	JMP CMP2
	CAM DAD;CURVAL
	DST;LOWER
CMP2,	ISZ CMPNT
	ISZ CMPNT
	ISZ IN
	JMP CMLOOP
	CLA
	TAD (-2
	DCA IN
	CAM DAD;LOWER
	DCM
	DAD;UPPER
	SHL;1
CMP3,	SHL;1
	SZL
	JMP CMP4
	ISZ IN
	JMP CMP3
	JMP CMP5
CMP4,	RAR
CMP5,	SNA
	IAC
	DCA DSCALE
	TAD IN
	CIA
	DCA NUMSHT
	CAM DAD;LOWER
	DCM
	SHL;1
	DST;DOFSET
	JMP I FIXIT
CURVAL,	0;0
UPPER,	0;0
LOWER,	0;0
ONE,	1;0
PAGE
CTAB,
"0;NUMI;"1;NUMI;"2;NUMI;"3;NUMI;"4;NUMI;"5;NUMI;"6;NUMI;"7;NUMI;"8;NUMI;"9;NUMI
"*;GO
"+;FIXUP
"A;ADJUST
"C;SETCHN
"D;SETDOH
"E;SETDOL
"F;SETSCL
"G;GOSCAN
"H;SETHSA
"K;KALIED
"N;SETSCS
"O;OFFSET
"P;PUNCH
"R;READ
"S;SETSCN
"T;SETTIC
"V;VALUES
"Z;ZERO
"_;SETSHT
0
0
$

