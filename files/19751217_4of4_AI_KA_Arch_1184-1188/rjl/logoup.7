TITLE 11 STATUS REPORTER
.MLLIT==1
A=1
B=2
C=3
E=4
F=5
P=17

TTYO==1

11PAGE==100
PDP11==11PAGE*2000




GO:	MOVEI P,PDL
	.OPEN TTYO,[SIXBIT /  !TTY/]
	.VALUE
	MOVEI B,11PAGE	;WHERE 11 MEM STARTS IN 10
	MOVEI A,14.	;28K OF 11 SPACE
	MOVE C,[602000,,1777]
11IN1:	.CALL  [SETZ
		SIXBIT /T11MP/
		B
		SETZ C]
	.VALUE
	AOS B
	ADD C,[4,,]
	SOJGE A,11IN1
	.VALUE	
	
	PUSHJ P,11WRDG
	[40]
	A
	PUSHJ P,11TYPE
	



	PUSHJ P,TYPE
	[ASCIZ / IS DOWN!!
/]
	.VALUE [ASCIZ /:KILL /]






11WRDG:	ADD P,[2,,2]
	PUSH P,A
	PUSH P,B
	MOVE A,-4(P)
	MOVE B,1(A)
	MOVEM B,-3(P)
	MOVE A,@(A)
	AOS -4(P)
	LSH A,-1	;FLUSH BYTE BIT
	MOVE B,A
	ANDI A,1	;GET THE HALF WORD BIT
	LSH B,-1	;FLUSH HALF WORD BIT IN B
	LDB A,(A)[242000,,PDP11(B) ? 042000,,PDP11(B)]
	MOVEM A,-2(P)
	POP P,B
	POP P,A
	POP P,@-1(P)
	SUB P,[1,,1]
	AOS (P)
	POPJ P,

11BYTG:	ADD P,[2,,2]
	PUSH P,A
	PUSH P,B
	MOVE A,-4(P)
	MOVE B,1(A)
	MOVEM B,-3(P)
	MOVE A,@(A)
	AOS -4(P)
	MOVE B,A
	ANDI A,3
	LSH B,-2
	LDB A,(A)[241000,,PDP11(B)
		  341000,,PDP11(B)
		  041000,,PDP11(B)
		  141000,,PDP11(B)]

	MOVEM A,-2(P)
	POP P,B
	POP P,A
	POP P,@-1(P)
	SUB P,[1,,1]
	AOS (P)
	POPJ P,

TYPE:	PUSH P,A
	MOVE A,@-1(P)
	AOS -1(P)
	HRLI A,440700
	PUSH P,B
TYPE1:	ILDB B,A
	JUMPE B,TYPE2
	.IOT TTYO,B
	JRST TYPE1
TYPE2:	POP P,B
	POP P,A
	POPJ P,

11TYPE:	PUSH P,B
11TYP1:	PUSHJ P,11BYTG
	A
	B
	JUMPE B,11TYP2
	.IOT TTYO,B
	AOS A
	JRST 11TYP1
11TYP2:	POP P,B
	POPJ P,

PDL:	BLOCK 100
END GO
