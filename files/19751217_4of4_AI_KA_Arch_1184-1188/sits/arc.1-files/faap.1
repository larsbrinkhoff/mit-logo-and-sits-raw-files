;FIRST ARG IS CAPABILITY TO FILE		
;SECOND IS THE PAGE NO.
MFAAP:	CMP E,#20			;IS IT A LEGAL PAGE
	BHIS MFAAP8			;NO
MFAAP4:	MOV E,F				;COPY IT
	MUL #UPTLEN,F			;FIND THE PAGE
	ADD #ITM1AD+SUPTS,F
	MOV (F),B			;GET THE DR
	BIC #360,B			;SEE IF IT EXISTS
	BNE .+6				;IT DOES
MFAAP8:	JMP ERETSZ
	BIT #UPTDEI,B			;IS D=I
	BEQ MFAAP3			;NO
	CMP F,ITM1AD+SUPTS+<UPTLEN*10>	;IS IT THE D PAGE OF THE PAIR
	BLO MFAAP3			;NO
	SUB #<UPTLEN*10>,F		;POINT TO THE I SPACE
MFAAP3:	MOVB 1(F),B			;GET THE START
	BIC #UPMSRE,B			;IT BETTER BE 0
	BNE MFAAP8
	MOV FAMFI(D),A			;MFI ITEM NO.
	JSR PC,ITM2LD			;LOAD IT UP
	BIT #MFDIRB,MFLAGW(A)		;IS IT A DIRECTORY
	BNE MFAWTE			;YES???
	BIT #.FAAP,(D)			;CAN I APPEND
	BEQ MFAACV			;NO
	MOV #MFLAGW,A			;LOCK THE APPENDING SWITCH
	MOV #MFIBEB,B
	MOV ITEM2,C
	JSR PC,LCKASW
	BIT #1777,MFBYTP(A)		;AM I ALREADY ON BLOCK BOUNDARY
	BEQ MFAAP1			;YES
	BIS #MFEBMB,MFLAGW(A)		;CHANGING THE ENTRY
	ADD #2000,MFBYTP(A)		;ROUND UP THE BYTE POINTER TO NEXT BLOCK
	CMP MFBYTP(A),#20000		;DID I ROUND TO NEXT PAGE
	BLO MFAAP1			;NO
	INC MFPGNO(A)			;SAY THERE IS ANOTHRE PAGE
	CLR MFBYTP(A)			;NO EXTRA BYTES OR BLOCKS
MFAAP1:	BIC #1777,MFBYTP(A)		;CLEAR TO GET ONTO A BLOCK BOUNDARY
	BIC #MFLSTB,MFENHD(A)		;CLEAR 
	TST MFBYTP(A)			;AM I ON A PAGE BOUNDARY
	BEQ MFAAP2			;YES, DONT DO A THING
	JSR PC,MFIEXP			;EXPAND THE ITEM FOR A HOLE TYPE DESCRIPTOR
	MOV #20000,B			;THE LENGTH IN BYTES OF A FULL PAGE
	SUB MFBYTP(A),B			;SUBTRACT THE NUMBER WE ALREADY HAVE
	ASH #-10.,B			;TURN INTO THE NUMBER OF BLOCKS NEEDED
	BIS #140,B			;SET IN THE HOLE TYPE DESRCIPTOR
	MOVB MFENHD(A),C		;FIND THE END OF THE ENTRY
	ADD #ITM2AD+MFENHD,C		;POINT TO IT DIRECTLY
	MOVB B,(C)+			;SET IN THE HOLE DESCRIPTOR
	CLRB (C)+			;AND CLEAR NEXT BYTE FOR LUCK
	INCB MFENHD(A)			;SAY ANOTHER BYTE EXISTS
	BIS #MFEBMB,MFLAGW(A)		;AND ENTRY HAS BEEN MUNGED AGAIN
	CLR MFBYTP(A)			;ON A PAGE BOUNDARY NOW
	SAVE F				;THE POINTER TO THE UPT
	MOV MFPGNO(A),F			;GET THE LAST PAGE NO.
	INC MFPGNO(A)			;INCREMENT IT FOR CORRECT EOF
	JSR PC,MFPTFX			;FIX UP THE OLD FPT
	REST F				;GET BACK POINTER TO UPT
MFAAP2:	MOVB MFENHD+ITM2AD,A		;FIND THE LENGTH OF THE ENTRY
	ADD #MFELEN+25.,A		;HAVE TO BE ABLE TO ADD 8. 3BYTE DESCRIPTORS+1
	ASH #-6,A			;GET NUMBER OF 32. WORD BLOCKS
	MOV ITEM2,B			;ITEM TO EXPAND
	JSR PC,EXITEM			;MAKE IT LARGER
	BEQ MFIRAN			;FAILED MAKE IT RANDOM ERROR
	MOV ITEM2,A			;LOAD IT BACK UP
	JSR PC,ITM2LD
	MOV MFLAGW(A),B			;GET THE DISK THAT THIS ONE IS ON
	BIC #177400,B
	MOV MFBITS(B),B			;GET THE BITTABLE FUPT FOR THIS DISK
	MOV B,C
	JSR PC,UPTPLD			;LOAD IT UP
	BIC #UPTMOD!UPTWON,(B)		;SAY WE MODIFIED IT
	JSR PC,MFSWAL			;SWAP IT IN AND LOCK IT
	MOV UPTCLP(F),B			;SWAP IN THE CURRENT PAGE
	JSR PC,SWPIN
MFAAP5:	MOV FPTFRE,E			;ALLOCATE A FPT
	BNE MFAAP6
MFAAP7:	JSR PC,LFLUSH
	TST FPTFRE
	BEQ MFAAP7
	JSR PC,RUNME
	BR MFAAP5
MFAAP6:	MOV (F),A			;GET THE DR
	BIC #UPMLRE,A
	ASH #-12.,A			;FIND ITS LENGTH
	CMP A,MFREBK(C)			;ARE THERE ENOUGH FREE BLOCKS ON THIS DISK
	BLO MFAAP9			;YES
	MOV #.FANSS,ITM0AD+PERRW	;SAY NOT ENOUGH ROOM
	JMP ERETSZ
MFAAP9:	SAVE F				;POINTER TO THE UPT
	INC A				;THE NUMBER OF BLOCKS TO ADD
MFAP10:	SAVE A
	JSR PC,ADDBK			;ADD A BLOCK
	REST A				;GET BACK COUNT
	SOB A,MFAP10			;TRY UNTIL ENOUGH ADDED
	MOV ITM2A,A
	MOV MFPGNO(A),F			;GET THE PAGE NO
	TST MFBYTP(A)			;DID WE END ON PAGE BOUNDARY
	BNE .+4				;NO
	DEC F				;WANT THE PRVIOUS PAGE
	JSR PC,MFMFPT			;MAKE A FPT FOR IT
	REST F				;GET BACK UPT POINTER
	BIT #MFIWSB,MFLAGW(A)		;IS IT WRITE TO SOURCE?
	BEQ MFAP11			;NO
	MOV UPTFPT(F),B			;THE OLD SWAP SPACE
	JSR PC,DSWPAL			;DEALLOCATE IT
	CLR UPTFPT(F)			;NO SWAP SPACE
MFAP11:	MOV UPTFPT(F),FPTSWA(E)		;SET IN THE OLD SWAP ADDRESS
	MOV UPTCLP(F),FPTUPT(E)		;MAKE THE FPT POINT TO THE UPTS
	MOV E,C				;COPY POINTER TO FPT
	ADD #FPTSRC,C			;POINT TO THE BLOCKS
	MOV #10,B
	BIC #FPTBAS,(C)+		;CLEAR ALL THE BAS BITS
	SOB B,.-2
	BIS #UPTWON!UPTMOD,(F)		;SAY WE HAVE WRITTEN AND MODIFIED THESE PAGES
	CLR C				;THIS WILL COUNT NUMBER OF SPHERE UPT
	MOV E,A				;COPY POINTER TO FPT
	SEC				;SET THE TOP BIT ON ROR
	ROR A				;TURN INTO SWAP ADDRESS POINTER
	MOV F,B				;COPY THE POINTER TO UPT
	ADD #ITM2AD-ITM1AD,F		;TURN IT INTO ONE LOADED BY UPTLD
UPTLP:	MOV A,UPTFPT(B)			;CHANGE THE SWAP ADDRESS
	MOV UPTCLP(B),B			;GET THE NEXT ONE
	TST ITEM2			;IS IT FUPT
	BMI .+4				;YES
	INC C				;SAY ANOTHER SPHERE UPT
	JSR PC,UPTLD			;LOAD IT UP
	CMP ITEM2,ITEM1			;IS IT IN THE SAME SPHERE AS THE FIRST
	BNE UPTLP			;NO, CHANGE IT
	CMP B,F				;IS IT THE SAME ONE
	BNE UPTLP			;NO, CHANGE IT
	MOV FAMFI(D),A			;GET THE MFI FOR THIS FILE
	JSR PC,ITM2LD			;LOAD IT BACK UP
	ADD C,MFREF(A)			;INCREMENT THE REF COUNT FOR EACH SPHERE UPT
	JMP ERETCZ			;DONE
