A=%0
B=%1
C=%2
D=%3
E=%4
F=%5
P=%6
PC=%7
.INSRT SITS;SITS SYMS

.=400
GO:	MOV #PDL,P
	MOV #-1,-(P)
	MOV #FILBLK,-(P)
	MOV #.FACAP*400,-(P)	;CREATE A FILE
	.INVOK
	BNE .+4
CREATF:	BPT
	MOV (P)+,FCAP1
	MOV #-1,-(P)
	MOV #FILBLK,-(P)
	MOV #.FACAP*400,-(P)	;CREATE ANOTHER FILE
	.INVOK
	BEQ CREATF
	MOV (P)+,FCAP2
	MOV TRNSLE,A		;NUMBER OF TRANSFERS TO MAKE
	CLR C			;THE DATA TO WRITE
INLOP:	MOV #BLKST,B		;POINT TO THE START OF THE BLOCK
	MOV BLKLEN,D		;THE LENGTH OF THE BLOCK
INLOP1:	MOV C,(B)+		;SET IN THE DATA
	INC C			;GET THE NEXT DATA
	INC C			;FIRST FILE IS ALL EVEN
	SOB D,INLOP1		;BLOCK IS NOW SET UP
	MOV #BLKST,-(P)		;POINTER TO THE BLOCK
	MOV BLKLEN,-(P)		;PUT IN THE LENGTH OF THE TRANGER
	ASL (P)			;MAKE IT BYTE COUNT
	NEG (P)			;MAKE IT NEGATIVE
	MOV FCAP1,-(P)		;THE FILE CAPABILITY
	.BLKO			;WRITE IT OUT
	BNE .+4			;OKAY?
BLKOF:	BPT
	MOV BLKLEN,D		;GET THE LENGTH
	MOV #BLKST,B		;THE BLOCK
	INC (B)+
	SOB D,.-2		;INCREMENT ALL THE DATA
	MOV #BLKST,-(P)		;POINTER TO THE BLOCK
	MOV BLKLEN,-(P)		;PUT IN THE LENGTH OF THE TRANGER
	ASL (P)			;MAKE IT BYTE COUNT
	NEG (P)			;MAKE IT NEGATIVE
	MOV FCAP2,-(P)		;THE FILE CAPABILITY
	.BLKO			;WRITE IT OUT
	BEQ BLKOF		;FAILED
	SOB A,INLOP		;TRY AGAIN
BLKRST:	CLR -(P)
	CLR -(P)
	MOV FCAP1,-(P)		;RESET THE POINTER FOR THIS CAPABILITY
	BIS #.FASP,(P)		;SET THE POINTER
	.INVOK
	BNE .+4
FASPF:	BPT
	CLR -(P)
	CLR -(P)
	MOV FCAP2,-(P)		;NOW THE OTHER FILE
	BIS #.FASP,(P)
	.INVOK
	BEQ FASPF
	CLR C			;CHECK THE DATA
	CLR E			;FOR SECOND BLOCK
	INC E
	MOV TRNSLE,A		;THE NUMBER OF BLOCKS TO TRANSMIT
OUTLOP:	MOV #BLKST,-(P)		;SET THE POINTER
	MOV BLKLEN,-(P)		;THE LENGTH
	ASL (P)
	NEG (P)
	MOV FCAP1,-(P)		;READ THE FIRST FILE
	.BLKI
	BNE .+4
INFAIL:	BPT
	MOV #BLKST,B		;POINTER TO THE BLOCK
	MOV BLKLEN,D		;THE NUMBER OF WORDS IN THIS BLOCK
INCMP:	CMP C,(B)+		;IS IT THE SAME
	BEQ .+4
INCHK:	BPT
	INC C
	INC C
	SOB D,INCMP
	MOV #BLKST,-(P)		;SET THE POINTER
	MOV BLKLEN,-(P)		;THE LENGTH
	ASL (P)
	NEG (P)
	MOV FCAP2,-(P)		;READ THE FIRST FILE
	.BLKI
	BEQ INFAIL
	MOV #BLKST,B		;SET THE POINTER
	MOV BLKLEN,D		;POINTER TO BLOCK
INCMP1:	CMP E,(B)+
	BNE INCHK
	INC E
	INC E
	SOB D,INCMP1		;DO ALL THE BLOCKS
	SOB A,OUTLOP		;DO ALL THE BLOCKS
DELCP:	CMP -(P),-(P)
	MOV FCAP1,-(P)
	BIS #.DELCP,(P)		;DELETE THE FILES
	.INVOK
	BNE .+4
DELLOS:	BPT
	CMP -(P),-(P)
	MOV FCAP2,-(P)
	BIS #.DELCP,(P)
	.INVOK
	BEQ DELLOS
	BPT
.BLKW 40
PDL::
FILBLK:	0
	.ASCIZ /TESTER #259/
	.EVEN
BLKLEN:	512.
FCAP1:	0
FCAP2:	0
TRNSLE:	21
BLKST:
.END GO
