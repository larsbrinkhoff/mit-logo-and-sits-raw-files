A=%0
B=%1
C=%2
D=%3
E=%4
F=%5
P=%6
PC=%7
.INSRT SITS;SITS SYMS
.MACRO SYSCAL AFOO,BFOO,CFOO,DFOO
	MOV CFOO,-(P)
	MOV BFOO,-(P)
	MOV AFOO,-(P)
.IFNB DFOO
	BIS DFOO,(P)
.ENDC
	.INVOK
	BNE .+4
	BPT
.ENDM

.=400
GO:	MOV #PDL,P
	SYSCAL ROOTCP,#0,#-1,#.CPYCP	;COPY ROOT CAP
	MOV (P)+,BEECP			;GOT BEE CAP
	SYSCAL BEECP,#MUTBLK,#0,#.FAMU	;MUTATE TO BEE'S DIR
ADLOOP:	SYSCAL BEECP,#FILBLK,#.FADIR,#.FAAD	;INSERT A DIR IN DIR
	SYSCAL BEECP,#0,#-1,#.CPYCP
	MOV (P)+,FILCAP
	SYSCAL FILCAP,#FILBLK,#0,#.FAMU	;MUTATE TO THE FILE
DELOOP:	SYSCAL BEECP,#0,#0,#.DELCP	;NOW DELETE BEE DIR CAP
	SYSCAL FILCAP,#0,#0,#.DELCP	;SEE IF WE WIN HERE
	SYSCAL ROOTCP,#0,#-1,#.CPYCP	;COPY THE ROOT CAPABILITY
	MOV (P)+,BEECP
	SYSCAL BEECP,#MUTBLK,#0,#.FAMU	;MUTATE TO THE . DIR
	SYSCAL ROOTCP,#0,#-1,#.CPYCP	;COPY ROOT
	MOV (P)+,FILCAP			;BACK TO THE FILE
	SYSCAL FILCAP,#FILBK1,#0,#.FAMU	;MUTATE TO THE FILE
DELT:	SYSCAL BEECP,#FILBK2,#0,#.FADL	;DELETE THE FILE
	SYSCAL BEECP,#0,#0,#.DELCP	;DELETE DIRECTORY CAPABILITY
	SYSCAL FILCAP,#0,#0,#.DELCP	;DELETE THE FILE CAPABILITY
	BPT
.BLKW 40
PDL::
MUTBLK:	.ASCIZ /./
	.EVEN
FILBLK:	.ASCIZ /FRED/
	.EVEN
FILBK1:	.ASCIZ /. FRED/
	.EVEN
FILBK2:	.ASCIZ /FRED/
	.EVEN
BLKLEN:	512.
ROOTCP:	10		;ROOT DIRECTORY CAP IS HERE
BEECP:	0		;BEE DIRECTORY CAP IS HERE
FILCAP:	0		;CAP TO VERSION #77 IS HERE
.END GO
