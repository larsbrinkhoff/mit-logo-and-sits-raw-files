;SWAP OUT PAGE WHOSE PB IS IN B
SWPPAG:	JSR PC,PAGPCL	;PCLOSER EVERYONE WHO MIGHT DEPEND ON THIS PAGE
	BIT #PBLOCK!PBDISK,PBFLAG(B)	;SHOULDN'T BE DISKING OR LOCKED
	NBUGC NE
	JSR PC,PBLCK	;NOW I LOCK IT!
	JSR PC,VALSWP	;VALIDATE ON SWAP SPACE
	JSR PC,PBCFRE	;FREE THE CORE IT TOOK
	JSR PC,LSWPOP	;POP SWITCH, UNLOCKING BLOCK
	RTS PC


;GET CORE TO EXPAND ITEM SPACE
;CALL WITH DESIRED AMOUNT OF COORE IN A
ITMSEX:	JSR F,ACSAV
	INC A		;0=1
ITMSE1:	MOV A,D		;COPY FOR SOB
	MOV CST1NS,C	;POINT TO FIRST NON-SYSTEM BLOCK
ITMSE2:	BIT #CSTFRB,(C)+	;IS THIS BLOCK FREE?
	BEQ ITMSE4	;NOPE, HAVE TO FREE IT
	SOB D,ITMSE2	;CHECK ALL THE BLOCKS WE WANT TO GOBBLE
	MOV CST1NS,C	;POINT TO START AGAIN, THEY ARE ALL FREEE
ITMSE3:	BIT #CSTFRB,(C)	;IS IT REALLY FREE?
	NBUGC EQ
	BIS #CSTSYB,(C)	;USED BY SYSTEM
	BIC #CSTFRB,(C)+	;AND NO LONGER FREE
	SOB A,ITMSE3	;FOR ALL THE BLOCKS
	MOV C,CST1NS	;NEW TOP OF SYS CORE
	JSR F,ACRES
	RTS PC
ITMSE4:	TST -(C)	;BACK TO BLOCK THAT ISN'T FREE
	MOV CSTPB(C),B	;WHO IS THAT, ANYWAY
	JSR PC,SWPPAG	;OUT WITH HIM!
	BR ITMSE1	;GO TRY IT ALL AGAIN


;ALLOCATE SWAP SWAP
;THIS CALL MAY ONLY BE EXECUTED BY THE SYSSPR
;THE 3 ARGS ON THE STACK ARE THE SIZE OF THESWAP SPACE
;IN BLOCKS, THE BLOCK ADDRESS OF THE START OF SWAP SPACE
;AND THE DISK NUMBER THAT THIS SWAP SPACE IS ON
;THE CALL IS EXECUTED ONCE FOR EACH SWAP SPACE TO BE ALLOCATED
EALLOC:	CMP CURSPH,SYSSPR	;IS THIS THE SYSSPR
	ERRORC NE,SYS
	JSR PC,RETNSW	;GET THE NUMBER OF BLOCKS
	MOV A,F		;STORE IT AWAY
	ASH #-3,F	;CONVERT TO 4K BLOCKS
	JSR PC,RETNSW	;GET START OF SWAP SPACE
	MOV A,E		;AND SAVE IT
	JSR PC,RETNSW	;FINNALLY, GET THE DISK NUMBER
	MOV #NSWPA,D	;NUMBER OF SWAP SPACES POSSIBLE
	CLR C		;POINTER INTO SWAP SPACE TABLES
EALLO1:	TST SWPBPT+2(C)	;IS THIS SPACE FREE?
			;IF IT IS, NEXT SPACE BEGGINING=0
	BEQ EALLO3	;FOUND A FREE SPACE
	TST (C)+	;TRY THE NEXT
	SOB D,EALLO1	;UNLESS WE RUN OUT
	ERROR BAD	;SYSSPR WAS BAD, SLAP ITS WRIST
EALLO3:	MOV A,SWPBDK(C)	;SAVE THE DISK NUMBER
	MOV F,SWPFCN(C)	;SAVE COUNT OF FREE SWAP SPACES
	MOV E,A		;COPY START ADDRESS
	BIC #17,A	;GET A FAKE START ADDRESS FOR WORD BOUNDARYNESS
	MOV A,SWPBDA(C)	;AND SAVE THAT AS THE START
	BIC #177760,E	;GET THE AMOUNT NOT REALLY THERE
	ASL E		;CONVERT TO WORD INDEX
	MOV SWPBPT(C),D	;GET THE POINTER TO THIS BIT TABLE
	V BITSST(E),(D)	;SET THE BITS NOT USED
ARG, MUST FUCK AROUND WITH FAC TTHAT BITS REPRESENT 4K, NOT 512!!!!
