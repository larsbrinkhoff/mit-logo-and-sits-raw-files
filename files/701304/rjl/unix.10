	TITLE UNIX TAPE READER/WRITER

A=1
B=2
C=3
D=4
E=5
T=6
TT=7

P=17

MT=10
FL=11
TTI=12
TTO=13

;MAGTAPE DEFINITIONS

128WRD==3000		;128 WORDS/BLOCK
IBMMOD==400		;4 8-BIT CHARACTERS PER WORD
800BPI==300		;CODE FOR 800 BPI
CNKMOD==10		;CHUNK MODE, EACH BLOCK IOT TRIES TO DO ONE TAPE BLOCK

BLKSIZ==512.		;RAYTHEON WANTS 20. CARDS PER BLOCK


CONFLG:	0

TBLOCK:	BLOCK <BLKSIZ+3>/4	;TAPE BLOCK 4 BYTES PER WORD

NXCARD:	(SETZ)			;POINTER TO NEXT CARD IN BLOCK
LSCARD:	(SETZ)			;WHERE TO STOP

BUFL==1200			;FILE BUFFER SIZE
BUFFER:	BLOCK <BUFL+4>/5
BUFP:	(SETZ)			;BUFFER BYTE POINTER
BUFC:	0			;# BYTES IN BUFFER

FN1:	0
FN2:	0
DEV:	SIXBIT/DSK/
SNM:	0

;.MTAPE AC,
;AC/ CHANNEL,,ADDR
;ADDR/ COUNT,,FUNC
;  COUNT IS NEGATIVE FOR BACKWARDS, POSITIVE FOR FORWARDS (?)
;  FUNCS:
MT%IDL==0	;WAIT FOR TAPE OPERATIONS TO COMPLETE
MT%RWN==1	;REWIND
MT%EOF==5	;WRITE EOF MARK
MT%EOT==10	;SPACE TO LOGICAL END OF TAPE
;ALSO CLOSING A WRITE WRITES TWO EOF MARKS AND BACKSPACES OVER ONE

PDL:	-20,,.
	BLOCK 20

GO:	MOVE P,PDL
	.OPEN TTI,[.UAI,,'TTY]
	 .LOSE 1400
	.OPEN TTO,[.UAO,,'TTY]
	 .LOSE 1400
CMD:	MOVEI TT,[ASCIZ/
COMMAND: (IN, OUT, OR REWIND) /]
	PUSHJ P,OUTSTR
	.IOT TTI,TT
	CAIL TT,140
	 SUBI TT,40
	CAIN TT,"I
	 JRST ICMD
	CAIN TT,"O
	 JRST OCMD
	CAIN TT,"R
	 JRST RCMD
	.IOT TTO,[7]
	JRST CMD

RCMD:	MOVEI TT,[ASCIZ/EWIND./]
	PUSHJ P,OUTSTR
	PUSHJ P,TAPRED
	MOVE TT,[1,,MT%RWN]
	MOVE T,[MT,,TT]
	.MTAPE T,
	 .LOSE 1000
	MOVEI TT,MT%IDL
	MOVE T,[MT,,TT]
	.MTAPE T,
	 .LOSE 1000
	.CLOSE MT,
	JRST CMD

OCMD:	MOVEI TT,[ASCIZ/UTPUT TO TAPE.
/]
	PUSHJ P,OUTSTR
	CONSO 344,100000
	 JRST OCMD1
	MOVEI TT,[ASCIZ/
BEGINNING OF TAPE
/]
	PUSHJ P,OUTSTR
OCMD1:	MOVEI TT,[ASCIZ/FROM /]
	PUSHJ P,OUTSTR
	PUSHJ P,FNR
	MOVEI TT,[ASCIZ /FLUSH ALL <CR>'S?/]
	PUSHJ P,OUTSTR
	PUSHJ P,CONVA
	SETZM E
	PUSHJ P,FILRED
	PUSHJ P,TAPWRT
OCMD2:	PUSHJ P,PTBUF
	AOJA E,OCMD2

EOFTRP:	MOVE P,PDL
	PUSHJ P,CLSTAP
	.CLOSE MT,
	.CLOSE FL,
	MOVE T,E
	PUSHJ P,DECNUM
	MOVEI TT,[ASCIZ/ BLOCKS WRITTEN./]
	PUSHJ P,OUTSTR
	JRST CMD

ICMD:	MOVEI TT,[ASCIZ/NPUT FROM TAPE.  TO /]
	PUSHJ P,OUTSTR
	PUSHJ P,FNR
	MOVEI TT,[ASCIZ/
CONVERT <LF> TO <CR><LF>?/]
	PUSHJ P,OUTSTR
	PUSHJ P,CONVA
	SETZM E
	PUSHJ P,FILWRT
	PUSHJ P,TAPRED
ICMD2:	PUSHJ P,PFBUF
	AOJA E,ICMD2

EOTTRP:	MOVE P,PDL
	PUSHJ P,CLSFIL
	.CLOSE MT,
	.CLOSE FL,
	MOVE T,E
	PUSHJ P,DECNUM
	MOVEI TT,[ASCIZ/ BLOCKS READ./]
	PUSHJ P,OUTSTR
	JRST CMD

;OPEN FILE FOR READING
FILRED:	MOVEI TT,.BAI
	.CALL FILOPN
	 .LOSE 1400
	SETZM BUFC
	MOVEI TT,(SETZ)
	MOVEM TT,BUFP
	POPJ P,

;OPEN FILE FOR WRITING
FILWRT:	MOVEI TT,.BAO
	.CALL FILOPN
	 .LOSE 1400
FILWR1:	MOVEI TT,BUFL
FILWR2:	MOVEM TT,BUFC
	MOVE TT,[440700,,BUFFER]
	MOVEM TT,BUFP
	POPJ P,

FILOPN:	SETZ
	SIXBIT/OPEN/
	5000,,(TT)
	MOVEI FL
	DEV ? FN1 ? FN2 ? SNM ((SETZ))

CONVA:	.IOT TTI,TT
	SETZM CONFLG
	CAIE TT,"Y
	 CAIN TT,"y
	  SETOM CONFLG
	POPJ P,

;INPUT CHAR IN TT FROM FILE
FILTYI:	SOSL BUFC
	 JRST FILTI1
	MOVE TT,[-BUFL/5,,BUFFER]
	.IOT FL,TT
	MOVEI TT,-BUFFER(TT)
	JUMPE TT,EOFTRP
	IMULI TT,5
	PUSHJ P,FILWR2
	JRST FILTYI

FILTI1:	ILDB TT,BUFP
	CAIE TT,^M
	POPJ P,
	JRST FILTYI


;OUTPUT CHAR IN TT TO FILE
FILTYO:	CAIN TT,^J
	 JRST FILTO2
FILTO3:	SOSL BUFC
	 JRST FILTO1
	PUSH P,TT
	PUSHJ P,CLSFL1
	PUSHJ P,FILWR1
	POP P,TT
	JRST FILTYO

FILTO1:	IDPB TT,BUFP
	POPJ P,

FILTO2:	MOVEI TT,^M
	PUSHJ P,FILTYO
	MOVEI TT,^J
	JRST FILTO3


CLSFIL:	HLRZ TT,BUFP
	CAIE TT,010700
	 CAIN TT,440700
	  JRST CLSFL1
	MOVEI TT,^C
	PUSHJ P,FILTYO
	JRST CLSFIL

CLSFL1:	MOVE TT,BUFP
	MOVEI TT,-BUFFER+1(TT)
	MOVNS TT
	HRLI TT,BUFFER
	MOVSS TT
	.IOT FL,TT
	POPJ P,

;OPEN MAG TAPE FOR WRITING
TAPWRT:	.OPEN MT,[IBMMOD+800BPI+128WRD+.BIO,,'MT0]
	 .LOSE 1400
	MOVEI TT,TBLOCK
	MOVEM TT,NXCARD
	MOVEI TT,TBLOCK+<BLKSIZ/4>
	MOVEM TT,LSCARD
	POPJ P,

;OPEN MAG TAPE FOR READING
TAPRED:	.OPEN MT,[IBMMOD+800BPI+CNKMOD+.BII,,'MT0]
	 .LOSE 1400
	MOVEI TT,TBLOCK
	MOVEM TT,NXCARD
	MOVEM TT,LSCARD
	POPJ P,

;PUT TAPE BLOCK
PTBLOK:		;DROP INTO GTBLOCK
	
;GET TAPE BLOCK
GTBLOK:	MOVE TT,[-BLKSIZ/4,,TBLOCK]
	.IOT MT,TT
	HRRZM TT,LSCARD
	MOVEI TT,TBLOCK
	CAMN TT,LSCARD
	 JRST EOTTRP
	MOVEM TT,NXCARD
	POPJ P,

;PUT  BUFFER TO TAPE
PTBUF:	MOVE B,[440800,,TBLOCK]
	MOVEI C,BLKSIZ
PTCAR1:	PUSHJ P,FILTYI
	IDPB TT,B
	SOJG C,PTCAR1
	PUSHJ P,PTBLOK
	POPJ P,

;GET CARD FROM TAPE
PFBUF:	PUSHJ P,GTBLOK
	MOVE B,[440800,,TBLOCK]
	MOVEI C,BLKSIZ
GTCAR1:	ILDB TT,B
	PUSHJ P,FILTYO
	SOJG C,GTCAR1
	POPJ P,

;CLOSE TAPE OUTPUT.  MAYBE THE LAST BLOCK SHOULD BE SHORTER, BUT THIS FILLS
;WITH BLANKS

CLSTAP:	MOVEI TT," 
	IDPB TT,B
	SOJG C,CLSTAP
	JRST PTBLOK


;FILE NAME READER
FNR:	MOVEI TT,[ASCIZ/FILE=/]
	PUSHJ P,OUTSTR
	SETZM FN1
	MOVSI TT,360000		;GREATER THAN
	MOVEM TT,FN2	
FNR0:	SETZ A,
	MOVE B,[440600,,A]
FNR1:	.IOT TTI,C
	CAIN C,40
	 JRST FNR1
	CAIN C,177
	 JRST FNR
	JRST FNR3 

FNR2:	.IOT TTI,C
FNR3:	CAIN C,":
	 JRST [	MOVEM A,DEV
		JRST FNR0 ]
	CAIN C,";		
	 JRST [	MOVEM A,SNM
		JRST FNR0 ]
	CAIG C,40
	 JRST FNR4
	CAIN C,177
	 JRST [	JUMPL B,FNR
		LDB TT,B
		ADDI TT,40
		.IOT TTO,TT
		MOVEI TT,0
		DPB TT,B
		ADD B,[060000,,]
		JRST FNR2 ]
	CAIGE C,140
	 SUBI C,40
	TLNE B,770000
	 IDPB C,B
	JRST FNR2

FNR4:	JUMPE A,FNR5
	SKIPE FN1
	 JRST [	MOVEM A,FN2
		JRST FNR5 ]
	MOVEM A,FN1
FNR5:	CAIN C,40
	 JRST FNR0
CPOPJ:	POPJ P,

OUTSTR:	HRLI TT,440700
OUTST1:	ILDB T,TT
	JUMPE T,CPOPJ
	.IOT TTO,T
	JRST OUTST1

DECNUM:	IDIVI T,10.
	HRLM TT,(P)
	SKIPE T
	 PUSHJ P,DECNUM
	HLRZ TT,(P)
	ADDI TT,"0
	.IOT TTO,TT
	POPJ P,


PAT: PATCH: BLOCK 100

END GO
