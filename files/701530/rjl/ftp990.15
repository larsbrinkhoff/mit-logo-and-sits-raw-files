TITLE 990 ftp

A=1
B=2
C=3
D=4
E=5
F=6
T=7
P=17

TYI==1
TYO==2
PYI==3
PYO==4
DKI==5

GO:	MOVEI P,PDL-1
	.OPEN TYI,[SIXBIT /  $TTY/]
	.VALUE
	.OPEN TYO,[SIXBIT /  %TTY/]
	.VALUE
	.OPEN PYI,[SIXBIT /  $T31/]
	.VALUE
	.OPEN PYO,[45,,SIXBIT /   T31/]
	.VALUE
	MOVE A,[10700,,DBUF-1]
	MOVEM A,DBUFP
	MOVEM A,DBFEND
	SETZM EOFFLG
	.CALL [ SETZ
		SIXBIT /OPEN/
		5000,,6
		1000,,DKI
		DEVNAM
		FN1
		FN2
		SETZ SNAME ]
	 .LOSE 1000
LLOOP:	.IOT PYO,["A]
	.IOT PYO,[" ]
CLOOP:	PUSHJ P,GCHR		;GET A CHARACTER FROM THE FILE
	CAIE F,14		;FLUSH FORMFEED
	 CAIN F,12		;AND LINEFEED
	  JRST CLOOP
	CAIN F,11		;TAB?
	 MOVEI F,40		;CHANGE TO SPACE
	CAIE F,3
	 SKIPE EOFFLG
	  MOVEI F,15
	CAIN F,15
	 .IOT PYO,[40]		;TO AVOID LAST CHARACTER IN LINE SCREWUP
	.IOT PYO,F
	.IOT PYI,A		;TO FLUSH THE %&$# ECHO
	CAIE F,15
	 JRST CLOOP
CALOOP:	.IOT PYI,F
	CAIE F,15		;FIND THE ECHOED CR
	 JRST CALOOP		;FLUSH UP TO THE CR
ALOOP:	.IOT PYI,F
	CAIE F,"O
	 JRST ALOOP
	SKIPN EOFFLG
	 JRST LLOOP

EFILE:	.IOT PYO,["E]
	.IOT PYO,[" ]
	.IOT PYO,[15]
	.VALUE

;GETS CHARACTER POINTED TO BY DBUFP
;IF DBUFP = DBFEND, READ IN ANOTHER BUFFER
; IF NOTHING IS READ, SET EOFFLG, AND RETURN A SPACE
;IF EOFFLG IS SET RETURN A SPACE
GCHR:	SKIPE EOFFLG
	 JRST FNDEOF
	MOVE T,DBUFP
	CAMN T,DBFEND
	 JRST NEWBUF
	ILDB F,DBUFP
	POPJ P,
NEWBUF:	MOVE T,[-DBUFL,,DBUF]
	.IOT DKI,T
	CAMN T,[-DBUFL,,DBUF]
	 JRST FNDEOF
	SOS T
	HRLI T,10700
	MOVEM T,DBFEND
	MOVE T,[10700,,DBUF-1]
	MOVEM T,DBUFP
	JRST GCHR
FNDEOF:	SETOM EOFFLG
	MOVEI F," 
	POPJ P,

DBUFL=2000
DBUF:	BLOCK DBUFL
DBUFP:	0
DBFEND:	0
EOFFLG:	0

DEVNAM:	SIXBIT /DSK/
FN1:	SIXBIT /PLOGO/
FN2:	SIXBIT />/
SNAME:	SIXBIT /PLOGO/

PDL:	BLOCK 50

END GO
