; RT-11 BOOTSTRAP
;
; COPYRIGHT (C) 1978
; DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS 01754
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE FOR USE ONLY
; ON A SINGLE COMPUTER SYSTEM AND MAY BE COPIED ONLY WITH
; THE INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE,
; OR ANY OTHER COPIES THEREOF, MAY NOT BE PROVIDED OR OTHERWISE MADE
; AVAILABLE TO ANY OTHER PERSON EXCEPT FOR USE ON SUCH SYSTEM AND TO
; ONE WHO AGREES TO THESE LICENSE TERMS. TITLE TO AND OWNERSHIP OF THE 
; SOFTWARE SHALL AT ALL TIMES REMAIN IN DEC.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO
; CHANGE WITHOUT NOTICE AND SHOULD NOT BE CONSTRUED
; AS A COMMITMENT BY DIGITAL EQUIPMENT CORPORATION.
;
; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.

.ENABL	LC
.NLIST	BEX

			
.IIF NDF BF	BF=0	
.IIF NDF MMG$T,	MMG$T=0	
.IF NE BF
.IF EQ MMG$T						
MONTYP =^R FB
.IFF							
MONTYP =^R XM
.ENDC							
.IFF
MONTYP =^R SJ
.ENDC
.IF NDF	$RFSYS	
.IF NDF	$DTSYS
.IF NDF	$DPSYS
.IF NDF	$DSSYS
.IF NDF $DMSYS
.IF NDF $DLSYS					
.IF NDF	$DXSYS
.IF NDF $DYSYS					
.IF NDF $FLSYS
.IF NDF $FDSYS
$RKSYS=	0		
MONDV =^RRK 
.IFF
MONDV =^RFD
.ENDC
.IFF
MONDV =^RFL
.ENDC
.IFF
MONDV =^RDY						
.ENDC
.IFF
MONDV =^RDX 
.ENDC
.IFF
MONDV =^RDL					
.ENDC
.IFF
MONDV =^RDM 
.ENDC
.IFF
MONDV =^RDS 
.ENDC
.IFF
MONDV =^RDP 
.ENDC
.IFF
MONDV =^RDT 
.ENDC
.IFF
MONDV =^RRF
.ENDC
RAD50M = 'M-100
RAD50N = 'N-100*50*50 
UPDATE = 01.			
.RADIX	10.
.IRP	...,<\UPDATE>
.IF DF $RKSYS
.TITLE	RKBOOT	V03-0'... FOR RT-11 V03
.ENDC
.IF DF $RFSYS
.TITLE	RFBOOT	V03-0'... FOR RT-11 V03
.ENDC
.IF DF $DTSYS
.TITLE	DTBOOT	V03-0'... FOR RT-11 V03
.ENDC
.IF DF $DPSYS
.TITLE	DPBOOT	V03-0'... FOR RT-11 V03
.ENDC
.IF DF $DSSYS
.TITLE	DSBOOT	V03-0'... FOR RT-11 V03
.ENDC
.IF DF $DXSYS
.TITLE	DXBOOT	V03-0'... FOR RT-11 V03
.ENDC
.IF DF $DMSYS
.TITLE	DMBOOT	V03-0'... FOR RT-11 V03
.ENDC
.IF DF $DLSYS
.TITLE	DLBOOT	V03-0'... FOR RT-11 V03
.ENDC
.IF DF $DYSYS
.TITLE	DYBOOT	V03-0'... FOR RT-11 V03
.ENDC
.IF DF $FDSYS
.TITLE	FDBOOT	V03-0'... FOR RT-11 V03
.ENDC
.IF DF $FLSYS
.TITLE	FLBOOT	V03-0'... FOR RT-11 V03
.ENDC
.ENDR
.RADIX	8.
.IIF NDF DYT$O, DYT$O=0	
.IIF NDF DY$CV2, DY$CV2=270
.IIF NDF DXT$O, DXT$O=0	
.IIF NDF DX$CV2, DX$CV2=270
.IIF NDF VT11$,	VT11$=0	
.IIF NDF VS60$,	VS60$=0	
.IIF NDF GT40, GT40=172000
.IIF NDF KW11$P, KW11$P=0
.IIF NDF MPT$Y,	MPT$Y=0	
.IIF NDF PWF$L,	PWF$L=0	
.IIF NDF ERL$G,	ERL$G=0	
.IIF NDF MTT$Y,	MTT$Y=0	
.IIF NDF TIME$R, TIME$R=0
.IIF NDF ESC$P, ESC$P=0	
.IIF NDF TIM$IT, TIM$IT=0
.IIF NDF STAR$T, STAR$T=0
.IIF NDF PANI$C, PANI$C=0
.IIF NDF CONT$N, CONT$N=0
.IIF NDF SILN$T, SILN$T=0
.IIF NDF MTS$J,  MTS$J	= 0
.IIF EQ  BF!MTS$J, MTT$Y = 0
.IF NE MTT$Y		
.IIF NDF MTI$M,  MTI$M  = 0
.IIF NDF DZ11$N, DZ11$N = 0
.IIF NDF DZ11$L, DZ11$L = 0
.IIF NDF DZ11$M, DZ11$M = 0
.IF NE DZ11$N
.IIF NDF DZSP$D, DZSP$D = 2400
.IIF NDF DZLE$N, DZLE$N = 30
.IIF NDF DZST$P, DZST$P = 00
.IIF EQ DZSP$D-1000, DZST$P = 40
.ENDC
.ENDC
.IIF NDF HSR$B,	HSR$B = 0

.MCALL	..V2..
..V2..
.MCALL	.EXIT, .LOOKUP, .PRINT, .SAVES, .READW
.MCALL	.CLOSE,	.SETTOP					
.ENABL	GBL
.MACRO	.ASTX						
	EMT	^O356
.ENDM							
.GLOBL	$DVREC,	$ENTRY,	$INPTR,	$KMLOC,	$MONBL,	$PNAME,	$SLOT
.GLOBL	$SWPBL,	$USRLC, $FKPTR, SYSOP$, $STAT
.GLOBL	BSTRNG,	CORPTR,	DKASSG,	HWFPU$,	HWDSP$,	KMLOC
.GLOBL	KMON,	KMONSZ,	CLOCK$,	MAPOFF,	QCOMP, CLK50$
.GLOBL	SWAPSZ, SYINDO, SYNCH, SYASSG
.GLOBL	SYSLOW,	USRLOC,	USRSZ, $SYHSZ
.GLOBL	LSI11$,$MFPS,$INTEN,GETPSW,PUTPSW		
.GLOBL	KT11$, KW11P$, LKCS$, LKCS, LKVEC, LKPB, LKSTAT
.GLOBL	TKS, TKB, TPS, TPB, V.TKB, V.TPS		
.GLOBL	$DATE,$TIME,$RMON
.GLOBL	KMSIZE, KMLEN, KMONIN, KMLOC			
.GLOBL	$RMEND, LOCATE, LOCAT1, USRBUF			
.IF NE BF!<MTT$Y-1>					
.GLOBL	TTIBUF,	TTOBUF					
.ENDC							
.GLOBL	RELLST, SILNT$					
.GLOBL	CACHE$, MPTY$, SWREG$, LIGHT$, EIS$, PDP60$, PDP70$
.GLOBL	VS6$0						
.IF NE BF
.GLOBL	BCNTXT,	BKGND1,	BKGND2,	BKGND3,	CNTXT,	FUDGE1,	FUDGE2
.GLOBL	MSGENT,	SWIPTR, SWOPTR,	TTIUSR,	TTOUSR,	.$CRTN
.GLOBL	IMPLOC,	RMONPS					
.IF EQ MMG$T						
.GLOBL	RMONSP						
.IFF							
.GLOBL	BGFPPT, RMSTAK, BGWPTR, $MPPTR, $GTBYT, $PTBYT, $PTWRD
.GLOBL	$RLPTR, TRPLST					
.ENDC							
.IFF
.GLOBL	AVAIL,	I.CSW,	FPPADD,	FPPIGN,	MONLOC,	TRAPLC,	TRAPER
.GLOBL	PAVAIL,	PMONLC,	P$SYSC				
.GLOBL	PQCNT,	PI.CSW,	P$SWPB				
 .IF EQ	MTT$Y						
 .GLOBL	PTIBF4,	PTTPRE,	PTOBF2,	PTOBF4,	PTIBF6
 .ENDC							
 .IF NE PANI$C						
 .GLOBL PSPTR
 .ENDC							
 .IF NE ESC$P						
 .GLOBL	ESCPTG,	ESCPTO,	ESCPTI, ESCPTT	PESCRT
 .ENDC							
 .IF NE TIME$R						
 .GLOBL PLKQUE,	P$TIM2,	PLKQU2
  .IF EQ MTT$Y						
  .GLOBL PTBCNT
  .ENDC							
 .ENDC							
.ENDC
.IF NE BF+TIME$R					
.GLOBL	GTM.HI,	GTM.LO					
.ENDC							
.IF NE MMG$T						
.GLOBL	$XMSIZ,V.MMU,$SYSCH				
.ENDC							
.IF NE MPT$Y						
.GLOBL	V.MPTY						
.ENDC							
.IF NE TIM$IT
.GLOBL	$TIMIT
.ENDC
.IF NE <VT11$!VS60$> & ^CMTT$Y				
.GLOBL	SCLNK2
.ENDC
.IF NE ERL$G
.GLOBL	$ELPTR, $ERLOG
.IF NE MPT$Y
.GLOBL	PARTBL
.ENDC
.ENDC
PERM	= 2000		
ENDBLK	= 4000		
PARMS	= 40		
JSW	= 44		
USERTOP = 50		
SYSPTR	= 54		
SR	= 177570	
PS	= 177776	
PR0	= 0
PR4	= 200
PR7	= 340
CONFIG	= 300	
CONFG2	= 370	
SYUNIT	= 274	
STATWD	= 366	
	IFEKO$	= 2000
	IFCTNU	= 20
V.TR4	=	4		
V.TR10	=	10		
V.BPT	=	14		
V.IOT	=	20		
V.PWFL	=	24		
V.EMT	=	30		
V.TRAP	=	34		
V.FPU	=	244		
.IF NE MMG$T
KISAR0	= 172340		
KISAR6	= 172354		
KISDR0	= 172300		
UISAR0	= 177640		
UISDR0	= 177600		
SR0	= 177572		
PMODE	= 30000			
CMODE	= 140000		
LOWCOR	= 1600			
.ENDC							

	.ASECT
	. = 0
	240		
	BR	BOOT1	
.IF NDF	$DXSYS!$FLSYS!$FDSYS
	. = 40		
BOOT1:	JMP	BOOT	
.ENDC
.IF DF $FLSYS!$FDSYS
	.=34
DCMD:
CMD2:	BIT #SMSCMW,(R4)	;READY FOR COMMAND?
	BEQ CMD2
	MOV R5,SMSCMD
	RTS PC

.IIF GT .-52,.ERROR

.=104
GETST:	BIT #SMSXFW,(R4)
	BEQ GETST
	BR 2$
. = 120
2$:	TST (R4)		;ERROR?
	BLT 1$			;YUP
	TST SMSDBF		;FLUSH STATUS BYTE
	RTS PC

1$:	TST SMSDBF		;FLUSH STATUS BYTE
	TST (SP)+		;FLUSH RETURN ADDRESS
	JMP BIOERR

READS:	MOV #SMSCSR,R4		;COMMONLY USED POINTER
	MOV #SMSSEK,R5		;SEEK COMMAND
	JSR PC,DCMD		;FIRST WORD
	BR 1$
.IIF GT .-166, .ERROR
. = 200
1$:	MOV R0,R5		;TRACK ADDRESS
	JSR PC,CMD2		;SECOND COMMAND BYTE
	JSR PC,GETST		;GET STATUS OF OP
	MOV #SMSRED!SMSDTB,R5	;READ COMMAND
	JSR PC,DCMD
	MOV R3,R5		;SECTOR ADDRESS
	JSR PC,CMD2
	JSR PC,GETST
	MOV #SMSRED!SMSBTH,R5	;READ FROM BUFFER
	JMP READS2

BOOT1:	JMP BOOT
.ENDC

.IIF NDF $DXSYS,	.NLIST
.IF DF $DXSYS
	CSGO=	1	
	CSEBUF=	2	
	CSRD=	6	
	CSUNIT=	20	
	CSDONE=	40	
	CSTR=	200	
	CSERR=	100000	
.IIF NDF DX$CSR, DX$CSR == 177170
	. = 14		
	.WORD	READS	
	.WORD	340	
	.WORD	WAIT	
	.WORD	340	
	. = 34		
BOOT1:	MOVB	UNITRD(R0),RDCMD;SET READ FUNCTION FOR CORRECT UNIT
RETRY:	MOV	@PC,SP	
	MOV	#200,R2	
	CLR	R0	
	BR	2$	
	. = 70		
2$:	MOV	SP,R1	
	INC	R0	
	BR	3$	
	. = 104		
3$:	MOV	@PC,R3	
	BPT		
	BR	BOOT2	
UNITRD:	.BYTE	CSGO+CSRD
	.BYTE	CSGO+CSRD+CSUNIT;READ FROM UNIT 1
	. = 120		
READS:	MOV	#DX$CSR,R4
	MOV	R4,R5	
	MOV	(PC)+,(R5)+
RDCMD:	.WORD	0	
	IOT		
	MOV	R3,@R5	
	IOT		
	MOV	R0,@R5	
	IOT		
	MOV	#CSGO+CSEBUF,@R4;LOAD EMPTY BUFFER FUNCTION INTO RXCS
BROFFS	=	READF-.	
RDX:	IOT		
	TSTB	@R4	
	BPL	RTIRET	
	MOVB	@R5,(R2)+
	DEC	R1	
	BGT	RDX	
	CLR	R2	
	BR	RDX	
WAIT:	TST	@R4	
	BEQ	WAIT	
	BMI	RETRY	
RTIRET:	RTI		
	. = 200		
BOOT2:	CMPB	(R3)+,(R3)+
	BPT		
	CMPB	(R3)+,(R3)+
	BPT		
	BIT	#CSUNIT,RDCMD
	BNE	1$	
	CLR	R0	
1$:	MOV	R0,(PC)+
BTUNIT:	.WORD	0	
	MOV	#TRWAIT,@#20
	JMP	BOOT	
.DSABL LSB
.ENDC
.IIF NDF $DXSYS,	.LIST

.MACRO	SYSDEV	NAME,VECTOR
.GLOBL	NAME'INT	
SYNAME	= 0
.IRPC	X,<NAME>
SYNAME	= <SYNAME+''X-100>*50
.ENDR
SYVEC	= VECTOR	
. = SYVEC		
	.WORD	NAME'INT,340
. = 522			
SYBITO	= VECTOR / 20	
SYBITS	= ^B11000000	
.REPT	<VECTOR & 17> / 4
SYBITS	= SYBITS / 4	
.ENDR
.ENDM	SYSDEV
. = 500
	.WORD	BOOTIN	
. = 510
	.WORD	10
.IF EQ BF
	.ASCIZ	/@STARTS/
.IFF
.IF EQ MMG$T
	.ASCIZ	/@STARTF/
.IFF
	.ASCIZ	/@STARTX/
.ENDC
.ENDC

.IIF NDF $DYSYS,		.NLIST			
.IF DF $DYSYS
.IIF EQ DYT$O,	.NLIST	
	.MACRO	MULTDEV	NAME,VECTOR
	SYVEC2	= VECTOR
	.=VECTOR
	.WORD	NAME'INT,340
	.= 522
	BITO.2	= VECTOR/20
	BITS.2	= ^B11000000
	.REPT	<VECTOR& 17> /4
	BITS.2	= BITS.2/4
	.ENDR
	.ENDM	MULTDEV
.IIF NE DYT$O,	MULTDEV	DY,DY$CV2
.IIF EQ DYT$O,	.LIST
.IIF NDF DY$CSR, DY$CSR == 177164
.IIF NDF DY$VEC, DY$VEC == 264
	SYSDEV	DY,DY$VEC
CSGO	=	1	
CSEBUF	=	2	
CSRD	=	6	
CSUNIT	=	20	
CSDONE	=	40	
CSTR	=	200	
CSDN	=	400	
CSERR	=	100000	
DNERR	=	20	
DBDN	=	40	
.=120
UNTRED:	.WORD	CSGO+CSRD+CSDN	
	.WORD	CSGO+CSRD+CSDN+CSUNIT
.ENABL LSB
READ:	MOV	BTUNIT,R3
	ASL	R3	
	MOV	UNTRED(R3),REDCMD
	BR	LSN	
NXT..=.
.=400
LSN:	ASL	R0	
1$:	MOV	#DY$CSR,R5
	MOV	(PC)+,(R5)+
REDCMD:	.WORD	0	
	MOV	R0,-(SP)
	MOV	R0,R3	
	MOV	R0,R4	
	CLR	R0	
	BR	3$	
2$:	SUB	#23.,R3	
3$:	INC	R0	
	SUB	#26.,R4	
	BPL	2$	
	CMP	#-14.,R4
	ROL	R3	
	BR	CONRD
..NXT=.
.=522
CONRD:
4$:	SUB	#26.,R3	
	BPL	4$	
	ADD	#27.,R3	
	MOV	#DY$CSR,R4
	JSR	PC,WAIT	
	MOV	R3,@R5	
	JSR	PC,WAIT	
	MOV	R0,@R5	
5$:	BIT	#CSDONE,@R4
	BEQ	5$	
	TST	@R4	
	BPL	14$	
	JMP	RETRY	
14$:	MOV	(PC)+,(R4)
EMTCMD:	.WORD	CSEBUF+CSGO+CSDN;STORE EMTY BUFFER COMMAND HERE
	MOV	#64.,R3	
	BIT	#CSDN,EMTCMD
	BEQ	6$	
	ASL	R3	
6$:	CMP	R1,R3	
	BHIS	7$	
	MOV	R1,R3	
7$:	JSR	PC,WAIT	
	MOV	R3,@R5	
	JSR	PC,WAIT	
	MOV	R2,@R5	
10$:	BIT	#CSDONE,@R4
	BEQ	10$	
	TST	@R4	
	BPL	15$	
	JMP	RETRY	
15$:	MOV	(SP)+,R0
	SUB	R3,R1	
	BGT	11$	
	RTS	PC	
11$:	ADD	R3,R2	
	ADD	R3,R2	
	INC	R0	
	JMP	1$	
N..XT=.
.=..NXT
WAIT:	BITB	#CSTR!CSDONE,@R4;TRANSFER OR DONE?
	BMI	RTNINT	
	BEQ	WAIT	
	BR	RETRY	
RTNINT:	RTS	PC	
.=NXT..
RETRY:	MOV	(SP)+,R0
	BITB	#DNERR,@R5
	BNE	16$	
	JMP	BIOERR	
16$:	BIC	#CSDN,REDCMD
	BIC	#CSDN,EMTCMD
	ASL	R0	
	BR	1$	
BIOERR:	JSR	R0,REPORT	
	.ASCIZ	<15><12>\?BOOT-F-I/O error\<15><12><12>
	.EVEN
.=N..XT
.ENDC
.IIF NDF $DYSYS,		.LIST			

.IIF NDF $DSSYS,	.NLIST
.IF DF	$DSSYS
.IIF NDF DS$CSR, DS$CSR == 172040
.IIF NDF DS$VEC, DS$VEC == 204
	SYSDEV	DS,DS$VEC
RSCS2	= DS$CSR+10
READ:	MOV	R0,R4	
	MOV	#RSCS2,R5
	MOV	(R5),-(SP)
	MOV	#40,@R5	
	BIT	#2,16(R5)
	BNE	1$	
	ASL	R4	
1$:	ASL	R4	
	BIC	#^C7,(SP)
	MOV	(SP)+,(R5)
	MOV	R4,-(R5)
	MOV	R2,-(R5)
	MOV	R1,-(R5)
	NEG	@R5
	MOV	#71,-(R5)
2$:	BIT	#100200,@R5
	BEQ	2$
	BMI	BIOERR	
	RTS	PC
.ENDC
.IIF NDF $DSSYS,	.LIST

.IIF NDF $DPSYS,	.NLIST
.IF DF	$DPSYS		
.IIF NDF DP$CSR, DP$CSR == 176710
.IIF NDF DP$VEC, DP$VEC == 254
	SYSDEV	DP,DP$VEC
RPCS=	DP$CSR+4		
RPDA=	DP$CSR+14		
CS.GO=	000001			
CS.RD=	000004			
CS.DRV=	003400			
DS.ATT=	000377			
READ:	MOV	R0,R3	
	JSR	R2,DIV	
	.WORD	10.	
	MOV	R4,-(SP)
	MOV	R5,R3	
	JSR	R2,DIV	
	.WORD	20.	
	SWAB	R4	
	BIS	(SP)+,R4
	MOV	#RPDA,R3
	MOV	R4,@R3	
	MOV	R5,-(R3)
	MOV	R2,-(R3)
	MOV	R1,-(R3)
	NEG	@R3	
	BIC	#^C<CS.DRV>,-(R3)
	BIS	#CS.RD+CS.GO,@R3
1$:	TSTB	@R3	
	BPL	1$
	TST	@R3	
	BMI	BIOERR	
	MOVB	#DS.ATT,@#DP$CSR
	CLRB	@#DP$CSR
	RTS	PC	
DIV:	CLR	R5	
	CLR	R4	
	TST	R3	
	BEQ	4$	
	COM	R5	
1$:	ROL	R3	
	BCC	1$
2$:	ROL	R4	
	CMP	R4,@R2
	BLO	3$
	SUB	@R2,R4
3$:	ROL	R5
	ASL	R3
	BNE	2$
	COM	R5	
4$:	TST	(R2)+
	RTS	R2
.ENDC
.IIF NDF $DPSYS,	.LIST

.IIF NDF $RKSYS&$RFSYS,	.NLIST
.IF DF	$RKSYS!$RFSYS
.IF DF	$RFSYS
.IIF NDF RF$CSR, RF$CSR == 177460
.IIF NDF RF$VEC, RF$VEC == 204
	SYSDEV	RF,RF$VEC
RFDA	= RF$CSR+6	
READ:	MOV	#RFDA,R3
	MOV	R0,R5	
	SWAB	R5	
	MOV	R5,R4	
	CLRB	R5	
	MOV	R5,(R3)+
	BIC	#177740,R4
	MOV	R4,(R3)	
	TST	-(R3)	
.ENDC
.IF DF	$RKSYS
.IIF NDF RK$CSR, RK$CSR == 177400
.IIF NDF RK$VEC, RK$VEC == 220
	SYSDEV	RK,RK$VEC
RKDA	= RK$CSR+12	
READ:	MOV	#14,R3	
	BR	2$	
1$:	ADD	#20,R3	
2$:	SUB	#14,R0
	BPL	1$
	ADD	R3,R0	
5$:	MOV	#RKDA,R3
	BIC	#17777,@R3
	BIS	R0,(R3)	
.ENDC
	MOV	R2,-(R3)
	MOV	R1,-(R3)
	NEG	(R3)	
	MOV	#5,-(R3)
3$:	TSTB	(R3)	
	BPL	3$
	TST	(R3)	
	BMI	BIOERR	
	RTS	PC
.ENDC
.IIF NDF $RKSYS&$RFSYS,	.LIST

.IIF NDF $DTSYS,	.NLIST
.IF DF $DTSYS
.IIF NDF DT$CSR, DT$CSR == 177340
.IIF NDF DT$VEC, DT$VEC == 214
	SYSDEV	DT,DT$VEC
TCCM	= DT$CSR+2	
TCDT	= DT$CSR+10	
READ:	MOV	#TCCM,R4
	MOV	#TCDT,R3
1$:	MOV	R0,R5	
	SUB	#2,R5	
	MOV	#4003,@R4
2$:	BIT	#100200,@R4
	BEQ	2$
	BMI	6$
	CMP	R5,@R3	
	BLT	1$	
3$:	MOV	#3,@R4	
4$:	BIT	#100200,@R4
	BEQ	4$
	BMI	6$
	CMP	R0,@R3	
	BGT	3$	
	BLT	1$	
	MOV	R2,-(R3)
	NEG	R1
	MOV	R1,-(R3)
	MOV	#5,@R4	
5$:	BIT	#100200,@R4
	BEQ	5$
	BMI	BIOERR	
	CLR	@R4	
	RTS	PC
6$:	TST	@#DT$CSR
	BPL	BIOERR	
	BIT	#4000,@R4
	BNE	3$	
	BR	1$	
.ENDC
.IIF NDF $DTSYS,	.LIST

.IIF NDF $DMSYS,	.NLIST
.IF DF	$DMSYS
.IIF NDF DM$CSR, DM$CSR == 177440
.IIF NDF DM$VEC, DM$VEC == 210
	SYSDEV	DM,DM$VEC	
	RKCS2=	DM$CSR+10
	RKDS=	DM$CSR+12
	RKDC=	DM$CSR+20
READ:	MOV	#<3.*22.>,R3
	MOV	#-1,R4	
	MOV	R4,R5	
1$:	INC	R4	
	SUB	R3,R0	
	BCC	1$	
	ADD	R3,R0	
	MOV	#22.,R3	
2$:	INC	R5	
	SUB	R3,R0	
	BCC	2$	
	ADD	R3,R0	
	SWAB	R5	
	BIS	R5,R0	
	MOV	#RKCS2,R5
	BIT	#400,RKDS-RKCS2(R5)
	BEQ	3$	
	BIS	#2000,RDFCN
3$:	MOV	R4,RKDC-RKCS2(R5)
	MOV	R0,-(R5)
	MOV	R2,-(R5)
	MOV	R1,-(R5)
	NEG	@R5	
	MOV	(PC)+,-(R5)
RDFCN:	.WORD	21	
1$:	BIT	#100200,@R5
	BEQ	1$	
	BMI	BIOERR	
	RTS	PC	
.ENDC		
.IIF NDF $DMSYS,	.LIST

.IIF NDF $DLSYS,	.NLIST				
.IF DF 	$DLSYS
.IIF NDF DL$CSR, DL$CSR == 174400
.IIF NDF DL$VEC, DL$VEC == 330
	SYSDEV	DL,DL$VEC
RLCS=	DL$CSR		
RLBA=	2		
RLDA=	4		
RLMP=	6		
READ:
	CLR	R4	
	MOV	#20.,R3	
2$:	CMP	R0,R3	
	BLT	3$	
	ADD	#100,R4	
	SUB	R3,R0	
	BGT	2$	
3$:	ASL	R0	
	BIS	R4,R0	
	MOV	#RLCS,R5
	MOV	(R5),DLCS
	BIC	#^C<1400>,DLCS
	CMP	#4,R0	
	BEQ	10$	
	JSR	PC,DLSEEK
10$:	NEG	R1	
	MOV	R2,RLBA(R5)
DLREAD:	MOV	R1,RLMP(R5)
	MOV	R0,RLDA(R5)
	MOV	#14,R3	
	JSR	PC,DLXCT
	BMI	DLERR	
	RTS	PC	
DLERR:	MOV	RLDA(R5),R3
	BIC	#177700,R3
	CMP	#50,R3	
	BNE	BIOERR	
	JSR	PC,DLNEXT
	BR	DLREAD	
DLXCT:	BIS	(PC)+,R3
DLCS:	.WORD	0	
	MOV	R3,(R5)	
100$:	TSTB	(R5)	
	BPL	100$	
	TST	(R5)	
	RTS	PC	
.ENDC		
.IIF NDF $DLSYS,	.LIST				

.IIF NDF $DXSYS,	.NLIST
.IF DF	$DXSYS		
.IIF EQ DXT$O,	.NLIST	
	.MACRO	MULTDEV	NAME,VECTOR
	SYVEC2	= VECTOR
	. = VECTOR	
	.WORD	NAME'INT,340
	. = 522		
	BITO.2	= VECTOR / 20
	BITS.2	= ^B11000000
	.REPT	<VECTOR & 17> / 4
	BITS.2	= BITS.2 / 4
	.ENDR
	.ENDM	MULTDEV
.IIF NE DXT$O,	MULTDEV	DX,DX$CV2
.IIF EQ DXT$O,	.LIST					
.IIF NDF DX$VEC, DX$VEC == 264
	SYSDEV	DX,DX$VEC
READ:	ASL	R0	
	ASL	R0	
	ASL	R1	
1$:	MOV	R0,-(SP)
	MOV	R0,R3	
	MOV	R0,R4
	CLR	R0	
	BR	3$	
2$:	SUB	#23.,R3	
3$:	INC	R0	
	SUB	#26.,R4	
	BPL	2$	
	CMP	#-14.,R4
	ROL	R3	
4$:	SUB	#26.,R3	
	BPL	4$	
	ADD	#27.,R3	
	BPT		
	MOV	(SP)+,R0
	INC	R0	
	TST	R1	
	BGT	1$	
	RTS	PC	
READF:	TST	@R4	
	BEQ	READF	
	BMI	BIOERR	
	TSTB	@R4	
	BPL	READFX	
	MOVB	@R5,(R2)+
	DEC	R1	
	BGT	READF	
	MOV	#1,R2	
			
	BR	READF	
READFX:	RTI
TRWAIT:	TST	@R4	
	BEQ	TRWAIT	
	BPL	READFX	
.ENDC
.IIF NDF $DXSYS,	.LIST
.IF NDF $DYSYS					
BIOERR:	JSR	R0,REPORT
	.ASCIZ	<15><12>\?BOOT-F-I/O error\<15><12><12>
	.EVEN
.ENDC						

.IIF NDF $FLSYS&$FDSYS,	.NLIST

.IF DF $FLSYS!$FDSYS

	.SBTTL	BOOTSTRAP I/O DRIVER - FLOPPY
SMSCSR=177200	;CSR
SMSDBF=SMSCSR+2	;DATA BUFFER
SMSCMD=SMSDBF+2	;COMMAND BUFFER

SMSSEK=1	;SEEK COMMAND
SMSRED=2	;READ
 SMSDTB=100	;DISK TO BUFFER
 SMSBTH=200	;BUFFER TO HOST

SMSCLE=2000	;CLOCK ENABLE
SMSCMW=20	;COMMAND WAIT
SMSXFW=1	;XFER WAIT

.IF DF $FLSYS
	SYSDEV	FL,270		;FLOPPY VECTORS THROUGH 264

READ:	ASL	R0		;CONVERT BLOCK TO LOGICAL SECTOR
	ASL	R0		;LSN=BLOCK*4
	ASL	R1		;MAKE WORD COUNT BYTE COUNT
1$:	MOV	R0,-(SP)	;SAVE LSN FOR LATER
	MOV	R0,R3		;WE NEED 2 COPIES OF LSN FOR MAPPER
	MOV	R0,R4
	CLR	R0		;INIT FOR TRACK QUOTIENT
	BR	3$		;JUMP INTO DIVIDE LOOP
2$:	SUB	#23.,R3		;PERFORM MAGIC TRACK DISPLACEMENT
3$:	INC	R0		;BUMP QUOTIENT, STARTS AT TRACK 1
	SUB	#26.,R4		;TRACK=INTEGER(LSN/26)
	BPL	2$		;LOOP - R4=REM(LSN/26)-26
	CMP	#-14.,R4	;SET C IF SECTOR MAPS TO 1-13
	ROL	R3		;PERFORM 2:1 INTERLEAVE
4$:	SUB	#26.,R3		;ADJUST SECTOR INTO RANGE -1,-26
	BPL	4$		;(DIVIDE FOR REMAINDER ONLY)
	ADD	#27.,R3		;NOW PUT SECTOR INTO RANGE 1-26
.ENDC
.IF DF $FDSYS
	SYSDEV	FD,270		;FLOPPY VECTORS THROUGH 264

READ:	ASL	R1		;MAKE WORD COUNT BYTE COUNT
1$:	MOV R0,-(SP)
	MOV R0,R3		;COPY BLOCK NUMBER
	ASR R0
	ASR R0
	ASR R0
	ASR R0			;TURNS R0 INTO A TRACK ADDRESS
	INC R0			;TRACKS START AT ONE
	BIC #177760,R3
	MOVB FDSECM(R3),R3	;MAP SECTOR NUMBER
.ENDC
	JSR PC,READS		;CALL READS SUBROUTINE
	MOV	(SP)+,R0	;GET THE LSN AGAIN
	INC	R0		;SET UP FOR NEXT LSN
	TST	R1		;WHATS LEFT IN THE WORD COUNT
	BGT	1$		;BRANCH TO TRANSFER ANOTHER SECTOR
	RTS	PC		;RETURN
.IIF DF $FDSYS,FDSECM:	.BYTE 1,5,9.,13.,2,6,10.,14.,3,7,11.,15.,4,8.,12.,16.

;THE START OF THE READS ROUTINE IS IN LOW MEMORY, TO SAVE SPACE

READS2:	JSR PC,DCMD
	JSR PC,CMD2		;SECOND BYTE IGNORED
1$:	BIT #SMSXFW,(R4)
	BEQ 1$
.IIF DF $FDSYS,	MOV #512.,R5
.IIF DF $FLSYS,	MOV #128.,R5
2$:	MOVB SMSDBF,(R2)+
	DEC R5			;OUR COUNT
	BEQ 3$			;SECTOR EXHAUSTED
	DEC R1			;CALLER'S COUNT
	BGT 2$			;HE ISN'T TIRED EITHER
	BR 4$
3$:	DEC R1
4$:	BIT #SMSXFW,(R4)	;READY TO XFER?
	BEQ 4$
	TST SMSDBF		;CALLER DOESN'T WANT IT, EMPTY IT OUT
	DEC R5
	BGE 4$			;THSI WILL FLUSH STATUS BYTE ALSO
	RTS PC
.ENDC

.IIF NDF $FLSYS&$FDSYS,	.LIST


REPOR1:	MOVB	(R0)+,@#TPB
REPORT:	TSTB	@#TPS	
	BPL	REPORT	
	TSTB	@R0	
	BNE	REPOR1	
	RESET		
	HALT
	BR	.-2	
BOOT:
.IF DF $DXSYS
	MOV	(PC)+,@(PC)+
	.WORD	167
	.WORD	RDX
	MOV	(PC)+,@(PC)+
	.WORD	READF-RDX-4
	.WORD	RDX+2
.ENDC
.IF DF $DYSYS						
	MOV	R0,(PC)+
BTUNIT:	.WORD	0	
101$:	BIT	#CSDONE,@#DY$CSR
	BEQ	101$	
.ENDC							
	MOV	#10000,SP
	MOV	#2,R0	
	MOV	#<BOOTSZ-1>*400,R1
	MOV	#1000,R2
	JSR	PC,READ
.IIF GT	.-1000,	.ERROR	
BOOTIN:	MOV	#V.TR4,R3
	MOV	@R3,R5	
.ENABL	LSB
.IF EQ MMG$T						
	MOV	#NXM,@R3
BHALT::	BR	1$	
			
			
	MOV	@#SR,R2	
	BIC	#3777,R2
	CMP	R2,#160000
	BLO	NXM	
1$:	CLR	R2	
2$:	ADD	#4000,R2
	CMP	R2,#170000
	BEQ	NXM	
	TST	@R2	
	BR	2$	
NXM:	MOV	R2,-(SP)
.IF NE BF		
	CMP	R2,#160000
	BLOS	3$	
	MOV	R2,MSV11
.ENDC
.ENDC							
3$:	MOV	#BCLR,@R3
	MOV	@R3,@#V.TR10

	MOV	#TSLIST,R1
	MOV	R1,R0
	TST	@#PS	
	BIC	(R1)+,-(R0)
	BNE	5$	
	TST	@#LKCS	
5$:	BIS	(R1)+,@R0
	TST	@#LKCS	
	BIS	(R1)+,@R0
.DSABL	LSB
.IF NE VT11$!VS60$
	TST	@#GT40	
	BIS	(R1)+,@R0
.ENDC
.IF EQ KW11$P
	TST	@#172540
	BIS	(R1)+,@R0
.ENDC
.IF NE MMG$T						
	TST	@#SR0	
	BIS	(R1)+,@R0
	BIT	#KT11$,@R0
	BNE	7$	
	JSR	R0,REPORT
	.ASCIZ	<15><12>\?BOOT-F-No memory management hardware\<15><12><12>
	.EVEN
7$:
.ENDC
	CFCC		
	BIS	(R1)+,@R0
	TST	-(R0)	
	CLR	@#172032
	BIS	(R1)+,@R0
	CLR	@#177746
	BIS	(R1)+,@R0
	TST	@#177760
	BIS	(R1)+,@R0
	MOV	#3,R3	
	MUL	R3,R3	
	BIS	(R1)+,@R0
	CMP	#9.,R3	
	BEQ	20$	
	BIC	-2(R1),@R0
20$:	TST	@#177570
	BIS	(R1)+,@R0
	CLR	@#177570
	BIS	(R1)+,@R0
	BIT	#LSI11$,BCNFG
	BEQ	25$	
	TST	(R1)+	
	BR	30$
25$:	MOV	R0,-(SP)
	.WORD	76600,100
	MOV	(SP)+,R0
	BIS	(R1)+,@R0

30$:
.IF NE MPT$Y
	MOV	#BSEC,@#V.TR4
.IF NE ERL$G
	MOV	#PARTB,R2
.IFTF
	MOV	#172076,R3
35$:	ADD	#2,R3	
	MOV	#1,@R3	
	BCS	40$	
.IFT
	MOV	R3,(R2)+
.IFTF
	BIS	@R1,@R0	
40$:	CMP	R3,#172136
	BLO	35$	
.ENDC
.ENDC
	MOV	R5,@#V.TR4
	MOV	R5,@#V.TR10
.IF NE MMG$T						
.ENABL	LSB
	MOV	#PMODE+PR7,@#PS
	MOV	#KISAR0,R0
	MOV	#KISDR0,R1
	MOV	#UISAR0,R2
	MOV	#UISDR0,R3
	MOV	#8.,R4	
	CLR	R5	
10$:	MOV	R5,(R0)+
	MOV	R5,(R2)+
	MOV	#77406,@R1
	MOV	(R1)+,(R3)+
	ADD	#200,R5	
	DEC	R4	
	BNE	10$	
	MOV	#177600,-(R0)
	MOV	@R0,-(R2)
	INC	SR0	
	MOV	@#V.TR4,R5
	MOV	#BSEC,@#V.TR4
	MOV	#400,@#KISAR6
	MOV	#400,R2	
20$:	MOV	#140000,R0
	CLC		
	MOV	#0,776(R0)
	BCS	30$	
25$:	MOV	#0,(R0)+
	CMP	#144000,R0
	BHI	25$	
	ADD	#40,R2	
	ADD	#40,@#KISAR6
.IF DF MEX$T
	CMP	@#KISAR6,#170000
.IFF
	CMP	@#KISAR6,#7600
.ENDC
	BLO	20$	
30$:	MOV	#1400,@#KISAR6
	MOV	R2,R0	
	SUB	#1600,R0
	BGE	35$	
	CLR	R0	
35$:	MOV	R0,(PC)+
MEMSIZ:	.WORD	0	
	MOV	R5,@#V.TR4
	CMP	R2,#LOWCOR
	BLO	4$	
	MOV	#LOWCOR,R2
4$:	ASL	R2	
	ASL	R2
	ASL	R2
	ASL	R2
	ASL	R2
	ASL	R2
.IFF
	MOV	(SP)+,R2
.ENDC							
.DSABL	LSB

	.IIF NDF $DLSYS,	.NLIST			
	.IF DF $DLSYS
	BR	AROUND	
DLSEEK:	MOV	#10,R3	
	JSR	PC,DLXCT
	MOV	RLMP(R5),R3
	MOV	R0,R4	
	BIC	#77,R4	
	BIC	#177,R3	
	MOV	R4,-(SP)
	BIC	#^C<100>,(SP)
	ASR	(SP)	
	ASR	(SP)	
	BIC	#100,R4	
	SUB	R4,R3	
	BCC	200$	
	NEG	R3	
	BIS	#4,R3	
200$:	INC	R3	
	BIS	(SP)+,R3
	MOV	R3,RLDA(R5)
	MOV	#6,R3	
	JSR	PC,DLXCT
	BPL	300$	
	JMP	BIOERR	
300$:	MOV	R0,RLDA(R5)
	RTS	PC	
DLNEXT:	MOV	RLDA(R5),R3
	SUB	R0,R3	
	SWAB	R3	
	ASR	R3	
	ADD	R3,R1	
	MOV	RLDA(R5),R0
	ADD	#30,R0	
	JSR	PC,DLSEEK
	RTS	PC	
AROUND:
	.ENDC
.IIF NDF $DLSYS,	.LIST				

	SUB	$RMSIZ,R2
	BLO	TSMSG	
RTRY:	CMP	R2,BSZCK
	BLO	TOOSML	
	MOV	R2,-(SP)
	MOV	#1,R0	
DFND:	ASL	R0
	ADD	#4,R0	
	MOV	#1000,R1
	MOV	#BUFFB,R2
	JSR	PC,READ	
	MOV	#BUFFB+10,R1
	MOV	(R1)+,R0
MONF:	MOV	R1,R2	
	BIT	#PERM,(R1)+
	BEQ	1$	
	CMP	#^RSYS,4(R1)
	BNE	1$	
	CMP	#<MONDV+RAD50M>,(R1)
	BNE	3$	
	CMP	#<RAD50N+MONTYP>,2(R1)
	BNE	3$	
	MOV	R0,MONBLK
	TST	SWPBLK	
	BNE	MONFND	
	BR	1$	
3$:	CMP	#^RSWA,(R1)+
	BNE	1$	
	CMP	#^RP  ,(R1)+
	BNE	1$	
	CMP	#SWAPSZ,2(R1)
	BHI	NOSWAP	
	MOV	R0,SWPBLK
	TST	MONBLK	
	BNE	MONFND	
			
1$:	BIT	#ENDBLK,(R2)
	BNE	2$	
	ADD	10(R2),R0
	ADD	#16,R2	
	ADD	BUFFB+6,R2
	MOV	R2,R1	
	BR	MONF
2$:	MOV	BUFFB+2,R0
	BNE	DFND	
	TST	MONBLK	
	BEQ	4$	
	JSR	R0,REPORT
	.ASCIZ	<15><12>/?BOOT-F-No swap file on volume/<15><12><12>
	.EVEN						
4$:	JSR	R0,REPORT
	.ASCIZ	<15><12>\?BOOT-F-No monitor file on volume\<15><12><12>
	.EVEN
TOOSML:
.IF EQ BF						
	TST	(PC)+	
TSMLF:	0
	BNE	TSMSG	
	INC	TSMLF	
	MOV	#1000,BSZCK
			
	BR	RTRY	
.ENDC
TSMSG:	JSR	R0,REPORT
	.ASCIZ	<15><12>\?BOOT-F-Insufficient memory\<15><12><12>
	.EVEN
NOSWAP:	JSR	R0,REPORT
	.ASCIZ	<15><12>/?BOOT-F-Swap file is too small/<15><12><12>
	.even
BSZCK:	10000		
LDLOC:	0		

MONFND:	MOV	@SP,R2	
	MOV	R2,LDLOC
	MOV	MONBLK,R0
.IF EQ BF						
	TST	TSMLF	
	BEQ	5$	
	ADD	#KMSIZE,R2
.ENDC
5$:	ADD	#BOOTSZ,R0
	MOV	$RMSIZ,R1
	ASR	R1	
.IF EQ BF						
	TST	TSMLF	
	BEQ	6$	
	SUB	#KMLEN,R1
	ADD	#KMONSZ,R0
.ENDC
6$:	JSR	PC,READ	
	MOV	#RELLST,R0
	MOV	(SP)+,R4
	SUB	#KMON,R4
	MOV	SWPBLK,$SWPBL(R4);R4 = BIAS. SET UP SWAP BLOCK #;SL18
	ADD	#<KMONSZ+BOOTSZ>,MONBLK			
	MOV	MONBLK,$MONBL(R4);SET USR BLOCK #	
.IF EQ BF						
	TST	TSMLF	
	BEQ	1$	
	CLR	KMLOC(R4)
			
	CLR	KMONIN(R4)
.ENDC							
1$:	ADD	R4,@(R0)+
	CMP	R0,#RELST2
	BLO	1$	
	MOV	(R0)+,R5
2$:	ADD	R4,R5	
	ADD	R4,@R5	
	MOV	(R0)+,R5
	BNE	2$
	MOV	LDLOC,-(SP)
	ADD	#LOCATE-USRBUF+KMSIZE,(SP)
.IF EQ BF
	TST	TSMLF	
	BEQ	82$	
	ADD	#LOCAT1-LOCATE,(SP)
.ENDC
82$:	JSR	PC,@(SP)+
			
.IF NE MMG$T		
	MOV	#V.BPT,R3
	MOV	-4(R0),R5
	ADD	R4,R5	
	MOV	(R5)+,(R3)+
	MOV	(R5)+,(R3)+
	MOV	(R5)+,(R3)+
	MOV	(R5)+,(R3)+
	MOV	(R5)+,10(R3)
	MOV	(R5)+,12(R3)
.ENDC							

.IF NE	MTT$Y						
.ENABL	LSB
	MOV	R0,-(SP)
	MOV	@#V.TR4,-(SP)
	MOV	#BSEC,@#V.TR4
	MOV	(PC)+,R0
PTCBLST:.WORD	TCBLST	
10$:	CMP	R0,(PC)+
PTCBEND:.WORD	TCBPEND	
	BHIS	20$	
	TST	(R0)+	
	BEQ	10$	
	ADD	R4,-2(R0)
	BR	10$	
20$:	MOV	(PC)+,R0
PTCBBLK:.WORD	DLTCB	
	CLR	R5	
30$:	INC	R5	
	CMP	R5,#TCBMAX
	BHI	80$	
	MOV	R0,R3	
	TST	T.CSR(R3)
	BEQ	70$	
	TST	@T.CSR(R3)
	BCC	35$	
	CLR	T.CSR(R3)
.IF	NE	DZ11$N
	BIT	#DZ11$,T.STAT(R0)
	BEQ	70$	
	MOV	T.VEC(R0),R3
	BIC	#^C3,R3	
	ASL	R3	
	ADD	(PC)+,R3
PDZCS3:	 .WORD	DZCSR	
	CLR	(R3)	
.ENDC
	BR	70$	
35$:	ADD	#T.IRNG,R3
	ADD	R4,(R3)+
	ADD	R4,(R3)+
	TST	(R3)+	
	ADD	R4,(R3)+
	ADD	R4,(R3)+
	ADD	#TTYIN,R3
	ADD	R4,(R3)+
	TST	(R3)+	
	ADD	R4,(R3)+
	ADD	R4,(R3)+
.IF NE DZ11$N
	TSTB	T.PUN(R0)
	BNE	55$	
.ENDC
	MOV	T.VEC(R0),R1
	MOV	R1,R3	
	ASR	R3	
	ASR	R3	
	ASR	R3	
	MOV	R3,R2	
	ASR	R3	
	MOV	#^B11110000,-(SP)
	ROR	R2	
	BCC	40$	
	COMB	(SP)	
40$:	ADD	@#SYSPTR,R3
	BISB	(SP)+,MAPOFF(R3)
.IF	NE	DZ11$N
	BIT	#DZ11$,T.STAT(R0)
	BEQ	60$	
	MOV	(PC)+,(R1)+
PDZIINT: .WORD	DZIINT	
	MOV	T.PRI(R0),(R1)+
	MOV	(PC)+,(R1)+
PDZOINT: .WORD	DZOINT	
	MOV	T.PRI(R0),(R1)+
	MOV	T.CSR(R0),R3
	MOV	#DZMSE$+DZRCV$+DZTIE$,(R3)
55$:	MOV	(R0),R1	
	BIC	#^C<LINSP$>,R1
	CMP	R1,#1000
	BNE	57$	
	BIS	#40,R1	
57$:	BISB	T.PUN(R0),R1
	BIS	#DZRCO$+DZLE$N,R1
			
	MOV	T.CSR(R0),R3
	MOV	R1,DZ.LPR(R3)
			
	BR	70$
.ENDC
60$:	MOV	@#V.TKB,(R1)+
	MOV	T.PRI(R0),(R1)+
	MOV	@#V.TPS,(R1)+
	MOV	T.PRI(R0),(R1)+
	MOV	#IENABL,@T.CSR(R0)
.IF NE DL11$M		
	BIT	#REMOT$,(R0)
			
	BEQ	61$	
	BIS	#DL.DIE,@T.CSR(R0)
61$:
.ENDC								
				
70$:
	ADD	#TCBSZ,R0
	BR	30$	
80$:	MOV	(SP)+,@#V.TR4
	MOV	(SP)+,R0
.DSABL	LSB
.ENDC							
.IF NE MMG$T						
	MOV	MEMSIZ,@(PC)+
XMPTR:	.WORD	$XMSIZ	
	MOV	#RMSTAK,R5
	ADD	R4,R5	
	MOV	R5,SP	
	MOV	#10000,-(SP)
	MTPI	SP
.ENDC							

	BIT	#LSI11$,BCNFG
	BEQ	4$	
	MOV	(R0)+,R5
3$:	ADD	R4,R5	
	MOV	(R0)+,@R5
	MOV	(R0)+,R5
	BNE	3$	
4$:	MOV	@#SYSPTR,R0
.IF DF	$RKSYS!$DXSYS!$DPSYS!$DSSYS!$DMSYS!$DLSYS!$DYSYS
.IF DF	$RKSYS		
	MOV	@#RKDA,R1
	ROL	R1
	ROL	R1
	ROL	R1
	ROL	R1
	BIC	#^C7,R1	
.ENDC			
.IF DF	$DSSYS		
	MOV	@#RSCS2,R1
	BIC	#^C7,R1	
.ENDC
.IF DF $DMSYS		
	MOV	@#RKCS2,R1
	BIC	#^C7,R1	
.ENDC
.IF DF	$DLSYS						
	MOV	@#RLCS,R1
	BIC	#^C1400,R1
	SWAB	R1	
.ENDC							
.IF DF	$DPSYS		
	MOV	@#RPCS,R1
	BIC	#^C<CS.DRV>,R1
	SWAB	R1	
.ENDC			
.IF DF	$DXSYS!$DYSYS		
	MOV	BTUNIT,R1
.ENDC			
	ADD	R1,DKASSG-$RMON(R0)
	ADD	R1,SYASSG-$RMON(R0)
	MOVB	R1,SYUNIT+1(R0)
.IF NE MMG$T					
	MOVB	R1,$SYSCH+11-$RMON(R0)
.ENDC			
.ENDC			
.ENABL	LSB
	BIS	BCNFG,CONFIG(R0)
	BIS	BCNFG2,CONFG2(R0)
.IF NE MPT$Y&ERL$G
	MOV	R0,R3	
	ADD	#PARTBL-$RMON,R3
	MOV	#PARTB,R1
	MOV	#16.,-(SP)
20$:	MOV	(R1)+,(R3)+
	DEC	(SP)	
	BNE	20$	
	TST	(SP)+	
.ENDC
.IF NE KW11$P
	BISB	#60,MAPOFF+4(R0)
.ENDC
.IF NE	BF+TIME$R					
	BIT	#CLK50$,CONFIG(R0)
	BEQ	2$	
	MOV	#101,@(PC)+
PG.HI:	GTM.HI
	MOV	#165400,@(PC)+
PG.LO:	GTM.LO
.ENDC							
2$:	TST	@#0	
	BNE	21$	
	MOV	#BTIME,R3
	MOV	(R3)+,$TIME-$RMON(R0)
	MOV	(R3)+,$TIME+2-$RMON(R0)
	MOV	@R3,$DATE-$RMON(R0)
21$:	CLR	R3
	MOV	(PC)+,(R3)+
	 BIC	R0,R0
.IF EQ MMG$T						
	MOV	(PC)+,(R3)+
	 .EXIT
.IFF							
	MOV	(PC)+,(R3)+
	 .ASTX
	BISB	#14,MAPOFF+12(R0)
.ENDC
	BIT	#LKCS$,BCNFG
	BEQ	10$	
	BIT	#CLOCK$,BCNFG
	BEQ	10$	
	MOV	#1,@#LKPB
	MOV	#LKSTAT,@#LKCS
10$:

	CLR	R3	
	MOV	#$ENTRY,R1
	ADD	R4,R1
4$:	TST	@R1	
	BEQ	5$	
	ADD	R4,@R1	
	CMP	$PNAME-$ENTRY(R1),#SYNAME
	BNE	5$	
	MOV	R3,SYINDO(R0)
	ADD	R3,SYINDO(R0)
.IF NE MMG$T						
	MOV	R3,-(SP)
	ADD	#40000,(SP)
	ASL	(SP)	
	MOV	(SP)+,$SYSCH-$RMON(R0)
			
	CLR	$SYSCH+2-$RMON(R0)
			
.ENDC			
	MOV	@R1,SYENTR-$RMON(R0)
5$:	INC	R3	
	TST	(R1)+	
	CMP	#-1,@R1	
	BNE	4$	
			
	BISB	#SYBITS,SYBITO+MAPOFF(R0)
.IF DF $DYSYS		
.IF NE DYT$O
	BISB	#BITS.2,BITO.2+MAPOFF(R0);PROTECT 2ND VECTOR
.ENDC
.ENDC							
.IF DF $DXSYS
.IF NE DXT$O		
	BISB	#BITS.2,BITO.2+MAPOFF(R0)
.ENDC							
.ENDC
	TST	(PC)+	
SILNT$:	.WORD	SILN$T	
	BNE	55$	
	.PRINT	#BSTRNG	
55$:	MOV	#$PNAME,R1
	ADD	R4,R1
	ADD	#$DVREC,R4
	MOV	#$SLOT,R3
	CLR	R0	
	.SETTOP		
6$:	MOV	(R1)+,FNAME
.IF NE MMG$T						
	ADD	#'X-100,FNAME
.ENDC							
	.LOOKUP	#LKAREA,#0,#BLOOK
	BCC	7$	
65$:	TST	$ENTRY-$DVREC(R4)
	BNE	70$	
	CMP	#^RBA,-2(R1)
	BEQ	70$	
.IF EQ BF						
	CMP	#^RTT,-2(R1)
	BNE	67$	
	.PRINT	#TTMSG	
	BR	70$	
.ENDC							
67$:	CLR	-2(R1)	
70$:	CLR	(R4)+	
	.CLOSE	#0	
	BR	8$
7$:	.READW	#LKAREA,#0,#BUFFB,#256.,#0
	BCS	65$	
	CMPB	BUFFB+60,#SYSOP$
	BNE	65$	
	MOV	BUFFB+56,$STAT-$DVREC(R4)
	.SAVEST	#LKAREA,#0,#CBLOK
	MOV	CBLOK+2,@R4
	INC	(R4)+	
8$:	DEC	R3
	BNE	6$
.IF NE STAR$T
	MOV	#104000,@#JSW
.IFF
	MOV	#100000,@#JSW
.ENDC
	MOV	@#SYSPTR,R3
.IF NE CONT$N
	BIS	#IFCTNU+IFEKO$,STATWD(R3);SET CONTINUE @FILE BIT
			
.ENDC							
	TST	SILNT$	
	BEQ	9$	
	BIS	#IFEKO$,STATWD(R3)
9$:
.IF NE	MTT$Y						
.IF NE	DZ11$M+MTI$M
	MOV	@#SYSPTR,R3
TOUT$::							
.IF NE	DZ11$M
	JSR	PC,DZMCTL-$RMON(R3)
.ENDC
.IF NE	MTI$M
	JSR	PC,DLMPOL-$RMON(R3)
.ENDC
.ENDC
.ENDC							
	CLR	R0	
	.EXIT
.DSABL	LSB
BCLR:	CLR	@R1	
	RTI		
.IF NE MMG$T+MPT$Y+MTT$Y				
BSEC:	BIS	#1,2(SP)
	RTI						
.ENDC							

RELLST:	V.TR4		
	V.TR10						
.IF NE PWF$L						
	V.PWFL		
.ENDC							
	V.EMT		
	SYSPTR		
	V.TKB		
	V.TPS
	LKVEC		
.IF NE MPT$Y						
	V.MPTY		
.ENDC							
.IF NE MMG$T
	V.BPT		
	V.IOT		
	V.TRAP		
	V.MMU		
	XMPTR		
.ENDC							
	SYVEC		
.IF DF $DXSYS
.IF NE DXT$O		
	SYVEC2
.ENDC							
.ENDC
.IF DF $DYSYS		
.IF NE DYT$O
	SYVEC2		
.ENDC
.ENDC							
	V.FPU		
.IF NE	BF+TIME$R					
	PG.HI		
	PG.LO		
.ENDC							
.IF NE MTT$Y						
	PTCBLST
	PTCBEND
	PTCBBLK
.IF NE DZ11$N
	PDZIINT		
	PDZOINT		
	PDZCS3		
.ENDC
.ENDC							
RELST2:	USRLOC		
	$USRLC		
	QCOMP		
	$KMLOC		
.IF NE	BF!<MTT$Y-1>					
	TTIBUF		
	TTIBUF+2
	TTIBUF+6
	TTIBUF+10
	TTOBUF		
	TTOBUF+4
	TTOBUF+6
.ENDC							
.IF NE HSR$B						
	PHSRRT
	PHSRRB
	HSRB
	HSRB+2
	HSRB+4
	HSRB+10
.ENDC							
	SYSLOW		
	CORPTR+2	
	$INPTR		
	SYNCH		
	$FKPTR		
.IF NE ERL$G
	$ELPTR		
.ENDC
 .IF NE ESC$P						
	ESCPTG
	ESCPTO
	ESCPTI
	ESCPTT
	PESCRT
 .ENDC							
.IF NE TIM$IT
	$TIMIT		
.ENDC
.IF NE <VT11$!VS60$> & ^CMTT$Y
			
	SCLNK2
.ENDC
.IF NE BF
	MSGENT		
	IMPLOC						
	TTIUSR
	TTOUSR
	FUDGE1
	FUDGE2
	BKGND1
	BKGND2
	BKGND3
	CNTXT
	BCNTXT
.IF EQ MMG$T						
	RMONSPîî.IFF
	BGFPPT
	BGWPTR						
							
	TRPLST+4					
	TRPLST+10
	TRPLST+14
	$RLPTR
	$MPPTR
	$GTBYT
	$PTBYT
	$PTWRD
.ENDC							
	SWIPTR
	SWOPTR
	.$CRTN
.IFTF							
.IF NE MTT$Y						
 .IF NE BF						
	BKCNSL
 .ENDC							
	PDLTBL		
.IF NE DL11$M
	DLCRP1		
	DLCRP2		
.ENDC
.IF NE MTI$M						
	PDLCS1		
	DLTMCP		
.ENDC							
.IF GT DZ11$N-1
	PDZTB1		
	PDZCS1		
.ENDC
.IF NE DZ11$N
	PBMSK1		
	PBMSK2		
	PBMSK3		
.IF NE DZ11$M
	PBMSK4		
	PBMSK5		
	PDZCS2		
	PDZTB2		
	DZTMCP		
.ENDC
.ENDC
.ENDC							
.IFF
	TRAPLC
	TRAPER		
	FPPADD
	FPPIGN		
	MONLOC		
	I.CSW		
	AVAIL		
.IF EQ	MTT$Y						
	PTIBF4						
	PTTPRE
.IFF
	PMTCBB						
	PBCTCB		
	I.CNSL		
.IFTF							
	PAVAIL
.IFT							
	PTOBF2
	PTOBF4
.IFTF							
	PMONLC
	P$SYSC
	PQCNT
	PI.CSW
	P$SWPB
.IFT							
	PTIBF6						
.IFTF							
 .IF NE PANI$C						
	PSPTR
 .ENDC							
 .IF NE TIME$R						
	PLKQUE
	P$TIM2
	PLKQU2
  .IF EQ MTT$Y						
	PTBCNT
  .ENDC							
 .ENDC							
.ENDC							
.ENDC
.IF NE MMG$T						
	TRPLST		
.ENDC							
	0		
.IF NE BF		
	M$SV11		
MSV11:	.WORD	160000	
.ENDC
	GETPSW
	 MFPS	-(SP)
	GETPSW+2
	 NOP
	PUTPSW
	 MTPS	2(SP)
	. = . - 2
	PUTPSW+2
	 2
	PUTPSW+4
	 NOP
.IF EQ BF						
	$INTEN+2
	 MFPS	R4
	$INTEN+4
	 BIC	(R5)+,R4
	$INTEN+6
	 MTPS	R4
.IFF
	RMONPS
	 MFPS	R4
	RMONPS+2
	 BIC	(R5)+,R4
	RMONPS+4
	 MTPS	R4
.ENDC
	0		
BCNFG2:	.WORD	0	
BCNFG:	.WORD	LSI11$	
TSLIST:	.WORD	LSI11$	
	.WORD	CLOCK$	
	.WORD	LKCS$
.IF NE VT11$!VS60$					
	.WORD	HWDSP$	
.ENDC							
.IF EQ KW11$P
	.WORD	KW11P$	
.ENDC
.IF NE MMG$T						
	.WORD	KT11$	
.ENDC							
	.WORD	HWFPU$	
	.WORD	VS6$0	
	.WORD	CACHE$	
	.WORD	PDP70$	
	.WORD	EIS$	
	.WORD	SWREG$	
	.WORD	LIGHT$	
	.WORD	PDP60$	
.IF NE MPT$Y
	.WORD	MPTY$	
.ENDC
BLOOK:	.RAD50	/SY /
FNAME:	.WORD	0,0	
	.RAD50	/SYS/
LKAREA:	.BLKW	5
CBLOK:	.BLKW	5	
MONBLK:	.WORD	0	
SWPBLK:	.WORD	0	
.IF NE MPT$Y&ERL$G
PARTB:
.REPT	16.
	.WORD	0
.ENDR
.ENDC
.IF EQ BF
TTMSG:	.ASCIZ	/?BOOT-W-Invalid or missing TT.SYS/	
.ENDC

.IIF LE 4736-.,	.ERROR
. = 4736	
$RMSIZ::.WORD	$RMEND-KMON			
. = 4740	
. = 4776	
	.WORD	0
BOOTSZ	= . + 777 / 1000
. = BOOTSZ * 1000
BTIME	= .
BDATE	= BTIME + 4
BUFFB	= BDATE + 2
	.END
