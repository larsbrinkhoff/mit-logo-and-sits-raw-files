
.TITLE ABS LOADER
L.CKSM=%0
L.A=%1
L.BYT=%2
L.BY=%3
R4=%4
L.PTR=%5
SP=%6
PC=%7
R1=%1
R2=%2

LOAD=17400
.=LOAD+75
.BYTE 75
L.DEV=DEVICE
L.LOAD:	HALT

L.LD1:	MOV PC,SP
	CMP -(SP),-(SP)
	MOV PC,L.PTR
	ADD #L.READ-.,L.PTR
	CLR L.A
L.LD1B:	MOV (PC)+,(SP)
L.SR:	0			;SWITCH REGISTER
	ROR @SP
	BCS L.LD1C	;NEED RELOCATION
	CLR @SP		;NO RELOACTE
	BR L.LD2
L.LD1C:	ASL @SP		;CHECK SWITCH REG FOR NON-ZERO
	BNE L.LD2
	MOV L.A,@SP
;LOOK FOR BEG OF A BLOCK
L.LD2:	CLR L.CKSM
	JSR PC,@L.PTR
	DECB L.BY
	BNE L.LD2
	JSR PC,@L.PTR

	JSR PC,L.GWRD
	MOV R4,L.BYT
	SUB #4,L.BYT
	CMP #2,L.BYT
	BEQ L.JMP
	JSR PC,L.GWRD
	ADD @SP,R4	;ADD IN RELOCATE
	MOV R4,L.A

L.LD3:	JSR PC,@L.PTR
	BGE L.LD4
	TSTB L.CKSM
	BEQ L.LD2
L.BAD:	0
	BR L.LD2

L.LD4:	MOVB L.BYT,(L.A)+
	BR L.LD3

L.READ:	MOV L.DEV,L.BY
	INCB @L.BY
L.R1:	TSTB @L.BY
	BPL L.R1
	MOVB 2(L.BY),L.BY
	ADD L.BY,L.CKSM
	BIC #177400,L.BY
	DEC L.BYT
	RTS PC

L.GWRD:	MOV (SP)+,L.TMP
	JSR PC,@L.PTR
	MOV L.BY,R4
	JSR PC,@L.PTR
	SWAB L.BY
	BIS L.BY,R4
	MOV L.TMP,PC

L.JMP:	JSR PC,L.GWRD
	JSR PC,@L.PTR
	TSTB L.CKSM
	BNE L.BAD
	ASR R4
	BCC L.JMP1
	HALT
	BR L.LD1B
L.JMP1:	ASL R4
	ADD @SP,R4
	JMP @R4
L.TMP:	.WORD 0

L.INIT:	MOV #352,LOOP+2
	MOV #765,BRNCH
	JMP L.LOAD

START:	MOV DEVICE,R1
LOOP:	MOV (PC)+,R2
	.BYTE BRNCH-LOAD-1
	.BYTE -<BRNCH-L.INIT+2/2>
	.=LOAD+374
BRNCH:	; BR START
	.=.+2
DEVICE:	; 0

.END
