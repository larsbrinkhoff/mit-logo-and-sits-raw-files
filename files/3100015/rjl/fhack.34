TITLE FILES TESTER
;HACK TO CONVERT ASCII TO LOGO FILES, LOGO FILES TO ASCII
;AND VARIOUS OTHER HACKS

F=0
TA=1
N==1
TB=2
I==2
TC=3
TD=4
TE=5
CH=6
A=7
B=10
C=11
D=12
E=13
COLNM=14
FN1==14
SCOLNM=15
FN2==15
P=17
DCHN==1
TYIC==2
TYOC==3
ENDFIL==0
FD=600000

FDC==6
ERRC==7
ADCHN==10
LPTC==11
DISC==12

;FLAGS IN RIGHT HALF OF F
COLF==1
SCLF==2
SPCF==4
CRF==10
CHF==20
ALLF==40
ENFS==COLF\SCLF\SPCF\CRF\ALLF
;FLAGS IN LEFT HALF OF OF F
TTYF==1
LPTF==2
DISF==4
SHUTPF==10

FILLNG:	0
FILPLX:	0
FILBFX:	0
FILOX:	0
FILEO:	SIXBIT /   DSK/
	0
	SIXBIT /FILES/
FILTEM:	0
FILTM1:	0
FILACC:	0
FILBYT:	0
FILNCH:	0
FILNFL:	0
FLNI:	-1
HEDSW:	0


TYO:	CAIN CH,^T
	MOVEI CH,""
	CAIN CH,^_
	JRST TYONAM
	CAIN CH,^Z
	JRST TYOTHI
	TLNE F,LPTF
	.IOT LPTC,CH
	TLNE F,DISF
	.IOT DISC,CH
	TLNE F,SHUTPF
	POPJ P,
	TLNE F,TTYF
	.IOT TYOC,CH
	POPJ P,
TYONAM:	PUSH P,TD
	MOVEI TD,[ASCIZ /
    NAME   /]
	PJ FILATY
	POP P,TD
	POPJ P,
TYOTHI:	PUSH P,TD
	MOVEI TD,[ASCIZ /
    THING  /]
	PJ FILATY
	POP P,TD
	POPJ P,
CRLF:	MOVEI CH,15
	PJ TYO
	MOVEI CH,12
	JRST TYO
TAB:	MOVEI CH,11
	JRST TYO


NM1:	BLOCK 40
NM2:	BLOCK 40
7NMBUF:	BLOCK 40
6NMBUF:	BLOCK 40
AFILO:	SIXBIT \  !DSK\
	SIXBIT \LOGO\
	SIXBIT \HACK\
	0
ASNAME:	SIXBIT \LOGO\
LSNAME:	SIXBIT \LOGO\
PDL:	BLOCK 40

PATCH:	BLOCK 40

TBL:	SIXBIT \HACK\
	NM1,,NM2
	0
TBL2:	SIXBIT \HACK\
	0
PJ=PUSHJ P,



FOO==.

LOC 42
	JSR TSINT

LOC FOO

TSINT:	0
	0
	EXCH A,TSINT
	TLNN A,400000
	JRST TSDIS
	TRNN A,1_<TYIC>
	JRST TSDIS
	MOVEI A,TYIC
	.ITYIC A,
	JRST TSDIS
	CAIN A,^G
	.DISMISS [GO+1]
	CAIN A,^B
	JRST LPTGO
	CAIN A,^E
	JRST LPTSTP
	CAIN A,^W
	TLZ F,TTYF
	CAIN A,^V
	TLO F,TTYF
	CAIN A,^S
	JRST TTYSTP
TSDIS:	EXCH A,TSINT
	.DISMISS TSINT+1
LPTGO:	.OPEN LPTC,[SIXBIT \  !LPTWALL  PAPER\]
	JRST TSDIS
	TLO F,LPTF
	JRST TSDIS
LPTSTP:	.CLOSE LPTC,
	TLZ F,LPTF
	JRST TSDIS
TTYSTP:	TLO F,SHUTPF
	.RESET TYOC,
	JRST TSDIS
GO:	MOVSI F,TTYF	;TURN ON TTY
	MOVEI TA,0
	MOVEI TB,1_<TYIC>
	.SETM2 TA,
	.OPEN TYIC,[SIXBIT \   TTY\]
	.VALUE
	.OPEN TYOC,[SIXBIT \  !TTY\]
	.VALUE
	.RESET TYOC,
	JRST LOOP
ERR:	JUMPE A,ERR1
	MOVEI TD,@LOSTAB(A)
	PJ FILATY
	JRST ERR2
ERR1:	.OPEN ERRC,[SIXBIT \   ERR     !\
	JRST ERR2
ERR3:	.IOT ERRC,CH
	CAIN CH,15
	JRST ERR2
	PJ TYO
	JRST ERR3
ERR2:	MOVEI CH,"?
	PJ TYO
LOOP:	MOVEI P,PDL
	TLZ F,SHUTPF
	MOVEI A,0
	SETZM HEDSW
	.CLOSE ADCHN,
	MOVEI TD,[ASCIZ \
*\]
	PJ FILATY
	PJ TYI
	CAIN CH,"?
	JRST LISTCM
	CAIG CH,40
	JRST LOOP
	CAIG CH,77
	JRST ERR2
	CAIL CH,"V
	JRST ERR2
	JRST @.+1-100(CH)
	ERR2
	ERR2
	ERR2
	COPY
	DEL
	ERR2
	FLUSH
	GARCOL
	HIDE
	INIT
	ERR2
	ERR2
	LIST
	MUNGE
	ERR2
	ERR2
	PRINT
	QUIT
	READ
	SPEW
	ERR2
	USERS


LISTCM:	MOVEI TD,[ASCIZ \
?-LIST COMMANDS
C-COPY LOGO FILE TO ASCII FILE
D-DELETE LOGO FILE
G-GARBAGE COLLECT USER
H-HIDE FILE(SET PROTECT BITS)
I-INITIALIZE USER
L-LIST FILE DIRECTORY
M-ENTER MUNGE MODE (ONLY IF YOU KNOW WHAT TO DO!!)
P-PRINT LOGO FILE
Q-COMMIT SUCIDE
R-READ FROM ASCII INTO LOGO FILE
S-SPEW USER ONTO LPT
U-SET CURRENT USER
\]
	PJ FILATY
	JRST LOOP

QUIT:	.VALUE [ASCIZ \:KILL
:V \]

	[ASCIZ \FILES LOST REAL BAD??????\]
LOSTAB:	,,-1
	[ASCIZ \USER NOT FOUND\]
	[ASCIZ \FILE NOT FOUND\]
	[ASCIZ \FILE ALREADY EXISTS\]
	[ASCIZ \NO FILE OPEN??? LOSSAGE!!!!\]
	[ASCIZ \SOMEONE ELSE IS HACKING THAT DIRECTORY.\]
	[ASCIZ \FILE DIRECTORY FULL.\]
	[ASCIZ \CAN'T GET CORE.\]
	[ASCIZ \PROTECTION VIOLATION.\]



PRINT:	PJ LNMGB
	MOVEI TA,TBL
	.SUSET [.SSNAM,,LSNAME]
	PJ READF
	JUMPN A,ERR
	PJ CRLF
PRINT1:	PJ READC
	CAIN CH,ENDFIL
	JRST LOOP
	PJ TYO
	JRST PRINT1

COPY:	PJ LNMGB
	MOVEI TA,TBL
	.SUSET [.SSNAM,,LSNAME]
	PJ READF
	JUMPN A,ERR
	PJ 6NMGB
	MOVEI A,1
	HRLM A,AFILO
	.SUSET [.SSNAM,,ASNAME]
	.OPEN ADCHN,AFILO
	JRST ERR1
COPY1:	PJ READC
	CAIN CH,ENDFIL
	JRST COPY2
	.IOT ADCHN,CH
	JRST COPY1
COPY2:	.CLOSE ADCHN,
	JRST LOOP

LIST:	.SUSET [.SSNAM,,LSNAME]
	PJ CRLF
	MOVEI TA,TBL
	PJ LISTFD
	JUMPN A,ERR
	JRST LOOP

USERS:	PJ LUSERG
	TRNN F,CHF
	JRST LOOP
	MOVE TD,6NMBUF
	MOVEM TD,TBL
	MOVEM TD,TBL2
	JRST LOOP
MUNGE:	PJ TYI
	CAIE CH,"%
	JRST ERR2
	SETOM FILPLX
	JRST LOOP

HIDE:	PJ LNMGB
	MOVEI TA,TBL
	.SUSET [.SSNAM,,LSNAME]
	PJ FILPRR
	JUMPN A,ERR
	MOVEI TD,[ASCIZ \
OLD PROTECT BITS ARE \]
	PJ FILATY
	MOVE TB,TBL+2
	MOVEI TD,[ASCIZ \R\]
	TRNE TB,FILPRD
	PJ FILATY
	MOVEI TD,[ASCIZ \L\]
	TRNE TB,FILPLS
	PJ FILATY
	MOVEI TD,[ASCIZ \D\]
	TRNE TB,FILPDL
	PJ FILATY
	SETZB TB,TBL+2
	MOVEI TD,[ASCIZ \
THE NEW BITS ARE \]
	PJ FILATY
HIDE1:	MOVEI TC,0
	PJ TYI
	CAIN CH,"R
	TRO TC,FILPRD
	CAIN CH,"L
	TRO TC,FILPLS
	CAIN CH,"D
	TRO TC,FILPDL
	CAIN CH,15
	JRST HIDE2
	JUMPE TC,ERR2
	IORM TC,TBL+2
	JRST HIDE1
HIDE2:	MOVEI TA,TBL
	PJ FILPRO
	JUMPN A,ERR
	JRST LOOP
READ:	PJ 6NMGB
	HRRZS AFILO
	PJ LNMGB
	MOVEI TA,TBL
	.SUSET [.SSNAM,,LSNAME]
	PJ WRITEF
	JUMPN A,ERR
	.SUSET [.SSNAM,,ASNAME]
	.OPEN ADCHN,AFILO
	JRST ERR1
READ1:	.IOT ADCHN,CH
	JUMPLE CH,READ2
	CAIN CH,^C
	JRST READ2
	PJ WRITEC
	JUMPN A,ERR
	JRST READ1
READ2:	MOVEI CH,0
	PJ WRITEC
	.CLOSE ADCHN,
	JRST LOOP

DEL:	PJ LNMGB
	MOVEI TA,TBL
	PJ DELETE
	JUMPN A,ERR
	JRST LOOP

INIT:	SKIPN FILPLX
	JRST ERR2
	PJ LUSERG
	TRNN F,CHF
	JRST INIT1
	MOVE TD,6NMBUF
	MOVEM TD,TBL
	MOVEM TD,TBL2
INIT1:	.SUSET [.SSNAM,,LSNAME]
	MOVEI TA,TBL2
	PJ DELETE
	JRST LOOP

FLUSH:	PJ 6NMGB
	.FDELE AFILO
	JRST ERR1
	JRST LOOP

SPEW:	PJ LUSERG
	MOVE TA,6NMBUF
	TRNN F,CHF
	JRST SPEW1
	MOVEM TA,TBL
SPEW1:	MOVEI TA,TBL
	TLZ F,TTYF
	TLO F,LPTF
	.OPEN LPTC,[SIXBIT /  !LPTWALL  PAPER/]
	SKIPA
	JRST SPEW2
	.OPEN LPTC,[SIXBIT /  !TPLWALL  PAPER/]
	JRST ERR
SPEW2:	MOVEI CH,^L
	PJ TYO
	MOVEI TD,[ASCIZ /

î*****************************************************************

                          /]
	PJ FILATY
	MOVEI TD,7NMBUF
	PJ FILATY
	MOVEI TD,[ASCIZ /

*****************************************************************

******************
FILE DIRECTORY:
******************
/]
	PJ FILATY
	.SUSET [.SSNAM,,LSNAME]
	PJ LISTFD
	JUMPN A,SERR
	SETZM TBL+1
	SETOM HEDSW
	MOVEI TA,TBL
	PJ READF
	JUMPN A,SERR
SPEW3:	PJ READC
	CAIN CH,ENDFIL
	JRST SPEW4
	PJ TYO
	JRST SPEW3
SPEW4:	.CLOSE LPTC,
	MOVSI F,TTYF
	JRST LOOP
SERR:	.CLOSE LPTC,
	MOVSI F,TTYF
	JRST ERR

FILHED:	SKIPN HEDSW
	POPJ P,
	PUSH P,TD
	PUSH P,TC
	PUSH P,TA
	MOVEI TD,[ASCIZ/
*************************************************
/]
	PJ FILATY
	LDB TD,[301400,,TB]
	ADDI TD,FD
	PJ FILATY
	MOVEI CH,40
	PJ TYO
	LDB TD,[141400,,TB]
	ADDI TD,FD
	PJ FILATY
	MOVEI TD,[ASCIZ/
*************************************************
/]
	PJ FILATY
	POP P,TA
	POP P,TC
	POP P,TD
	POPJ P,

GARCOL:	PJ LUSERG
	.SUSET [.SSNAM,,LSNAME]
	MOVE TA,6NMBUF
	TRNN F,CHF
	JRST GAR6
	MOVEM TA,TBL
	MOVEM TA,TBL2
GAR6:	TRNE F,ALLF
	JRST ALLGC
	PUSHJ P,GARBAG
	JRST LOOP
ALLGC:	SETOM FLNI
ALLGC1:	PUSHJ P,FNG
	JRST LOOP
	MOVEM FN1,TBL2
	CAMN FN2,[SIXBIT \FILES\]
	PUSHJ P,GARBAG
	JRST ALLGC1

GARBAG:	MOVEI TA,TBL2
	PUSH P,[ERR]
	PJ FILOPR
	MOVEI TD,7
	HRLM TD,FILEO
	.OPEN ADCHN,FILEO
	POPJ P,
	POP P,A
	MOVE A,[-1400,,FD]
	.IOT ADCHN,A
	MOVE TB,[300100,,FD]
	ADD TB,FILBTB
	SETZM (TB)
	MOVSI TA,(TB)
	HRRI TA,1(TB)
	BLT TA,100(TB)
	MOVSI TA,777700
	MOVEM TA,(TB)
	MOVEI TA,14
	MOVE A,FILPTB
	MOVEI B,1
GAR1:	CAMLE A,FILPTE
	JRST GAR2
	LDB C,[1200,,FD(A)]
	JUMPE C,GAR3
	DPB TA,[1200,,FD(A)]
	ADDI TA,1
GAR4:	MOVEM C,FILACC
	PJ FILBIO
	IDPB B,TB
	MOVE D,[-100,,FILBUF]
	MOVE C,FILBUF+77
	JUMPE C,GAR5
	MOVEM TA,FILBUF+77
	ADDI TA,1
GAR5:	.IOT ADCHN,D
	JUMPN C,GAR4
GAR3:	ADDI A,2
	JRST GAR1
GAR2:	.ACCESS ADCHN,[0]
	MOVE A,[-1400,,FD]
	.IOT ADCHN,A
	.CLOSE ADCHN,
	POPJ P,
LNMGB:	MOVEI TD,[ASCIZ \
LOGO FILE=\]
	PJ FILATY
	PJ NMEGB
	SKIPE SCOLNM
	MOVEM SCOLNM,TBL
	SKIPE COLNM
	MOVEM COLNM,LSNAME
	MOVE A,[7NMBUF,,NM1]
	TRNN F,CHF
	JRST LNMGB1
	BLT A,NM1+37
	MOVEI A,0
	TRNN F,ALLF
	MOVEI A,NM1
	HRLM A,TBL+1
	TRNE F,CRF
	POPJ P,
LNMGB1:	PJ NMEGB
	SKIPE SCOLNM
	MOVEM SCOLNM,TBL
	SKIPE COLNM
	MOVEM COLNM,LSNAME
	MOVE A,[7NMBUF,,NM2]
	TRNN F,CHF
	POPJ P,
	BLT A,NM2+37
	MOVEI A,0
	TRNN F,ALLF
	MOVEI A,NM2
	HRRM A,TBL+1
	POPJ P,


6NMGB:	MOVEI TD,[ASCIZ \
ASCII FILE=\]
	PJ FILATY
	PJ NMEGB
	SKIPE SCOLNM
	MOVEM SCOLNM,ASNAME
	SKIPE COLNM
	HLRM COLNM,AFILO
	MOVE A,6NMBUF
	TRNE F,CHF
	MOVEM A,AFILO+1
	TRNE F,CRF
	POPJ P,
	PJ NMEGB
	SKIPE SCOLNM
	MOVEM SCOLNM,ASNAME
	SKIPE COLNM
	HLRM COLNM,AFILO
	MOVE A,6NMBUF
	TRNE F,CHF
	MOVEM A,AFILO+2
	POPJ P,

LUSERG:	MOVEI TD,[ASCIZ \
LOGO USER=\]
	PJ FILATY
	PJ NMEGB
	POPJ P,

NMEGB:	SETZB COLNM,SCOLNM
	TRZ F,-1
	MOVE B,[440700,,7NMBUF]
	MOVE C,[440600,,6NMBUF]
NMEGB1:	PJ TYI
	CAIN CH,^A
	TRO F,ALLF\CHF
	CAIN CH,177
	JRST ERR2
	CAIN CH,":
	TRO F,COLF
	CAIN CH,";
	TRO F,SCLF
	CAIN CH,40
	TRO F,SPCF
	CAIN CH,15
	TRO F,CRF
	TRNE F,ENFS
	JRST NMEGB2
	CAIN CH,^Q
	PJ TYI
	IDPB CH,B
	SUBI CH,40
	IDPB CH,C
	TRO F,CHF
	JRST NMEGB1
NMEGB2:	MOVEI D,6
	MOVEI CH,0
	IDPB CH,B
	IDPB CH,C
	SOJG D,.-2
	TRNE F,SPCF\CRF\ALLF
	POPJ P,
	TRNE F,COLF
	JRST .+3
	MOVE SCOLNM,6NMBUF
	JRST NMEGB+1
	MOVE COLNM,6NMBUF
	JRST NMEGB+1

FNG:	MOVEI B,1	;READ ONE ENTRY IN FILE DIRECTORY (PUT IN FN1,FN2 AND PACKN)
	AOSE FLNI
	JRST FNG2
FNG8:	.OPEN FDC,[SIXBIT \   DSK.FILE.(DIR)\]	;OPEN FIRST TIME ROUND
	.VALUE
	MOVEI B,2	;FLUSH FIRST TWO LINES
FNG2:	.IOT FDC,A
	CAIN A,^C
	POPJ P,
	CAIE A,12	;SEARCH FOR CR
	JRST FNG2
	SOJG B,FNG2

FNG1:	MOVNI I,1
	CLEARB N,FN1
	CLEARM FN2

FNG3:	.IOT FDC,A
	CAIN A,^C
	POPJ P,
	CAIN A,"*
	JUMPL I,FNG6	;DOESN'T REALLY EXIST
	CAIN A,14
	POPJ P,		;END OF FILE DIRECTORY
	CAIN A,40
	JRST FNG4
	JUMPG I,FNG5	;HAVE FLUSHED PACK, GOBBLE NAMES
	IMULI N,10.
	ADDI N,-"0(A)	;ASSEMBLE PACK NUMBER
	JUMPE I,FNG3
	AOJA I,FNG3

FNG4:	JUMPN I,FNG3
	AOJA I,FNG3	;SPACE AFTER PACK

FNG7:	.IOT FDC,A	; FLUSH SPACES
	.IOT FDC,A
	.IOT FDC,A
	CAIN A,^C
	POPJ P,
FNG5:	MOVEI D,2
FNG5B:	MOVE B,FNG5T-1(D)
	MOVEI C,6
FNG5A:	SUBI A,40	;CONVERT TO SIXBIT
	IDPB A,B
	.IOT FDC,A
	SOJG C,FNG5A
	.IOT FDC,A	;FLUSH SPACE
	SOJG D,FNG5B
	AOS (P)
	POPJ P,

FNG5T:	440600,,FN2	;BYTE-POINTERS TO FILE NAMES
	440600,,FN1


FNG6:	.IOT FDC,A	;FLUSH TO END OF LINE
	CAIN A,14
	POPJ P,
	CAIN A,12
	JRST FNG1
	JRST FNG6

TYI:	.IOT TYIC,CH
	POPJ P,


.INSRT FILES >

END GO


