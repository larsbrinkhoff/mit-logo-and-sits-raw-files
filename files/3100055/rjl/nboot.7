	.TITLE 3500 FLOPPY DISK BOOTSTRAP
.INSRT SITS;SITMAC >
.INSRT RJL;FLOPDF >
TPS==177564
TPB==177566



.=0
	12.	
	JMP 10000
.=40
	111111
.=10000	;CAHNGE TO 173000 FOR PROM
GO:	BR 2$		;START HERE TO DO NORMAL THING
	BR RESTAR	;START HERE IF F IS SET UP
2$:	MOV #1000,P	;TEMP PDL
	BR 4$		;******MAGIC******** !!!!!!
3$:	MOV (P)+,@#4	;SET UP NXM TRAP
	CLR F		;START LOOKING AT ZERO
1$:	TST (F)+	;TEST TILL YOU GET A TRAP
	BR 1$
4$:	JSR PC,3$	;*****MAGIC****** USED TO GET REAL ADDRESS OF MEMEND
MEMEND:	TST -(F)	;THIS WILL TRAP TO ITSELF THE FIRST TIME
	CLR (F)		;THIS IS A FLAG TO BLOADR, IF NON ZERO GIVES FILE TO LOAD
RESTAR:	RESET		;PREVENT ANY INTERUPTS THAT MIGHT BE PENDING
	CLR SMSLGT	;TURN OFF THE LIGHTS WHEN YOU COME IN (? ISN'T THAT BACKWARDS?)
	MOV #SMSCSR,E	;COMMONLY USED CONSTANT
	MOV #SMSPWR!SMSRSB,(E)	;RESET THE SMS CONTROLLER
6$:	BIT #SMSFDO,(E)	;IS IT ON?
	BNE 6$		;NOT YET
5$:	MOV #SMSSTS,A	;STATUS
	JSR PC,CMDWA2
	JSR PC,GETSTB	;GET STATUS BYTE
	BIT #SMSDNR,A	;IS DISK READY
	BNE 5$		;NOPE, WAIT
	MOV F,P
	CLR C		;C IS THE CORE ADDRESS TO READ INTO
	MOV #1,F	;SECTOR TO READ
3$:	JSR PC,READS
	MOV #128.,D
2$:	MOVB @#SMSDBF,(C)+
	SOB D,2$
	JSR PC,GETST	;GET STATUS BYTE
	ADD #2,F	;NEXT SECTOR
	CMP #27.,F	;ARE WE OVER END?
	BGT 3$		;NOPE, READ NEXT
	BLT 4$		;JUST READ BLOCK 26., DONE
	MOV #2,F	;GO BACK TO GOBBLE THE EVEN SECTORS
	BR 3$
4$:	CLR PC		;START THE MAGIC PROGRAM

READS:	MOV #SMSSEK,A
	JSR PC,CMDWAI	;GIVE A SEEK COMMAND
	MOV #1,A
	JSR PC,CMDWAI	;GIVE THE TRACK ADDRESS
	JSR PC,GETST		;GET THE STATUS WHICH IS THE RESULT
	MOV #SMSDTB!SMSRED,A	;READ INTO BUFFER
	JSR PC,CMDWAI
	MOV F,A
	JSR PC,CMDWAI
	JSR PC,GETST		;GET STATUS OF THE READ
	MOV #SMSBTH!SMSRED,A
	JMP CMDWA2		;BUFFER TO HOST READ
				;SECOND BYTE IS IGNORED

GETST:	TSTB (E)		;WAIT FOR DONE
	BPL GETST
	TST (E)		;ERROR?
	BLT ERROR
GETSTB:	BIT #SMSXFW,(E)	;WAITING TO XFR?
	BEQ GETSTB
	MOV @#SMSDBF,A		;GOBBLE STATUS
	RTS PC			;AND IGNORE IT

CMDWA2:	JSR PC,(PC)		;CALL THIS ROUTINE TWICE
CMDWAI:	BIT #SMSCMW,(E)	;READY FOR COMMAND BYTE?
	BEQ CMDWAI		;NOPE
	MOV A,@#SMSCMD		;GIVE IT TO IT
	RTS PC

ERROR:	JSR A,1$
	.ASCIZ /DISK ERROR
/
.EVEN
1$:	TSTB @#TPS
	BPL 1$
	MOVB (A)+,@#TPB
	BNE 1$
	BR RESTAR


LEN==.-GO

.MACRO PRAD A,B
.PRINT /A'B
/
.ENDM
.IF2
PRAD <END=>,\.
PRAD <LENGTH=>,\LEN
.IIF GT LEN-256.,.ERROR TOO LONG!!!!!
.ENDC
.END 2
