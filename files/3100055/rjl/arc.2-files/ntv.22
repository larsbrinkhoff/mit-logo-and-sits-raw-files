.ABS
A=%0
B=%1
C=%2
D=%3
E=%4
F=%5
P=%6
PC=%7

.NLIST
.=0
.REPT 100
.+2
HALT
.ENDR
.LIST

SMLFNT=20000
BIGFNT=SMLFNT+400

.=1000
JMP GO
.BLKW 100
PDL:	0
TVBPNT:	TVBUF
TVBUF:	.ASCII /ABCDEFGHIJKLMNOPQRSTUVWXYZ/
	.ASCII /abcdefghijklmnopqrstuvwxyz/
	.ascii /0123456789/
ccnt==.-tvbuf
.EVEN


TVINCR==164140	;THE INCREMENT REGISTER FOR THE TV'S
TVINC==77	;THE MASK FOR THE INCREMENT
TVRSET==100000	;THE RESET BIT
TVCLRW==400	;THE COLOR WRITE BIT

TVSEL==164142	;THE CONSOLE SELECT REGISTER
TVRCNS==77	;THE CONSOLE NUMBER MASK
TVRWMD==300	;THE REGULAR WRITE MODE MASK
TVNSH==0	;NO SHIFT WRITE MODE
TVINOR==100	;THE INCLUSIVE OR MODE
TVXOR==200	;THE XOR MODE
TVMOV==300	;THE MOVE FUNCTION
TVDCNS==TVRCNS*400	;THE DISK CONSOLE NUMBER (SAME, BUT IN TOP BYTE)
TVDWMD==TVRWMD*400	;THE DISK WRITE MODE MASK

TVRADR==164144	;THE REGULAR ADDRESS REGISTER

TVWC==164146	;THE WORD COUNT FOR THE BLOCK WRITE

TVDADR==164150	;THE DISK TRANSFER ADDRESS REGISTER

TVSHR==164152	;THE SHIFT REGISTER
TVSHCN==17	;THE SHIFT COUNT
TVMAP==17400	;THE START OF THE 16K PAGE (IN 4K BLOCKS)
TVAMAP==20000	;THE ACTIVATE TVMAP BIT

TVMSK==164154	;THE MASK REGISTER

TVDWIN==164156	;THE WINDOW FOR DISK TRANSFERS

TVRWIN==164160	;THE WINDOW FOR REGULAR TRANSFERS

TVCNSL==164162	;THE CONSOLE REGISTER FOR THE MEMORY
TVCLR==160000	;THE COLOR NUMBER



GO:	RESET
	MOV #PDL,P
	MOV #TVBUF,TVBPNT
	MOV #1,TVINCR
	CLR TVRADR
	CLR TVRWIN
	CLR TVWC
	TST TVWC
	BNE .-4
	TST 177570
	BLT BIGONE
	CLR B
	MOV #44,TVINCR
	MOV #100,TVSEL
	MOV #TVRWIN,C
	MOV #96.,D
	MOV #CCNT,E
	JMP CHAR0
BIGONE:	CLR B
	MOV #44,TVINCR
	MOV #100,TVSEL
	MOV #TVRWIN,C
	MOV #30.,D
	MOV #CCNT,E
	JMP BCHAR
;CHARACTER GENERATION MACRO
;ASSUMES INCREMENT SET TO 44
;ADDRESS TO PUT CHAR IN B
;#TVRWIN IN C
;WRITE MODE SET TO IOR
;NUMBER OF CHARS LEFT ON LINE IN D
;NUMBER OF CHARACTERS IN BUFFER IN E
;CLOBBERS A
.MACRO TVCHAR SHIFT,MASK
	MOVB @TVBPNT,A	;GET A CHARACTER
	ASL A
	BIC #177401,A
	MOV SMLFNT(A),A	;POINTER TO BEGINING OF CHAR DEF
	BNE .+6
	JMP TVSPC	;SPECIAL CHAR
	MOV B,TVRADR	;ADDRESS KEPT IN B
	.IFNB SHIFT
	.IFZ SHIFT
	CLR TVSHR
	.IFF
	MOV #SHIFT,TVSHR
	.ENDC
	MOV #MASK,TVMSK
	.IFF
	COM TVMSK
	.ENDC
	.REPT 10.
	MOVB (A)+,(C)
	.ENDR
	DEC D
.ENDM
.MACRO TVCHK
	DEC E
	BNE .+6
	JMP TVBFE
	INC TVBPNT
.ENDM

;ACTUAL CHARACTER GENERATOR, OPEN CODED FOR 8 CHARACTERS
CHAR0:	TVCHAR 10.,1777
	TVCHK
CHAR1:	TVCHAR 4.,176017
	TVCHK
CHAR2:	TVCHAR 14.,177760
	ADD #2,B
	TVCHAR ,37777
	TVCHK
CHAR3:	TVCHAR 8.,140377
	TVCHK
	BR CHAR4
CHAR4:	TVCHAR 2.,177403
	TVCHK
CHAR5:	TVCHAR 12.,177774
	ADD #2, B
	TVCHAR ,7777
	TVCHK
CHAR6:	TVCHAR 6.,170077
	TVCHK
CHAR7:	TVCHAR 0.,177700
	ADD #2,B
	TST D
	BEQ TVLFUL
	TVCHK
	JMP CHAR0
TVBFE:	HALT
TVSPC:	HALT
TVLFUL:	HALT

;EXPECTS TVINCR SET TO 44
;B HAS POINTER INTO TV MEMORY
;A GETS CLOBBERED
;C HAS #TVRWIN
;D HAS NUMBER OF CHARS LEFT ON LINE
;E HAS NUMBER OF CHARS LEFT IN BUFFER
BCHAR:	MOVB @TVBPNT,A
	ASL A
	BIC #177401,A
	MOV BIGFNT(A),A
	BEQ BSPCCH
	MOV B,TVRADR
	.REPT 24.
	MOV (A)+,(C)
	.ENDR
	ADD #2,B
	INC TVBPNT
	DEC D
	BEQ BLINFL
	DEC E
	BNE BCHAR
BBUFE:	HALT
BLINFL:	HALT
BSPCCH:	HALT
	.END GO
