	.TITLE 3500 FLOPPY DISK BOOTSTRAP
.INSRT SITS;SITMAC >
.INSRT RJL;FLOPDF >
TPS==177564
TPB==177566


VAREND==-2
.MACRO VAR A
A=VAREND
VAREND==VAREND-2
.ENDM

VAR SECTOR
VAR TRACK


.=3000	;CAHNGE TO 173000 FOR PROM
GO:	BR 2$		;START HERE TO DO NORMAL THING
	BR RESTAR	;START HERE IF F IS SET UP
2$:	MOV #1000,P	;TEMP PDL
	BR 4$		;******MAGIC******** !!!!!!
3$:	MOV (P)+,@#4	;SET UP NXM TRAP
	CLR F		;START LOOKING AT ZERO
1$:	TST (F)+	;TEST TILL YOU GET A TRAP
	BR 1$
4$:	JSR PC,3$	;*****MAGIC****** USED TO GET REAL ADDRESS OF MEMEND
MEMEND:	TST -(F)	;THIS WILL TRAP TO ITSELF THE FIRST TIME
	CLR (F)		;THIS IS A FLAG TO BLOADR, IF NON ZERO GIVES FILE TO LOAD
RESTAR:	MOV #SMSCSR,E	;COMMONLY USED CONSTANT
	MOV #SMSPWR!SMSRSB,(E)	;RESET THE SMS CONTROLLER
6$:	BIT #SMSFDO,(E)	;IS IT ON?
	BNE 6$		;NOT YET
5$:	MOV #SMSSTS,A	;STATUS
	JSR PC,CMDWA2
	JSR PC,GETSTB	;GET STATUS BYTE
	BIT #SMSDNR,A	;IS DISK READY
	BNE 5$		;NOPE, WAIT
	MOV F,P
	ADD #VAREND-2,P	;RESERVE VARIABLE SPACE
	CLR C		;C IS THE CORE ADDRESS TO READ INTO
	CLR SECTOR(F)
	CLR TRACK(F)
3$:	INC SECTOR(F)
	CMP #27.,SECTOR(F)
	BNE 4$
	INC TRACK(F)
	MOV #1,SECTOR(F)	
4$:	JSR PC,READS
	MOV #128.,D
2$:	MOVB @#SMSDBF,(C)+
	SOB D,2$
	JSR PC,GETST	;GET STATUS BYTE
	DEC @#0		;ZERO GETS THE SECTOR COUNT
	BGE 3$
	JMP @#300		;START THE MAGIC PROGRAM

READS:	MOV #SMSSEK,A
	JSR PC,CMDWAI	;GIVE A SEEK COMMAND
	MOV TRACK(F),A
	JSR PC,CMDWAI	;GIVE THE TRACK ADDRESS
	JSR PC,GETST		;GET THE STATUS WHICH IS THE RESULT
	MOV #SMSDTB!SMSRED,A	;READ INTO BUFFER
	JSR PC,CMDWAI
	MOV SECTOR(F),A
	JSR PC,CMDWAI
	JSR PC,GETST		;GET STATUS OF THE READ
	MOV #SMSBTH!SMSRED,A
	JMP CMDWA2		;BUFFER TO HOST READ
				;SECOND BYTE IS IGNORED

GETST:	TSTB (E)		;WAIT FOR DONE
	BPL GETST
	TST (E)		;ERROR?
	BLT ERROR
GETSTB:	BIT #SMSXFW,(E)	;WAITING TO XFR?
	BEQ GETSTB
	MOV @#SMSDBF,A		;GOBBLE STATUS
	RTS PC			;AND IGNORE IT

CMDWA2:	JSR PC,(PC)		;CALL THIS ROUTINE TWICE
CMDWAI:	BIT #SMSCMW,(E)	;READY FOR COMMAND BYTE?
	BEQ CMDWAI		;NOPE
	MOV A,@#SMSCMD		;GIVE IT TO IT
	RTS PC

ERROR:	JSR A,1$
	.ASCIZ /DISK ERROR
/
.EVEN
1$:	TSTB @#TPS
	BPL 1$
	MOVB (A)+,@#TPB
	BNE 1$
	BR RESTAR


LEN==.-GO

.MACRO PRAD A,B
.PRINT /A'B
/
.ENDM
.IF2
PRAD <END=>,\.
PRAD <LENGTH=>,\LEN
.IIF GT LEN-256.,.ERROR TOO LONG!!!!!
.ENDC
.END GO
