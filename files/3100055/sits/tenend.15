.MLLIT==1
TITLE PICTURE DISPLAY

DCHN==1

A=1
B=2
C=3
D=4
E=5
F=6
T=7
Q=10
R=11
S=12
W=13
X=14
Y=15
COMP=16
P=17

ELVP==100
ELVAD==ELVP*2000
ELVPNT==ELVAD+10	;POINTER TO COM AREA IN 11
ELVRDY==ELVAD
ELVCOM==ELVAD+1
TENRDY==ELVAD+2
ELVBUF==ELVAD+3

FILBLK:	SIXBIT /   DSKDISK  >/


BEG:	MOVEI P,PDL
	MOVEI B,ELVP	;WHERE 11 MEM STARTS IN 10
	MOVEI A,40
	MOVE C,[602000,,1777]
11IN1:	.CALL  [SETZ
		SIXBIT /T11MP/
		B
		SETZ C]
	.VALUE
	AOS B
	ADD C,[4,,]
	SOJGE A,11IN1

11WAI:	MOVE COMP,ELVPNT
	CAIL COMP,100
	 CAML COMP,[10000]
	  SKIPA
	   JRST 11PNGD
	CAMN COMP,ELVPNT
	 .HANG
	JRST 11WAI

11PNGD:	MOVE A,ELVRDY(COMP)	;GET 11 READY FLAG
	CAMN A,ELVRDY(COMP)	;WAIT FOR IT TO CHANGE
	 .HANG
	CAME COMP,ELVPNT	;SAME POINTER?
	 JRST 11WAI
	MOVE A,ELVRDY(COMP)	;GET NEW VALUE
	CAMN A,ELVRDY(COMP)	;WAIT FOR IT TO CHANGE AGAIN
	 .HANG
	CAME COMP,ELVPNT	;SAME POINTER?
	 JRST 11WAI
	MOVSI A,-4
	MOVEM A,TENRDY(COMP)	;SAY THE 10 IS READY
	SKIPE TENRDY(COMP)	;WAIT FOR ACK
	 .HANG
	MOVEI A,15.
	.SLEEP A,	;WAIT 1/2 SEC


	PUSHJ P,ELVWA1
	SKIPN ELVCOM(COMP)	;ZERO => GO AWAY
	 JRST 11WAI
	SKIPL ELVCOM(COMP)		;POS => WRITE TO FILE ON 10
	 JRST WRTFIL
	JRST REDFIL		;NEG => READ FROM FILE ON 10

WRTFIL:	MOVEI A,7		;BLOCK IMAGE MODE
	HRLM A,FILBLK
	.OPEN DCHN,FILBLK
	 .VALUE
	MOVE B,ELVBUF(COMP)
	ADDI B,ELVAD
WRTLOP:	PUSHJ P,ELVWAI
	SKIPN ELVCOM(COMP)	;STILL GOING?
	 JRST WRTDNE
	MOVSI A,-400
	HRR A,B
	.IOT DCHN,A
	JRST WRTLOP

WRTDNE:	.CLOSE DCHN,
	MOVSI A,-4
	MOVEM A,TENRDY(COMP)	;TELL 11 WE'VE CLOSED UP
	JRST 11WAI

REDFIL:	MOVEI A,6
	HRLM A,FILBLK
	.OPEN DCHN,FILBLK
	 .VALUE
	PUSHJ P,ELVWAIT		;TELL 11 WE'RE READY TO SEND
	MOVE B,ELVBUF(COMP)
	ADDI B,ELVAD
REDLOP:	MOVSI A,-400
	HRR A,B
	.IOT DCHN,A
	PUSHJ P,ELVWAI
	SKIPN ELVCOM(COMP)
	 JRST WRTDNE
	JRST REDLOP


ELVWAI:	MOVSI T,-4
	MOVEM T,TENRDY(COMP)
ELVWA1:	SKIPN ELVRDY(COMP)
	 .HANG
	SETZM ELVRDY(COMP)
	POPJ P,

PDL:	BLOCK 100

END BEG
