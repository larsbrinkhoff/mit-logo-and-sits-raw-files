TITLE LOGO
;         TM
;(C) COPYRIGHT,1969,BOLT BERANEK AND NEWMAN INC., CAMBRIDGE, MASS.


;CONFIGURATION PARAMETERS

TEN50==1
TENEX==2
CONFIG==TENEX

;ACCUMULATOR ASSIGNMENTS

F=0	;FLAGS AND BITS
A=1
B=2
C=3
D=4
E=5
G=7
H=10
L=12
M=13
N=14
W=15
S=16	;ARGUMENT PUSHDOWN STACK
P=17	;CONTROL PUSHDOWN STACK

;FLAGS FOR THE LH OF F

LDONF==1	;LINE DONE FLAG FOR EDIT MODE
EDITF==2
UPFF==4		;DENOTES COMEXR CALLED FROM USER DEFINED COMMAND
TIF==10
BREAKF==20
ERRORF==40	;CALLING UPROD FROM ERROR PROCESSOR
GOF==100	;USER TYPED  DIRECT GO
RQF==200	;A REQUEST IN PROGRESS
COMF==400	;1 IF USER PROCEDURE BEING LEFT SHOULD BE TREATED LIKE COMMAND
GCF==1000	;1 IF THIS K HAS NOT BEEN GARBAGE COLLECTED YET
TITLEF==2000	;DOING TITLE DURING A TO
BROKE==4000	;ERROR WAS NON-TYI BREAK KEY
GETF==10000	;GET IN PROGRESS
SAVEF==40000	;SAVE IN PROGRESS, FOR ALTERNATE FORMS OF TYPEOUT


;FLAGS FOR THE RIGHT HALF OF F

TF==1	;A TEMPORARY FLAG FOR MANY ROUTINES
PMF==2	;+- SIGN
NWF==4	;NOT WORD INPUT
CRF==10
FSYMF==20	;0 IF FIRST ELEMENT OF LINE BEING SCANNED,USED W NNUMF
NNUMF==40	;NON-NUMERIC INPUT
PREFIX==200	;FOR PWORD
SUFFIX==400	;ALSO FOR PWORD
MAKEF==1000	;COMPIL CALLED WITH THIS=1 MEANS CALLED FROM MAKE
STORED==2000
FLUSHF==4000	;GETTING AN ALREADY DEFINED PROCEDURE
EABBRF==10000	;DOING LIST ENTRY ABBREVIATIONS
ECONTF==20000	;  "    "     "   CONTENTS
EENTRF==40000	;  "    "     "   ENTRY
ENAMEF==100000	;  "    "     "   NAMES

;FLAG DEFINITIONS FOR LH OF STRING POINTER

WORDF==100	;STRING IS A WORD
SENTF==200	;SENTENCE
EMPTYF==400	;EMPTY

;FLAG DEFINITIONS FOR LH OF COMPILED ELEMENT

MPF==400000
UPRF==200000
VARF==100000
LITF==40000
COMMTF==20000	;IS A COMMENT
ANDF==1000	;USED IN TO LINES, IMPLIES THIS DUMMY PRECEDED BY AND
MAKESC==40	;INDIRECT BIT MEANS 2 LINE MAKE AND END OF NAME:
;ALSO IN THE LEFT HALF OF COMPILED ELEMENT ACAN BE STRING POINTERS


;FLAG DEFINITIONS FOR LEFT HALF OF FIRST WORD OF RP PAIR

TRACEF==400000	;THIS PROCEDURE IS BEING TRACED

;FLAG DEFINITION FOR LH OF 2ND WORD OF MACHINE NAME PAIR

COMPUT==400000	;IF ONE, THIS NAME MUST BE COMPUTED, ADDR IN RH


;UUO DEFINITIONS

OPDEF ERROR [1B8]
OPDEF EXPAND [2B8]
OPDEF GARBAGE [3B8]
OPDEF SQUEZE [4B8]

;STORAGE ALLOCATION TABLE DEFINITIONS

DEFINE TABLES
<TT RP,40,<[EXP RPA,0]>	;THESE THREE MAY BE IN ANY ORDER
 TT VP,40
 TT UA,40
 TT PS,200,<[EXP PSA,PSD,PSM,0]>	;THESE THREE MUST BE IN THIS ORDER
 TT CS,200,<[EXP CSA,CSD,CSM,0]>
 TT DP,200
 TT SP,40,<[EXP UUOACS+S,0]>	;THESE TWO MUST OCCUR IN THE SAME ORDER AS IN ACS
 TT PP,200,<[EXP UUOACS+P,0]>
 TT WS,2000,<[EXP WSB,WSD,UUOACS+W,0]>	;THIS ONE MUST ALWAYS BE LAST SO MEM TRAP CAN BE USED AS END TEST
>
DEFINE POINTR
<MM RP,A
 MM PS,A
 MM PS,D
 MM PS,M
 MM CS,A
 MM CS,D
 MM CS,M
 MM WS,D
 MM WS,B
>
DEFINE U(A1)<UU(A1,1)>
DEFINE UU(A1,A2)<
A1=.ZZZ
.ZZZ=.ZZZ+A2>
.ZZZ=140

;MACRO TO MAKE CONFIGURATION DEPENDENT STUFF READ LIKE ENGLISH

DEFINE FOR (WHAT)
<IFE CONFIG-WHAT>



;UNSHARED AREA, NON-ZERO STUFF FIRST

FURST==.ZZZ	;IN CASE DIFFERENT SYSTEMS PUT DATA IN DIFFERENT PLACES
UU UUOTRP,2	;Z
		;JRST DOUUO
DEFINE MM (A1,A2)
<U A1'A2>
POINTR
U PTOP	;ORDER OF THE NEXT THREE IS EXTREMELY IMPORTANT, MUST BE
U CTOP	; THE SAME AS THEIR CORRESPONDING TABLES IN TT
U DTOP
U WTOP
U CDBOT		;BASE OF ALL DIRECT LINES
U RANNO		;STUFF TO COMPUTE PSEUDO-RANDOM DIGIT

U INFILE	;THESE MAY OR MAY NOT NEED TO BE INITIALIZED NON-ZERO
U OUTFILE	; DEPENDING ON THE SYSTEM
FOR TENEX,<	UU JFNTAB,10	;FOR OPENNING FILES
		UU CHNTAB,^D17>	;PSEUDO-INTERRUPT CHANNEL DISPATCHES

DEFINE TT(A1,A2,A3)
<U A1>
TABLES
U CHARNO	;THESE FOR INPUT SECTION
U BCHAR
U TRACEM	;LIKE BCHAR BUT ONLY FOR TRACE OUTPUT
U SCOUNT
U CHGNO		;XWD SEQNO,CBOT; FOR CURRENT LINE. REPLACE WHEN COMING BACK TO THAT LEVEL
U LINENO
U PRODNM	;NAME OF PROCEDURE IN PROGRESS
U TRUTH
U CPP		;POINTER INTO COMPILED LINE
U SSP		;CONTENTS OF S AT START OF LINE
U SPP		;     "   "  P
		;ALSO DTOP ABOVE
U LINBOT
U SRCBOT
U NEWBOT
U NXLINE	;PTR TO NEXT LINE IN CURRENT PROCEDURE
U THISPR	;MACHINE PROCEDURE LAST IN, PTR TO 2ND ENTRY IN CMPT
U CBOT		;BASE OF DIRECT LINE
U TOPROD	;NAME OF PROCEDURE BEING DEFINED
U BRKTEM	;TEM STORAGE FOR BREAKY ROUTINE
U SEQNO		;VERSION NUMBER OF LAST LINE INPUTTED OR EDITED
U RADIX		;RADIX FOR CURRENT NUMBER CONVERSION
U GODEPT	;THE NUMBER OF GO'S IN THE STACK
U GCTEM		;TEM STORAGE FOR GC AND ALLOCATOR
UU UUOACS,20
FOR TENEX,<	UU LEVLTB,3>	;FOR PSI SYSTEM
BLAST=.ZZZ-1

EXTERNAL JOBUUO,JOB41,JOBSA,JOBREL

FOR TEN50,<
EXTERNAL JOBREN,JOBAPR,JOBOPC
EXTERNAL JOBDDT,JOBFF
EXTERNAL JOBCNI,JOBTPC

HISEG>
FOR TENEX,<RELOC>	;PAGE 0 WILL BE WRITABLE,1-LOGEND NOT

LOGO:	MOVEI	F,0	;CLEAR FLAGS, MAY BE SKIPPED ON CERTAIN RESTARTS
FOR TEN50,<
	CALLI	0	;RESET
	MOVEI	A,BREAKY
	MOVEM	A,JOBREN	;FOR BREAK KEY
	MOVEI	A,ALLOC
	MOVEM	A,JOBAPR	;FOR ILLEGAL MEMORY REFS
	HRRZ	A,JOBFF	;INITIALIZE THE LOW SEG
	HRLI	A,(A)
	ADDI	A,1
	HRRZ	B,JOBREL	;TOP OF HIGHEST K ASSIGNED
	SETZM	-1(A)
	BLT	A,(B)	;CLEAR FROM JOBFF TO TOP OF ASSIGNED CORE
>
FOR TENEX,<
	MOVE	A,[XWD 20000,20001]
	SETZM	20000
	BLT	A,21777
>

	MOVE	A,[JSR UUOTRP]
	MOVEM	A,JOB41		;FOR UUOS

	MOVE	A,[XWD FURST,FURST+1]
	SETZM	FURST
	BLT	A,BLAST	;CLEAR FROM START OF CORE TO EITHER DDT OR JOBFF

	MOVE	A,[XWD SPFRST,FURST]
	BLT	A,FURST+SPLLEN-1 ;SET UP NON-ZERO PART OF UNSHARED CORE

FOR TEN50,<	HLRZ	A,JOBSA>	;ALWAYS THE FIRST FREE LOCATION
FOR TENEX,<	MOVEI	A,20000>	;ENOUGH ROOM FOR CODE AND SYMBOLS
	HRRZI	B,ALOCTB
	HRRZI	C,RP

	HRRZM	A,(C)
	ADD	A,(B)
	ADDI	B,1
	CAIE	C,WS
	AOJA	C,.-4	;SET UP ALL INITIAL ALLOCATIONS


	MOVE	A,RP	;SET UP RIGHT HALVES OF SPACE POINTERS
	HRRM	A,RPA
	MOVE	A,PS
	HRRM	A,PSA
	HRRM	A,PSD
	HRRM	A,PSM
	MOVE	A,CS
	HRRM	A,CSA
	HRRM	A,CSD
	HRRM	A,CSM
	MOVE	W,WS
	HRRM	W,WSB
	HRRM	W,WSD
	MOVE	P,PP

	HRRZI	A,(W)
	HRLI	A,.WBASE
	BLT	A,.WTOP-.WBASE-1(W)
	HRRZI	A,RESET
	HRRM	A,JOBSA	;DON'T REDO THE INITIAL STORAGE ALLOCATION
FOR TEN50,<
	HRRZ	A,JOBREL
	CAMGE	A,@WTOP	;IS WTOP INSIDE CURRENT ALLOCATION?
	EXPAND	WS	;NO, GET A K SO WS WILL FIT
	PUSHJ	P,SETTTM	;SET TELETYPE MODE TO BBN'S TELCOMP MODE
	MOVEI	A,220000
	CALL	A,[SIXBIT /APRENB/]	;REQUEST PDL AND ILL MEM REF TRAPS
>
FOR TENEX,<
	MOVEI	A,@WTOP
	JSP	E,SETMEM	;LOCK PAGE ABOVE ABOVE ADDRESS
>
	MOVEI	B,[ASCIZ /
LOGO

/]
	TRZN	F,TF	;1 IF CALLED BY ERASE ALL
	PUSHJ	P,TOSS


RESET:	MOVE	P,PP
	SUB	P,PP+1
	HRLOS	P
	HRR	P,PP
	SUBI	P,1	;DON'T WASTE ONE ON PROCEDURE PUSHDOWN
	MOVE	S,SP
	SUB	S,SP+1
	HRLOS	S
	ADD	S,SP
	SETZB	F,PRODNM
	SETZM	GODEPTH
	MOVE	A,CDBOT		;MAKE THE BASE OF ALL DIRECT LINES
	MOVEM	A,CTOP		;THE TOP OF ALL COMPILED LINES AND
	MOVEM	A,CBOT		;BASE OF CURRENT DIRECT LINE
	SETZM	TRACEM	;TRACE MARGIN 
	HRRZ	W,WS	;W INDEXES TO BASE OF WORK SPACE
	MOVEI	B,2
	MOVEM	B,DTOP	;FLUSH ALL DUMMIES
FOR TEN50,<	PUSHJ	P,SETTTM	;ENTER TELCOMP MODE
	MOVEI	A,220000
	CALLI	A,16>		;APR ENABLE

FOR TENEX,<
	CIS		;CLEAR PSI SYSTEM
	MOVEI	A,400000	;DENOTE THIS FORK
	MOVE	B,[XWD LEVTAB,CHNTAB]
	SIR			;SET UP PSI SYSTEM
	MOVEI	A,0		;BREAK TO CHANNEL 0
	ATI			;ASSIGN TERMINAL INTERRUPT
	MOVEI	A,400000	;THIS FORK
	MOVE	B,[XWD 400702,0];CHANNELS 0,9,10,11,16
	AIC			;ACTIVATE THESE CHANNELS
	EIR		;ENABLE THE SYSTEM
	MOVEI	A,101
	MOVE	B,[EXP 525252525252]
	MOVE	C,B
	SFCOC		;SET ALL CONTROL CHAR TYPEOUTS TO BE THEMSELVES
	MOVEI	A,100
	RFMOD		;READ THE TERMINAL INPUT MODES
	ANDCMI	B,770000	;ZERO OUT THE WAKEUP SET
	IORI	B,140000	;SET THE WAKEUP SET TO ALL CONTROLS
	SFMOD			;SET THEM
>

MAINL:	MOVEI	B,[ASCIZ /_/]
	SKIPE	TOPROD	;ARE WE DEFINING A PROCEDURE NOW?
	MOVEI	B,[ASCIZ />/]	;YES
	PUSHJ	P,TIS
	 JRST	MAINL
	PUSHJ	P,COMPIL
	 JRST	MAINL	;GOT A STORED LINE
	PUSHJ	P,EXECUT
	JRST	MAINL

DOUUO:	MOVEM	17,UUOACS+17
	HRRZI	17,UUOACS
	BLT	17,UUOACS+16
	MOVE	17,UUOACS+17
	HLRZ	A,JOBUUO
	ASH	A,-11	;FLUSH AC, IF ONE
	CAILE	A,4	;SQUEZE IS THE HIGHEST KNOWN UUO
	JRST	ILLUUO
	JRST	@.(A)	;ZERO CAUGHT BY MONITOR
	 ERRORR
	 ALLOCATOR
	 CALGAR
	 CMPRSS
CALGAR:	JSP	P,GARCOL	;NO PUSHES OR POPS IN GC
	JRST	UUORET

FOR TEN50,<
CALDDT:	MOVEI	A,20000
	TTCALL	6,A
	TLZ	A,600000
	TTCALL	7,A	;LEAVE TELCOMP MODE
	HRRZ	A,JOBDDT
	JUMPE	A,[ERROR .]
	PUSHJ	P,(A)	;SO DEBUGGER MAY POPJ P,$X TO RETURN
	PUSHJ	P,SETTTM
	JRST	COMEX

SETTTM:	MOVEI	A,20000	;SUBROUTINE TO SET TELCOMP TTY MODE
	TTCALL	6,A
	TLO	A,600000
	TTCALL	7,A
	POPJ	P,

BREAKY:	TTCALL	11,
	TTCALL	12,
	TLNE	F,TIF	;LEAVE IT SET FOR BRAKER ROUTINE
	ERROR	BREAK	;BREAK IMMEDIATELY IF TYPE IN HUNG
	TLO	F,BREAKF	;OTHERWISE, JUST FLAG IT FOR LATER
	JRST	2,@JOBOPC
>
FOR TENEX,<
BREAKY:	MOVEM	A,BRKTEM
	MOVEI	A,100
	CFIBF		;CLEAR THE INPUT BUFFER
	MOVEI	A,101
	CFOBF		;OUTPUT BUFFER TOO
	MOVE	A,BRKTEM
	TLNE	F,TIF
	ERROR	BREAK
	TLO	F,BREAKF
	DEBRK			;GOT HERE FROM PSI, NOT TTY

CALDDT:	PUSHJ	P,770000	;FOR NOW, ASSUME RESIDENT DDT
	JRST	COMEX
>

COMPIL:	TRZ	F,STORED
	TRZ	F,FSYMF		;ENTER HERE FROM CMAKE
	POP	S,L
	PUSH	P,CTOP
	MOVEI	M,0
COMPAB:	HRLI	L,10700+W
	MOVEM	L,LINBOT
	IBP	L
GNS:	TRZ	F,NNUMF!PMF
	SETZB	N,SCOUNT	;CLEAR TYPE OF SYMBOL (N) AAND CHAR COUNT
	PUSHJ	P,NEWSTR
GNS1:	LDB	C,L	;PICK UP PREVIOUS TERMINATOR, MAY BE RELEVANT
	JRST	.+2
	ILDB	C,L
	CAIN	C," "
	JRST	.-2
	JUMPE	C,ABBJ	;END OF STRING
	CAIN	C,"("	;LEFT PARANTHESIS
	JRST	GNSLP
	CAIN	C,")"
	JRST	GNSRP	;RIGHT PARANTHESIS
	CAIN	C,"/"
	JRST	GNSVAR	;VARIABLE
	CAIE	C,024	;^T, DELIMITS LITERALS FROM FILES
	CAIN	C,042	;"-A LITERAL
	JRST	GNSLIT
	CAIN	C,";"
	JRST	GNSCOM
	CAIE	C,"+"
	CAIN	C,"-"
	JRST	GNSPM	;PLUS OR MINUS
GNSA:	CAIL	C,"0"	;IS IT A NUMBER
	CAILE	C,"9"
	TRO	F,NNUMF	;NOT NUMBER-SET NON NUMBER FLAG
GNSB:	IDPB	C,B		;STORE THE CHARACTER
	AOS	SCOUNT	;COUNT CHARACTER
	ILDB	C,L
	CAIE	C," "	;IS NEXT BYTE A TERMINATOR
	CAIN	C,"("
	JRST	GNSEND
	CAIE	C,")"
	CAIN	C,"/"
	JRST	GNSEND
	CAIN	C,";"
	JRST	GNSEND
	CAIN	C,024	;^T, LIKE DOUBLE QUOTE
	JRST	GNSEND
	CAIE	C,042	;DOUBLE QUOTE
	JUMPN	C,GNSA
GNSEND:	PUSH	P,WTOP		;IN CASE WE WANT TO FLUSH NEW SYMBOL
	PUSHJ	P,ENDSTR	;GOT A SYMBOL, FINISH IT
	POP	S,A		;THIS IS IT
	TRNN	F,NNUMF	;MIGHT IT BE A NUMBER, IE A LITERAL
	JRST	GNSN	;IT IS A POSSIBLE LINE NUMBER

;CHECK ELEMENT TO SEEE IF IT IS AN ABBREVIATION

ABBA:
	HRRZ	B,UA	;USER DEFINED ABBREVIATIONS FIRST
	PUSHJ	P,LOOKUP
	 JRST	ABBC
	MOVE	C,1(N)
	CAMN	C,[EXP -1]
	JRST	NOABBS	;A DELETED ABBREVIATION
	POP	P,WTOP	;THIS ELEMENT EXISTS, DON'T NEED ANOTHER COPY
	PUSH	S,LINBOT
	SUB	L,LINBOT
	PUSH	P,L	;SAVE RELATIVE POINTER INTO SOURCE STRING
	HRRZ	L,1(N)	;USE THE VALUE OF THE ABBREV AS THE NEW SOURCE
	AOJA	M,COMPAB	;WE ARE NOW ONE LEVEL DEEPER IN ABBREVS

ABBC:	MOVE	A,NEWBOT
	HRRZI	B,ABBT	;NOT USER ABBREV, TRY SYSTEM ABBREVS
	PUSHJ	P,SYSLUK
	 JRST	NOABBS	;NOT AN ABBREVIATION
	POP	P,WTOP
	PUSH	S,LINBOT
	SUB	L,LINBOT
	PUSH	P,L
	HRRZ	L,1(N)
	HRLI	L,350700
	SETZM	LINBOT
	AOJA	M,GNS


ABBJ:	JUMPE	M,CMPLA	;TOP LEVEL,DONE WITH WHOLE INPUT,ADD TERMINATOR
	POP	S,LINBOT	;NOT TOP LEVEL
	POP	P,L
	ADD	L,LINBOT
	SOJA	M,GNS1	;UP A LEVEL

CMPLA:	PUSHJ	P,CSTORE
	TRO	F,FSYMF	;NO LONGER FIRST ELEMENT
	JUMPE	N,CMPEND
	TLNN	N,COMMTF
	JRST	GNS
	SETZ	N,
	JRST	CMPLA	;FOR NOW, COMMENT IMMEDIATELY PRECEDES EOL
CMPEND:	TRNE	F,MAKEF
	JRST	APOPJ
	TRZE	F,STORED
	JRST	TOLINE
	POP	P,A
	MOVEM	A,CPP	;WILL START EXECUTING HERE
	MOVEM	A,CBOT	;REMEMBER FOR POSTERITY
	SUB	A,CDBOT	;MAKE IT RELATIVE TO BASE OF DIRECT LINES
	EXCH	A,0(P)	;PUT IT ON THE STACK
	JRST	1(A)	;R2 TO DENOTE IT NEEDS EXECUTION

;NOT AN ABBREVIATION, CHECK FOR EXISTING PROCEDURE NAMES

NOABBS:	MOVE	A,NEWBOT
	HRRZ	B,RP
	PUSHJ	P,LOOKUP
	 JRST	GNSP2
	POP	P,WTOP	;FLUSH	THE SYMBOL
GNSP1:	SUB	N,RP
	TLO	N,UPRF
	AOJA	N,CMPLA

GNSP2:	PUSH	P,N	;SAVE POINTER TO END OF USER TABLE
	MOVE	A,NEWBOT
	MOVEI	B,CMPT	;MACHINE PROCEDURE TABLE FOR COMPILER
	PUSHJ	P,SYSLUK
	 JRST	GNSP3
	POP	P,A	;FLUSH THE POINTER TO END OF USER TABLE, NOT NEEDED
	POP	P,WTOP	;FLUSH THE SYMBOL
	TLO	N,MPF	;CALL IT A MACHINE PROCEDURE
	SKIPL	(N)	;IS IT SPECIAL (MAKE)
	AOJA	N,CMPLA
	JRST	CMAKE	;WHY, YES

GNSP3:	POP	P,N	;POINTER TO END OF USER PROCEDURE TABLE
	POP	P,E	;FLUSH OLD WTOP, LEAVE NEW ONE, SAVE THE NEW SYMBLO
	MOVEI	E,2(N)	;SPACE FOR THE NEW ENTRY IN TABLE
	CAML	E,RP+1	;IS IT THERE ALREADY?
	EXPAND	RP
	MOVE	A,NEWBOT
	MOVEM	A,(N)	;NAME OF NEW PROCEDURE
	SETOM	1(N)	;NOT YET TO'D
	SETZM	2(N)	;NEW END OF TABLE
	JRST	GNSP1

GNSLP:	SKIPA	N,[EXP LPREN]
GNSRP:	MOVEI	N,RPREN
	IBP	L	;DON'T WANT TO SEE THIS CHARACTER AGAIN
	TLO	N,MPF	;TREAT LIKE ANY MACHINE PROCEDURE
	AOJA	N,CMPLA

GNSN:	MOVEI	A,1
	TRNE	F,PMF	;SAW A SIGN?
	CAME	A,SCOUNT	;YES, WAS IT THE ONLY CHARACTER?
	SKIPA			;NO TO EITHER
	JRST	ABBA		;YES TO BOTH, "+" OR "-" ONLY
	POP	P,A	;FLUSH THE OLD WTOP
	HRLZI	N,LITF!WORDF+W
	HRR	N,NEWBOT
	TRNN	F,FSYMF!MAKEF!PMF  ;IS IT FIRST ELEMENT AND UNSIGNED?
	TRO	F,STORED	;YES, NUMBER+FIRST_STORED LINE
	JRST	CMPLA

GNSPM:	TRO	F,PMF	;SET PLUS-MINUS FLAG
	JRST	GNSB

;COMMENTS, LITERALS, AND VARIABLE NAMES GET COPIED AND EXTRA SPACES FLUSHED

GNSCOM:	TLO	N,COMMTF
	JRST	WEFA	;COMMENTS TERMINATED EITHER BY ";" OR EOL
GNSLIT:	TLOA	N,LITF
GNSVAR:	TLO	N,VARF
WEFA:	MOVEI	D,(C)	;SAVE THE PROPER TERMINATOR
	PUSHJ	P,WEFS
	POP	S,B
	IOR N,B
	TLNE	N,VARF
	TLNN	N,EMPTYF
	SKIPA
	 ERROR	NMERR5		;NAME AND EMPTY
	IBP	L
	JRST	CMPLA

WEFS:	TRZ	F,TF!NWF	;MAKE A WELL-FORMED STRING,
				; I.E. NO LEADING SPACES, NO TRAILING,
				; AND AT MOST ONE IN THE MIDDLE
	ILDB	C,L
	CAIN	C," "
	JRST	.-2
WEFC:	CAIN	C,(D)	;DONE YET?
	JRST	WEFB
	JUMPE	C,WEFD	;NO SECOND " / OR ;
	TRO	F,TF	;HAVE SEEN A REAL CHAR
	IDPB	C,B
	ILDB	C,L
	CAIE	C," "
	JRST	WEFC
	ILDB	C,L
	CAIN	C," "
	JRST	.-2	;SAD SPC
	CAIN	C,(D)
	JRST	WEFB
	JUMPE	C,WEFD
	MOVEI	C," "
	IDPB	C,B
	LDB	C,L	;GET FIRST CHAR OF NEXT WORD AGAIN
	TRO	F,NWF	;GOT MORE THAN ONE WORD
	JRST	WEFC

WEFD:	TLNN	N,COMMTF	;IF A COMMENT, EOL IS OK
	ERROR	INERR1	;BUT NOT LITS OR VARS
WEFB:	TRNE	F,TF	;IS THIS THING EMPTY?
	JRST	ENDSTR	;NO, FINISH THE STRING NORMALLY
	PUSH	S,[XWD W+WORDF!SENTF!EMPTYF!,]	;EMPTY
	POPJ	P,

CMAKE:	TRNE	F,MAKEF	;ALREADY IN A 2 LINE MAKE?
	AOJA	N,CMPLA	;YES
	LDB	C,L
	JUMPE	C,CMAKE2
	MOVE	A,L
	ILDB	C,A
	CAIN	C," "
	JRST	.-2	;SAD SPC
	CAIE	C,";"	;MAKE-COMMENT-CR
	AOJA	N,CMPLA
	PUSHJ	P,NEWSTR
	MOVE	C,LINBOT
	MOVEM	C,SRCBOT	;FOR GC
	PUSH	S,[XWD W+COMMTF,0]
	PUSHJ	P,BTFCP1
	ADD	N,[XWD MAKESC,1]
	PUSHJ	P,CSTORE
	POP	S,N
	JRST	CMAKE3
CMAKE2:	ADD	N,[XWD MAKESC,1]
CMAKE3:	TRO	F,MAKEF
	PUSHJ	P,CSTORE
	MOVEI	B,[ASCIZ /   NAME: /]
	PUSHJ	P,TIS		;ASK FOJ IT
	JRST	.-2
	PUSHJ	P,COMPIL+1
	SOS	A,CTOP
	MOVSI	B,MAKESC
	IORM	B,@CSA
	MOVEI	B,[ASCIZ /   THING: /]	;CONTINUE TO ASK
	PUSHJ	P,TIS
	JRST	.-2
	PUSHJ	P,COMPIL+1
	TRZ	F,MAKEF
	JRST	CMPEND


CSTORE:	MOVE	A,CTOP
	MOVEM	N,@CSA
	AOS	A,CTOP
	MOVEI	A,@CSA
	CAML	A,CS+1
	EXPAND	CS
	POPJ	P,

EXECUTE:
	HRRZI	A,(S)
	SUB	A,SP
	MOVEM	A,SSP
	HRRZI	A,(P)
	SUB	A,PP
	MOVEM	A,SPP
EXECU0:	PUSH	P,[EXP [EXP [ERROR NOCMD]]]
	TLZE	F,BREAKF
	ERROR	BREAK
	MOVE	A,CPP
EXECU1:	SKIPN	A,@CSA	;IS IT EOL?
	PUSHJ	P,SCOMEX	;YES, CALL THE EXIT
	TLNN	A,COMMTF	;IS IT A COMMENT?
	JRST	EXCTL1		;NO, GO TO IT
	AOS	A,CPP
	JRST	EXECU1		;IGNORE COMMENT

EVAL:	PUSH	P,[EXP [EXP APOPJ]]
	SKIPA	A,CPP
EXCTLL:	AOS	A,CPP	;POINT TO NEXT ELEMENT
	MOVE	A,@CSA
EXCTL1:	JFFO	A,.+2
	ERROR	ERMSSG	;END OF LINE, SOMETHING MISSING
	JRST	@.+1(B)
	 EXCTMP
	 EXCTUP
	 EXCTV
	 EXCTL
	 EXCTLL	;COMMENT, TREATED JUST LIKE IT DOESN'T EXIST


EXCTMP:	HLL	A,(A)		;FETCH NUMBER OF ARGUMENTS
EXCTP1:	PUSH	P,A
	JUMPL	A,EXCTP4	;NEED NO ARGS
	AOS	A,CPP
	MOVE	A,@CSA
	CAMN	A,[XWD MPF,OFL+1]
	JRST	EXCTLL	;IS OF
	JRST	EXCTL1	;NOT OF

EXCTP4:	POP	P,THISPR
	MOVE	A,(A)		;FETCH ADDRESS OF ROUTINE
	PUSHJ	P,(A)	;CALL PROCEDURE

OPEX:	HRLZI	A,-1
	ADDB	A,0(P)
	JUMPL	A,EXCTP4	;NEED NO MORE ARGS

	AOS	A,CPP
	MOVE	A,@CSA
	CAMN	A,[XWD MPF,ANDL+1]
	JRST	EXCTLL	;IS AND
	JRST	EXCTL1	;NOT AND

EXCTUP:	HLL	A,@RPA	;FETCH NO ARGS-1
	PUSH	P,A
	HRRI	A,[EXP UPROD]	;PRESERVE NO ARGS IN L HALF
	JRST	EXCTP1


EXCTV:	PUSH	S,A
	PUSHJ	P,THING1
	JRST	OPEX

EXCTL:	PUSH	S,A
	JRST	OPEX

EXCTLP:	MOVE	A,[EXP [EXP EXPARR]]
	MOVEM	A,(P)	;REPLACE PTR TO OPEX WITH THIS
	JRST	EXCTLL

EXCTRP:	POP	P,A
	POP	P,A
	CAIE	A,[EXP EXPARR]
	ERROR	PRNER1	;MATCHING (?
	PUSHJ	P,GNE		;WAS (), WHERE TO GO?
	 PUSHJ	P,SCOMEX	;WAS () ALONE, DONE
	JRST	EXCTL1	;CONTINUE

EXPARR:	PUSHJ	P,GNE
	 SKIPA
	CAME	A,[XWD MPF,RPREN+1]
	ERROR	PRNER2	;MISSING )?
	POPJ	P,	;PRENS MATCH

SPECWD:	ERROR	EVER5
EXIT:	ERROR	XITERR

SCOMEX:	SOS	CPP
COMEX:	JSP	D,COMEXR
	POPJ	P,

COMEXU:	TLOA	F,UPFF		;A USER DEFINED COMMAND
COMEXR:	TLZ	F,UPFF		;A SYSTEM COMMAND
	POP	P,A	;THE RETURN TO OPEX
COMXRA:	POP	P,A
	CAMN	A,[EXP [ERROR NOCMD]]
	JRST	COMXRB	;PRENS MATCH AND ONLY ONE COMMAND ON LINE
	CAIE	A,[EXP EXPARR]	;POSSIBLE LEADING "("?
	 ERROR	COMERR	;NO
	AOS	A,CPP	;CHECK FOR MATCHING ")"
	MOVE	B,[XWD MPF,RPREN+1]
	CAMN	B,@CSA
	JRST	COMXRA	;THIS PAIR MATCH, TRY TEST OVER
	 ERROR	PRNER2
COMXRB:	SETZ	M,	;PREN COUNTER
COMXRC:	PUSHJ	P,GNE	;NEXT ELEMENT
	 JRST	COMXRD	;NO MORE
	CAMN	A,[XWD MPF,LPREN+1]
	AOJA	M,COMXRC	;A LEFT PREN, BUMP COUNTER
	CAME	A,[XWD MPF,RPREN+1]
	ERROR	ERXTRA		;NEITHER ( OR ), MUST BE ILLEGAL
	SOJGE	M,COMXRC	;),DECREMENT CTR, BACK FOR MORE
	ERROR	PRENR1		;M<0 _ MORE ) THAN (

COMXRD:	JUMPN	M,[ERROR PRENR2]	;PRENS DON'T COUNT OUT
	AOS	A,CPP
	ANDI	A,-1	;NOT NECESSARY IF ALL SETS OF CPP ARE HRRZM'S
	CAME	A,CTOP	;DID WE JUST FINISH A DIRECT LINE?
	JRST	(D)	;NO
	MOVE	A,-1(P)
	POP	P,-1(P)
	ADD	A,CDBOT
	MOVEM	A,CBOT
	MOVEM	A,CTOP
	JRST	(D)


CALLDO:	JSP	D,COMEXR ;CHECK IF AT BEG OF LINE, IF NOT DON'T RETURN
	PUSHJ	P,COMPIL
	 POPJ	P,	;STORED THE LINE, NO NEED TO EXECUTE
	MOVE	A,(P)
	EXCH	A,-1(P)
	MOVEM	A,(P)	;PUT PTR TO "DIRECT" LINE IN BEHIND THE RETURN
	JRST	EXECU0

;TABLE ADDRESS IN B
;SYMBOL POINTER IN A
;INDEX FOR ENTRIES IN TABLE B IN LH OF C IF NECESSARY


LOOKBK:	MOVEI	N,-2(B)	;SEARCH BACKWARDS, POINT AT LAST ENTRY
	HRROI	E,-2	;NOT JUST AFTER IT
	HRLZI	C,W
	JRST	LOOK1


SYSLUK:	MOVEI	C,0
	JRST	LOOK0

LOOKUP:	HRLZI	C,W
LOOK0:	MOVEI	E,2
	MOVEI	N,(B)	;USE N FOR STEPPING THRU TABLE

LOOK1:	ADDI	A,(W)	;MAKE A ABSOLUTE
	MOVN	B,(A)	;GET THE LENGTH OF THE WS ELEMENT
	HRLI	A,-1(B)	;PUT -L-1 OF WS ELEMENT IN LH OF A
	TLNN	C,W
	AOBJN	A,.+1

LOOKL:	MOVE	B,A	;USE B FOR CHANGING A
	HRR	C,(N)	;GET THE NAME OF THIS ENTRY IN THIS TABLE
	TRNN	C,-1	;0 AT END OF TABLE
	POPJ	P,

	MOVE	D,@C	;GET A WORD OF THIS ENTRY
	CAME	D,(B)	;IS IT THE SAME AS THIS WORD OF NEW ELEMENT
	JRST	LOOKN	;NO, NOT A MATCH FOR THIS SYMBOL
	ADDI	C,1	;POINT AT NEXT WORD OF THIS ENTRY
	AOBJN	B,.-4	;IF NOT DONE WITH THIS ELEMENT, GO BACK
	JRST	CPOPJ1	;DONE WITH THIS SYMBOL, COMPLETE MATCH

LOOKN:	ADD	N,E
	JRST	LOOKL	;NEXT ENTRY
THING:	MOVE	A,(S)
THING1:	MOVE	B,DP
	ADD	B,DTOP
	PUSHJ	P,LOOKBK
	 JRST	THING2	;NOT A DUMMY
	JRST	THING4

THING2:	MOVE	A,(S)
	MOVE	B,VP
	PUSHJ	P,LOOKUP
	 JRST	THING3	;NOT A GLOBAL VARIABLE
	JRST	THING4

THING3:	MOVE	A,(S)
	MOVEI	B,MV
	PUSHJ	P,SYSLUK
	 MOVEI	N,MV	;FIRST MACHINE VAR IS EMPTY
	MOVE	A,1(N)	;MACHINE VARIABLES MUST BE CHECKED
	JUMPL	A,(A)	;THIS ONE MUST BE COMPUTED, (FLAG IS SIGN BIT)
THING4:	MOVE	A,1(N)
	MOVEM	A,(S)
	POPJ	P,


CCONTE:	POP	S,A	;COMPUTE /CONTENTS/
	PUSHJ	P,NEWSTR
	TRZ	F,TF!NWF
	MOVEI	C,0
	MOVE	D,RP
	MOVNI	E,1
CCNTE1:	SKIPN	A,(D)		;ANY MORE PROCEDURE NAMES?
	JRST	ENDST1		;NO
	MOVEI	D,1(D)
	CAMN	E,(D)		;IS THIS ONE DEFINED?
	AOJA	D,CCNTE1	;NO
	MOVEI	C," "
	TRON	F,TF		;FIRST ONE?
	JRST	.+3		;YES
	TRO	F,NWF		;NOT FIRST
	DPB	C,B		;NO REPLACE EOM WITH SPACE
	PUSHJ	P,NEWSR0
	PUSHJ	P,COPYAB
	AOJA	D,CCNTE1

FIRST:
	PUSHJ	P,NEWOPS	;CHECK EMPTY,SET UP SRCBOT+NEWBOT
	JRST	FSTSNT	;NO, SENTENCE

	ILDB	C,A	;FIRST BYTE
	IDPB	C,B	;BOMBS HERE ON FULL WORKSPACE

	MOVEI	C,0	;FILL ZEROES AND RETURN NEW ARG
	JRST	BTFCLO

FSTSNT:	ILDB	C,A	;FIRST OF SENTENCE
	IDPB	C,B	;COPY CHARACTERS TO END OF FIRST WORD
	CAIE	C," "	;IS IT THE END OF A WORD
	JUMPN	C,FSTSNT	;JUMP IF NOT THE ONE WORD SENTENCE

	MOVEI	C,0	;CLEAR " " OR 177
	DPB	C,B	;CLOBBER OTHER TERMINATOR

	HRLZI	A,W+WORDF
	HLLM	A,0(S)
	JRST	BTFCL1


BUTFIRST:
	PUSHJ	P,NEWOPS
	JRST	BTFSNT	;NOT A WORD OR EMPTY, IE SENTENCE

	IBP	A		;BUTFIRST OF WORD, SKIP OVER FIRST CHAR
	ILDB	C,A		;THIS IS THE SECOND CHAR
	JUMPN	C,BTFCOP	;MORE THAN ONE CHAR, COPY TO END OF STRING

BTFEMP:	HRLZI	A,W+WORDF!SENTF!EMPTYF	;ONE OF THE MANY WAYS T
	MOVEM	A,(S)	;TO GET A POINTER TO EMPTY
	POPJ	P,

BTFSNT:	ILDB	C,A	;SKIP OVER FIRST WORD
	JUMPE	C,BTFEMP	;WAS ONLY WORD, RESULT IS EMPTY
	CAIE	C," "	;END OF WORD?
	JRST	BTFSNT	;CONTINUE SKIPPING OVER FIRST WORD

BTFCP1:	ILDB	C,A
BTFCOP:
BTFCLO:	IDPB	C,B
	JUMPN	C,.-2	;COPY THRU END OF ARG

BTFCL1:	PUSHJ	P,ENDST1
	POP	S,A
	HRRM	A,(S)	;NEW STRING, OLD TYPE
	POPJ	P,

LAST:
	PUSHJ	P,NEWOPS
	JRST	LSTSNT

	ILDB	C,A
	IDPB	C,B	;COPY FIRST CHAR, BUMPING THE CHAR PTR IN B ONCE

	ILDB	C,A
	JUMPE	C,BTFCLO	;DONE WITH THE WORD
	DPB	C,B	;PUT THIS CHAR ON TOP OF PREVIOUS ONE
	JRST	.-3

LSTSNT:	MOVE	E,A	;SAVE POINTER TO BEGINNING OF WORD
LSTSN2:	ILDB	C,A	;CONTINUE WITH THE CURRENT WORD
	CAIN	C," "
	JRST	LSTSNT	;END OF CURRENT WORD, NOT LAST ONE
	JUMPN	C,LSTSN2	;NULL_END OF SENT, ELSE NOT END OF ANY WORD

	MOVE	A,E
	JRST	FSTSNT

BUTLAST:
	PUSHJ	P,NEWOPS
	JRST	BTLSNT

	ILDB	C,A	;BUTLAST OF WORD, COPY WORD AND CLOBBER LAST CHAR
	IDPB	C,B
	ILDB	C,A
	JUMPE	C,BTFEMP	;WAS A ONE CHAR WORD, MAKE AN EMPTY
	IDPB	C,B
	ILDB	C,A
	JUMPN	C,.-2	;COPY REST OF WORD
BTLCLO:	DPB	C,B	;CLOBBER LAST CHAR
	JRST	BTFCL1	;FILL TO END OF WORD

BTLSNT:	MOVE	E,A	;SAVE POINTER TO BEGINNING
BTLSN1:	ILDB	C,A	;STEP THRU FIRST WORD
	JUMPE	C,BTFEMP	;ALSO LAST, MAKE AN EMPTY
	CAIE	C," "
	JRST	BTLSN1	;CONTINUE WITH FIRST WORD

BTLSN2:	MOVE	A,E
BTLSN9:	ILDB	C,A		;COPY STEPPED OVER WORD
	IDPB	C,B
	CAIE	C," "
	JRST	BTLSN9

BTLSN3:	MOVE	E,A	;SAVE POINTER TO NEXT WORD
BTLSN4:	ILDB	C,A
	JUMPE	C,BTLCLO	;THIS IS END OF LAST WORD
	CAIN	C," "
	JRST	BTLSN2	;END OF WORD, NOT LAST ONE, COPY IT
	JRST	BTLSN4	;NOT END OF WORD, FIND IT

WORD:
	HRLZI	A,WORDF
	TDNE	A,0(S)
	TDNN	A,-1(S)
	ERROR	WRDERR	;YOU CAN'T MAKE A WORD OUT OF A SENTENCE

	HRLZI	C,EMPTYF
	TDNN	C,-1(S)	;IS FIRST ARG EMPTY?
	JRST	WORD0	;NO
	POP	S,-1(S)	;YES, THEN SECOND ARG IS THE RESULT
	POPJ	P,

WORD0:	TDNE	C,(S)
	JRST	SPOPJ
WORD1:	PUSHJ	P,NEWSTR	;SET UP B AND NEWBOT
	PUSHJ	P,NEWSR1	;SET UP A TO -1(S) AND SRCBOT

	ILDB	C,A
	JUMPE	C,WORD2	;DON'T COPY EOM FROM FIRST ARG
	IDPB	C,B
	JRST	.-3	;COPY WHOLE FIRST ARG

WORD2:	PUSHJ	P,NEWSRC	;SET UP A TO 0(S) AND SRCBOT
	ILDB	C,A
	IDPB	C,B	;COPY SECOND ARG, INCLUDING EOM
	JUMPN	C,.-2
	POP	S,A	;FLUSH SECOND ARG
	JRST	BTFCL1	;FILL ZEROES AND RETURN ONE ARG

SENTENCE:
	HRLZI	E,EMPTYF
	TDNE	E,0(S)
	TDNN	E,-1(S)
	JRST	SENT1

SPOPJ:	POP	S,A	;BOTH EMPTY
	POPJ	P,	;RETURN EMPTY

SENT1:	PUSHJ	P,NEWSTR
	PUSHJ	P,NEWSR1

SENT2:	ILDB	C,A
	JUMPE	C,SENT3
	IDPB	C,B
	JRST	SENT2

SENT3:	MOVEI	C," "
	PUSHJ	P,NEWSRC
	TDNN	E,-1(S)		;IF FIRST ARG IS EMPTY, DON'T SPACE

SENT4:	IDPB	C,B
	ILDB	C,A
	JUMPN	C,SENT4

	POP	S,A	;FLUSH SECOND ARG
	HRLZI	A,W+SENTF	;CALL NEW THING A SENTENCE
	HLLM	A,0(S)
	JRST	BTFCLO


COUNT:	MOVE	A,(S)	;THING TO COUNT
	MOVEI	B,0	;COUNT INITIALLY ZERO
	TLNE	A,EMPTYF	;IS IT EMPTY?
	JRST	COUNT2	;COUNT=0
	TLNN	A,WORDF	;IS IT A WORD?
	AOJA	B,COUNT1	;NO, IT IS A NON-EMPTY SENTENCE

	HRLI	A,440700+W
	MOVNI	B,1
	ADD	B,@A	;NOW NUMBER OF WORDS -1
	IMULI	B,5	;NUMBER OF CHARS IN ALL BUT LAST WORD
	ADD	A,@A	;NOW A POINTS AT LAST WORD
	ILDB	C,A	;WORD
	JUMPE	C,COUNT2
	AOJA	B,.-2

COUNT1:	HRLI	A,010700+W
COUNTL:	ILDB	C,A
	JUMPE	C,COUNT2
	CAIN	C," "
	AOJA	B,COUNTL	;COUNT WORDS OF SENT AT BEG OF WORD
	JRST	COUNTL		;MORE OF THE SAME WORD

COUNT2:
	MOVE	A,B	;ARG TO SNM IN A
	PUSHJ	P,SNM
	MOVSI	C,W+WORDF
	HLLM	C,(S)
	JRST	BTFCLO

;THIS RANDOM NUMBER GENERATOR IS FROM PDP-9 KALEIDASCOPE

RANDOM:	MOVE	C,[EXP 1000003]
	IMULB	C,RANNO
	MULI	C,12
	ADDI	C,"0"
	PUSHJ	P,NEWSTR
	IDPB	C,B
	JRST	ENDSTR


EITHER:	TROA	F,TF
BOTH:	TRZ	F,TF
	MOVE	A,(S)
	PUSHJ	P,PREDIQ
	 ERROR	PREDR2
	MOVE	A,-1(S)
	PUSHJ	P,PREDIQ
	 ERROR	PREDR2
	POP	S,B	;SECOND ARG
	MOVEI	A,1	;LENGTH OF "TRUE"
	TRZN	F,TF	;SKIP IF DOING EITHER
	MOVEI	A,2	;LENGTH OF "FALSE"
	CAMN	A,@WSB	;COMPARE AGAINST LENGTH OF SECOND ARG
	MOVEM	B,(S)	;JAM IF EITHER AND TRUE OR BOTH AND FALSE
	POPJ	P,


EMPTYP:	HRLZI	A,EMPTYF
PREDS1:	POP	S,B
	TDNN	A,B
	JRST	ISFALSE
	JRST	ISTRUE
SENTP:	HRLZI	A,SENTF
	JRST	PREDS1
WORDP:	HRLZI	A,WORDF
	JRST	PREDS1

IS:	POP	S,A
	POP	S,B
IS1:	MOVE	C,@A
	CAME	C,@B
	JRST	ISFALSE
	ADDI	A,1
	TRNE	C,377
	AOJA	B,IS1

ISTRUE:	SKIPA	A,[XWD WORDF+W,TRUEV]	;POINTER TO "TRUE"
ISFALSE:MOVE	A,[XWD WORDF+W,FALSEV]	;WS PTR TO "FALSE"
	PUSH	S,A
	POPJ	P,

NUMBRP:	POP	S,A
	PUSHJ	P,NUMBRQ
	JRST	ISFALSE
	JRST	ISTRUE

GRATRP:	PUSH	P,WTOP	;BOTH ARGS ARE NUMBERS, REMEMBER CURRENT TOP OF WS
	PUSHJ	P,DIFF	;GET THE DIFFERENCE
	POP	P,A	;GET THE OLD WTOP
	CAIGE	A,WTOP	;SKIP IF GC IN BETWEEN
	MOVEM	A,WTOP	;NO, FLUSH ALL GARBAGE GENERATED BY DIFF
	POP	S,A	;GET THE RESULT
	HRLI	A,010700+W
	ILDB	C,A	;GET THE SIGN OF THE RESULT
	CAIE	C,"0"	;IS IT ZERO?
	CAIN	C,"-"	;IS IT NEGATIVE
	JRST	ISFALSE	;EITHER ZERO OR NEGATIVE, CANNOT BE GREATER
	JRST	ISTRUE

ZEROP:	MOVE	A,(S)
	PUSHJ	P,NUMBRQ
	 ERROR	ZERERR
	POP	S,A
	HRLI	A,010700+W
	ILDB	C,A		;GET THE FIRST CHAR
	CAIE	C,"+"	;IF A SIGN, GET ANOTHER CHAR
	CAIN	C,"-"
ZEROP1:	ILDB	C,A
	JUMPE	C,ISTRUE	;NUMBRQ WOULD FAIL ON SIGN ONLY OR EMPTY
	CAIN	C,"0"
	JRST	ZEROP1
	JRST	ISFALSE


MAXIM:	PUSH	S,-1(S)
	PUSH	S,-1(S)	;COPY OF ARGS TO BE DESTROYED BY DIFF
	JRST	MINMAX
MINIM:	PUSH	S,0(S)
	PUSH	S,-2(S)	;OPPOSITE ORDER FROM MAX
MINMAX:	PUSHJ	P,DIFF
	POP	S,A	;RESULT OF SUBTRACTION
	POP	S,B	;SECOND INPUT TO MIN OR MAX
	HRLI	A,010700+W
	ILDB	C,A		;GET SIGN OF RESULT
	CAIN	C,"-"	;IF POSITIVE, THEN FIRST IS RESULT
	MOVEM	B,0(S)	; ELSE SECOND ARG IS THE ANSWER
	POPJ	P,

DIFF:	TROA	F,PMF
SUM:	TRZ	F,PMF
	MOVE	A,(S)
	PUSHJ	P,NUMBRQ
	 ERROR	SUMERR
	MOVE	A,-1(S)
	PUSHJ	P,NUMBRQ
	 ERROR	SUMERR

	PUSHJ	P,REVERS
	CAIE	D,"-"
	TRON	F,PMF	;SKIP IF SECOND ARG + AND IS SUBTRACTION
	TRON	F,PMF	;SKIP IF ARG2 - & DIFF OR ARG2 + & SUM
	PUSHJ	P,TENCOM	;CALLED WITH PMF=1_FILL 9'S

	MOVE	A,(S)
	EXCH	A,-1(S)
	MOVEM	A,(S)

	PUSHJ	P,REVERS
	CAIN	D,"-"
	PUSHJ	P,TENCOM

	PUSHJ	P,SUMMER
	TRZ	F,PMF
	CAIN	G,"9"	;RESULT NEG IFF HIGH ORDER DIG =9
	PUSHJ	P,TENCOM	;CALLED WITH PMF=0_PUT A "-" AT THE END

	POP	S,A	;FLUSH EXTRA LEADING ZEROES
	HRLI	A,350700+W	;SKIPPING OVER FIRST CHAR
	AOJA	A,.+2
SUM8:	CAIE	C,"0"	;CONSECUTIVE 0?
	MOVE	B,A	;NO, SAVE POINTER TO HIGHEST SIGNIFICANT DIG SO FAR
	ILDB	C,A
	JUMPE	C,.+4	;END OF STRING
	CAIE	C,"-"	;MINUS SIGN LIKE A TERMINATOR FOR NEG STRING
	JRST	SUM8
	IDPB	C,B	;PUT TERMINATOR (IN C) ABOVE MOST SIGNIFICANT DIGIT
	PUSHJ	P,ENDSTR

;FALL INTO REVERB - REVERSE BACK - AND EXIT FROM SUM FROM REVERB


REVERB:	TROA	F,TF	;REVERSING BACK, NO MODIFICATION TO STRING
REVERS:	TRZ	F,TF	;FIRST REVERSE OF NUMBER, FIDDLE WITH SIGNS
	PUSHJ	P,NEWSTR
	PUSHJ	P,NEWSRC
	MOVE	D,@A
	ADDI	A,(D)	;A POINTS AT LAST WORD
	MOVEI	G,2(D)
	MOVE	C,@A
	TRNE	C,77400	;IS NEXT TO LAST CHAR EOM?
	TRNN	F,TF	;SKIP IF BOTH PENULT NON-ZERO AND ADDING A CHAR
	MOVEI	G,-1(G)
	HRRZI	C,@WTOP
	ADDI	C,(G)
	CAMLE	C,JOBREL
	 EXPAND	WS
	MOVE	C,@A
	MOVEI	G,0
	JUMPE	C,REVERE
	ROT	C,6
	ROT	C,-7
	TRNN	C,177
	AOJA	G,.-2
	ROT	G,1	;TIMES TWO
	JRST	.+4(G)
REVERL:	SUBI	A,1
	MOVE	C,@A
	ROT	C,-1	;FLUSH BIT 35
	IDPB	C,B
	ROT	C,-7
	IDPB	C,B	;D
	ROT	C,-7
	IDPB	C,B	;C
	ROT	C,-7
	IDPB	C,B	;B
	ROT	C,-7
	IDPB	C,B	;A
REVERE:	SOJG	D,REVERL	;MORE WORDS TO REVERSE
	TRNE	F,TF
	JRST	REVERO
	MOVEI	D,177
	ANDI	D,(C)	;SAVE STATE OF SIGN IN D
	MOVEI	C,"0"
	CAIE	D,"+"
	CAIN	D,"-"
	DPB	C,B	;REPLACE THE SIGN WITH A "0"
	IDPB	C,B		;PUT A ZERO FOR UNSIGNED NUMBERS

REVERO:	MOVEI	C,0
		JRST	BTFCLO


TENCOM:
	PUSHJ	P,NEWSRC
	PUSHJ	P,NEWSTR
	TRO	F,TF	;SET CARRY INTO LOW ORDER DIGIT
	ILDB	C,A
	JUMPE	C,[ERROR]
NINECM:	SUBI	C,"0"
	MOVNS	C
	ADDI	C,"9"
	TRZE	F,TF	;WAS THERE A CARRY INTO THIS POSITION?
	ADDI	C,1	;YES
	CAIG	C,"9"	;IS THERE A CARRY OUT OF THIS POS?
	JRST	.+3	;NO
	SUBI	C,12
	TRO	F,TF
	IDPB	C,B
	ILDB	C,A
	JUMPN	C,NINECM

	MOVEI	D,"-"
	TRNN	F,PMF
	DPB	D,B
	JRST	ENDSTP

SUMMER:	MOVE	L,-1(S)
	TRZ	F,TF!PMF
	HRLI	L,010700+W
	MOVEM	L,LINBOT
	PUSHJ	P,NEWSRC
	PUSHJ	P,NEWSTR

SUMMRL:	ILDB	C,A
	JUMPE	C,SUMMR2
	SUBI	C,"0"
	ILDB	E,L
	JUMPE	E,SUMMR3
	ADDI	C,(E)
	TRZE	F,TF	;CARRY?
	ADDI	C,1	;YES
	CAIG	C,"9"	;CARRY OUT OF THIS POSITION?
	JRST	.+3	;NO
	SUBI	C,12
	TRO	F,TF
	IDPB	C,B
	MOVEI	G,(C)
	JRST	SUMMRL

SUMMR2:	MOVE	C,SRCBOT
	EXCH	C,LINBOT
	MOVEM	C,SRCBOT
	EXCH	A,L
	IBP	A
SUMMR3:	SUBI	L,1	;UNDEX PTR IN L
	REPEAT 3,<IBP L>
	ILDB	C,L	;GET PREVIOUS CHAR, (WAS 0 OR 9)
	CAIN	C,"9"
	TRO	F,PMF	;NO, IT WAS TEN'S COMPLEMENT
	LDB	C,A
	JUMPE	C,SUMMR5

SUMMR4:	TRZE	F,TF
	ADDI	C,1
	TRNE	F,PMF	;WAS IT NEGATIVE?
	ADDI	C,11	;YES, LEADING NINES
	CAIG	C,"9"
	JRST	.+3
	SUBI	C,12
	TRO	F,TF
	IDPB	C,B
	MOVEI	G,(C)
	ILDB	C,A
	JUMPN	C,SUMMR4

SUMMR5:	POP	S,A
	SETZM	LINBOT
	JRST	BTFCLO

;HERE FOLLOW THE COMMANDS

TYPE:	TRZA	F,CRF	;DON'T DO CRLF AFTER TOS
PRINT:	TRO	F,CRF
	POP	S,A	;GET THE THING TO TYPE OR PRINT
	PUSHJ	P,PTOSS
	TRZE	F,CRF	;CALLED BY TYPE OR PRINT?
LISTXT:	PUSHJ	P,CRLF	;PRINT
	JRST	COMEX

ENND:	TRZ	F,FLUSHF	;IF PROD ALREADY DEFINED THEN WE'RE DONE FLUSHING IT
	SKIPN	A,TOPROD
	ERROR	ENDERR	;END WHAT? YOU ARE NOT DEFINING ANYTHING
	TLNE	F,GETF		;IN ANY CASE, IF GETTING
	JRST	ENND1		;DO NOT TYPE "DEFINED"
	MOVEI	A,-1(A)
	MOVE	A,@RPA
	PUSHJ	P,PTOSS
	MOVEI	A,[ASCIZ / DEFINED/]
	PUSHJ	P,PTOSSM
	PUSHJ	P,CRLF
ENND1:	SETZB	A,TOPROD
	PUSHJ	P,MOVEMP
	JRST	COMEX


ABBREVIATE:
	AOS	CPP
	PUSHJ	P,EVAL		;EVAL FIRST ARG
	PUSHJ	P,GNE
	 ERROR	ERMSSG		;SOMETHING MISSING, IE SECOND ARG
	CAMN	A,[XWD MPF,ASL+1]	;IS IT "AS"?
	AOS	CPP		;YES, SKIP IT
	PUSHJ	P,EVAL		;GET SECOND ARG

	MOVE	A,0(S)		;SECOND ARG, ABBREVIATION
	MOVE	B,UA		;USER ABBREVIATION TABLE
	PUSHJ	P,LOOKUP
	 JRST	ABBRV1
	POP	S,A		;FOUND IT, FLUSH ABBREVIATION
	POP	S,1(N)		;SAVE ITS NEW VALUE
	JRST	COMEX

ABBRV1:	MOVEI	A,2(N)		;NOT FOUND, INSERT AT END OF TABLE
	CAML	A,UA+1		;ROOM FOR TWO MORE WORDS AT END?
	EXPAND	UA		;NO, MAKE ROOM
	POP	S,0(N)		;ABBREVIATION FIRST
	POP	S,1(N)		;VALUE SECOND
	SETZM	2(N)		;REPLACE END CONDITION
	JRST	COMEX

TEST:	POP	S,A
	PUSHJ	P,PREDIQ	;IS THE INPUT A PREDICATE?
	 ERROR	PREDR1		;NO
	MOVEI	B,0
	SKIPN	@A	;0 FOR "FALSE"; "TRUE" FOR "TRUE"
	MOVNI	B,1
	MOVEM	B,TRUTH
	JRST	COMEX

IF:	PUSHJ	P,GNE	;GET NEXT ELEMENT
	 ERROR	ERMSSG
	MOVE	C,TRUTH
	CAMN	A,[XWD MPF,TRUEL+1]
	JRST	IFX1	;IF TRUE
	CAME	A,[XWD MPF,FALSEL+1]
	ERROR	IFERR	;MUST BE EITHER TRUE OR FALSE

	JUMPN	C,IFX2	;IF FALSE AND TRUTH=FALSE
IFXFLS:	AOS	A,CPP	;FLUSH THIS LINE
	SKIPE	@CSA
	AOJA	A,.-1
	SUBI	A,1
	MOVEM	A,CPP
	JRST	COMEX

IFX1:	JUMPN	C,IFXFLS	;IF TRUE AND TRUTH=FALSE, FLUSH LINE
IFX2:	POP	P,0(P)	;DO REST OF LINE AS IF IF WASN'T THERE
	JRST	EXCTLL

MAKE:	MOVE	A,-1(S)
	TLNE	A,EMPTYF
	ERROR	NMERR5	;NAME NOT ALLOWED TO BE EMPTY

	HRRZI	B,MV
	PUSHJ	P,SYSLUK
	 JRST	.+2
	ERROR	TOERR3	;X IS USED BY LOGO
	MOVE	A,-1(S)
	HRRZ	B,DTOP
	ADD	B,DP
	PUSHJ	P,LOOKBK	;SEE IF A LOCAL
	 JRST	.+2	;NOT
	JRST	MAKE1
	MOVE	A,-1(S)
	HRRZ	B,VP
	PUSHJ	P,LOOKUP
	 PUSHJ	P,MAKEN	;NOTA, MAKE A NEW GLOBAL
MAKE1:	POP	S,1(N)	;PUT THE VALUE AWAY
	POP	S,A	;FLUSH THE NAME
	JRST COMEX

MAKEN:	MOVEI	B,2(N)
	CAMLE	B,VP+1
	EXPAND	VP
	MOVE	A,-1(S)
	MOVEM	A,(N)
	SETZM	2(N)
	POPJ	P,


LOCAL:	SKIPN	PRODNM
	 ERROR	.	;LOCAL CAN ONLY BE STORED
	MOVE	E,DTOP
	MOVE	D,DP+1
	SUB	D,DP	;END RELATIVE TO BEGINNING
	CAIL	D,2(E)	;WILL ANOTHER ENTRY FIT
	EXPAND	DP	;NO
	ADD	E,DP	;MAKE ABSOLUTE
	POP	S,(E)	;NAME OF VARIABLE
	HRLZI	A,W+WORDF!SENTF!EMPTYF
	MOVEM	A,1(E)	;VALUE OF NEW LOCAL IS EMPTY
	SETZM	2(E)	;NEW END
	MOVEI	A,2
	ADDM	A,DTOP	;TOP OF LIST IS HIGHER

	PUSHJ	P,GNE
	JRST	SCOMEX	;COMMENT ALSO DENOTES END
	CAME	A,[XWD MPF,ANDL+1]	;IS IT A NOISE WORD?
	SOS	CPP	;NO, CONTINUE EVALUATING FROM THIS WORD
	MOVEI	A,[EXP LOCAL]
	MOVEM	A,(P)
	JRST	EXCTLL

;HERE FOLLOWS ALL THERE IS TO USER DEFINED PROCEDURES
;DEFINING THEM, EXECUTING THEM, CHANGING TITLES AND TRACING

TITLE:	SKIPN	TOPROD	;IS THERE A PROCEDURE OPEN?
	ERROR		;NO, CANNOT RETITLE NO PROCEDURE
	PUSHJ	P,GNE
	 ERROR	ERMSSG
	CAME	A,[XWD MPF,TOL+1]	;IS THE NEXT THING "TO"
	ERROR		;WORD AFTER TITLE MUST BE TO
	TLO	F,TITLEF
	JRST	TO0
TO:	TLZ	F,TITLEF
	SKIPE	TOPROD	;IS THERE A PROCEDURE ALREADY OPEN
	ERROR	TOERR4	;YES
TO0:	AOS	D,CPP
	MOVE	A,@CSD
	TLNN	A,UPRF
	ERROR
	MOVNI	B,1
	CAMN	B,@RPA		;IS THE PROCEDURE ALREADY DEFINED?
	JRST	TO1		;NO, ALWAYS OK
	TLNE	F,GETF	;DOING A GET?
	JRST	TOGET		;YES
	HRRZ	B,TOPROD
	TLNE	F,TITLEF	;ARE WE DOING "TITLE"?
	CAIE	B,(A)		;YES, IS IT THIS PROCEDURE?
	ERROR			;NO TO EITHER
TO1:	MOVEI	A,1(D)
	MOVE	B,@CSA		;CHECK THE LINE FOR ACCURACY COMPLETELY
	TLNE	B,COMMTF	;BEFORE MAKING ANY DATABASE CHANGES
	JRST	TOA		;ALL OK
	JUMPE	B,TOA		;ALL OK
TO2:	TLNN	B,VARF		;IS IT A DUMMY?
	ERROR	TOERR2	;NO
	MOVEI	A,1(A)	;NEXT ONE
TO3:	MOVE	B,@CSA
	JUMPE	B,TOA
	TLNE	B,COMMTF
	JRST	TOA
	CAME	B,[XWD MPF,ANDL+1]	;IS IT "AND"?
	JRST	TO2		;NOT AND, TRY DUMMY
	MOVEI	A,1(A)
	MOVE	B,@CSA
	TLNN	B,VARF
	ERROR	TOERR2
	AOJA	A,TO3	;IS AND, GO FOR ANOTHER DUMMY


TOGET:	TRO	F,FLUSHF	;LOADING AN ALREADY DEFINED PROCEDURE
	MOVEM	A,TOPROD	;SO LET IT BE OPEN
	MOVEI	A,-1(A)
	MOVE	A,@RPA		;PROCEDURE NAME
	PUSHJ	P,PTOSS
	MOVEI	A,[ASCIZ / IS ALREADY DEFINED/]
	PUSHJ	P,PTOSSM
	PUSHJ	P,CRLF
	JRST	IFXFLS

TOA:	MOVE	A,CPP
	HRRZ	E,@CSA
	MOVEI	A,0
	PUSHJ	P,MOVEMP	;PTR TO TO LINE
	MOVEI	A,0
	MOVE	M,PTOP	;PTR TO NO OF DUMMIES
	PUSHJ	P,MOVEMP
	TRZ	F,TF	;TF=1_AND PRECEDING THE NEXT DUMMY

TOC:	AOS	A,CPP
	MOVE	A,@CSA
TOC1:	JUMPE	A,TOD
	TLNE	A,COMMTF
	JRST	TOE
	TRZE	F,TF
	TLO	A,ANDF
	PUSHJ	P,MOVEMP
	AOS	@PSM	;BUMP NO OF DUMMIES
	AOS	A,CPP
	MOVE	A,@CSA
	CAME	A,[XWD MPF,ANDL+1]
	JRST	TOC1
	TRO	F,TF
	JRST	TOC


TOE:	MOVEI	D,-1(M)
	MOVEM	A,@PSD	;COMMENT GETS STASHED HERE
	AOS	CPP	;CPP POINTS AT A ZERO WHEN FALLING INTO TOD
TOD:	TLZN	F,TITLEF	;DOING TITLE?
	SOJA	M,TOF
	MOVE	A,TOPROD	;COPY OLD LINES,CAN ONLY BE CURRENT PROC
	MOVE	D,@RPA
	PUSH	P,D
	PUSH	P,M
	MOVEI	M,1(D)
	ADD	M,@PSM
	MOVEI	M,1(M)
	MOVE	A,@PSM		;GET NEXT LINE OF NUMBERED STEPS
	JUMPE	A,.+3		;END OF LIST
	PUSHJ	P,MOVEMP	;COPY TO END OF NEW PROCEDURE DIRECTORY
	AOJA	M,.-3
	PUSHJ	P,MOVEMP	;PUT LAST ONE AWAY

	POP	P,M
	MOVEI	D,-1(M)		;PTR TO BEGINNING OF NEW DIRECTORY
	MOVEI	A,@PSD		;ABSOLUTE

	POP	P,M
	MOVEI	C,@PSM		;PTR TO BEG OF OLD DIR

	MOVE	D,PTOP		;END OF NEW DIRECTORY
	ADDI	D,(A)
	SUBI	D,(C)		;WHERE END WILL END UP

	HRLI	C,(A)		;FROM BEG OF NEW DIR TO BEG OF OLD
	BLT	C,@PSD
	MOVEM	D,PTOP		;OFFSET BY AMOUNT FLUSHED
	MOVE	A,TOPROD
	SETOM	@RPA		;OLD PROCEDURE NAME BECOMES UNDEFINED

TOF:	MOVEM	E,TOPROD	;NOW OK TO HAVE PROCEDURE OPEN
	MOVEI	D,1(M)
	MOVE	D,@PSD
	HRLI	M,-1(D)		;NUMBER OF ARGS-1
	MOVEI	A,(E)
	MOVEM	M,@RPA
	JRST	SCOMEX

MOVEMP:	MOVE	D,PTOP
	MOVEM	A,@PSD
	AOS	A,PTOP
	MOVEI	B,@PSA
	CAML	B,PS+1
	EXPAND	PS
	SETZM	@PSA
	POPJ	P,


TOFLUS:	POP	P,CTOP
	POPJ	P,

TOLINE:	SKIPN	TOPROD	;IS THERE A PROCEDURE OPEN
	ERROR	INERR3	;LINE X OF WHAT PROCEDURE?
	TRNE	F,FLUSHF
	JRST	TOFLUS
	MOVE	A,0(P)	;POINTER TO BASE OF NEW COMPILED LINE ON STACK
	MOVE	L,@CSA
	HRLI	L,010700+W
	PUSHJ	P,DNM
	ERROR	IOPERR	;I AM IN TROOUBLE
	CAIL	M,^D100000
	ERROR	INERR2	;LINE NO TOO LARGE
	JUMPE	M,[ERROR INERR4]	;LINE NO =0

	HRRZ	A,TOPROD
	JSP	C,SRCHL1
	 CAIGE	B,(M)
	 JRST	TOLING

	CAIN	B,(M)		;LINE NO ALREADY EXISTS?
	AOJA	A,TOLINH	;YES

TOLING:	SETOM	NXLINE	;CANNOT SAFELY ASSUME THAT THE PROCEDURE WE'RE IN
	MOVEI	B,2	; HAS NOT MOVED
	ADD	B,PTOP
	ADD	B,PS
	CAML	B,PS+1	;ROOM IN PS FOR ANOTHER 2 WORD ENTRY?
	EXPAND	PS	;NO

	MOVE	B,PTOP
	ADD	B,PS
	MOVEI	C,@PSA

	MOVE	D,(B)	;COPY EVERYTHING DOWN 2, UP TO PLACE TO INSERT
	MOVEM	D,2(B)
	CAIE	B,(C)
	SOJA	B,.-3

	MOVEI	B,2
	ADDM	B,PTOP

	MOVEM	M,(C)	;LINE NUMBER
	AOJA	A,TOLINJ

TOLINH:	PUSHJ	P,GCCS

TOLINJ:	AOS	B,SEQNO	;PUTTING AWAY THE STEP, GIVE IT A NEW SEQUENCE NUMBER
	HRL	B,CDBOT	;WHAT IS THE POINTER TO THE NEW LINE
	MOVSM	B,@PSA	;PPD SEQNO,CBOT INTO PROCEDURE DIRECTORY

	MOVE	A,CTOP
	MOVEI	B,@CSA	;B_CTOP+CS
	POP	P,C	;CBOT, BASE OF THIS LINE TO BE STORED
	MOVE	A,CDBOT
	SUBI	C,(A)	;C_CBOT-CDBOT, LENGTH OF OTHER DIRECT LINES
	MOVEI	A,@CSA	;A_CS+CDBOT, ABS LOC OF CDBOT
	MOVEI	D,(B)
	ADDI	D,(C)	;D_CS+CTOP+CBOT-CDBOT, END OF COPIED DIRECT LINES
	CAML	D,CS+1	;IS THAT INSIDE CURRENT CS ALLOCATION
	 EXPAND	C,CS	;NO, NEED AT LEAST ENOUGH ROOM FOR COPIED LINES

	HRLZI	E,(A)	;FROM BASE OF DIRECT LINES
	HRRI	E,(B)	;TO CURRENT END OF COMPILED CODE
	BLT	E,(D)	;ENDING WITH END OF ALL OTHER DIRECT LINES

	SOS	D,CTOP	;NET EFFECT OF SWITCH IS ONE LESS WORD
	SUBI	D,(C)	;D_CTOP+CDBOT-CBOT-1
	MOVEM	D,CDBOT	;BECAUSE LINE NO WORD OF STORED LINE IS FLUSHED

	ADDI	C,1(A)	;C_CS+(CDBOT-CDBOT)+CBOT+1
	HRLI	A,(C)	;COPY FROM WORD AFTER LINE NO WORD
	BLT	A,-1(B)	;TO CDBOT ENDING AT OLD TOP MINUS ONE
	POPJ	P,	;R1 FROM COMPIL


TRACE:	PUSHJ	P,GNE	;TRACE WHAT?
	 ERROR		;NOTHING
	TLNN	A,UPRF	;TRACE A USER PROCEDURE?
	 ERROR		;CANNOT TRACE THAT
	MOVEI	A,-1(A)	;POINT AT FIRST WORD OF RP PAIR
	HRLZI	B,TRACEF
	IORM	B,@RPA	;MARK THIS PROCEDURE AS TRACED
	JRST	COMEX

UPROD:	MOVE	A,-1(P)
	POP	P,-1(P)
	MOVNI	B,1
	CAMN	B,@RPA	;IS THE PROCEDURE DEFINED?
	 ERROR	EVER3	;NO, X NEEDS A MEANING
	MOVEI	B,(A)
	CAMN	B,TOPROD	;IS THIS THE ONE BEING DEFINED?
	 ERROR	EVER4	;YES, X HAS NOT BEEN COMPLETELY DEFINED

UPROD1:	PUSH	P,PRODNM	;PUSH THIS FIRST SO IT CAN BE LAST OFF
				; FOR COMERR IN A STORED LINE
	PUSH	P,CHGNO
	PUSH	P,LINENO
	PUSH	P,DTOP
	PUSH	P,TRUTH
	HRRZ	B,CPP
	CAMGE	B,CDBOT
	JRST	.+4
	SUB	B,CDBOT
	TLO	B,400000	;NOTE THIS ONE RELATIVE TO CDBOT
	JRST	.+3	;INSTEAD OF RELATIVE TO BASE OF THIS LINE
	SUB	B,CHGNO	;LINE GENERATION NO AND LOCATION REL TO CS
	HRRZI	B,(B)	;MAKE SURE LH IS ZERO
	PUSH	P,B
	PUSH	P,SSP
	PUSH	P,SPP

	SETZM	TRUTH
	SETZM	LINENO
	HRRZM	A,PRODNM

	TLZE	F,ERRORF	;WAS UPROD CALLED BY ERROR
	JRST	MAINL

	MOVE	B,DP	;LOC OF DUMMY ARG TABLE
	HLRE	C,A	; + NO OF ARGS AT SCAN TIME -1
	ADDI	C,1
	MOVEI	E,(C)
	ADDI	E,1(C)
	ADD	B,DTOP	;PART OF TABLE ALREADY USED
	ADDI	B,(E)	;AMT FOR DUMMY NAMES
	CAML	B,DP+1	;ALL FIT IN DP?
	EXPAND	E,DP	;EXPAND THE TABLE AT LEAST THIS MUCH

	ADDM	C,DTOP
	ADDM	C,DTOP	;UPDATE AMT USED

	HRRZ	D,@RPA	;GET PTR TO PROCEDURE
	MOVEI	D,1(D)	;POINT TO NUMBER OF ARGS
	CAME	C,@PSD	;SAME NUMBER OF ARGS AS WHEN SCANNED?
	ERROR		;NO

	PUSH	P,BCHAR	;SAVE CURRENT STATE OF MARGIN
	MOVE	E,TRACEM	;MARGIN FOR TRACE
	ANDI	E,17		;INDENT AT MOST 14 SPACES
	MOVEM	E,BCHAR
	TRZ	F,TF	;IF SET WILL DENOTE THAT THIS PROC IS TRACED
	MOVEI	A,-1(A)
	SKIPGE	A,@RPA	;FETCH NAME OF PROCEDURE AND SKIP IF NOT TRACED
	TRO	F,TF		;TRACEF IS THE SIGN BIT
	PUSH	P,C
	TRNE	F,TF
	SKIPN	CHARNO
	SKIPA		;NOT TRACING OR AT MARGIN
	PUSHJ	P,CRLF	;NOT AT MARGIN, GET THERE
	TRNE	F,TF
	PUSHJ	P,INDENT
	POP	P,C
	TRNE	F,TF
	PUSHJ	P,CALPTS	;TRACED, TYPE OUT THE NAME


	MOVEI	D,1(D)	;POINT TO FIRST DUMMY NAME
	MOVN	E,C	;-N
	MOVEI	C,1(S)	;STACK LOCATION
	ADD	C,E	;LOCATION IN STACK OF FIRST DUMMY
	HRL	C,E	;-COUNT OF DUMMIES
	HRRZ	G,DP
	ADD	G,-5(P)	;OLD VALUE OF DTOP
	JUMPGE	C,UPFRST	;NO DUMMIES, GET FIRST LINE

	MOVE	A,[POINT 7,[ASCIZ / OF /]]
UPDVCL:	TRNE	F,TF
	PUSHJ	P,CLPTS1	;IF TRACED, TYPE "OF" OR " AND "
	MOVEI	E,042		;TO QUOTE INPUTS IF TRACING

	MOVE	A,@PSD	;LOOP TO COPY INTO DP, GET DUMMY NAME
	MOVEM	A,(G)	;INTO DUMMY VAR TABLE
	MOVE	A,(C)	;VALUE OFF S STACK
	MOVEM	A,1(G)	;INTO DUMMY VAR TABLE
	TRNE	F,TF		;TRACING?
	PUSHJ	P,CLPTS0	;YES, TYPE INPUT QUOTED
	MOVEI	G,2(G)
	POP	S,0(S)	;POP 1 THING OFF S STACK WITHOUT CLOBBERING
	MOVEI	D,1(D)
	MOVE	A,[POINT 7,[ASCIZ / AND /]]
	AOBJN	C,UPDVCL	;UPROD DUMMY VAR COPY LOOP

	TRNE	F,TF
	PUSHJ	P,CRLF		;NEATEN UP

	MOVEI	A,2
	TRZE	F,TF	;TRACING? ALSO, FLAG NO LONGER NEEDED
	ADDM	A,TRACEM	;NEXT TRACED CALL SHOULD BE INDENTED TWO MORE SPACES


UPFRST:	POP	P,BCHAR		;RESTORE OLD MARGIN
UPNEXT:	JSP	C,SRCHLN
	 CAMG	B,LINENO
	 JRST	OUTPTA
	MOVEM	A,NXLINE
UPNXT1:	MOVEM	B,LINENO	;FOUND A LINE NO > PREVIOUS LINE
	AOS	A,NXLINE
	MOVE	A,@PSA		;GET COMPILE PTR & CHANGE NO
	MOVEM	A,CHGNO
	MOVEM	A,CPP

	PUSHJ	P,EXECUTE

	AOS	A,NXLINE	;DONE WITH THE LINE, GET NEXT ONE
	JUMPE	A,UPNEXT	;DO IT THE LONG WAY IF ANY PROCEDURES CHANGED
	MOVE	B,@PSA		;FETCH THE LINE NO
	JUMPN	B,UPNXT1	;LINE NO<>0_MORE TO DO
	JRST	OUTPTA		;NO MORE LINES, DONE WITH PROCEDURE

CLPTS0:	TRO	F,PREFIX!SUFFIX
CALPTS:	HRLI	A,010700+W	;WORKSPACE ELEMENT
CLPTS1:	PUSH	P,B
	PUSH	P,C
	PUSH	P,D
	PUSHJ	P,PTOS		;FINALLY!
	POP	P,D
	POP	P,C
	POP	P,B
	POPJ	P,

OUTPTA:	TLO	F,COMF	;DENOTE THAT THERE IS NO OUTPUT
	JRST	RETA

RETTRN:	SKIPE	PRODNM
	ERROR		;"GO" ALONE CAN ONLY BE DIRECT
	SKIPG	GODEPTH
	ERROR	GOERR9	;NOPLACE TO GO
	SOS	GODEPTH	;TEST ABOVE SO IT DOESN'T GO NEGATIVE
GETOUT:	TLO	F,GOF	;GO AND GET TREATED MUCH THE SAME
	POP	P,A
	POP	P,A
	JRST	RET0
ESTOP:	SKIPN	PRODNM
	ERROR			;STOP CAN ONLY BE STORED
	TLO	F,COMF
	JRST	RETURN

OUTPUT:	SKIPN	PRODNM
	ERROR
RETURN:	JSP	D,COMEXR	;DO THE END OF LINE CHECKS
RET0:	POP	P,0(P)
RETA:	POP	P,SPP
	POP	P,SSP
	POP	P,A	;CPP
	POP	P,TRUTH
	POP	P,DTOP
	POP	P,LINENO
	POP	P,CHGNO
	JUMPGE	A,.+3	;RELATIVE TO BASE OF THIS LINE ?
	ADD	A,CDBOT	; NO, RELATIVE TO CDBOT
	JRST	.+2
	ADD	A,CHGNO
	HRRZM	A,CPP
	TLNE	F,GOF		;IS THIS RETURN UNTRACEABLE?
	JRST	RETD		;YES IT IS, SKIP TRACE STUFF


	MOVE	A,PRODNM
	MOVEI	A,-1(A)	;DON'T CHANGE PRODNM, COMERU MAY NEED IT
	SKIPL	A,@RPA	;ENDING A TRACED PROCEDURE?
	JRST	RETD	;NO
	MOVNI	B,2
	ADDB	B,TRACEM
	PUSH	P,BCHAR
	ANDI	B,17
	MOVEM	B,BCHAR
	PUSHJ	P,INDENT
	PUSHJ	P,PTOSS	;TYPE PROCEDURE NAME
	MOVEI	A,[ASCIZ / OUTPUTS /]
	TLNE	F,COMF	;DID IT OUTPUT?
	MOVEI	A,[ASCIZ / STOPS/]	;NO, IT STOPPED
	PUSHJ	P,PTOSSM

RETB:	TLNE	F,COMF
	JRST	RETC	;NO NEED TO TYPE WHAT IT OUTPUTTED, IT STOPPED
	TRO	F,PREFIX!SUFFIX
	MOVEI	E,042
	MOVE	A,(S)
	PUSHJ	P,PTOSS
RETC:	POP	P,BCHAR
	PUSHJ	P,CRLF


RETD:	TLZN	F,COMF	;DID THIS PROCEDURE OUTPUT?
	JRST	RETE	;YES,TREAT LIKE AN OPERATION
	MOVE	E,(P)	;SAVE THE OLD PRODNM
	JSP	D,COMEXU	;NO, TREAT IT LIKE A COMMAND
	MOVEM	E,PRODNM
	JRST	.+2
RETE:	POP	P,PRODNM
	SETOM	NXLINE	;DON'T TRY TO REMEMBER WHERE WE WERE

	TLZN	F,GOF	;WAS RETURN CALLED BY GO
	SKIPN	PRODNM	;DIRECT COMMAND?
	POPJ	P,
	JSP	C,SRCHLN	;SEARCH FOR THAT LINE AGAIN
	 CAME	B,LINENO	;THIS TIME FOR =
	 ERROR		;NO LONGER EXISTS, PROBABLY AN ERROR
	MOVEI	A,1(A)
	MOVE	B,CHGNO
	SUB	B,@PSA	;NEW POSITION OF SAME LINE IS ALWAYS <= OLD POS
	TLNE	B,-1	;ONLY CHECK THE SEQUENCE NUMBERS FOR EQUALITY
	 ERROR		;LINE CHANGED WHILE IN IT
	MOVN	B,B	;THE AMOUNT THE SAME LINE GOT MOVED BACK OR 0
	ADDM	B,CPP	;THAT THE RUNNING POINTER NEEDS TO BE OFFSET
	POPJ	P,	;OPERATIONS ALL EXIT WITH POPJ,
			; COMMAND DOES TOO AFTER JSP D,COMEXR

SRCHLN:	HRRZ	A,PRODNM	;THIS IS THE PROCEDURE TO SEARCH
SRCHL1:	HRRZ	B,@RPA	;GET RELATIVE PTR INTO PS SPACE
	CAIN	B,-1	;ERASED?
	ERROR	NOPROD	;THERE IS NO PROCEDURE X

	MOVEI	A,1(B)	;POINTS TO NUMBER OF DUMMIES
	ADD	A,@PSA	;STEP OVER THEM
	AOJA	A,.+2	;STEP TO FIRST LINE NO

SRCHLL:	MOVEI	A,2(A)	;POINT TO NEXT LINE NO
	HRRZ	B,@PSA	;GET THE LINE NO
	JUMPE	B,1(C)	;R1, FOUND END BEFORE THE LINE
	XCT	(C)	;ARG TO ROUTINE IS THE COMPARE
	JRST	SRCHLL	;CONTINUE ON NO COMPARE
	JRST	2,2(C)	;GOOD COMPARE, R2



GOTO:	PUSHJ	P,GNE
	 JRST	RETTRN
	SKIPE	PRODNM	;ERROR IF NOT STORED
	CAME	A,[XWD MPF,TOL+1]
	ERROR	GOERR1	;GO WHERE?
	AOS	A,CPP
	MOVE	A,@CSA	;FETCH NEXT WORD
	CAME	A,[XWD MPF,LINELL+1]
	ERROR	GOERR1	;NOT "LINE", GO WHERE?
	PUSHJ	P,NUMEVL

	JSP	C,SRCHLN	;IS THERE THAT LINE IN THIS PROCEDURE
	 CAIE	B,(M)
	 ERROR	GOERR3	;THERE IS NO LINE X
	MOVEI	A,-1(A)
	MOVEM	A,NXLINE	;SO STEP TO NEXT LINE WILL FIND THIS ONE WITHOUT SEARCHING
	JRST	COMEX

NUMEVL:	AOS	A,CPP
	MOVE	L,@CSA	;FETCH LINE NUMBER
	TLNE	L,LITF	;IF THIS, DOES NOT NEED TO BE EVALED
	JRST	GOTO1	;IS A LITERAL, COULD BE A NUMBER
	PUSHJ	P,EVAL	;NOT A LITERAL, MUST EVALUATE
	POP	S,L	;VALUE RETURNED
GOTO1:	MOVEI	D,(L)	;SAVE POINTER FOR ERROR COMMENT
	TLNE	L,SENTF
	ERROR	GOERR2	;CANNOT BE A NUMBER IF IT IS A SENTENCE
	HRLI	L,010700+W
	PUSHJ	P,DNM
	ERROR	GOERR2	;IS NOT A LINE NUMBER
	POPJ	P,


;TYPE IN STRING
;CHECK FOR CONTROL CHARACTERS,EDIT CHARACTERS,ILLEGAL CHARACTERS
;CMDFLG=1_COMMAND STRING,=0_TEXT STRING
;EDITF=1_EDIT MODE, THEN A CONTAINS BYTE POINTER TO BEGINNING OF STRING
;BYTE POINTER TO OUTPUT STRING IN B
;RETURN STRING POINTER TO INPUT ON S-LIST
;R1_FAILURE, TRY AGAIN, R2_SUCCESS.
TIS:	MOVEI	C,2	;SET CONTINUATION TO SPACE TWO CHARACTERS
	MOVEM	C,BCHAR
	TLZ	F,LDONF	;CLEAR LINE DONE FLAG FOR EDIT MODE
	TLNN	F,EDITF!GETF	;DON'T TYPE > OR _ IF EDITING
	PUSHJ	P,TOSS	;PRINT OUT COMMENT FROM B
	PUSHJ	P,NEWSTR
	TLNN	F,EDITF	;ARE WE IN EDIT MODE?
	JRST	TISB	;NO
	PUSHJ	P,NEWSRC
	MOVEM	A,LINBOT	;FOR PURPOSES OF GARBAGE COLLECTION
	PUSHJ	P,TEDIT	;AND COPY IT TO THE INPUT STRING
TISB:	PUSHJ	P,TYI	;GET NEXT CHARACTER INTO C
TISB1:	CAIGE	C,40	;CONTROL CHAR?
	JRST	TISC	;YES
	CAIN	C,134	;BACKSLASH
	JRST	TISN	;YES
	CAIL	C,175	;RUBOUT OR EOM?
	JRST	TISO	;YES
	AOS	CHARNO	;PRINT CHARACTER SO COUNT
TISF:	IDPB	C,B
	JRST	TISB

TISC:	ROT	C,-1
	HRRZ	D,ITAB(C)
	JUMPL	C,.+2
	HLRZ	D,ITAB(C)
	JUMPN	D,(D)	;DISPATCH IF IT IS A LEGAL CONTROL CHAR
	JRST	TISD	;NOT A LEGAL CONTROL CHAR

DEFINE CC (A,B,C,D)
<XWD A,B
 XWD C,D>

ITAB:
CC 0,0,TISG,0	;NUL ABC
CC 0,0,0,TISF	;DEFG
CC 0,TISH,TISI,0	;H TAB LF K
CC 0,TISJ,TISK,0	;L CR NO
FOR TEN50,<CC 0,0,TISL,TISDC3>	;TEN50 USES DC3 AS TERMINATOR IN FILES
FOR TENEX,<CC 0,0,TISL,0>	;PQRS
CC TISLIT,0,0,TISM	;TUVW
CC 0,0,0,TISZ	;XYZ ALTMODE
FOR TEN50,<CC 0,0,0,0>
FOR TENEX,<CC 0,0,0,TISEOL>	;END OF LINE IS 37, LF ALREADY FLUSHED


FOR TEN50,<
TISDC3:	TLNE	F,GETF	;COMING FROM A FILE?
	JRST	TISEOL	;YES, OK>
TISD:
FOR TEN50,<	TTCALL 11,>	;FLUSH ALL INPUT
FOR TENEX,<	MOVEI	A,100
	CFIBF>
	MOVEI	B,[ASCIZ "
MEANINGLESS CHARACTER"]
TISA:	PUSHJ	P,TOSS
	PUSHJ	P,CRLF
TISR1:
	TLZE	F,EDITF
	POP	S,A	;CLEAN UP THE PUSH-DOWN LISTS
	POPJ	P,	;GIVE R1
TISG:	MOVEI	C," "	;CONTROL B
	TLNN	F,GETF	;NO ECHO IF FROM FILE
	PUSHJ	P,TYO	;TYPE BACK A SPACE
	MOVEI	C,002	;AND PUT CONTROL B IN BUFFER
	JRST	TISF
TISH:	MOVE	D,CHARNO	;CONTROL I-SIMULATE TABS
	ADDI	D,10
	ANDCMI	D,7	;FIND NEXT HIGHER MULTIPLE OF 8
	SUB	D,CHARNO
	MOVEI	C," "	;TYPE THE APPROPRIATE NUMBER OF SPACES
	PUSHJ	P,TYO
	SOJN	D,.-1
	MOVEI	C,011
	JRST	TISF


TISLIT:	TLNN	F,GETF	;DOING A GET?
	JRST	TISD	;NO, ^T NOT LEGAL FROM TERMINAL
	MOVEI	C,024
	IDPB	C,B	;COPY TEXT BETWEEN ^T'S LITERALLY, NO INTERP
	PUSHJ	P,TYI
	CAIE	C,024	;LOOKING FOR MATCHING ^T
	JRST	.-3	;NOT FOUND
	JRST	TISF	;BACK TO NORMAL READING

TISI:
	TLNE	F,GETF
	JRST	TISF		;COMING FROM FILE, LET IT THRU
	PUSH	P,B
	MOVEI	B,[BYTE (7) 15,15,40,40]
	PUSHJ	P,TOSS
	POP	P,B
	MOVEI	C," "	;AND TREAT LIKE A SPACE
	JRST	TISF

TISO:	CAIE	C,177	;RUBOUT?
	JRST	TISZ	;NO, EOM
	MOVEI	B,[BYTE (7) 15,177,43,12]
	JRST	TISA

TISZ:	PUSHJ	P,CRLF
	JRST	TISEOL
TISJ:	SETZM	CHARNO
	PUSHJ	P,TYI
	CAIE	C,12		;LINEFEED FOLLOWING CR?
	JRST	TISCR		;NO
TISEOL:	SETZB	C,CHARNO
	IDPB	C,B		;FINISH THIS STRING
	HRLZI	L,010700+W
	HRR	L,NEWBOT
	MOVEM	L,LINBOT
	MOVE	B,L		;SAME SRC AND DEST FOR WELL-FORMING
	MOVEI	D,0		;TERMINATE ON EOL
	PUSHJ	P,WEFS		;REMOVE EXTRA SPACES
	TLZE	F,EDITF	;CLEAR PDL+EDIT FLAG
	POP	S,-1(S)

CPOPJ1:	AOS	0(P)
CPOPJ:	POPJ	P,


TISCR:	MOVEI	A,15	;CR
	IDPB	A,B		;PUT IT INTO THE TEXT
	JRST	TISB1		;TREAT THE CHAR AFTER CR FOR WHAT IT IS

TISK:	TLNN	F,EDITF	;TYPE NEXT WORD IN EDIT MODE
	JRST	TISD	;NOT IN EDIT MODE
	PUSHJ	P,TEDIT	;COPY NEXT WORD TO TT AND BUFFER
	JRST	TISB
TISL:	TLNN	F,EDITF	;TYPE REST OF LINE IN EDIT MODE
	JRST	TISD	;NOT IN EDIT MODE
	PUSHJ	P,TEDIT
	TLNN	F,LDONF
	JRST	.-2
	JRST	TISB

TEDIT:	TLNE	F,LDONF	;IF LINE DONE JUST EXIT
	POPJ	P,
	PUSH	P,A	;SAVE POINTERS
	PUSH	P,B	;NO GARBAGE COLLECTION POSSIBLE
	PUSHJ	P,PWORD	;PRINT WORD
	POP	P,B
	POP	P,L
TEDITA:	CAMN	A,L	;HAVE WE CAUGHT UP?
	POPJ	P,
	ILDB	C,L	;COPY THE NEXT CHAR INTO THE INPUT STRING
	JUMPE	C,TEDITB	;IS IT A NULL
	IDPB	C,B
	JRST	TEDITA
TEDITB:	TLO	F,LDONF	;SET LINE DONE FLAG
	POPJ	P,	;AND EXIT
TBACK:	CAMN	B,NEWBOT	;ARE WE AT BEGINNING
	JRST	TISB	;OFF BEGINNING OF STRING
	HRRI	B,-1(B)	;BACK UP POINTER
	REPEAT 4,<IBP	B>
	JRST	2,@D

TISN:	JSP	D,TBACK	;BACK UP ONE CHARACTER
	JRST	TISB

TISMA:	JSP	D,TBACK	;REMOVE ONE MORE CHARACTER
	MOVEI	C,134	;TYPE BACKSLASH
	PUSHJ	P,TYO
TISM:	CAMN	B,NEWBOT	;IS THERE A PREVIOUS CHAR
	JRST	TISB	;NO-SO QUIT
	LDB	C,B	;YES-REMOVE LEADINGS SPACES
	CAIN	C," "
	JRST	TISMA
TISMB:	CAMN	B,NEWBOT	;REMOVE NON-SPACES TIL A SPACE
	JRST	TISB	;OFF BEGINNING OF LINE SO QUIT
	LDB	C,B
	CAIN	C," "
	JRST	TISB
	JSP	D,TBACK
	MOVEI	C,134	;TYPE A BACKSLASH WHEN CHARACTERS REMOVED
	PUSHJ	P,TYO
	JRST	TISMB

;PRINT WORD
;ENTER WITH BYTE POINTER TO WORK-SPACE IN A
;PRINT WORD AND FOLLOWING SPACE IF EXISTS
;TYO UPDATES CHARNO AND TAKES INPUT IN C

PWORDH:	PUSHJ	P,TYO	;JUST THE CR, NO LF
PWORD:	MOVEI	D,0	;INITIALIZE COUNT OF PRINTING CHARACTERS
	MOVE	B,A	;COPY POINTER
	TRNE	F,PREFIX	;MUST WE PREFIX " OR / TO THIS WORD?
	MOVEI	D,1	;YES, COUNT THE EXTRA CHAR
PWORDB:	ILDB	C,B	;COUNT PRINTING CHARACTERS
	CAIE	C,002	;CONTROL-B IS A SPACING CHARACTER
	CAIL	C," "
	MOVEI	D,1(D)	;FASTER THAN ADDI D,1
	CAIE	C,015	;STOP IF CR
	CAIN	C," "	;STOP IF SPACE
	JRST	PWORDA
	JUMPN	C,PWORDB	;CONTINUE ON NOT NULL
	TRNE	F,SUFFIX	;MUST WE SUFFIX " OR / TO END OF THIS STRING?
	MOVEI	D,1(D)	;YES, COUNT THE EXTRA CHAR AT END
PWORDA:	TLNE	F,SAVEF	;WHEN SAVING
	JRST	PWORDS		;THE WORD ALWAYS FITS ON A LINE
	MOVEI	C,^D72
	SUBI	C,(D)
	CAMGE	C,BCHAR
	JRST	PWORDC	;YES
	CAMGE	C,CHARNO	;COUNT+CHARNO>72.?
	PUSHJ	P,LINE	;YES-CRLF AND SPACE TO BCHAR
PWORDS:	MOVEI	C,(E)	;PUT PREFIX IN C
	TRZE	F,PREFIX	;SHOULD WE TYPE A PREFIX?
PWORDE:	PUSHJ	P,TYO	;YES
	ILDB	C,A	;OTHERWISE PRINT WORD
	CAME	A,B	;HAVE WE CAUGHT UP
	JRST	PWORDE	;NO
PWORDF:	LDB	C,A	;WHAT WAS THE TERMINATING CHARACTER
	JUMPE	C,PWORDX
	CAIN	C,015
	JRST	PWORDH	;CR- HANDLE REST OF WORD JUST LIKE WORD
	CAIN	C," "
	JRST	PWORDD	;SPACE
	PUSHJ	P,LINE	;OTHER-MEANS WE ARE AT END OF LINE BUT NOT OWRD
	JRST	PWORD	;AND GO PROCESS REST AS WORD

PWORDX:	MOVEI	C,(E)	;AT EOM
	TRZE	F,SUFFIX	;MUST WE AFFIX A SUFFIX?
	PUSHJ	P,TYO	;YES, TYPE IT
	POPJ	P,

PWORDC:	MOVE	D,BCHAR	;IF BCHAR<>CHARNO CRLF AND SPACE
	CAME	D,CHARNO
	PUSHJ	P,LINE
	MOVEI	C,(E)
	TRZE	F,PREFIX
	PUSHJ	P,TYO
PWORDG:	MOVEI	C,110	;PRINT TILL CHARNO=72.
	CAMN	C,CHARNO
	JRST	PWORDF
	ILDB	C,A
	PUSHJ	P,TYO
	JRST	PWORDG
PWORDD:	MOVEI	C,110	;TYPE SPACE IF CHARNO<>72.
	CAMN	C,CHARNO	;TYPE CRLF AND SPACES IF CHARNO=72.
	JRST	LINE
	MOVEI	C," "
	JRST	TYO


PTYO:	MOVEI	C,^D72
	CAMN	C,CHARNO
	PUSHJ	P,LINE
	MOVEI	C,(B)
	JRST	TYO

PSPACE:	PUSH	P,B
	MOVEI	B," "
	PUSHJ	P,PTYO
	POP	P,B
	POPJ	P,

PTAB:	MOVEI	B,7
	PUSHJ	P,PSPACE
	TDNE	B,CHARNO
	JRST	.-2
	POPJ	P,

LINE:
	SETZM	CHARNO
	TLNE	F,SAVEF
	POPJ	P,	;MAY BE REDUNDANT
	PUSHJ	P,CRLF	;TYPE CRLF AND SPACES USING C
INDENT:	MOVE	C,BCHAR
	CAMN	C,CHARNO
	POPJ	P,
	MOVEI	C," "
	PUSHJ	P,TYO
	JRST	INDENT
CRLF:
FOR TENEX,<SETZM CHARNO
	MOVEI	C,37	;EOL
	JRST	TYO>
FOR TEN50,<TLNN	F,SAVEF	;OUTPUTTING TO A FILE?
	JRST	CRLF1	;NO
	SETZM	CHARNO
	MOVEI	C,023	;^S(DC3),EOL TO A FILE
	JRST	TYO
CRLF1:	MOVEI	C,015
	PUSHJ	P,TYO
	MOVEI	C,012
	JRST	TYO>

REQUEST:	TLO	F,RQF
	MOVEI	B,[ASCIZ /*/]
	SKIPE	CHARNO	;AT LEFT MARGIN?
	MOVEI	B,[EXP 0]	;NO, DON'T TYPE ANYTHING
	PUSHJ	P,TIS
	JRST	REQUEST
	TLZ	F,RQF
	POPJ	P,

TYO:	TLZE	F,BREAKF
	 ERROR	BREAK
	TLNE	F,SAVEF	;GOING TO A FILE?
	JRST	.+3	;YES, LET ^B THROUGH UNTRANSLATED
	CAIN	C,002
TYOSPC:	MOVEI	C,40		;SUBSTITUTE SPACE FOR CONTROL-B
FOR TEN50,<	CAIN	C,015	;IS THE CHAR CR
	TTCALL	1,C	;YES, TYPE TWICE, NOT THE BEST WAY TO GET TIMING ON CR ALONE
	TTCALL	1,C>
FOR TENEX,<
	PUSH	P,A
	PUSH	P,B
	MOVE	A,OUTFIL
	MOVEI	B,(C)
	CAIN	B,015	;CR? THIS WAY OK BECAUSE CRLF APPEARS AS 37
	BOUT		;TYPE TWICE SO CARRIAGE CAN GET BACK TO MARGIN
	BOUT
	POP	P,B
	POP	P,A
>
	CAIL	C," "	;IS IT A PRINTING CHARACTER
	AOS	CHARNO
	CAIN	C,015	;CR-CLEAR CHARNO
	SETZM	CHARNO
	POPJ	P,

TYI:	TLO	F,TIF	;SET TYPEIN FLAG
	TLZE	F,BREAKF
	 ERROR	BREAK
FOR TEN50,<	TTCALL	4,C>	;GET A CHAR IN LINE MODE
FOR TENEX,<
	PUSH	P,A
	MOVE	C,B
	MOVE	A,INFILE
	BIN
	EXCH	C,B
	POP	P,A
>
	TLZ	F,TIF
	POPJ	P,
PTOSSM:	HRLI	A,440700	;FOR MACHINE STRINGS
	JRST	PTOS
PTOSS:	HRLI	A,010700+W	;FOR GENERATED STRINGS
PTOS:	PUSHJ	P,PWORD
	LDB	C,A
	JUMPN	C,.-2
	POPJ	P,

TOSS:	HRLI	B,440700
TOS:	ILDB	C,B	;BYTE POINTER IN B USE C
	JUMPE	C,CPOPJ
	PUSHJ	P,TYO
	JRST	TOS

NUMBRQ:	TLNE	A,EMPTYF	;IS IT EMPTY?
	JRST	CPOPJ	;YES
	TRZ	F,TF
	HRLI	A,010700+W

	ILDB	C,A
	JUMPE	C,CPOPJ	;EMPTY
	CAIE	C,"+"
	CAIN	C,"-"
NMBRQ1:	ILDB	C,A
	JUMPE	C,NMBRQ2
	TRO	F,TF	;HAVE SEEN A CHARACTER
	CAIL	C,"0"
	CAILE	C,"9"
	POPJ	P,	;NOT A DIGIT, FAIL
	JRST	NMBRQ1

NMBRQ2:	TRZE	F,TF	;ALL THE CHARS WE SAW WERE DIGITS, BUT WERE THERE ANY
	AOS	0(P)	;SAW SOME DIGITS
	POPJ	P,	;SAW NO CHARACTERS


PREDIQ:	MOVE	B,@A
	ADDI	A,1
	CAIE	B,1	;IS LENGTH OF TEXT 1?
	JRST	PREDQ1	;NO, CANNOT BE "TRUE"
	MOVE	B,@A	;GET TEXT OF ONE WORD ELEMENT
	CAME	B,[ASCIZ /TRUE/]
	POPJ	P,	;NOT "TRUE"
	JRST	CPOPJ1	;IS "TRUE"
PREDQ1:	CAIE	B,2	;FALSE MUST BE TWO WORDS LONG
	POPJ	P,	;NOT "FALSE"
	MOVE	B,@A
	ADDI	A,1
	CAMN	B,[ASCII /FALSE/]
	SKIPE	@A	;NEXT WORD MUST BE 0 TERMINATOR FOR "FALSE"
	POPJ	P,	;R1, IS NOT "FALSE"
	JRST	CPOPJ1	;R2

DNM:	MOVEI	M,0
	TRZ	F,NNUMF
DNM1:	ILDB	C,L
	CAIL	C,"0"
	CAILE	C,"9"
	JRST	DNM2
	TRO	F,NNUMF
	IMULI	M,12
	ADDI	M,-60(C)
	JRST	DNM1
DNM2:	TRNN	F,NNUMF
	POPJ	P,
	JRST	CPOPJ1


DECPRT:	MOVEI	B,12
	JRST	.+2
OCTPRT:	MOVEI	B,10
	MOVEM	B,RADIX
ANYPRT:	IDIV	A,RADIX
	HRLM	B,(P)
	SKIPE	A
	PUSHJ	P,ANYPRT
	HLRZ	C,(P)
	ADDI	C,"0"
	JRST	TYO

SNM:	TRO	F,TF
	SETZM	SRCBOT
SNMA:	IDIVI	A,12
	HRLM	B,(P)
	SKIPE	A
	PUSHJ	P,SNMA
	TRZE	F,TF	;ONLY SET UP B ONCE
	PUSHJ	P,NEWSTR
	HLRZ	C,(P)
	ADDI	C,"0"
	IDPB	C,B
	POPJ	P,

COPYAB:	ILDB	C,A
	IDPB	C,B
	JUMPN	C,.-2
	POPJ	P,


;NEWOPS IS CALLED BY FIRST,LAST,BUTFIRST, AND BUTLAST
;ALL HAPPEN TO DO THE SAME THING

;NEWSTR IS USED BY ANYONE WHO GENERATES A NEW STRING

NEWOPS:	MOVE	A,0(S)	;GET ARG OFF S STACK WITHOUT DESTROYING IT
	TLNE	A,EMPTYF	;IS IT AN EMPTY STRING
	JRST	APOPJ		;YES EXIT FROM CALLING ROUTINE
	TLNE	A,WORDF
	AOS	0(P)	;SKIP JUMP TO SENT IF A WORD
	HRLI	A,010700+W	;IN ANY CASE, MAKE A STRING PTR
	MOVEM	A,SRCBOT
NEWSTR:	HRRZ	B,WTOP
	ADD	B,[XWD 010700+W,1]	;MUST BE EXACTLY THIS FOR TEST IN TBACK TO WORK
	MOVEM	B,NEWBOT
	POPJ	P,



;NEWSRC SETS UP A NEW SOURCE STRING FROM THE FIRST ARG ON THE PDL
;NEWSR1 DOES THE SAME FOR THE SECOND ARG BACK

NEWSR1:	SKIPA	A,-1(S)
NEWSRC:	MOVE	A,0(S)
NEWSR0:	HRLI	A,010700+W
	MOVEM	A,SRCBOT
	POPJ	P,


GNE:	AOS	A,CPP		;GET NEXT NON-COMMENT ELEMENT
	MOVE	A,@CSA
	JUMPE	A,CPOPJ		;END OF LINE, R1
	TLNE	A,COMMTF	;IS IT COMMENT
	JRST	GNE		;YES, SKIP IT
	JRST	CPOPJ1

ENDSTP:	POP	S,C	;FLUSH OLD INPUT FIRST BEFORE
ENDSTR:	MOVEI	C,0	;FINISH UP THE NEW STRING
	IDPB	C,B
ENDST1:	TLNE	B,760000
	JRST	.-2
	MOVEM	B,WTOP
	SUB	B,NEWBOT
	HRRZM	B,@NEWBOT
	HRLZI	B,W+WORDF
	HRR	B,NEWBOT
	TRZE	F,NWF
	TLC	B,WORDF!SENTF
	PUSH	S,B
	POPJ	P,

;THE NEXT SECTION (10-11 PAGES) CONTAINS STORAGE ALLOCATION
;AND GARBAGE COLLECTION ROUTINES FOR THE VARIOUS SPACES

;MOVE A PROCEDURE TO THE END OF THE PROCEDURE SPACE

MOVEPR:	SETOM	NXLINE	;POINTER INTO PS NO LONGER VALID
	HRRZ	B,@RPA	; A CONTAINS A PRODNM
	MOVEI	D,1(B)	;POINT TO NO OF DUMMY ARGS
	ADD	D,@PSD	;SKIP OVER DUMMY ARGS
	AOJA	D,.+2

	MOVEI	D,2(D)	;TWO WORDS PER LINE IN PROCEDURE DIRECTORY
	SKIPE	@PSD	;LINE NO=0?
	JRST	.-2	;NO, NEXT LINE

	MOVEI	D,1(D)
	HRRZ	C,PTOP
	CAIN	D,(C)
	POPJ	P,	;ALREADY THE LAST ONE IN THE SPACE

	SUBI	D,(B)	;D IS NOW THE LENGTH OF THE PROCEDURE
	ADDI	C,@PSD
	CAML	C,PS+1
	EXPAND	D,PS

	HRRZ	E,PTOP
	HRRM	E,@RPA
	HRLI	E,(B)
	MOVE	C,PS
	HRLI	C,(C)
	ADDB	E,C
	MOVE	G,E
	HRLI	D,(D)
	ADD	G,D
	BLT	C,-1(G)
	HLR	G,E
	BLT	G,-1(E)

	MOVEI	A,0	;SEARCH RP FOR ALL THAT POINT AFTER MOVED DIR.
	MOVNI	D,(D)
MPRL:	SKIPN	@RPA
	POPJ	P,	;DONE
	MOVEI	A,1(A)
	HRRZ	C,@RPA
	CAIN	C,-1	;IS THIS ONE DEFINED?
	AOJA	A,MPRL	;NO, DON'T OFFSET IT
	CAIL	C,(B)	;B CONTAINS PTR TO OLD BEG OF MOVED PROC
	ADDM	D,@RPA	;OFFSET IT BY AMOUNT MOVED DOWN
	AOJA	A,MPRL

;REMOVE ONE COMPILED LINE FROM CS

GCCS:	HRRZ	C,@PSA
	MOVEI	D,(C)	;BOTH POINT AT AFFECTED COMPILED LINE
	ADD	D,CS
	MOVEI	E,(D)
	SKIPE	(D)
	AOJA	D,.-1


	MOVE	G,CTOP
	ADD	G,CS
	ADDI	G,-1(E)
	SUBI	G,1(D)
	HRLI	E,1(D)
	SUBI	D,-1(E)
	MOVNI	B,(D)
	BLT	E,(G)	;COPY REST OF WORLD BACK OVER THE DELETED ENTRY
	ADDM	B,CTOP	;OFFSET CTOP
	HRRZ	G,CPP
	CAIL	G,(C)	;SHOULD CPP BE OFFSET?
	ADDM	B,CPP	; YES, IT IS ABOVE AFFECTED LINE
	ADDM	B,CBOT	;OFFSET BASE OF DIRECT LINE, IT ALWAYS MOVES
	ADDM	B,CDBOT	;OFFSET BASE OF ALL DIRECT LINES
	ADDM	B,-1(P)	;MODIFY PDL, WHEN CALLED BY EDL CAUSES A PROBLEM

;NOW OFFSET ALL THE POINTERS ABOVE THE THING REMOVED

	MOVE	E,RP	;BASE OF ALL PROCEDURE NAMES
TOLNH1:	SKIPN	(E)	;ANY MORE PROCEDURES?
	POPJ	P,	;NO
	MOVEI	E,1(E)	;POINT TO POINTER TO PROCEDURE DIRECTORY
	MOVE	D,(E)	;GET IT
	CAMN	D,[EXP -1]	;IS IT DEFINED?
	AOJA	E,TOLNH1	;NO, DO NEXT PROCEDURE
	MOVEI	D,1(D)	;POINT TO NUMBER OF DUMMIES
	ADD	D,PS	;MAKE IT AN ABSOLUTE POINTER,(D) FASTER THAN @PSD
	ADD	D,(D)	;SKIP OVER THEM
	MOVEI	D,1(D)	;POINT TO FIRST LINE NUMBER

TOLNH2:	SKIPN	(D)	;ANY MORE LINES OF THIS PROCEDURE?
	AOJA	E,TOLNH1	;NO, NEXT PROCEDURE
	MOVEI	D,1(D)	;POINT TO SEQNO AND COMPIL PTR
	HRRZ	G,(D)	;GET THE COMPIL PTR
	CAIL	G,(C)	;IS IT AFTER THE HOLE?
	ADDM	B,(D)	;YES, OFFSET IT
	AOJA	D,TOLNH2	;NEXT LINE THIS PROCEDURE


;GARBAGE COLLECTOR PASS ONE
;SEARCH ALL LISTS AND THINGS, REPLACING START OF ALL GOOD STRINGS

GARCOL:	MOVE	S,UUOACS+S
	SETZM	1(S)	;ALWAYS ROOM FOR ONE ZERO AFTER S LIST, BECAUSE
			; CS ALWAYS STARTS AT ONE
	MOVEI	G,0
	TLZ	F,GCF	;INDICATE THAT GC WAS DONE FOR THIS K
	MOVEI	N,.WTOP-.WBASE	;BOTTOM OF FLUSHABLE STRINGS

	MOVE	B,VP
	JSP	S,GCMARK
	MOVE	B,DP
	ADDI	B,2
	JSP	S,GCMARK
	MOVE	B,SP
	JSP	S,GCMARK
	MOVE	B,UA
	JSP	S,GCMARK	;USER ABBREVIATIONS ALSO

	MOVN	B,LINBOT
	ADDM	B,UUOACS+L	;MAKE L RELATIVE TO LINBOT
	MOVEI	B,LINBOT
	JSP	L,MARK1	;MARK LINBOT
	MOVN	B,SRCBOT
	ADDM	B,UUOACS+A
	MOVEI	B,SRCBOT
	JSP	L,MARK1

	MOVN	A,CTOP	;LOOP TO MARK STUFF IN COMPILED STORAGE
	HRLZI	A,(A)	;USE THIS AS END TEST
	MOVE	B,@CSA	;FIRST ONE NEVER LAST ONE BECAUSE CTOP>=1
	TLNN	B,VARF!LITF!COMMTF
	JRST	.+3	;NOTA
	MOVEI	B,@CSA
	JSP	L,MARK1
	AOBJN	A,.-5	;LOOP OVER ALL OF CS STORAGE

	MOVE	A,RP	;MARK PROCEDURES
GCMKP1:	MOVE	B,(A)	;PTR TO PROCEDURE NAME
	JUMPE	B,GCPSS2
	MOVEI	B,(A)
	JSP	L,MARK1	;MARK THE NAME

	MOVEI	A,1(A)	;PNT TO SECOND WORD OF ENTRY IN RP
	MOVE	M,(A)	;LOCATION OF FIRST WORD OF PROCEDURE ENTRY
	CAMN	M,[EXP -1]	;IS THE PROCEDURE DEFINED?
	AOJA	A,GCMKP1	;NO, TRY NEXT ONE
	MOVEI	B,@PSM	;PTR TO PTR TO TEXT OF TO LINE
	JSP	L,MARK1

	MOVEI	M,1(M)	;POINT TO NO OF INPUTS
	MOVN	B,@PSM	; - NO OF INPUTS
	HRLI	M,(B)	; -N,ADDR-1 NOW IN M
	AOJGE	M,.+4	;IF NO DUMMIES, NEXT PROCEDURE

	MOVEI	B,@PSM	;PTR TO PTR TO INPUT NAME
	JSP	L,MARK1
	AOBJN	M,.-2	;NEXT INPUT
	AOJA	A,GCMKP1


GCMARK:	SKIPN	(B)
	JRST	(S)
	JSP	L,MARK1
	AOJA	B,GCMARK

MARK1:	MOVE	D,(B)	;CALLED WITH MOVEI B,ADDR OR EQUIVALENT
	CAME	D,[EXP -1]
	TLNN	D,W
	JRST	(L)
	CAILE	N,(D)	;IS THIS A FLUSHABLE STRING?
	JRST	(L)	;NO
	HLRZ	C,@WSD	;GET CURRENT BACK POINTER
	HRRM	C,(B)	;PUT IT INTO THIS REFERRER
	HRLM	B,@WSD	;PUT ADDR OF THIS REFERRER AS TOP OF BACK CHAIN
	JRST	(L)


;GARBAGE COLLECTOR PASS TWO

GCPSS2:	HRRZ	E,NEWBOT
	ADDI	E,(W)	;FOR END TEST, QUIT BEFORE NEW STRING
	MOVEI	A,.WTOP-.WBASE(W)	;SOURCE IS ABSOLUTE
	MOVEI	B,.WTOP-.WBASE	;DESTINATION IS RELATIVE
GCPS2A:	CAIN	E,(A)	;IS THE NEXT STRING THE UNFINISHED ONE?
	JRST	GCPS2C	;YES, DO THE UNFINISHED ONE SPECIAL

	MOVE	C,(A)	;GET THE LENGTH WORD
	TLNN	C,-1	;DOES IT HAVE A BACK CHAIN POINTER, IE MARKED?
	JRST	GCPS2B	;NO, SKIP THE BLT

	HLRZ	D,C	;TRACE THE BACK CHAIN
	HRRZ	G,(D)
	HRRM	B,(D)	;REPLACE BACK CHAIN POINTER WITH NEW RELATIVE POINTER
	MOVEI	D,(G)	;PUT THE NEW BACK PTR IN D
	JUMPN	D,.-3	;THE CHAIN CONTINUES

	HRRZM	C,(A)	;CLEAR THE BACK CHAIN POINTER IN LENGTH WORD
	HRLZI	G,(A)	;BLT SOURCE
	HRRI	G,@WSB	;BLT DEST, MAKING IT ABSOLUTE
	ADDI	B,(C)	;DEST+LENGTH=END OF BLT
	BLT	G,@WSB	;MOVE IT
	ADDI	B,1	;MAKE B POINT BEYOND END OF THIS STR, AT BEG OF NEXT
GCPS2B:	ADDI	A,1(C)	;NEXT SOURCE
	JRST	GCPS2A

GCPS2C:	SUBI	E,@WSB	;MAKE E THE AMOUNT COLLECTED
	MOVNI	G,(E)		;USE G TO OFFSET WTOP,ETC
	HRRM	B,NEWBOT	;B IS THE RELATIVE DEST FOR THE STRING
	ADDM	G,UUOACS+B
	ADDM	G,WTOP

	HRRZ	D,JOBREL
	SUBI	D,(E)
	HRLI	B,(A)
	ADDI	B,(W)
	CAMG	A,JOBREL	;DON'T GET CAUGHT WITH ILL MEM REF
	BLT	B,(D)

GCPS2D:	CAIG	E,2000	;DID WE RECOVER MORE THAN A FULL K?
	JRST	GCPSS3	;NO
	MOVEI	A,@WTOP		;YES, REMOVE EXTRA K'S
FOR TEN50,<	CALL	A,[SIXBIT /CORE/]
	 HALT>

FOR TENEX,<	JSP	E,SETMEM>

;GARBAGE COLLECTOR PASS THREE

GCPSS3:	MOVEI	D,2(D)
	CAMLE	D,JOBREL
	JRST	GCPS3B		;AVOID DOING BLT ON FULL ALLOCATION
	SETZM	-1(D)
	HRLI	D,-1(D)
	BLT	D,@JOBREL

GCPS3B:	MOVE	G,LINBOT
	ADDM	G,UUOACS+L
	MOVE	G,SRCBOT
	ADDM	G,UUOACS+A
	JRST	(P)

FOR TEN50,<
ALLOC:	EXCH	A,JOBCNI	;SAVE A AND GET REASON FOR TRAP
	TRNE	A,20000	;WAS IT AN ILL MEM REF?
	JRST	ALOCWS	;YES, WORKSPACE SPACE NEEDED
	EXCH	A,JOBCNI	;CLEAN UP
>

ALLOCP:	JUMPG	S,ALOCSP	;PDL TRAP, WAS IT ARG STACK?
	JUMPL	P,[ERROR .]
	EXPAND	PP	;NO, CONTRO STACK
	HRL	P,ALOCTB+PP-RP
	TLC	P,-1
	ADD	P,[XWD 1,0]	;MAKE IT TWO'S COMPLEMENT
	JRST	ALOCAL

ALOCSP:	EXPAND	SP
	HRL	S,ALOCTB+SP-RP
	TLC	S,-1
	ADD	S,[XWD 1,0]
	JRST	ALOCAL

FOR TEN50,<
ALOCWS:	EXCH	A,JOBCNI
	EXPAND	WS

ALOCAL:	MOVEM	A,JOBCNI
	HRRZI	A,220000
	CALLI	A,16	;REENTER TRAPPING MODE
	MOVE	A,JOBCNI
	JRST	2,@JOBTPC
>

FOR TENEX,<

ALOCWS:	EXPAND WS
ALOCAL:	DEBRK


LEVTAB:	EXP LEVLTB,LEVLTB+1,LEVLTB+2

DATAEX:	ERROR	.
>

ALLOCATOR:
	LDB	C,[POINT 4,JOBUUO,12]	;GET THE AC NUMBER
	HRRZ	A,JOBUUO	;WHICH SPACE IS IT?
	HRRZ	B,ALOCTB-RP(A)	;HOW MUCH SPACE MUST WE ADD NOW?
	JUMPE	C,.+3	;SKIP NEXT TWO IF NO AC SPECIFIED
	CAMGE	B,UUOACS(C)	;IS THE SPECIFIED AMOUNT GREATER THAN THE NORMAK AMOUNT
	MOVE	B,UUOACS(C)	;REQUESTED AMOUNT GREATER THAN NORMAL
	MOVEM	B,GCTEM
	ADDI	B,@WTOP
	CAIN	A,WS	;IS THE REQUEST FOR MORE WORK SPACE?
	JRST	ALLOC0	;YES
	CAMG	B,JOBREL	;DOES THE REQUESTED AMOUNT FIT
	JRST	ALLOC1	;YES, DON'T ASK FOR MORE

FOR TEN50,<
	HRRZ	C,JOBREL	;NEED MORE CORE
	ADDI	C,2000	;ALWAYS ASK FOR ONE AT A TIME
	CALL	C,[SIXBIT /CORE/]
	 JRST	ALLC01	;NO MORE CORE AVAILABLE, TRY TO GET IT WITH GC
>
FOR TENEX,<
	AOS	A,JOBREL
	CAIL	A,770000
	JRST	ALLC01	;TOO MUCH, CANNOT ASK FOR MORE
	JSP	G,NEWMEM
	HRRZ	A,JOBUUO
>
	TLO	F,GCF
	JRST	ALLOC1	;GOT ONE

ALLOC0:	TLNE	F,GCF	;HAS THIS K BEEN GC'ED?
	JRST	ALLC01	;NO, GO DO IT
FOR TEN50,<
	HRRZ	C,JOBREL	;YES, TRY FOR MORE CORE
	ADDI	C,2000
	CALL	C,[SIXBIT /CORE/]
	 ERROR	PUNT	;CAN'T GET MORE CORE AND CAN'T SAVE BY GC
>
FOR TENEX,<
	AOS	A,JOBREL
	CAIL	A,770000
	 ERROR	PUNT
	JSP	G,NEWMEM
>
	TLO	F,GCF	;NEW K HAS NOT YET BEEN GC'ED
	JRST	UUORET	;RETURN

ALLC01:	JSP	P,GARCOL
	HRRZ	A,JOBUUO
	CAIN	A,WS	;WAS IT WORKSPACE CALL?
	JRST	UUORET	;YES, NOW DONE
	MOVE	C,GCTEM
	ADDI	C,@WTOP
	CAMLE	C,JOBREL	;DID THE GC GIVE US ENOUGH ROOM?
	 ERROR	PUNT	;NO

ALLOC1:	MOVE	B,GCTEM
	MOVEI	G,(B)
	HRLI	G,(G)
	MOVE	H,1(A)
	HRRZ	C,JOBREL
	HRRZI	D,1(C)
	SUBI	D,(B)
	HRLZI	D,(D)
	HRRI	D,1(C)
	JRST	ALLOC3

ALLOC2:	MOVE	E,D
	BLT	E,(C)
	SUBI	C,(B)
ALLOC3:	SUB	D,G
	CAIGE	H,(D)
	JRST	ALLOC2

	HRLZI	D,(H)
	HRRI	D,1(H)
	SETZM	(H)
	ADDI	H,(B)
	BLT	D,-1(H)

	MOVEI	A,1(A)
	JSP	D,RPOINT

UUORET:	MOVE	17,[XWD UUOACS+1,1]	;DON'T RESTORE 0
	BLT	17,17
	JRST	2,@UUOTRP


RPOINT:	HLRZ	C,ALOCTB-RP(A)	;POINTER TO LIST OF WORDS TO UPDATE
	ADDM	B,(A)		;UPDATE THE BASE IN THE BASE TABLE
	JUMPE	C,ALLOC5	;NO SECONDARY BASE POINTERS
	MOVE	E,(C)		;GET AN ADDRESS OF A SECONDARY BASE PTR
	JUMPE	E,ALLOC5	;NO MORE IN THIS LIST
	ADDM	B,(E)		;UPDATE IT
	AOJA	C,.-3
ALLOC5:	CAIE	A,WS
	AOJA	A,RPOINT	;MORE TO DO IN ALOCTB
	JRST	(D)


FOR TENEX,<

SETMEM:	IORI	A,1777
	MOVEM	A,JOBREL
	MOVEI	A,1(A)
	MOVEI	C,(A)
	ASH	A,-11	;DIVIDE BY PAGE SIZE
	HRLI	A,400000
	RPACS		;DID I CREATE THIS PAGE BEFORE?
	TLNN	B,(1B5)	;IF SO, THEN THIS BIT SET
	MOVE	B,(C)	;IF NOT, CREATE IT
	MOVEI	B,0	;NO ACCESS
	SPACS
	SUBI	A,2
	HRLZI	B,160000
	SPACS		;ALL ACCESS FOR PREVIOUS K
	JRST	(E)


NEWMEM:	JSP	E,SETMEM	;SET NEW BOUND
	MOVNI	A,1777
	HRRZ	B,JOBREL
	ADDI	A,(B)
	HRLI	A,(A)
	SETZM	(A)
	ADDI	A,1
	BLT	A,(B)	;AND ZERO THE NEW MEMORY
	JRST	(G)
>

;COMPRESS ALL AREAS TO BE AT MOST CURRENTLY USED PLUS INITIAL ALLOCATION

CMPRSS:	HRLZI	G,-3	;THREE PAIR TABLES
CMPRS1:	MOVE	A,RP(G)	;FETCH THE BASE OF THE TABLE
	SKIPN	(A)	;ANY MORE PAIRS?
	JRST	.+3	;NO
	MOVEI	A,2(A)
	JRST	.-3	;TRY NEXT PAIR

	JSP	L,SHORTN	;YES
	AOBJN	G,CMPRS1	;OTHER PAIR TABLES

	HRLI	G,-3	;THREE TABLES WITH TOP POINTERS
CMPRS2:	HRRZ	A,PTOP-3(G)	;PTOP FIRST IN ORDER
	ADD	A,RP(G)		;TOPS ARE RELATIVE, MAKE ABSOLUTE
	JSP	L,SHORTN
	AOBJN	G,CMPRS2	;OTHER TOP TABLES

	HRLI	G,-2	;TWO PDP'S
CMPRS3:	HRRZ	A,UUOACS+S-6(G)	;GET CURRENT TOP OF STACK
	JSP	L,SHORTN		;PDP'S ARE ALREADY ABSOLUTE
	AOBJN	G,CMPRS3	;OTHER PDP

	HRRZ	A,JOBREL
	SUBI	A,@WTOP	;DIFFERENCE BETWEEN LAST STRING AND TOP OF CORE
	CAIG	A,2000	;IS IT MORE THAN A K
	JRST	UUORET	;NO, DON'T CONTRACT

	MOVEI	A,@WTOP
FOR TEN50,<
	CALL	A,[SIXBIT /CORE/]	;ONLY HAVE TO BE ENOUGH FOR WHAT WE
	JRST	ILLUUO	;BIG TROUBLE
>
FOR TENEX,<	JSP	E,SETMEM>
	JRST	UUORET	;HAVE NOW TO FIT IN CORE

SHORTN:	HRRZ	B,ALOCTB(G)	;SHORTEN THIS SPACE IF IT NEEDS IT
	ADDI	A,(B)
	CAML	A,RP+1(G)	;MORE THAN MINIMUM ALLOC LEFT OVER?
	JRST	(L)		;NO
	HRL	A,RP+1(G)
	BLT	A,@WTOP
	HLRZ	B,A	;EVEN IF A CHANGES
	SUBI	B,(A)	;THE TWO HALVES REMAIN RELATIVELY CONSTANT
	MOVNI	B,(B)
	MOVEI	A,RP+1(G)
	JSP	D,RPOINT
	MOVE	W,UUOACS+W	;FOR NEXT @WTOP
	JRST	(L)

;STUFF TO BE COPIED TO UNSHARED CORE AT START OF RUN

DEFINE TT (A1,A2,A3)
<
IFB <A3>,<EXP A2>
IFNB <A3>,<XWD A3,A2>
>
ALOCTB:	TABLES

DEFINE MM (A1,A2)
<XWD A2,0>

SPFRST:	Z
	JRST	DOUUO
	POINTR	;ALL THE LH'S OF THE INDEXED POINTERS
	EXP 0,1,2	;PTOP,CTOP,DTOP, IN THAT ORDER!
	XWD W,.WTOP-.WBASE-1
	EXP 1			;CDBOT INITIALIZED THE SAME AS CTOP
	XWD 525252,123457	;AN INITIAL RANDOM BITS, FIX LATER
				;SO THE STARTING POINT IS ALSO RANDOM
FOR TENEX,< EXP 100,101	;PRIMARY INPUT FILE, PRIMARY OUTPUT FILE

	Z	;BITS FOR JFNTAB
	XWD 377777,377777	;NO INPUT OR OUTPUT FILE FOR STRING
	[ASCIZ /DSK/]		;DEVICE
	Z
	Z
	[ASCIZ /LGO/]		;EXTENSION
	Z
	Z
	XWD 1,BREAKY	;CH 0, PRIORITY  ABOVE ALL OTHERS
	REPEAT 8,<Z>	;CH 1-8 UNUSED
	XWD 2,ALLOCP	;CH 9, PDL TRAP
	XWD 2,EOFILE	;CH 10
	XWD 2,DATAEX	;CH 11
	REPEAT 4,<Z>	;CH 12-15 UNUSED
	XWD 2,ALOCWS	;CH 16 ILLEGAL MEMORY READ
>
SPLLEN==.-SPFRST



DEFINE ELM1 (NAME,VAL)
<NAME==.-.WBASE
EXP 1,VAL
>

.WBASE=.
ELM1 EMPTYV,0
ELM1 TRUEV,ASCIZ /TRUE/
FALSEV==.-.WBASE
EXP 2
ASCIZ /FALSE/
ELM1 LINEFV,012B6
ELM1 CARETV,015B6
ELM1 FORMFV,014B6
ELM1 BLANKV,002B6
ELM1 BELLV,007B6
ELM1 QUOTEV,042B6
ELM1 SKIPV,BYTE (7) 15,12
.WTOP==.



;MACHINE DEFINED PROCEDURES

DEFINE MPM (NAME,GOTO,NARG,BITS)
<IFB <BITS>,<XWD WORDF,[ASCIZ /NAME/]>
IFNB <BITS>,<XWD BITS!WORDF,[ASCIZ /NAME/]>
 XWD NARG-1,GOTO>



DEFINE PAIR (A,B)
<EXP [ASCIZ "A"],[ASCIZ "B"]>


DEFINE MVM (A,B,C)
<EXP [ASCIZ "A"]
IFNB <C>,<XWD C,B>
 IFB <C>,<XWD W+WORDF,B>>




ABBT:
PAIR ABB,ABBREVIATION
PAIR ABBS,ABBREVIATIONS
PAIR ABT,ABBREVIATE
PAIR B,BOTH
PAIR BF,BUTFIRST
PAIR BL,BUTLAST
PAIR C,COUNT
PAIR DIFF,DIFFERENCE
PAIR EDL,EDIT LINE
PAIR EDT,EDIT TITLE
PAIR EI,EITHER
PAIR EP,EMPTYP
PAIR ER,ERASE
PAIR ERL,ERASE LINE
PAIR F,FIRST
PAIR GP,GREATERP
PAIR GTL,GO TO LINE
PAIR IFF,IF FALSE
PAIR IFT,IF TRUE
PAIR L,LAST
PAIR LC,LIST CONTENTS
PAIR LE,LIST ENTRY
PAIR LL,LIST LINE
PAIR MAX,MAXIMUM
PAIR MIN,MINIMUM
PAIR NP,NUMBERP
PAIR OP,OUTPUT
PAIR P,PRINT
PAIR PRS,PROCEDURES
PAIR RQ,REQUEST
PAIR S,SENTENCE
PAIR SP,SENTENCEP
PAIR T,TEST
PAIR W,WORD
PAIR WP,WORDP
PAIR ZP,ZEROP
0


;MACHINE DEFINED VARIABLE NAMES

MV:
EXP [ASCIZ "EMPTY"]
XWD W+WORDF!SENTF!EMPTYF,EMPTYV
MVM LINE FEED,LINEFV
MVM CARRIAGE RETURN,CARETV
MVM FORM FEED,FORMFV
MVM BLANK,BLANKV
MVM BELL,BELLV
MVM QUOTE,QUOTEV
MVM SKIP,SKIPV
MVM CONTENTS,CCONTE,COMPUT
0


CMPT:
	MPM ABBREVIATE,ABBREVIATE,0
ABREVL:	MPM ABBREVIATION,SPECWD,0
ABRVSL:	MPM ABBREVIATIONS,SPECWD,0
ALLL:	MPM ALL,SPECWD,0
ANDL:	MPM AND,SPECWD,0
ASL:	MPM AS,SPECWD,0
	MPM BOTH,BOTH,2
	MPM BUTFIRST,BUTFIRST,1
	MPM BUTLAST,BUTLAST,1
	MPM CANCEL,RETTRN,0
CONTNL:	MPM CONTENTS,SPECWD,0
	MPM COUNT,COUNT,1
	MPM DDT,CALDDT,0
	MPM DIFFERENCE,DIFF,2
	MPM DO,CALLDO,1
	MPM EDIT,EDIT,0
	MPM EITHER,EITHER,2
	MPM EMPTYP,EMPTYP,1
	MPM END,ENND,0
ENTRYL:	MPM ENTRY,SPECWD,0
	MPM ERASE,ERASE,0
	MPM EXIT,EXIT,1
FALSEL:	MPM FALSE,SPECWD,0
	MPM FIRST,FIRST,1
	MPM GET,GET,0
	MPM GO,GOTO,0
	MPM GREATERP,GRATRP,2
	MPM IF,IF,0
	MPM IS,IS,2
	MPM LAST,LAST,1
LINELL:	MPM LINE,SPECWD,0
	MPM LIST,LIST,0
	MPM LOCAL,LOCAL,1
	MPM MAKE,MAKE,2,400000	;SPECIAL TO COMPIL
	MPM MAXIMUM,MAXIM,2
	MPM MINIMUM,MINIM,2

NAMESL:	MPM NAMES,SPECWD,0
	MPM NUMBERP,NUMBRP,1
OFL:	MPM OF,SPECWD,0
	MPM OUTPUT,OUTPUT,1
	MPM PRINT,PRINT,1
PROCDL:	MPM PROCEDURES,SPECWD,0
	MPM RANDOM,RANDOM,0
	MPM REQUEST,REQUEST,0
	MPM SAVE,SAVE,0
	MPM SENTENCE,SENTENCE,2
	MPM SENTENCEP,SENTP,1
	MPM STOP,ESTOP,0
	MPM SUM,SUM,2
	MPM TEST,TEST,1
	MPM THING,THING,1
TITLEL:	MPM TITLE,TITLE,0
TOL:	MPM TO,TO,0
TRACEL:	MPM TRACE,TRACE,0
TRACSL:	MPM TRACES,SPECWD,0
TRUEL:	MPM TRUE,SPECWD,0
	MPM TYPE,TYPE,1
	MPM WORD,WORD,2
	MPM WORDP,WORDP,1
	MPM ZEROP,ZEROP,1
0		;ALL TABLE SEARCHES TERMINATE ON A 0
LPREN:	MPM (,EXCTLP,0	;THESE ARE HERE FOR EASE IN LISTING AND EDITING
RPREN:	MPM ),EXCTRP,0

;LOGO SYSTEM STUFF - LISTING, EDITING, AND ERASING

LIST:
	PUSH	P,[EXP LISTXT]	;SO ALL SUBRS CALLED MAY EXIT WITH POPJ
	AOS	A,CPP
	MOVE	A,@CSA	;WHAT TO LIST
	TLNE	A,UPRF	;IS IT A USER PROCEDURE NAME?
	JRST	LISTPR	;YES, GO LIST IT
	TLZN	A,MPF
	ERROR	LSTER1
	CAIN	A,ALLL+1
	JRST	LALL
	CAIN	A,CONTNL+1	;IS IT "CONTENTS"?
	JRST	LISTCT	;GO LIST CONTENTS
	CAIN	A,TITLEL+1	;IS IT "TITLE"?
	JRST 	LISTTL
	CAIN	A,LINELL+1	;IS IT "LINE"?
	JRST	LISTLN
	CAIN	A,ENTRYL+1
	JRST	LISTEN
	CAIN	A,NAMESL+1
	JRST	LSTFNM
	CAIN	A,ABRVSL+1
	JRST	LSTFAB
	CAIN	A,ABREVL	;"ABBREVIATION"?
	JRST	LST1AB		;LIST ONE ABBREVIATION
	ERROR	LSTER1	;YOU CAN'T LIST THAT.

LALL:	PUSHJ	P,GNE
	JRST	LALLJ
	TLZN	A,MPF		;IS ARG A MACHINE NAME?
	ERROR	LSTER2		;NO, ALL THE REST MUST BE WELL KNOWN NAMES
	CAIN	A,PROCDL+1	;ALL PROCEDURES?
	JRST	LISTAP	;YUP
	CAIN	A,NAMESL+1	;ALL NAMES?
	JRST	LISTAN	;YUP
	CAIN	A,ABRVSL+1	;ALL ABBREVIATIONS?
	JRST	LISTAA
	ERROR	LSTER2	;LIST ALL WHAT?

LALLJ:	PUSHJ	P,LISTAP
	PUSHJ	P,LISTAN
	PUSHJ	P,LISTAA
	SOS	CPP
	POPJ	P,


;LIST CONTENTS AND LIST ALL PROCEDURES

LISTCT:	PUSHJ	P,GNE	;IS IT LC FROM AN ENTRY?
	 JRST	LSTCT0
	SOS	CPP
	TRO	F,ECONTF
	JRST	LISTFL
LSTCT0:	SKIPA	A,[EXP LISTTO]
LISTAP:	MOVEI	A,LISTPR
	PUSH	P,A
	PUSHJ	P,CRLF
	MOVEI	A,0	;FOR LOOP OVER ALL PROCEDURES
LSTCT1:	SKIPN	@RPA	;END OF PROCEDURE NAME LIST?
	JRST	APOPJ
	MOVEI	A,1(A)	;MAKE A LOOK LIKE UPROD COMPILE PTR
	MOVNI	B,1
	CAMN	B,@RPA
	AOJA	A,LSTCT1
	PUSH	P,A	;SAVE IT
	PUSHJ	P,@-1(P)	;CALL THE RIGHT ROUTINE
	JFCL		;DON'T CARE IF IT WASN'T DEFINED
	POP	P,A	;REMEMBER WHERE WE WERE IN LIST
	AOJA	A,LSTCT1	;GO GET NEXT PROD NAME

LISTTO:	MOVE	N,@RPA	;GET POINTER TO PROCEDURE DIRECTORY
	CAMN	N,[EXP -1]	;IS THIS PROCED DEFINED?
	POPJ	P,	;NO, R1 EXIT IMMEDIATELY
	MOVEI	A,-1(A)
	MOVE	B,[ASCIZ /(TRACED) /]
	SKIPGE	A,@RPA		;SKIP IF NOT TRACED
	PUSHJ	P,TOSS		;TYPE "(TRACED) " IF IT IS
	MOVEI	B,[ASCIZ /TO /]
	PUSHJ	P,TOSS

	PUSHJ	P,PTOSS	;TYPE PROCEDURE NAME

	MOVEI	E,"/"	;PREPARE FOR LISTING DUMMY ARG NAMES
	MOVEI	M,1(N)	;PTR TO NO. OF DUMMY ARGS TO M
	MOVN	A,@PSM	;GET -NO. ARGS
	HRLI	M,(A)	;-N,A-1 IN M
	ADDI	M,1	;-N,A IN M, WHERE A IS ADDR OF FIRST DUMMY ARG
	JUMPE	A,LISTT2	;JUMP IF NO DUMMIES AT ALL

LISTT1:	PUSHJ	P,PSPACE
	MOVE	B,@PSM	;GET PTR TO GUMMY ARG NAME
	MOVEI	A,[ASCIZ /AND /]
	TLNE	B,ANDF	;IS THIS DUMMY PRECEDED BY "AND"?
	PUSHJ	P,PTOSSM	;YES, TYPE "AND "
	MOVE	A,@PSM	;GET DUMMY AGAIN
	TRO	F,PREFIX!SUFFIX	;PUT "/" MARKS AROUND IT
	PUSHJ	P,PTOSS
	AOBJN	M,LISTT1	;GET NEXT DUMMY IF MORE

LISTT2:	MOVEI	A,(N)	;DONE WITH DUMMIES, SEE IF THERE'S A COMMENT
	MOVE	A,@PSA	;PRESERVE M FOR USE IN LISTLL
	JUMPE	A,LISTT3	;NO COMMENT
	PUSHJ	P,PSPACE
	MOVEI	E,";"	;PREFIX COMMENT WITH SEMICOLON
	TRO	F,PREFIX
	PUSHJ	P,PTOSS

LISTT3:	PUSHJ	P,CRLF	;PRETTY UP
	JRST	CPOPJ1	;R2



LISTTL:	MOVE	A,TOPROD	;LIST TITLE
	JUMPE	A,[ERROR .]	;MUST HAVE A PROCEDURE OPEN
	PUSHJ	P,LISTTO	;LIST IT
	ERROR	IOPERR	;BIG TROUBLE
	POPJ	P,

;LIST A PROCEDURE

LISTPR:	PUSHJ	P,CRLF	;ONE EXTRA IN FRONT
	PUSH	P,A	;SAVE WHICH PROCEDURE
	PUSHJ	P,LISTTO	;DO THE "TO" LINE
	 ERROR	EVER3	;X NEEDS A MEANING
	PUSHJ	P,LSTPLN	;DO CURRENT LINE
	AOJA	M,.-1	;DO NEXT LINE
	POP	P,A
	HRRZI	A,(A)
	CAMN	A,TOPROD
;SHOULD WE TYPE "END"? TESTING RH ONLY
	POPJ	P,		;NO, PROCEDURE IS STILL OPEN
	MOVEI	B,[ASCIZ /END/]
	PUSHJ	P,TOSS
	JRST	CRLF


LISTLN:	PUSHJ	P,SNMEVL
	MOVEI	M,(A)	;SRCHLN USES A, LSTPLN USES M
	PUSHJ	P,LSTPLN
	POPJ	P,
	ERROR	IOPERR


SNMEVL:	SKIPN	TOPROD
	ERROR	.		;MUST HAVE A PROCEDURE OPEN
	PUSHJ	P,NUMEVL
	MOVE	A,TOPROD
	JSP	C,SRCHL1	;IS IT A LINE NO OF THE CURRENT PROCEDURE?
	 CAIE	B,(M)
	 ERROR	GOERR3
	POPJ	P,

SAVAA:	SKIPA	A,[EXP SAVAAR]
LISTAA:	MOVEI	A,LSTAAR
	PUSH	P,A	;SAVE NAME OF ROUTINE TO CALL
	MOVEI	A,2
	MOVEM	A,BCHAR
	PUSHJ	P,CRLF
	MOVE	G,UA	;POINT AT FIRST ABBREV NAME
LSTAAL:	SKIPN	A,(G)	;ANY MORE ABBREVIATIONS?
	JRST	APOPJ	;NO
	MOVNI	C,1
	CAME	C,1(G)	;IS THIS ONE DEFINED?
	PUSHJ	P,@(P)	;YES, OUTPUT IT
	ADDI	G,2
	JRST	LSTAAL

LSTAAR:	MOVEI	E,":"
	TRO	F,SUFFIX
	PUSHJ	P,PTOSS		;TYPE NAME:
	PUSHJ	P,PTAB
	MOVE	A,1(G)
	PUSHJ	P,PTOSS	;TYPE VALUE
	PUSHJ	P,CRLF	;LINE
	POPJ	P,


LST1AB:	PUSHJ	P,EVAL
	POP	S,A
	HRRZ	B,UA
	PUSHJ	P,LOOKUP	;LOOK UP THAT ABBREVIATION
	 ERROR			;NO SUCH ABBREVIATION
	MOVEI	G,(N)
	MOVE	A,(G)
	JRST	LSTAAR

;LIST ONE LINE OF A PROCEDURE

LSTPLN:	HRRZ	A,@PSM	;GET LINE NO, M SET UP BY LISTTO
	JUMPE	A,CPOPJ1	;NO MORE LINES, GIVE R2
	PUSHJ	P,DECPRT	;TYPE LINE NO., MAX OF 5 CHARS
	MOVEI	A,1
	ADD	A,CHARNO
	MOVEM	A,BCHAR
	MOVEI	M,1(M)	;POINT AT PTR TO COMPILED CODE
	HRRZ	D,@PSM	;GET PTR TO COMPILED LINE
LSTPLL:	MOVE	A,@CSD	;GET NEXT ELEMENT OF THE LINE
	JFFO	A,.+3	;WHAT KIND OF ELEMENT?
	PUSHJ	P,CRLF	;EOL, TYPE CR
	POPJ	P,		;DONE WITH THIS LINE, GIVE R1
	PUSH	P,D	;REMEMBER WHERE WE ARE IN THE LINE
	PUSHJ	P,PSPACE	;SPACE BEFORE EACH ELEMENT
	JRST	@.+1(B)	;DISPATCH ON TYPE
	EXP LSTPMP,LSTPUP,LSTPV,LSTPL,LSTPCM

LSTPMP:	MOVE	A,-1(A)	;MACHINE PROCEDURE, GET PTR TO NAME
	HRLI	A,440700	;MAKE TEXT PTR
LSTPM1:	PUSHJ	P,PWORD	;TYPE ELEMENT
LSTPM2:	POP	P,D
	AOJA	D,LSTPLL	;GO BACK FOR NEXT ELEMENT ON THE LINE

LSTPUP:	MOVEI	A,-1(A)	;USER PROCEDURE, POINT AT NAME PTR
	MOVE	A,@RPA	;GET PTR TO NAME
	HRLI	A,010700+W	;MAKE TEXT PTR
	JRST	LSTPM1

LSTPV:	MOVEI	E,"/"	;VARIABLE, AFFIX "/" MARKS
	JRST	.+2
LSTPL:	MOVEI	E,042	;LITERAL, AFFIX QUOTES
	TRO	F,PREFIX!SUFFIX
LSTPL1:	PUSHJ	P,PTOSS
	JRST	LSTPM2

LSTPCM:	MOVEI	E,";"	;COMMENT, PREFIX SEMICOLON
	TRO	F,PREFIX
	JRST	LSTPL1


;LIST ALL NAMES

LISTAN:	MOVEI	A,2
	MOVEM	A,BCHAR
	PUSHJ	P,CRLF

	MOVE	A,DP	;DUMMIES FIRST
	ADD	A,DTOP	;FROM NEWEST FIRST
	SUBI	A,2
	SKIPN	(A)
	JRST	LSTANG
	PUSHJ	P,LSTANR	;EVEN ONES WITH EMPTY VALUES
	JRST	.-4

LSTANG:	SKIPA	A,[EXP LSTANR]	;LISTING DOES ALL
SAVAN:	MOVEI	A,SAVANR	;SAVE ONLY SAVES GLOBALS
	PUSH	P,A	;SAV ROUTINE TO CALL
	PUSHJ	P,CRLF
	MOVE	A,VP
LSTANH:	SKIPN	(A)	;ANY MORE?
	JRST	APOPJ	;NO, DONE
	MOVEI	C,-1	;RH=0_EMPTY
	TDNE	C,1(A)	;IS VALUE EMPTY?
	PUSHJ	P,@(P)	;NO, LIST THIS ONE
	ADDI	A,2
	JRST	LSTANH	;TRY NEXT ONE

LSTANR:	PUSH	P,A	;TYPE ANY KIND OF VALUE
	MOVEI	E,"/"	;SLASH THE NAME
	TRO	F,PREFIX!SUFFIX
	MOVE	A,(A)	;FETCH THE NAME
	PUSHJ	P,PTOSS
	MOVEI	A,[ASCIZ / IS /]
	PUSHJ	P,PTOSSM
	MOVEI	E,042	;QUOTE THE VALUE

LSTNR1:	MOVE	A,(P)
	TRO	F,PREFIX!SUFFIX
	MOVE	A,1(A)	;GET THE VALUE
	PUSHJ	P,PTOSS
	PUSHJ	P,CRLF
APOPJ:	POP	P,A
	POPJ	P,

;LIST X ENTRY NAME WHERE X CAN BE CONTENTS,NAMES,ABBREVIATIONS,ENTRY

LISTEN:	TRO	F,EENTRF
	JRST	LISTFL
LSTFNM:	TROA	F,ENAMEF
LSTFAB:	TRO	F,EABBRF

LISTFL:	TRZ	F,TF
	PUSHJ	P,GENFLN
	MOVEI	A,LEOFIL	;WHERE TO GO ON EOF
	PUSHJ	P,OPENR	;OPEN THE FILE FOR READING
LNLINE:	PUSHJ	P,TIS	;READ A LINE
	 JRST COMEX
	POP	S,A
	TRNE	F,EENTRF
	 JRST	LISTIT	;IF LIST ENTRY, LIST EVERY LINE
	PUSHJ	P,NEWSTR
	PUSHJ	P,NEWSR0
	PUSHJ	P,COPYWD
	 JRST	MBLIST	;NOTHING ON LINE, MAYBE TYPE CRLF
	POP	S,B
	MOVE	A,SRCBOT
	TRZE	F,STORED
	 JRST	LNLINE	;STORED LINES ONLY GET LISTED ON LIST ENTRY
	ADDI	B,1
	MOVE	C,@B	;FIRST WORD OF FIRST SYMBOL
	CAMN	C,[ASCIZ /TO/]
	TRNN	F,ECONTF
	SKIPA
	JRST	LISTIT	;TO LINE AND LIST CONTENETS
	CAMN	C,[ASCIZ /MAKE/]
	TRNN	F,ENAMEF
	SKIPA
	JRST	LISTIT	;NAME AND LIST NAMES
	CAME	C,[ASCII /ABBRE/]
	JRST	LNLINE	;NOT ABBREVIATE, SO NOTA
	ADDI	B,1
	MOVE	C,@B
	CAME	C,[ASCII /VIATE/]
	JRST	LNLINE	;AGAIN NOT ABBREVIATE
	ADDI	B,1
	SKIPN	@B
	TRNN	F,EABBRF
	JRST	LNLINE	;AND AGAIN
LISTIT:	PUSHJ	P,PTOSS	;LIST THE CURRENT LINE
	TROA	F,TF	;DENOTE THAT A LINE WAS TYPED SINCE LAST MBLIST

MBLIST:	TRZE	F,TF
	PUSHJ	P,CRLF
	JRST	LNLINE


COPYWD:	ILDB	C,A
	JUMPE	C,CPOPJ
	CAIN	C," "
	JRST	COPYW1
	IDPB	C,B
	JRST	COPYWD
COPYW1:	AOS	(P)
	JRST	ENDSTR

FOR TENEX,<
LEOFIL:	CLOSF		;COME HERE ON EOF FOR LIST
	 ERROR
	POP	P,A
	POP	P,A	;RETURN FROM TYI
	POP	P,A	;RETURN FROM TIS
	POP	P,INFILE
	POP	P,LEVLTB+1	;TO FINISH LIST LINE
	MOVEI	A,100
	CAMN	A,INFILE
	TLZ	F,GETF
	DEBRK
>


EDIT:	PUSHJ	P,GNE
	 ERROR	EDTER1
	TLNE	A,UPRF	;IS THAT WHAT A USER PROCEDURE?
	JRST	EDITPR	;YES
	CAMN	A,[XWD MPF,LINELL+1]	;IS THAT WHAT "LINE"?
	JRST	EDITLN	;YES
	CAMN	A,[XWD MPF,TITLEL+1]	;IS IT "TITLE"?
	JRST	EDITTL	;YES
	ERROR	EDTER1	;YOU CANNOT EDIT THAT

EDITPR:	SKIPE	TOPROD	;IS THERE A PROCEDURE OPEN?
	ERROR	EDTER2	;YES, YOU ARE ALREADY EDITING X
	MOVNI	B,1
	CAMN	B,@RPA		;IS THE PROCEDURE DEFINED?
	ERROR	EVER3		;CAN EDIT DEFINED PROCEDURES ONLY
	HRRZM	A,TOPROD	;THE PROCEDURE IS NOW OPEN
	PUSHJ	P,MOVEPR	;MAKE IT LAST
	SOS	PTOP		;PROCEDURE NO LONGER CLOSED
	JRST	COMEX

EDITLN:	PUSHJ	P,SNMEVL
	PUSHJ	P,LINGEN	;GENERATE THE TEXT OF THAT LINE FOR TIS

	JSP	D,COMEXR	;DO END OF LINE CHECKING
	TLO	F,EDITF	;WE ARE NOW EDITING
	POPJ	P,



EDITTL:	SKIPN	M,TOPROD	;CURRENTLY IN A PROCEDURE?
	ERROR
	PUSHJ	P,NEWSTR

	MOVEI	D,TITLEL+1	;"TITLE"
	PUSHJ	P,LNGMP		;COPY MACHINE PROCEDURE NAME
	PUSHJ	P,DSPACE
	MOVEI	D,TOL+1		;"TO"
	PUSHJ	P,LNGMP
	PUSHJ	P,DSPACE
	MOVEI	D,(M)		;NAME OF CURRENT PROCEDURE
	PUSHJ	P,LNGUP		;COPY USER PROCEDURE NAME

	MOVEI	A,(M)
	MOVE	M,@RPA
	MOVEI	M,1(M)
	MOVN	C,@PSM		;- NO OF DUMMIES
	JUMPE	C,EDTTL8	;NO DUMMIES, TRY FOR COMMENT
	MOVEI	M,1(M)		;POINT AT FIRST DUMMY
	HRLI	M,(C)

EDTTL2:	PUSHJ	P,DSPACE
	MOVE	D,@PSM		;GET A DUMMY
	TLNN	D,ANDF		;PRECEDED BY "AND"
	JRST	EDTTL3		;NO
	MOVEI	D,ANDL+1
	PUSHJ	P,LNGMP
	PUSHJ	P,DSPACE
	MOVE	D,@PSM		;GET DUMMY AGAIN

EDTTL3:	PUSHJ	P,LNGV		;TYPE DUMMY WITH SLASHES
	AOBJN	M,EDTTL2	;NEXT IF THERE IS ANOTHER

EDTTL8:	MOVE	A,TOPROD
	MOVE	M,@RPA
	SKIPN	D,@PSM		;IS THERE A COMMENT?
	JRST	EDTTL9		;NO
	PUSHJ	P,DSPACE
	PUSHJ	P,LNGCMT	;STORE COMMENT

EDTTL9:	PUSHJ	P,ENDSTR	;FINISH STRING
	TLO	F,EDITF		;DENOTE THAT TIS SHOULD WORK IN EDIT MODE
	JRST	COMEX

DSPACE:	MOVEI	C," "
	IDPB	C,B
	POPJ	P,


LINGEN:	PUSHJ	P,NEWSTR
	MOVEI	D,1(A)	;POINTER TO POINTER TO COMPILED CODE FOR LINE
	MOVEI	A,(M)	;LINE NUMBER
	PUSHJ	P,SNM	;IS THE FIRST WORD OF THE GENERATED LINE
	MOVE	M,@PSD
LINGE0:	MOVE	D,@CSM	;GET NEXT ELEMENT
	JFFO	D,LINGE1	;DIPATCH ON TYPE
	JRST	ENDSTR		;WHICH WILL EXIT FROM LINGEN WITH POPJ

LINGE1:	MOVEI	C," "
	IDPB	C,B	;BEFORE ALL BUT FIRST ELEMENT PUT A SPACE
	PUSHJ	P,@.+2(E)
	AOJA	M,LINGE0	;BACK FOR NEXT ELEMENT
	EXP	LNGMP,LNGUP,LNGV,LNGL,LNGCMT

LNGMP:	MOVE	A,-1(D)	;FETCH POINTER TO TEXT NAME OF PROCEDURE
	HRLI	A,440700
LNGALL:	MOVEM	A,SRCBOT
	TRZE	F,PREFIX
	IDPB	E,B	;AFFIX PREFIX IF THERE IS ONE
	ILDB	C,A		;COPY THE WHOLE ELEMENT
	JUMPE	C,LNGALE	;DONE IF EOM
	IDPB	C,B
	JRST	.-3	;NEXT CHAR
LNGALE:	TRZE	F,SUFFIX
	IDPB	E,B	;AFFIX SUFFIX IF ONE TOO
	POPJ	P,		;DONE WITH THIS ELEMENT

LNGUP:	MOVEI	A,-1(D)
	MOVE	A,@RPA	;GET NAME OUT OF TABLE
LNGUP1:	HRLI	A,010700+W	;IT IS A WORKSPACE ELEMENT
	JRST	LNGALL

LNGV:	MOVEI	E,"/"
	JRST	.+2
LNGL:	MOVEI	E,042	;LITERAL, AFFIX QUOTES
	TRO	F,PREFIX!SUFFIX
LNGL1:	MOVEI	A,(D)
	JRST	LNGUP1

LNGCMT:	MOVEI	E,";"	;COMMENT PRECEDED BY SEMICOLON
	TRO	F,PREFIX
	JRST	LNGL1

ERASE:	PUSH	P,[EXP ERASXT]
	AOS	A,CPP
	MOVE	A,@CSA	;ERASE WHAT?
	TLNE	A,UPRF	;IS IT A UPROD
	JRST	ERASPR	;YES, ERASE ONE PROCEDURE
	TLZN	A,MPF	;ALL OTHER PARAMETERS MUST BE MACHINE NAMES
	 ERROR	ERSER1
	CAIN	A,LINELL+1	;IS IT "LINE"
	JRST	ERASLN	;YES, ERASE LINE
	CAIN	A,ALLL+1	;IS IT ALL?
	JRST	ERSALL
	CAMN	A,ABREVL+1
	JRST	ERSABB
	CAIN	A,TRACEL+1
	JRST	ERASTR	;ERASE TRACE
	ERROR	ERSER1	;NONE OF THE ABOVE

ERASXT:	SQUEZE	;SEE IF WE CAN REDUCE THE STORAGE ALLOCATION
	JRST	COMEX


ERSALL:	PUSHJ	P,GNE	;ALL WHAT?
	 JRST	ERASAL	;ALL PERIOD
	TLZN	A,MPF	;MUST BE A MACHINE NAME
	 ERROR
	CAIN	A,NAMESL+1	;IS IT "NAMES"
	JRST	ERSALN
	CAIN	A,PROCDL+1	;IS IT "PROCEDURES"
	JRST	ERSALP
	CAIN	A,ABRVSL+1
	JRST	ERSALA	;ALL ABBREVIATIONS
	CAIN	A,TRACSL+1	;"TRACES"?
	JRST	ERSALT
	ERROR

ERASAL:
FOR TEN50,<
	HLRZ	A,JOBSA	;GET FIRST FREE LOC
	ADDI	A,1777	;ONE MORE K
	CALL	A,[SIXBIT /CORE/]
	ERROR	IOPERR	>
FOR TENEX,<
	MOVEI	A,20000
	JSP	E,SETMEM	>
	MOVEI	F,TF	;DON'T TYPE STARTUP MESSAGE AGAIN
	JRST	LOGO+1

ERSALA:	SETZM	@UA
	POPJ	P,
ERSALN:	SETZM	@VP	;ALL NAMES, FLUSH GLOBALS
	POPJ	P,
ERSALP:	SETZM	@RP	;FLUSH ALL PROCEDURE NAMES
	SETZM	PTOP	;FLUSH ALL PROCEDURE DIRECTORIES
	MOVEI	A,1
	MOVEM	A,CTOP	;FLUSH ALL COMPILED CODE
	MOVEI	A,2
	MOVEM	A,DTOP	;FLUSH ALL DUMMY ARGS
	MOVE	S,SP	;CLEAR ARG STACK
	MOVE	P,PP	;CLEAR PUSHDOWN STACK
	SQUEZE	;COMPRESS STORAGE
	JRST	RESET


ERSABB:	AOS	A,CPP
	PUSHJ	P,EVAL
	EXP	.+1
	POP	P,A
	MOVE	A,(S)
	MOVE	B,UA
	PUSHJ	P,LOOKUP
	 JRST	ERSAB1		;NOT A USER ABBREVIATION
	SETOM	1(N)
	POP	S,A
NOSQZE:	POP	P,A
	JRST	COMEX

ERSAB1:	MOVE	A,(S)
	HRRZI	B,ABBT
	PUSHJ	P,SYSLUK
	 ERROR
	PUSH	S,(S)
	SETOM	-1(S)
	POP	P,A
	JRST	ABBREVIATE


ERASTR:	PUSHJ	P,GNE	;ERASE TRACE ON WHAT PROCEDURE?
	 ERROR
	TLNN	A,UPRF	;ONLY USER DEFINED PROCEDURES ARE TRACED
	 ERROR
	MOVEI	A,-1(A)
	HRLZI	B,TRACEF
	ANDCAM	B,@RPA	;CLEAR THE APPROPRIATE BIT
	JRST	NOSQZE

ERSALT:	MOVE	A,RP	;ERASE ALL TRACES
ERSLT1:	HRLZI	B,TRACEF
	ANDCAB	B,(A)	;CLEAR THE BIT
	MOVEI	A,2(A)	;POINT AT NEXT ENTRY
	JUMPN	B,ERSLT1	;NAME NON-ZERO IF NOT AT END OF TABLE
	JRST	NOSQZE

ERASLN:	PUSHJ	P,SNMEVL	;ERASE LINE, WHAT LINE?
ERSLN1:	PUSH	P,A	;@PSA POINTS TO LINE NO
	PUSH	P,A
	MOVEI	A,1(A)	;POINT AT POINTER TO COMPILED CODE
	PUSHJ	P,GCCS	;FLUSH COMPILED CODE FOR LINE
	POP	P,A	;GCCS CLOBBERS THIS ONE
	POP	P,A
	MOVEI	C,@PSA
	MOVE	B,2(C)	;COPY ALL THE REST OF THE LINES BACK TWO
	MOVEM	B,(C)
	MOVEI	C,1(C)
	JUMPN	B,.-3	;TERMINATE ON ZERO LINE NUMBER

	MOVNI	B,2
	ADDM	B,PTOP
	POPJ	P,

ERASPR:	MOVNI	B,1	;ERASE PROCEDURE
	CAMN	B,@RPA	;IS THE PROCEDURE DEFINED?
	 POPJ	P,	;NO, QUIT EARLY
	MOVEI	A,-1(A)	;CLEAR LH OF A
	HRLZI	B,TRACEF
	ANDCAM	B,@RPA	;ERASE THE TRACE ON IT IF ANY
	MOVEI	A,1(A)
	CAMN	A,TOPROD	;IS IT THE PROCEDURE CURRENTLY BEING DEFINED
	SETZM	TOPROD	;YES, WHEN WE'RE DONE, NO PROCEDURE OPEN
	PUSH	P,TOPROD
	MOVEM	A,TOPROD
	PUSHJ	P,MOVEPR	;MAKE THIS ONE LAST TEMPORARILY
	MOVE	A,TOPROD
	MOVE	A,@RPA
	MOVEI	A,1(A)	;PT AT NO OF DUMMIES
	ADD	A,@PSA	;SKIP OVER DUMMIES
	MOVEI	A,1(A)	;PT AT FIRST LINE NO
	SKIPN	@PSA	;ANY MORE LINES?
	JRST	.+3	;NO
	PUSHJ	P,ERSLN1
	AOJA	A,.-3	;GO BACK FOR NEXT LINE

	MOVE	A,TOPROD
	MOVNI	B,1
	EXCH	B,@RPA	;MARK AS UNDEFINED AND FETCH PTR TO BEGINNING
	HRRM	B,PTOP	; OF THE STORAGE WE JUST FREED UP
	POP	P,TOPROD	;RESUME EDITING THE PROCEDURE THAT HAD BEEN
	POPJ	P,


;FILE STUFF

GET:	PUSHJ	P,GENFLN	;GENERATE FILE NAME
	MOVEI	A,EOFILE	;WHERE TO GO ON INPUT EOF
	PUSHJ	P,OPENR		;OPEN IT FOR READING
	TLO	F,ERRORF
	PUSH	P,[EXP EOF1]
	JRST	UPROD1		;CALL MAIN

FOR TENEX,<

EOFILE:	CAIN	A,100		;IS THE OPEN FILE THE TERMINAL?
	 DEBRK			;YES
	CLOSF
	 ERROR
	JRST	GETOUT		;RETURN TO PREVIOUS STATE

EOF1:	POP	P,A		;PREVIOUS FILE HANDLE
	MOVEM	A,INFILE
	CAIN	A,100
	TLZ	F,GETF		;NOT STILL GETTING
	MOVEI	A,COMEX
	MOVEM	A,LEVLTB+1
	DEBRK

OPENR:	HRRM	A,CHNTAB+12	;EOF ERROR DISPATCH LOCATION
	MOVE	A,[XWD 100000,1]
	MOVEM	A,JFNTAB
	MOVEI	A,JFNTAB
	MOVEI	B,0
	GTJFN
	 ERROR	.
	MOVE	B,[XWD 070000,600000]
	OPENF
	 ERROR	.
	POP	S,B		;FLUSH PTR TO FILE NAME

	TLO	F,GETF
	EXCH	A,INFILE
	EXCH	A,(P)
	JRST	(A)		;POPJ WITH AN INTERVENING PUSH
>


FOR TENEX,<
GENFLN:	PUSHJ	P,GETFLN	;FILENAME
	PUSHJ	P,GETFLN	;ENTRYNAME
	PUSHJ	P,NEWSTR
	PUSHJ	P,NEWSR1
	PUSHJ	P,COPYAB
	MOVEI	C,"/"
	DPB	C,B
	PUSHJ	P,NEWSRC
	PUSHJ	P,COPYAB	;FILENAME/ENTRYNAME
	POP	S,A		;FLUSH AN ARG
	PUSHJ	P,ENDSTP	;FLUSH OTHER ARG AND PUT THIS ON STACK
SJNAME:	MOVE	B,(S)
	MOVEI	A,@WSB
	MOVEI	A,1(A)
	MOVEM	A,JFNTAB+4	;SLOT FOR NAME
	POPJ	P,

GETFLN:	PUSHJ	P,GNE	
	 ERROR	ERMSSG
	TLNN	A,UPRF	
	 ERROR	FILER1
	MOVEI	A,-1(A)
	PUSH	S,@RPA
	POPJ	P,
>


SAVE:	PUSHJ	P,GENFLN	;GENERATE FILE NAME
	PUSHJ	P,OPENW		;OPEN FILE FOR WRITING
	TLO	F,SAVEF		;DENOTE A SAVE IN PROGRESS
	PUSHJ	P,LISTAP	;TYPE ALL PROCEDURES TO FILE
	PUSHJ	P,SAVAN		;TYPE ALL GLOBAL NAMES TO FILE
	PUSHJ	P,SAVAA		;ALL ABBREVIATIONS TO FILE
	TLZ	F,SAVEF		;NO MORE TYPING IN THE SAVE
	PUSHJ	P,CLOSEF
	JRST	COMEX

SAVANR:	PUSH	P,A
	MOVEI	A,[ASCIZ /MAKE/]
	PUSHJ	P,PTOSSM
	MOVE	A,(P)	;WHAT NAME
	MOVE	A,(A)	;NAME OF THING
	MOVEI	E,024	;PSEUDO-QUOTE
	TRO	F,PREFIX!SUFFIX
	PUSHJ	P,PTOSS
	PUSHJ	P,DSPACE
	JRST	LSTNR1

SAVAAR:	MOVEI	A,[ASCIZ /ABBREVIATE /]
	PUSHJ	P,PTOSSM
	MOVEI	E,042
	TRO	F,PREFIX!SUFFIX
	MOVE	A,1(G)
	PUSHJ	P,PTOSS
	MOVEI	A,[ASCIZ / AS /]
	PUSHJ	P,PTOSSM
	MOVE	A,(G)
	TRO	F,PREFIX!SUFFIX		;QUOTE THIS ONE TOO
	PUSHJ	P,PTOSS
	JRST	CRLF



;OPENING AND CLOSING FILES FOR SAVE FOR TENEX

FOR TENEX,<

OPENW:	HRLZI	A,400000
	MOVEM	A,JFNTAB
	MOVEI	A,JFNTAB
	MOVEI	B,0
	GTJFN
	 ERROR
	MOVE	B,[XWD 070000,500000]
	OPENF
	 ERROR
	EXCH	A,OUTFILE
	EXCH	A,(P)
	JRST	(A)

CLOSEF:	MOVE	A,OUTFILE
	HRLI	A,400000
	CLOSF
	 ERROR
	HRRZ	A,OUTFILE
	MOVE	B,[XWD 1,7]	;LH OF 7'TH WD OF FDB IS VERSION
	MOVEI	C,GCTEM
	GTFDB
	HLRZ	B,GCTEM
	CAIN	B,1		;IS THIS VERSION VERSION ONE
	JRST	CLOSW1		;YES, ALREADY PROPERLY NAMED
	MOVE	A,[XWD 0,1]
	MOVEM	A,JFNTAB
	PUSHJ	P,SJNAME

	MOVEI	A,JFNTAB
	MOVEI	B,0
	GTJFN
	 ERROR
	MOVEI	B,(A)	;NEW JFN IN B
	MOVE	A,OUTFILE	;OLD JFN IN A
	RNAMF		;MAKE SURE ALL CLSED FILES ARE VERSION 1
	 ERROR
	MOVEI	A,(B)
CLOSW1:	RLJFN
	 ERROR
	POP	S,A	;FLUSH POINTER TO NAME STRING
	POP	P,A	;RETURN
	POP	P,OUTFILE
	JRST	(A)
>


ERRORR:	SKIPE	CHARNO
	PUSHJ	P,CRLF
	HRRZ	C,JOBUUO
	CAIL	C,MAXERR
	JRST	ERR0
	HLRZ	A,ERRORS(C)	;GET TEXT MESSAGE
	HRLI	A,440700
	HRRZ	B,ERRORS(C)	;GET DISPATCH FOR COMPUTATIONAL ERRORS
	JUMPN	B,(B)	;DISPATCH IF A COMPUTATIONAL ERROR
ALLERP:	PUSHJ	P,PTOS	;TYPE GENERAL MESSAGE
ALLERR:	PUSHJ	P,CRLF
	SKIPN	PRODNM	;WERE WE INSIDE A USER PROCEDURE?
	JRST	ERROUT
	MOVEI	A,[ASCIZ /I WAS AT LINE /]
	PUSHJ	P,PTOSSM
	MOVE	A,LINENO
	PUSHJ	P,DECPRT
	MOVEI	A,[ASCIZ / IN /]
	PUSHJ	P,PTOSSM
	JSP	N,PUNAME	;PRINT PROCEDURE NAME
	PUSHJ	P,CRLF
ERROUT:	TLO	F,ERRORF
	TLZE	F,TIF
	JRST	ERROT2	;WERE IN TYPEIN AT BREAK
ERROT1:	MOVE	C,CBOT
	MOVEM	C,CTOP	;FLUSH THE DIRECT LINE THAT LAST LOST
	JRST	RESET
	AOS	GODEPTH	;IS ANOTHER GO ON THE STACK
	JRST	UPROD1

ERROT2:	MOVEI	A,TISPOP
	TLZE	F,RQF	;WERE WE IN A REQUEST
	JRST	ERROT1	;YES
TISPOP:	TLZ	F,ERRORF!BROKE
FOR TENEX,<POP P,A>
	POP	P,0(P)
	JRST	TISR1

ERR0:	MOVEI	A,[ASCIZ /ERROR AT /]
	PUSHJ	P,PTOSSM
ERR2:	HRRZ	A,UUOTRP
	SUBI	A,1
	PUSHJ	P,OCTPRT
	JRST	ALLERR

NONAME:	PUSHJ	P,PTOS
	JRST	ERR2

MATCHG:	PUSHJ	P,PTOS
	MOVE	C,UUOACS+D	;THE OFFENDING TERMINATOR
	PUSHJ	P,TYO
	MOVEI	C,"?"
	PUSHJ	P,TYO
	JRST	ALLERR

ILLUUO:	MOVEI	B,[ASCIZ /HELP!!/]
	PUSHJ	P,TOSS
	JRST	ALLERR

PUNTER:	PUSHJ	P,PTOS
	PUSHJ	P,CRLF
	JRST	RESET

.INER3:	PUSHJ	P,PTOS
	MOVE	A,0(P)
	MOVE	A,@CSA
	PUSHJ	P,PTOSS
	MOVE	A,[POINT 7,[ASCIZ / OF WHAT PROCEDURE?/],]
	JRST	ALLERP

.TOER3:	MOVE	E,A
	MOVE	A,1(S)
	HRLI	A,010700+W
TYPAFT:	PUSHJ	P,PTOS
	MOVE	A,E
	JRST	ALLERP

CANTBA:	SKIPA	E,UUOACS+A
CANTBT:	MOVE	E,THISPR
	EXCH	A,E
	JSP	H,ETYPIT
	MOVEI	A,[ASCIZ / CAN'T BE A /]
CANTB1:	HRLI	A,440700
	JRST	TYPAFT

.GOER2:	MOVE	E,A
	MOVE	A,UUOACS+D
	JRST	TYPAFT-1

.GOER3:	PUSHJ	P,PTOS
	SKIPA	A,UUOACS+D
.XITER:	POP	S,A
.GOER4:	HRLI	A,010700+W
	JRST	ALLERP

.NOPRO:	PUSHJ	P,PTOS
	SOS	A,UUOACS+A
	HRRZ	A,@RPA
	JRST	.GOER4

.TNAME:	PUSHJ	P,PTOS
	JSP	N,TUNAME
	JRST	ALLERR
.PMNAM:	JSP	N,PMNAME
	JRST	ALLERP

UNDFND:	MOVE	E,UUOACS+A
	JSP	N,PUNAM1
	JRST	ALLERP

TWOARG:	PUSH	P,A
	JSP	N,PMNAME
	MOVEI	B,[ASCIZ / OF /]
	PUSHJ	P,TOSS
	MOVEI	E,042	;QUOTE
	TRO	F,PREFIX!SUFFIX
	MOVE	A,-1(S)
	PUSHJ	P,PTOSS	;ARG1 OF TWO
	MOVEI	A,[ASCIZ / AND /]
TWARG9:	PUSHJ	P,PTOSSM
	MOVE	A,(S)	;GET SECOND ARG OF TWO OR ONLY ARG IF ONE
	TRO	F,PREFIX!SUFFIX
	MOVEI	E,042
	PUSHJ	P,PTOSS
	MOVEI	B,"?"
	PUSHJ	P,PTYO
	PUSHJ	P,CRLF
	POP	P,E
	JRST	TYPAFT+1

ONEARG:	PUSH	P,A
	JSP	N,PMNAME
	MOVEI	A,[ASCIZ /OF /]
	JRST TWARG9



TUNAME:	SKIPA	E,TOPROD
PUNAME:	MOVE	E,PRODNM
PUNAM1:	EXCH	A,E
	MOVEI	A,-1(A)
	MOVE	A,@RPA
	PUSHJ	P,PTOSS
	MOVE	A,E
	JRST	(N)

PMNAME:	MOVE	B,THISPR
	MOVE	B,-1(B)
	PUSHJ	P,TOSS
	JRST	(N)

BRAKER:	PUSHJ	P,PTOS
FOR TEN50,<	PUSHJ	P,SETTTM >
FOR TENEX,<	MOVEI	A,.+3
	MOVEM	A,LEVLTB
	DEBRK
>
	TLO	F,BROKE
	JRST	ALLERR

REDO:	MOVE	S,SP
	SUB	S,SP+1
	ADD	S,SSP
	HRLI	S,(S)
	HRR	S,SP
	ADD	S,SSP

	MOVE	P,PP
	SUB	P,PP+1
	ADD	P,SPP
	HRLI	P,(P)
	HRR	P,PP
	ADD	P,SPP
	JRST	MAINL	;HAD CALLED BREAKERR FROM MIDDLE OF LINE (DURING TYPEOUT)


ETYPIT:	JFFO	A,.+2
	JRST	(H)
	JRST	@.+1(B)
	 EXP	ETMP,ETUP,ETVAR,ETLIT,ETCOM

ETMP:	HRLI	A,440700
	JRST	ETUP1
ETUP:	HRLI	A,010700+W
	ADD	A,RP
ETUP1:	HRR	A,-1(A)
	PUSHJ	P,PTOS
	JRST	(H)

ETCOM:	MOVEI	G,";"
	JRST	ETLIT1
ETVAR:	SKIPA	G,[EXP "/"]
ETLIT:	MOVEI	G,042
ETLIT1:	EXCH	G,E
	TLO	F,PREFIX!SUFFIX
	PUSHJ	P,PTOSS
	MOVE	E,(G)
	JRST	(H)

DEFINE EE (NAME,TEXT,CODE)
<NAME==.-ERRORS
XWD [ASCIZ *TEXT*],CODE>

ERRORS:
EE .BARF,ERROR ,NONAME
EE COMERR, CANNOT BE USED AS AN INPUT. IT DOES NOT OUTPUT.,.PMNAM
EE EDTER1,YOU CANNOT EDIT THAT.
EE EDTER2,YOU ARE ALREADY EDITING ,.TNAME
EE ENDERR,END WHAT? YOU ARE NOT DEFINING ANYTHING.
EE ERXTRA,SOMETHING EXTRA.,
EE ERMSSG,SOMETHING MISSING.,
EE ERSER1,ERASE WHAT?
EE EVER3, NEEDS A MEANING.,UNDFND
EE EVER4, HAS NOT BEEN COMPLETELY DEFINED.,UNDFND
EE EVER5,PROCEDURE NAME.,CANTBT
EE FILER1,FILE NAME.,CANTBA
EE GOERR1,GO WHERE?
EE GOERR2, IS NOT A LINE NUMBER.,.GOER2
EE GOERR3,THERE IS NO LINE ,.GOER3
EE GOERR9,NO PLACE TO GO.
EE IFERR,IF WHAT?
EE INERR1,MATCHING ,MATCHG
EE INERR2,LINE NUMBER IS TOO LARGE.,
EE INERR3,LINE ,.INER3
EE INERR4,YOU CAN'T HAVE A LINE 0.,
EE IOPERR,I AM IN TROUBLE. TELL YOUR TEACHER.,PUNTER
EE LSTER1,LIST WHAT?
EE LSTER2,LIST ALL WHAT?
EE NMERR5,DON'T USE THE EMPTY WORD FOR A NAME.,
EE NOCMD,THERE IS NO COMMAND ON THIS LINE.
EE NOPROD,THERE IS NO PROCEDURE ,.NOPRO
EE PREDR1,INPUT MUST BE A PREDICATE.,ONEARG
EE PREDR2,INPUTS MUST BE PREDICATES.,TWOARG
EE PRNER1,MATCHING (?
EE PRNER2,MISSING )?
EE SUMERR,INPUTS MUST BE NUMBERS.,TWOARG
EE TOERR2,YOU NEED / MARKS AROUND EACH INPUT.,
EE TOERR3, IS USED BY LOGO.,.TOER3
EE TOERR4,DON'T TRY TO DEFINE ANOTHER PROCEDURE INSIDE THIS ONE.,
EE WRDERR,YOU CAN'T MAKE A WORD OUT OF A SENTENCE.,TWOARG
EE XITERR,,.XITER  ;NO SYSTEM ERROR MESSAGE, ONLY THAT SUPPLIED BY USER
EE ZERERR,INPUT MUST BE A NUMBER.,ONEARG
EE BREAK,BREAK,BRAKER
EE PUNT,STORAGE FULL,PUNTER
BLOCK 5	;FOR PATCHING THIS LIST
MAXERR==.-ERRORS

FOO:	BLOCK 200
LIT

LOGEND:	END LOGO
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                      	MOVE	E,THISPR
	EXCH	A,E
	JSP	H,ETYPIT
	MOVEI	A,[ASCIZ / CAN'T BE A /]
CANTB1:	HRLI	A,440700
	JRST	TYPAFT

.GOER2:	MOVE	E,A
	MOVE	A,UUOACS+D
	JRST	TYPAFT-1

.GOER3:	PUSHJ	P,PTOS
	SKIPA	A,UUOACS+D
.XITER:	POP	S,A
.GOER4:	HRLI	A,010700+W
	JRST	ALLERP

.NOPRO:	PUSHJ	P,PTOS
	SOS	A,UUOACS+A
	HRRZ	A,@RPA
	JRST	.GOER4

.TNAME:	PUSHJ	P,PTOS
	JSP	N,TUNAME
	JRST	ALLERR
.PMNAM:	JSP	N,PMNAME
	JRST	ALLERP

UNDFND:	MOVE	E,UUOACS+A
	JSP	N,PUNAM1
	JRST	ALLERP

TWOARG:	PUSH	P,A
	JSP	N,PMNAME
	MOVEI	B,[ASCIZ / OF /]
	PUSHJ	P,TOSS
	MOVEI	E,042	;QUOTE
	TRO	F,PREFIX!SUFFIX
	MOVE	A,-1(S)
	PUSHJ	P,PTOSS	;ARG1 OOLLECTED
	MOVNI	G,(E)		;USE G TO OFFSET WTOP,ETC
	HRRM	B,NEWBOT	;B IS THE RELATIVE DEST FOR THE STRING
	ADDM	G,UUOACS+B
	ADDM	G,WTOP

	HRRZ	D,JOBREL
	SUBI	D,(E)
	HRLI	B,(A)
	ADDI	B,(W)
	CAMG	A,JOBREL	;DON'T GET CAUGHT WITH ILL MEM REF
	BLT	B,(D)

GCPS2D:	CAIG	E,2000	;DID WE RECOVER MORE THAN A FULL K?
	JRST	GCPSS3	;NO
	MOVEI	A,@WTOP		;YES, REMOVE EXTRA K'S
FOR TEN50,<	CALL	A,[SIXBIT /CORE/]
	 HALT>

FOR TENEX,<	JSP	E,SETMEM>

;GARBAGE COLLECTOR PASS THREE

GCPSS3:	MOVEI	D,2(D)
	CAMLE	D,JOBREL
	JRST	GCPS3B		;AVOID DOING BLT ON FULL ALLOCATION
	SETZM	-1(D)
	HRLI	D,-1(D)
	BLT	D,@JOBREL

GCPS3B:	MOVE	OVE	A,@RPA	;GET PTR TO NAME
	HRLI	A,010700+W	;MAKE TEXT PTR
	JRST	LSTPM1

LSTPV:	MOVEI	E,"/"	;VARIABLE, AFFIX "/" MARKS
	JRST	.+2
LSTPL:	MOVEI	E,042	;LITERAL, AFFIX QUOTES
	TRO	F,PREFIX!SUFFIX
LSTPL1:	PUSHJ	P,PTOSS
	JRST	LSTPM2

LSTPCM:	MOVEI	E,";"	;COMMENT, PREFIX SEMICOLON
	TRO	F,PREFIX
	JRST	LSTPL1


;LIST ALL NAMES

LISTAN:	MOVEI	A,2
	MOVEM	A,BCHAR
	PUSHJ	P,CRLF

	MOVE	A,DP	;DUMMIES FIRST
	ADD	A,DTOP	;FROM NEWEST FIRST
	SUBI	A,2
	SKIPN	(A)
	JRST	LSTANG
	PUSHJ	P,LSTANR	;EVEN ONES WITH EMPTY VALUES
	JRST	.-4

LSTANG:	SKIPA	A,[EXP LSTANR]	;LISTING DOES ALL
SAVAN:	MOVEI	A,SAVANR	;SAVE ONLY SAVES 