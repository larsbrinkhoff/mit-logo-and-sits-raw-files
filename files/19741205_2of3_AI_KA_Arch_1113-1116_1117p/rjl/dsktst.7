.TITLE 65K TESTER
A=%0
B=%1
C=%2
D=%3
E=%4
F=%5
P=%6
PC=%7

SWR=177570
DSKBRV=204
DSKS=177460
DSKWC=177462
DSKCA=177464
DSKA=177466
DSKAE=177470
DHSCR=160020
DHNRC=160022
DHLPR=160024
DHCAR=160026
DHBCR=160030
DHBAR=160032
DHBRC=160034
DHSSR=160036
DHBRV=340
SSR0=177572
SSR1=177574
SSR2=177576
SSR3=172516
KIAR=172340
KIDR=172300
KDAR=172360
KDDR=172320
FAR=KDAR+12
FDR=KDDR+12
FAD=120000
PS=177776
.=0
.REPT 100
.+2
HALT
.ENDR
.=600
PDL=.
.=200
JMP START
.=1000

START:	RESET
	MOV #PDL,P
	MOV #5,SEED
	CLR SSR0
	MOV #KIDR,A
	MOV #20,B
	MOV #77406,(A)+
	SOB B,.-4
	MOV #10,C
SRT1:	MOV B,20(A)
	MOV B,(A)+
	SOB C,SRT1
	MOV #7600,KDAR+16
	MOV #7600,KIAR+16
	MOV #4,SSR3
	MOV #1,SSR0
;HAVING SET UP THE SEGMENTER, NOW SET UP THE 1ST 64K OF CORE
;STARTING AFTER THE PROGRAM 
	MOV #100,A
	MOV #2000,B	;64K/64
SETAR:	MOV A,FAR
	MOV #20,C
	MOV #FAD,D
SETLOP:	MOV D,(D)+
	MOV A,(D)+
	SOB C,SETLOP
	INC A
	SOB B,SETAR
;NOW WRITE IT ONTO DISK
	MOV #10000,DSKCA
	MOV #1,DSKWC
	MOV #5,DSKAE
	CLR DSKA
	MOV #3,DSKS
	TSTB DSKS
	BGE .-4
	TST DSKS
	BGE .+4
	HALT
	MOV #-1,DSKWC
	MOV #DSKBRK,DSKBRV
	MOV #340,DSKBRV+2
	MOV #103,DSKS
;NOW THAT THE DISK IS GOING, GET THE DH11 GOING
;NOW THAT THE DISK ROUTINES ARE GOING, TEST THE CONTENTS OF CORE
	CLR E
TSTMEM:	MOV #100,A
	MOV #1777,B	;64K/64
TSTAR:	MOV A,FAR
	MOV #20,C
	MOV #FAD,D
TSTLOP:	CMP D,(D)+
	BEQ .+4
	HALT
	CMP A,(D)+
	BEQ .+4
	HALT
	SOB C,TSTLOP
	INC A
	SOB B,TSTAR
	INC E
	MOV E,SWR
	BR TSTMEM

SEED:	5
DSKBRK:	TST DSKS
	BGE .+4
	HALT
	MOV B,-(P)
	MOV SEED,B
	MUL #5,B
	MOV B,SEED
	BIC #177770,B
	BNE .+4
	INC B
	ASH #6,B
	NEG B
	MOV B,DSKWC
	MOV SEED,B
	BIC #37,B
	CLR DSKS
	MOV B,DSKA
	ASL B
	BCC .+10
	BIS #20,DSKS
	ADD #10000,B
	BCC .+10
	BIS #20,DSKS
	MOV B,DSKCA
	MOV #5,DSKAE
	BIS #105,DSKS
	MOV (P)+,B
	RTT
	.END START
