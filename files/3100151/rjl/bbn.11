TITLE BBN MINI DUMP TAPE READER
A=1
B=2
C=3
D=4
P=17

TYIC==1
TYOC==2
MAGIC==3
DSKOC==4

PDL:	BLOCK 20
MAGOP:	SIXBIT /  &MT0/
DSKOP:	SIXBIT /  'DSK/
	SIXBIT /BBNTAP>/
BUF:	BLOCK 512.+6

GO:	MOVEI P,PDL-1
	.OPEN TYIC,[SIXBIT /   TTY/]
	.VALUE
	.OPEN TYOC,[SIXBIT /  !TTY/]
	.VALUE
	.OPEN MAGIC,MAGOP
	.VALUE
	.MSPAC
	.CLOSE MAGIC,
OPEN:	.OPEN MAGIC,MAGOP
	.VALUE
READ:	MOVE A,[-1006,,BUF]
	.IOT MAGIC,A
	JUMPL A,OPEN
	MOVN A,BUF+4
	JRST @.+1(A)
	DATA
	HEADER
	FHEADR
	EOF
	EOT

DATA:	MOVE A,[-1000,,BUF+6]
	.IOT DSKOC,A
	JRST READ

FHEADR:	SETZM DSKOP+1
	MOVE A,[SIXBIT /BBNBBN/]
	MOVEM A,DSKOP+2
	MOVE A,[440700,,BUF+6]
	ILDB B,A
	CAIE B,">
	JRST .-2
	MOVE C,[440600,,DSKOP+1]
FSTNAM:	ILDB B,A
	CAIN B,".
	JRST SECNAM
	SUBI B,40
	TLNE C,770000
	IDPB B,C
	JRST FSTNAM
SECNAM:	MOVE C,[440600,,DSKOP+2]
SECNM1:	ILDB B,A
	CAIN B,";
	JRST DOPEN
	SUBI B,40
	TLNE C,770000
	IDPB B,C
	JRST SECNM1
DOPEN:	.OPEN DSKOC,DSKOP
	.VALUE

HEADER:	MOVE A,[440700,,BUF+6]
	.IOT TYOC,[15]
TYPE:	ILDB B,A
	JUMPE B,READ
	.IOT TYOC,B
	JRST TYPE

EOF:	.CLOSE DSKOC,
	JRST READ

EOT:	.MSPAC
	.CLOSE DSKOC,
	.BREAK 16,140000
	JRST READ

	END GO
