

TO MEMBERP :X :L
10 IF EMPTYP :L OUTPUT "FALSE"
20 IF :X=FIRST :L OUTPUT "TRUE"
30 MAKE "L" BUTFIRST :L GO 10
END

TO POSITION :X :LIST
10 LOCAL "Y"
20 MAKE "Y" 1
30 IF EMPTYP :LIST OUTPUT 0
40 IF :X=FIRST :LIST OUTPUT :Y
50 MAKE "Y" :Y+1
60 MAKE "LIST" BUTFIRST :LIST
70 GO 30
END

TO NTH :X :LIST
10 IF EMPTYP :LIST EXIT "LIST TOO SHORT"
20 IF :X=1 OUTPUT FIRST :LIST
30 MAKE "LIST" BUTFIRST :LIST
40 MAKE "X" :X-1
50 GO 10
END

TO INTERPRET :XX
10 LOCAL "X" "Y" "Z"
20 MAKE "X" :XX
25 MAKE "Y" :TUNEBASE
30 IF NUMBERP :X GO 1010
40 IF "*"=FIRST :X MAKE "Y" :Y-12 MAKE "X" BUTFIRST :X GO 40
50 IF EMPTYP :X GO 500
60 IF "/"=FIRST :X MAKE "X" BUTFIRST :X GO 500
70 IF "*"=FIRST :X MAKE "Y" :Y+12 MAKE "X" BUTFIRST :X GO 50
80 MAKE "Z" :Z&FIRST :X
90 MAKE "X" BUTFIRST :X
100 GO 50
500 IF NUMBERP :Z MAKE "Y" :Z+:Y GO 1000
505 IF EMPTYP :Z MAKE "Y" :LASTPITCH GO 1000
510 IF ":"=FIRST :Z MAKE "Y" :Y+SCALE BUTFIRST :Z GO 1000
520 IF "+"=LAST :Z MAKE "Y" :Y-:TUNEBASE+:LASTPITCH+BUTLAST :Z GO 1000
530 IF "-"=LAST :Z MAKE "Y" :Y-:TUNEBASE+:LASTPITCH-BUTLAST :Z GO 1000
531 IF :Z="REST" MAKE "Y" 0 GO 1000
532 IF :Z="BOOM" MAKE "Y" 1 GO 1000
533 IF :Z="GRITCH" MAKE "Y" 2 GO 1000
540 MAKE "Y" :Y+SCALE :Z
1000 IF EMPTYP :X MAKE "X" :LASTDURATION
1010 IF NUMBERP :X MAKE "LASTDURATION" :X OUTPUT SENTENCE :Y :X
1020 MAKE "Z" ""
1030 IF EMPTYP :X GO 2000
1040 IF ","=FIRST :X MAKE "X" BUTFIRST :X GO 2000
1050 MAKE "Z" :Z&FIRST :X MAKE "X" BUTFIRST :X GO 1030
2000 IF NOT NUMBERP :Z MAKE "Z" TEMPO :Z
2005 MAKE "LASTDURATION" :Z
2010 IF EMPTYP :X OUTPUT SENTENCE :Y :Z
2020 OUTPUT (SENTENCE :Y :Z :X)
END

TO SCALE :XX
10 LOCAL "X" "Y" "Z" "NAT"
20 MAKE "Y" 0
25 MAKE "X" :XX
30 IF "#"=LAST :X MAKE "Y" :Y+1 MAKE "X" BUTLAST :X GO 30
40 IF "$"=LAST :X MAKE "Y" :Y-1 MAKE "X" BUTLAST :X GO 40
45 IF "%"=LAST :X MAKE "NAT" 1 MAKE "X" BUTLAST :X
50 IF NUMBERP :X GO 500
55 IF "+"=LAST :X MAKE "X" :LASTSCALE+BUTLAST :X GO 500
56 IF "-"=LAST :X MAKE "X" :LASTSCALE-BUTLAST :X GO 500
70 MAKE "LASTSCALE" POSITION :X "C D E F G A B"
75 IF :LASTSCALE=0 PRINT :XX EXIT " IS NOT IN THE SCALE"
80 IF 1=:NAT GO 100
82 IF NOT :Y=0 GO 100
85 IF MEMBERP :X :FLATS MAKE "Y" -1
90 IF MEMBERP :X :SHARPS MAKE "Y" 1
100 OUTPUT :PIANOC-:TUNEBASE+:Y+NTH :LASTSCALE "0 2 4 5 7 9 11"
500 MAKE "LASTSCALE" :X
502 MAKE "X" :X+:SCALEBASE
505 IF :X<1 MAKE "X" :X+COUNT :SCALE MAKE "Y" :Y-12 GO 505
510 MAKE "Z" :SCALE
520 IF :X=1 OUTPUT :Y+FIRST :Z
530 MAKE "Z" BUTFIRST :Z
535 MAKE "X" :X-1
540 IF EMPTYP :Z MAKE "Y" :Y+12 GO 510
550 GO 520
END

TO TEMPO :XX
10 LOCAL "X" "DOT"
20 MAKE "X" POSITION FIRST :XX "S E Q H W"
30 IF :X=0 GO 80
40 MAKE "X" NTH :X "1 2 4 8 16"
50 MAKE "DOT" :X/2
60 MAKE "XX" BUTFIRST :XX
70 IF EMPTYP :XX OUTPUT :X
71 IF "."=FIRST :XX MAKE "X" :X+:DOT MAKE "DOT" :DOT/2 GO 60
80 TYPE :XX EXIT " IS NOT A DURATION"
END

TO PLAY :X
5 LOCAL "Y" "Z" "PLNEXT"
10 IF WORDP :X MAKE "X" BUTFIRST SENTENCE 1 :X
12 IF EMPTYP FIRST :X GO 405
15 IF "<"=FIRST FIRST :X GO 500
20 PLAYNOTE INTERPRET FIRST :X
405 MAKE "X" BUTFIRST :X
410 IF EMPTYP :X STOP
420 GO 15
500 MAKE "Y" BUTFIRST SENTENCE 1 BUTFIRST FIRST :X
510 MAKE "X" BUTFIRST :X
520 IF ">"=LAST FIRST :X MAKE "PLNEXT" SENTENCE :Y BUTLAST FIRST :X GO 600
530 MAKE "Y" SENTENCE :Y FIRST :X GO 510
600 MAKE "X" BUTFIRST :X
610 CHORUS "PLNEXT PLNEXT PLNEXT PLNEXT"
620 GO 410
END

TO PLAYNOTE :Y
10 LOCAL "SAVEINST"
15 IF 1>FIRST BUTFIRST :Y EXIT "DURATION LESS THAN 1 NOT ALLOWED"
22 IF EMPTYP BUTFIRST BUTFIRST :Y GO 30
24 MAKE "SAVEINST" :INSTRUMENT
26 MAKE "INSTRUMENT" LAST :Y
30 IF NOT :INSTRUMENT="DRUM" GO 90
40 IF 4>FIRST :Y GO 90
60 MAKE "Z" 1 GO 100
90 MAKE "Z" FIRST :Y
100 MAKE "Y" FIRST BUTFIRST :Y
105 MBUFPUT :Z
108 IF :Z<4 GO 130
109 MAKE "LASTPITCH" :Z
110 IF :INSTRUMENT="LEGATO" GO 200
120 IF :INSTRUMENT="SLUR" GO 300
130 MBUFNEXT :NVOICES*:Y GO 400
200 IF :Y=2 MBUFNEXT :NVOICES*2 GO 400
300 MBUFNEXT :NVOICES
310 IF :Y=1 GO 400
320 MAKE "Y" :Y-1 GO 105
400 IF NOT EMPTYP :SAVEINST MAKE "INSTRUMENT" :SAVEINST
END

TO CHORUS :X
10 LOCAL "MAX" "BUF" "Y"
15 MAKE "Y" 1
20 MAKE "MAX" MBUFCOUNT
30 MAKE "BUF" :MAX-1
40 IF EMPTYP :X GO 200
45 IF :X="VOICES" PLAY THING "V"&:Y MAKE "Y" :Y+1 GO 70
50 DO FIRST :X
60 MAKE "X" BUTFIRST :X
70 MAKE "MAX" MAXIMUM :MAX MBUFCOUNT
90 MBUFSTART
100 MAKE "BUF" :BUF+1
110 MBUFNEXT :BUF
120 GO 40
200 MBUFSTART
210 MBUFNEXT ((:MAX-1)/4)*4
END

TO PLNEXT
10 LOCAL "X" "Z"
20 IF EMPTYP :PLNEXT STOP
25 IF EMPTYP FIRST :PLNEXT MAKE "PLNEXT" BUTFIRST :PLNEXT GO 20
30 IF "("=FIRST FIRST :PLNEXT MAKE "X" BUTLAST SENTENCE BUTFIRST FIRST :PLNEXT 1 GO 100
40 PLAYNOTE INTERPRET FIRST :PLNEXT MAKE "PLNEXT" BUTFIRST :PLNEXT STOP
100 MAKE "PLNEXT" BUTFIRST :PLNEXT
110 IF EMPTYP :PLNEXT GO 195
120 IF ")"=LAST FIRST :PLNEXT MAKE "X" SENTENCE :X BUTLAST FIRST :PLNEXT GO 190
130 MAKE "X" SENTENCE :X FIRST :PLNEXT MAKE "PLNEXT" BUTFIRST :PLNEXT GO 110
190 MAKE "PLNEXT" BUTFIRST :PLNEXT
195 PLAY :X
200 STOP
END

TO PLAYT :P :D
10 LOCAL "X"
20 IF WORDP :P MAKE "P" BUTFIRST SENTENCE 1 :P
30 IF WORDP :D MAKE "D" BUTFIRST SENTENCE 1 :D
40 IF EMPTYP :P STOP
50 IF EMPTYP :D STOP
55 IF EMPTYP FIRST :P MAKE "P" BUTFIRST :P GO 55
58 IF EMPTYP FIRST :D MAKE "D" BUTFIRST :D GO 58
60 IF NUMBERP FIRST :P MAKE "X" :TUNEBASE+FIRST :P GO 80
70 MAKE "X" FIRST INTERPRET FIRST :P
80 PLAYNOTE SENTENCE :X BUTFIRST INTERPRET FIRST :D
90 MAKE "P" BUTFIRST :P
100 MAKE "D" BUTFIRST :D
110 GO 40
END

TO PLAY4 :V1 :V2 :V3 :V4
10 CHORUS "VOICES"
END

TO NVOICES :N
10 MAKE "NVOICES" :N
20 VOICES :N
END

TO STARTMUSIC
10 MBUFINIT
20 NVOICES 4
30 MBUFCLEAR
40 MAKE "TUNEBASE" 23
50 MAKE "SCALEBASE" 0
60 MAKE "INSTRUMENT" "LEGATO"
70 MAKE "FLATS" ""
80 MAKE "SHARPS" ""
90 MAKE "LASTPITCH" 23
100 MAKE "LASTDURATION" 2
110 MAKE "SCALE" "0 2 4 5 7 9 11"
120 MAKE "PIANOC" 23
END

TO VOICE :N
10 MBUFSTART
20 MBUFNEXT :N-1
END

TO EM
10 MBUFOUT
20 MBUFCLEAR
END

