TO MEMBERP :X :L
10 IF EMPTYP :L OUTPUT "FALSE"
20 IF :X=F :L OUTPUT "TRUE"
30 MAKE "L" BF :L GO 10
END

TO POSITION :X :LIST    !"

          FIND THE POSITION OF AN ITEM IN A LIST"


10 LOCAL "Y"
20 MAKE "Y" 1
30 IF EMPTYP :LIST OUTPUT 0    !"

          OUTPUT OF 0 MEANS IT ISN'T IN THE LIST"

40 IF :X=F :LIST OUTPUT :Y
50 MAKE "Y" :Y+1
60 MAKE "LIST" BF :LIST
70 GO 30
END

TO NTH :X :LIST    !"

          TO FIND THE NTH MEMBER OF A LIST"

10 IF EMPTYP :LIST EXIT "LIST TOO SHORT"
20 IF :X=1 OUTPUT F :LIST
30 MAKE "LIST" BF :LIST
40 MAKE "X" :X-1
50 GO 10
END



TO INTERPRET :XX     !"

          INTERPRETS THE NOTE NOTATION - RETURNS PITCH# AND DURATION"

10 LOCAL "X" "Y" "Z"
20 MAKE "X" :XX
25 MAKE "Y" :TUNEBASE
30 IF NUMBERP :X GO 1010      !"

          A NUMBER ALONE IS INTERPRETED AS DURATION TO
                          BE PLAYED AT THE PITCH :TUNEBASE"

40 IF "*"=F :X MAKE "Y" :Y-12 MAKE "X" BF :X GO 40      !"

          STARS AT THE BEGINNING EACH LOWER THE PITCH AN OCTAVE."

50 IF EMPTYP :X GO 500      !"

           THIS IS IS END TEST OF A LOOP WHICH GOES
         THROUGH THE NOTE LOOKING FOR THE PITCH-SPECIFYING PART"

60 IF "/"=F :X MAKE "X" BF :X GO 500       !"

           A SLASH SEPARATES PITCH FROM DURATION"

70 IF "*"=F :X MAKE "Y" :Y+12 MAKE "X" BF :X GO 50      !"

          STARS  AT THE END OF THE PITCH EACH RAISE IT BY AN OCATAVE "



80 MAKE "Z"  :Z&F :X
90 MAKE "X" BF :X       !"

          Z SAVES UP THE PITCH SPECIFIER IN THE LOOP"

100 GO 50
500 IF NUMBERP :Z MAKE "Y" :Z+:Y GO 1000      !"

          IF THE PITCH IS A NUMBER, IT IS
                 ADDED TO TUNEBASE AND THE * MODIFICATIONS"

505 IF EMPTYP :Z MAKE "Y" :LASTPITCH GO 1000
510 IF ":"=F :Z MAKE "Y" :Y+SCALE BF :Z GO 1000      !"

           A NUMBER PRECEDED BY DOTS
                      INDICATES A SCALE NUMBER"

520 IF "+"=LAST :Z MAKE "Y" :Y-:TUNEBASE+:LASTPITCH+BL :Z GO 1000
530 IF "-" =LAST :Z MAKE "Y" :Y-:TUNEBASE+:LASTPITCH-BL :Z GO 1000  !"

          PLUS AND MINUS INDICATE AN INTERVAL RELATIVE TO THE LAST PITCH
           PLAYED RATHER THAN TUNEBASE"

531 IF :Z="REST" MAKE "Y"  0 GO 1000    !"

          0 IS THE SILENT CHARACTER"

532 IF :Z="BOOM" MAKE "Y" 1 GO 1000    !"

          1 BOOMS THE DRUM"

533 IF :Z="GRITCH" MAKE "Y" 2 GO 1000
540 MAKE "Y" :Y+SCALE :Z    !"

          ANY OTHER SYMBOL STRING IS ASSUMED
          TO BE SOME SORT OF SCALE REPRESENTATION"

1000 IF EMPTYP :X MAKE "X" :LASTDURATION    !"

          IF NO DURATION IS GIVEN, THE
          PREVIOUS ONE IS REPEATED"

1010 IF NUMBERP :X MAKE "LASTDURATION" :X OUTPUT SENTENCE :Y :X
1020 MAKE "Z" ""    !"

           THE DURATION NAME WILL BE SAVED IN Z"

1030 IF EMPTYP :X GO 2000
1040 IF ","=F :X MAKE "X" BF :X GO 2000    !"

          COMMA IS USED TO SEPARATE ARTICULATIONS"

1050 MAKE "Z" :Z&F :X MAKE "X" BF :X GO 1030
2000 IF NOT NUMBERP :Z MAKE "Z" TEMPO :Z    !"

          ANY NON-NUMERIC DURATION IS INTERPRETED BY TEMPO"

2005 MAKE "LASTDURATION" :Z
2010 IF EMPTYP :X OUTPUT SENTENCE :Y :Z    !"

          IF NO ARTICULATION IS GIVEN, PITCH AND DURATION ARE OUTPUT"

2020 OUTPUT (SENTENCE :Y :Z :X)    !"

          OTHERWISE, THE ARTICULATION IS TACKED ON THE END"

END
TO SCALE :XX    !"

          INTERPRETS SCALE NAMES AND NUMBERS"

10 LOCAL "X" "Y" "Z" "NAT"
20 MAKE "Y" 0    !"

          Y WILL SAVE THE PITCH"

25 MAKE "X" :XX
30 IF "#"=LAST :X MAKE "Y" :Y+1 MAKE "X" BL :X GO 30    !"

          EVERY SHARP ADDS ONE TO THE PITCH"

40 IF "$"=LAST :X MAKE "Y" :Y-1 MAKE "X" BL :X GO 40    !"

          DOLLAR SIGN IS USED AS FLAT"

45 IF "%"=LAST :X MAKE "NAT" 1 MAKE "X" BL :X    !"

           PERCENT IS USED AS A NATURAL SIGN"

50 IF NUMBERP :X GO 500
55 IF "+"=LAST :X MAKE "X" :LASTSCALE+BL :X GO 500    !"

          A SCALE NUMBER FOLLOWED BY + OR - INDICATES AN INTERVAL
          UP OR DOWN FROM THE LAST SCALE NOTE PLAYED"

56 IF "-"=LAST :X MAKE "X" :LASTSCALE-BL :X GO 500
70 MAKE "LASTSCALE" POSITION :X "C D E F G A B"    !"

           FIND THE NOTE IN THE SCALE"

75 IF :LASTSCALE=0 PRINT :XX EXIT " IS NOT IN THE SCALE"     !"

           A POSITION OF 0 INDICATES IT WASN'T FOUND"

80 IF 1=:NAT GO 100
82 IF NOT :Y=0 GO 100    !"

           ACCIDENTALS OVERRIDE THE KEY SIGNATURE"

85 IF MEMBERP :X :FLATS MAKE "Y" -1
90 IF MEMBERP :X :SHARPS MAKE "Y" 1    !"

          THE KEY SIGNATURE IS KEPT IN LISTS OF FLATS AND SHARPS"

100 OUTPUT :PIANOC-:TUNEBASE+:Y+NTH :LASTSCALE "0 2 4 5 7 9 11"    !"

          FIND THE RIGHT PITCH FOR THE SCALE POSITION
          AND ADD IT TO THE FACTOR FOR FLATS AND SHARPS"


500 MAKE "LASTSCALE" :X    !"

           NUMERICAL SCALE NUMBER"

502 MAKE "X" :X+:SCALEBASE    !"

          FOR DOING DIATONIC TRANSPOSITIONS"

505 IF :X<1 MAKE "X" :X+COUNT :SCALE MAKE "Y" :Y-12 GO 505    !"

           NEGATIVE NUMBERS ARE BROUGHT INTO RANGE BY
          DROPPING OCTAVES"

510 MAKE "Z" :SCALE    !"

           ANY ARBITRARY COLLECTION OF PITCHES CAN BE
          USED AS A SCALE"

520 IF :X=1 OUTPUT :Y+F :Z
530 MAKE "Z" BF :Z
535 MAKE "X" :X-1
540 IF EMPTYP :Z MAKE "Y" :Y+12 GO 510    !"

          NUMBERS HIGHER THAN THE NUMBER OF NOTES IN
          THE SCALE ARE TREATED BY RAISING OCTAVES"

550 GO 520
END
TO PLAY :X    !"

           ACCEPTS A SINGLE NOTE OR STRING, INCLUDING BRACKETS"

5 LOCAL "Y" "Z" "PLNEXT"
10 IF WORDP :X MAKE "X" BF S 1 :X    !"

         MAKE A SINGLE WORD INTO A SENTENCE CONTAINING ONLY THAT WORD"

12 IF EMPTYP F :X GO 405    !"

          IGNORE EMPTY WORDS IN INPUT LIST"


15 IF "<"=F F :X GO 500    !"

          LOOK FOR BRACKETED CHORDS"

20 PLAYNOTE INTERPRET FIRST :X
405 MAKE "X" BF :X    !"

          GO ON TO THE NEXT NOTE"

410 IF EMPTYP :X STOP
420 GO 15

500 MAKE "Y" BF S 1 BF  F :X
510 MAKE "X" BF :X
520 IF ">"=LAST F :X MAKE "PLNEXT" S :Y BL F :X GO 600    !"

          LOOK FOR MATCHING BRACKET"

530  MAKE "Y" S :Y F :X GO 510
600 MAKE "X" BF :X
610 CHORUS "PLNEXT PLNEXT PLNEXT PLNEXT"    !"

          THE FUNCTION PLNEXT PICKS OUT THE PARTS FROM THE CHORD"

620 GO 410    !"

          CONTINUE DOWN THE LIST OF NOTES"

END


TO PLAYNOTE :Y    !"

          PLAY NOTE IN INTERPRETED FORM"

10 LOCAL "SAVEINST"
15 IF 1>F BF :Y EXIT "DURATION LESS THAN 1 NOT ALLOWED"
22 IF EMPTYP BF BF :Y GO 30    !"

          CHECK FOR ARTICULATION"

24 MAKE "SAVEINST" :INSTRUMENT
26 MAKE "INSTRUMENT" LAST :Y    !"

          TEMPORARILY CHANGE THE INSTRUMENT, SAVING THE OLD ONE"

30 IF NOT :INSTRUMENT="DRUM" GO 90    !"

          THE PITCH IS READ DIFFERENTLY FOR THE DRUM"

40 IF 4>F :Y GO 90    !"

          IF IT IS ALREADY BOOM GRITCH OR REST, LEAVE IT"


60 MAKE "Z" 1 GO 100   !"

          MAKE A BOOOM UNLESS GRITCH WAS SPECIFIED"

90 MAKE "Z" F :Y
100 MAKE "Y" F BF :Y
105 MBUFPUT :Z    !"

          PLAY THE NOTE"

108 IF :Z<4 GO 130    !"

          DRUM SOUNDS ARE STACATTO"

109 MAKE "LASTPITCH" :Z
110 IF :INSTRUMENT="LEGATO" GO 200
120 IF :INSTRUMENT="SLUR" GO 300
130 MBUFNEXT :NVOICES*:Y GO 400    !"

          SPACE FULL LENGTH OF NOTE"

200 IF :Y=2 MBUFNEXT :NVOICES*2 GO 400    !"

          LEGATO SKIPS LAST BIT OF NOTE TO MAKE SPACE"

300 MBUFNEXT :NVOICES    !"

          SPACE TO NEXT POSITION IN THIS VOICE"

310 IF :Y=1 GO 400    !"

          THE NOTE IS DONE"

320 MAKE "Y" :Y-1 GO 105    !"

          THERE'S MORE OF IT TO PLAY"

400 IF NOT EMPTYP :SAVEINST MAKE "INSTRUMENT" :SAVEINST    !"

          RESTORE THE INSTRUMENT"

END
TO TEMPO :XX    !"

          INTERPRET A SYMBOLIC DURATION"

10 LOCAL "X" "DOT"
20 MAKE "X" POSITION F :XX "S E Q H W"
30 IF :X=0 GO 80
40 MAKE "X" NTH :X "1 2 4 8 16"
50 MAKE "DOT" :X/2
60 MAKE "XX" BF :XX
70 IF EMPTYP :XX OUTPUT :X
71 IF "."=F :XX MAKE "X" :X+:DOT MAKE "DOT" :DOT/2 GO 60
80 TYPE :XX EXIT " IS NOT A DURATION"
END


TO PLNEXT    !"

          PLAYS THE NEXT VOICE IN A BRACKETED SEQUENCE
          PICKS UP THE SEQUENCE IN :PLNEXT"

10 LOCAL "X" "Z"
20 IF EMPTYP :PLNEXT  STOP    !"

          MISSING VOICES ARE FILLED IN WITH RESTS"

25 IF EMPTYP F :PLNEXT MAKE "PLNEXT" BF :PLNEXT GO 20
30 IF "("=F F :PLNEXT MAKE "X" BL S BF F :PLNEXT 1 GO 100    !"

          PARENTHESES INDICATE MULTIPLE NOTES IN A VOICE"

40 PLAYNOTE INTERPRET F :PLNEXT MAKE "PLNEXT" BF :PLNEXT STOP    !"

          PLAY THE NOTE"

100 MAKE "PLNEXT" BF :PLNEXT
110 IF EMPTYP :PLNEXT GO 195    !"

          UNMATCHED PARENS ARE FILLED OUT"

120 IF ")"=LAST F :PLNEXT MAKE "X" S :X BL F :PLNEXT GO 190    !"

          END OF PARENTHESIZED LIST"

130 MAKE "X" S :X F :PLNEXT MAKE "PLNEXT" BF :PLNEXT GO 110
190 MAKE "PLNEXT" BF :PLNEXT
195 PLAY :X
200 STOP
 
END
TO CHORUS :X    !"

          CARRY OUT A SEQUENCE OF PROCEDURES IN SEQUENTIAL VOICES"

10 LOCAL "MAX" "BUF" "Y"
15 MAKE "Y" 1
20 MAKE "MAX" MBUFCOUNT    !"

          INITIALIZE THE VARIABLE TO FIND THE MAXIMUM LENGTH FOR
          ANY VOICE"

30 MAKE "BUF" :MAX-1    !"

          SAVE THE STARTING POSITION TO BE INCREMENTED"


40 IF EMPTYP :X GO 200
45 IF :X="VOICES" PLAY THING "V"&:Y MAKE "Y" :Y+1 GO 70    !"

           VOICES IS A SPECIAL INDICATOR PASSED ALONG BY THE FUNCTION PLAY4"

50 DO F :X
60 MAKE "X" BF :X
70 MAKE "MAX" MAX :MAX MBUFCOUNT
90 MBUFSTART
100 MAKE "BUF" :BUF+1
110 MBUFNEXT :BUF
120 GO 40
200 MBUFSTART
210 MBUFNEXT ((:MAX-1)/4)*4
END
TO PLAYT :P :D    !"

          PLAY A LINE OF PITCHES WITH A LINE OF DURATIONS"

10 LOCAL "X"
20 IF WORDP :P MAKE "P" BF S 1 :P
30 IF WORDP :D MAKE "D" BF S 1 :D
40 IF EMPTYP :P STOP
50 IF EMPTYP :D STOP
55 IF EMPTYP F :P MAKE "P" BF :P GO 55
58 IF EMPTYP F :D MAKE "D" BF  :D GO 58
60 IF NUMBERP F :P MAKE "X" :TUNEBASE + F :P GO 80   !"

          THIS PROGRAM ACCEPTS PITCHES AS PURE NUMBERS"

70 MAKE "X" F INTERPRET F :P
80 PLAYNOTE S :X BF INTERPRET F :D
90 MAKE "P" BF :P
100 MAKE "D" BF :D
110 GO 40
END
TO PLAY4 :V1 :V2 :V3 :V4    !"

          PLAY FOUR STRINGS TOGETHER"

10 CHORUS "VOICES"    !"

          VOICES IS A SPECIAL INDICATOR TO CHORUS"

END

TO NVOICES :N
10 MAKE "NVOICES" :N
20 VOICES :N
END

TO STARTMUSIC
10 MBUFINIT
20 NVOICES 4
30 MBUFCLEAR
40 MAKE "TUNEBASE" 23
50 MAKE "SCALEBASE" 0
60 MAKE "INSTRUMENT" "LEGATO"
70 MAKE "FLATS" ""
80 MAKE "SHARPS" ""
90 MAKE "LASTPITCH" 23
100 MAKE "LASTDURATION" 2
110 MAKE "SCALE" "0 2 4 5 7 9 11"
120 MAKE "PIANOC" 23
END

TO VOICE :N
10 MBUFSTART
20 MBUFNEXT :N-1
END

TO EM
10 MBUFOUT
20 MBUFCLEAR
END
