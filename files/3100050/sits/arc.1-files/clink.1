	.TITLE CORE LINK
A=%0
B=%1
C=%2
D=%3
E=%4
F=%5
P=%6
PC=%7
.NLIST
.INSRT SITS;SITS SYMS
.INSRT SITS;SITMAC >
.LIST

.=1000
.BLKW 40
PDL1:
.BLKW 40
PDL2:
CLOCAP:	0
CLICAP:	0

GO:	MOV #PDL1,P
	SAVE <#-1,#490.,#.CLCAP*400>
	.INVOK			;CREATE A CORE LINK
	BNE .+4
BADCRT:	BPT
	REST CLOCAP		;THE CORE LINK CAPABILITY
	SAVE <#-1,#0,CLOCAP>	;THIS IS TO COPY IT
	BIS #.CPYCP,(P)		;THE FUNCTION COPY
	.INVOK
	BNE .+4
BADCPY:	BPT
	REST CLICAP		;THIS IS THE INPUT CAPABILITY
	SAVE <#0,#-1,CLICAP>	;MAKE ME THE CONSUMER
	.INVOK
	BNE .+4
BADIVK:	BPT
	SAVE #RECEVE		;WHERE TO START OTHER PROCESS
	.FORK
	BNE .+4
BADFRK:	BPT
SENDL:	MOV #-1,A		;THE BYTE TO SEND
SENDLP:	SAVE <A,CLOCAP>		;OUTPUT TO THE CORE LINK
	.WRDO			;SEND IT
	BNE .+4
BADSND:	BPT
	SOB A,SENDLP		;SEND AS MANY AS POSSIBLE
	JSR PC,SLEEP		;GO TO SLEEP FOR A WHILE
	BR SENDL		;SEND SOME MORE
SLEEP:	SAVE <#0,#60.*60.>	;TIME TO SLEEP
	.SLEEP
	RTS PC
RECEVE:	MOV #PDL2,P		;SET UP THE PDL
RLOOP:	JSR PC,SLEEP		;TAKE A LONG SNOOZE
	MOV #-1,A		;THE BYTE WE SHOULD RECEIVE
RLOOP1:	SAVE CLICAP		;INPUT CORE LINK CAPABILITY
	.WRDI			;INPUT ONE
	BNE .+4
BADRCV:	BPT
	CMP (P)+,A		;IS IT WHAT IT SHOULD BE?
	BEQ .+4
BADDAT:	BPT
	SOB A,RLOOP1		;DO 65000 TIMES
	BR RLOOP		;DONE
.END GO
