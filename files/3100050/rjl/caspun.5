.TITLE TAPE GOBBLER

.INSRT SITS;SITMAC >
.=400
	JMP START	;TO READ IN A TAPE
	JMP TLOOP	;TO READ NEXT TAPE
	JMP WRITE	;TO BARF ONTO THE CASSETTE
	JMP ETAPE	;TO END A TAPE
.=500
PDL:	0

TEND:	0		;CURRENT END OF TAPE BARFER
PTCSR:	177560
PTBUF:	177562
PPCSR:	177564
PPBUF:	177566

START:	MOV #PDL,P
	MOV #TSTART,E	;E POINTS TO WHERE WE ARE IN THE BUFFER
TLOOP:	JSR PC,BEGIGN	;IGNORE FIRST N CHARS ON EVERY TAPE

CLOOP:	JSR PC,CGET	;GET A CHAR
	MOVB A,(E)+	;SAVE THE CHAR
	BR CLOOP

ETAPE:	SUB #4,E	;IGNORE LAST 4 CHARS
	MOV E,TEND
	HALT
	BR TLOOP

BEGIGN:	MOV #4,B
1$:	JSR PC,CGET
	SOB B,1$
	RTS PC

CGET:	INC @PTCSR
1$:	TSTB @PTCSR
	BPL 1$
	MOV @PTBUF,A
	RTS PC

WRITE:	MOV #PDL,P
	JSR PC,WAITL
	JSR PC,WAITL
;FIRST TYPE THE MAGIC INCANTATION
	MOV #MAGIC,B
	MOV #MAGICE,C
	JSR PC,TYPE
;THEN WAIT FOR THE LOAFER TO BE HAPPY
	JSR PC,WAITL
	MOV #TSTART,B
	MOV TEND,C
	JSR PC,TYPE	;NOW BLAST OUT THE REAL THING
	BR .		;LET SOMEONE TURN OFF THE TAPE


WAITL:	MOV #20.,A
1$:	SOB B,1$	;65K LOOPS = .3 SEC
	SOB A,1$	;SO THIS IS 6 SECONDS
	RTS PC

TYPE:	MOVB (B)+,A
	JSR PC,TYO
	CMP B,C
	BLO TYPE
	RTS PC

TYO:	TSTB @PPCSR
	BPL TYO
	MOVB A,@PPBUF
	RTS PC

MAGIC:	.ASCII /177560L/
MAGICE:
	.EVEN

TSTART:
	.END


	