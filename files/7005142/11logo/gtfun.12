.IIF NDF LSI,LSI==0	;FOR INSERT INTO OLOGO
	.SBTTL 2500 DISPLAY STUFF
.IFNZ GTI
GTIDIS:	MOV PC,GTIDF	;SAY WE ARE A GTI DISPLAY
.IIF NZ LSI,MOV PC,LSTVEE
	SEZ
	RTS PC

NOGTI:	CLR GTIDF
.IIF NZ LSI,CLR LSTVEE
	SEZ
	RTS PC

GTTEST:	TST GTIDF
	BNE .+4
	ERROR+OGT
	RTS PC

.IIF NDF HOME, HOME:
GTHME:	MOV #GTHOME,B
GT1WRD:	JSR PC,GTTEST
	JSR PC,GTOUT
	SEZ
	RTS PC

.IIF NDF SHOWTU, SHOWTU:
GTSHOW:	MOV #GTSTUR,B
	BR GT1WRD

.IIF NDF HIDETU, HIDETU:
GTHIDE:	MOV #GTHTUR,B
	BR GT1WRD

.IIF NDF PENUP, PENUP:
GTPU:	MOV #GTPENU,B
	BR GT1WRD

.IIF NDF PENDOW, PENDOW:
GTPD:	MOV #GTPEND,B
	BR GT1WRD

BLINK:	MOV #GTBLNK,B
	BR GT1WRD

.IIF NDF CLEARS, CLEARS:
GTCLEA:	MOV #GTCS,B
	BR GT1WRD

SPIN:	MOV #GTSPIN,F
GT11BT:	JSR PC,G1NARG
GT11B1:	JSR PC,GTTEST
	MOV B,C
	BIC #1777,C	;CLEAR GOOD BITS
	ASH #-10.,C	;EXTEND SIGN
	BEQ 1$		;ZERO
	INC C
	BEQ 1$		;1
	ERROR+OOB
1$:	BIC #174000,B	;CLEAR UNUSED BITS
	BIS F,B
	BR GT1WRD

MOVE:	MOV #GTMOVE,F
	BR GT11BT

.IIF NDF SETHEA, SETHEA:
GTSETH:	MOV #GTSHED,F
	BR GT11BT

.IIF NDF RIGHT, RIGHT:
GTRIGH:	MOV #GTRT,F
	BR GT11BT

.IIF NDF LEFT, LEFT:
GTLEFT:	MOV #GTRT,F
GT11NB:	JSR PC,G1NARG
	NEG B
	BR GT11B1

.IIF NDF FORWAR, FORWAR:
GTFORW:	MOV #GTFD,F
	BR GT11BT

.IIF NDF BACK, BACK:
GTBACK:	MOV #GTFD,F
	BR GT11NB

.IIF NDF SETXY, SETXY:
GTSXY:	MOV #GTSTXY,F
GTSXY1:	JSR PC,G2NARG
	JSR PC,GT11B1
	MOV A,B
	BR GT1WRD	

DELTAXY:	MOV #GTDLXY,F
	BR GTSXY1

.IIF NDF SNAP, SNAP:
GTSNP:	JSR PC,EVAL	;SINCE SNAP HAS NO ARG
	MOV #GTSNAP,B
GTSNP1:	JSR PC,GT1WRD
	CLR F	
	BR GT11BT

.IIF NDF DISPLA, DISPLA:
GTDIS:	MOV #GTDISP,B
	BR GTSNP1

RUBDIS:	MOV #GTRUB,F
	BR GT11NB

.ENDC
