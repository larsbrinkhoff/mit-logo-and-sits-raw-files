	.TITLE 3500 FLOPPY DISK BOOTSTRAP
.INSRT SITS;SITMAC >
.INSRT RJL;FLOPDF >
TPS==177564
TPB==177566





.=10000
BOOTS:
.MACRO BOOT DISKN,DOUBLE

DSTS=SMSSTS!<DISKN*10>
DSEK=SMSSEK!<DISKN*10>
DRED=SMSRED!<DISKN*10>

GO'DISKN'DOUBLE:	BR 2$		;START HERE TO DO NORMAL THING
	BR REST'DISKN'DOUBLE	;START HERE IF F IS SET UP
2$:	MOV #1000,P	;TEMP PDL
	BR 3$		;MAGIC!!!
4$:	MOV (P)+,@#4
	CLR F		;START LOOKING AT ZERO
1$:	TST (F)+	;TEST TILL YOU GET A TRAP
	BR 1$
3$:	JSR PC,4$	
				;THIS WILL TRAP TO ITSELF THE FIRST TIME
	CLR -(F)		;THIS IS A FLAG TO BLOADR, IF NON ZERO GIVES FILE TO LOAD
REST'DISKN'DOUBLE:	RESET		;PREVENT ANY INTERUPTS THAT MIGHT BE PENDING
.IF NZ DOUBLE
	MOV PC,A
	ADD #ITAB'DISKN'DOUBLE-.,A
	CMP A,D		;WAS IT SET TO SOMETHING OTHER THAN ITAB?
	BNE 10$		;YES, GO SET IT TO ITAB
	CMP (A)+,(A)+	;TURN IT INTO ITAB+4
10$:	MOV A,D
.ENDC
	MOV F,P
	MOV #SMSCSR,E	;COMMONLY USED CONSTANT
	MOV PC,B
	ADD #CMD2'DISKN'DOUBLE-.,B
	MOV #SMSPWR!SMSRSB,(E)	;RESET THE SMS CONTROLLER
6$:	BIT #SMSFDO,(E)	;IS IT ON?
	BNE 6$		;NOT YET
5$:	MOV #DSTS,A	;STATUS
	JSR PC,(B)	;CMDWA2
	JSR PC,GETB'DISKN'DOUBLE	;GET STATUS BYTE
	BIT #SMSDNR,A	;IS DISK READY
	BNE 5$		;NOPE, WAIT
	MOV #SMSRST!SMSINI,A
.IFNZ DOUBLE
	MOVB 2(D),F
	JSR PC,(B)	;RESET SINGLE DENSITY, SET DENSITY OF DOUBLE
.ENDC
	CLR F
	JSR PC,SEEK'DISKN'DOUBLE	;SEEK TO TRACK 1
	CLR C		;C IS THE CORE ADDRESS TO READ INTO
	MOV #1,F	;SECTOR TO READ
3$:	MOV #SMSDTB!DRED,A	;READ INTO BUFFER
	JSR PC,(B)	;CMDWA2
	JSR PC,GETS'DISKN'DOUBLE		;GET STATUS OF THE READ
	MOV #SMSBTH!DRED,A
	JSR PC,(B)		;BUFFER TO HOST READ
				;SECOND BYTE IS IGNORED

.IFNZ DOUBLE
	MOV (D),-(P)		;GET THE SECTOR LENGTH
.IFF
	MOV #128.,-(P)
.ENDC
11$:	BIT #SMSXFW,(E)		;WAITING FOR US?
	BEQ 11$
2$:	MOVB @#SMSDBF,(C)+
	DEC (P)
	BNE 2$
	JSR PC,GETS'DISKN'DOUBLE	;GET STATUS BYTE
.IFNZ DOUBLE
	TSTB (D)	;DOUBLE DENSITY MAKES THIS 0
	BNE 12$		;MEANS SINGLE DENSITY
	CMPB (F)+,(F)+
.ENDC
12$:	CMPB (F)+,(F)+
.IFNZ DOUBLE
	CMPB 3(D),F	;ARE WE OVER END?
.IFF
	CMP #27.,F
.ENDC
	BGT 3$		;NOPE, READ NEXT
	BLT 4$		;JUST READ BLOCK 26., DONE
	MOV #2,F	;GO BACK TO GOBBLE THE EVEN SECTORS
	BR 3$
4$:	CLR PC		;START THE MAGIC PROGRAM


SEEK'DISKN'DOUBLE:	JSR PC,(PC)
1$:	MOV #DSEK,A	;SEEK TO TRACK ZERO
	JSR PC,(B)
	INC F

GETS'DISKN'DOUBLE:	TSTB (E)		;WAIT FOR DONE
	BPL GETS'DISKN'DOUBLE
	TST (E)		;ERROR?
	BLT ERR'DISKN'DOUBLE
GETB'DISKN'DOUBLE:	BIT #SMSXFW,(E)	;WAITING TO XFR?
	BEQ GETB'DISKN'DOUBLE
	MOV @#SMSDBF,A		;GOBBLE STATUS
	RTS PC			;AND IGNORE IT

CMD2'DISKN'DOUBLE:	JSR PC,(PC)		;CALL THIS ROUTINE TWICE
1$:	BIT #SMSCMW,(E)	;READY FOR COMMAND BYTE?
	BEQ 1$		;NOPE
	MOV A,@#SMSCMD		;GIVE IT TO IT
	MOV F,A
	RTS PC

ERR'DISKN'DOUBLE:	MOV P,F
.IFNZ DOUBLE
	BIT #4,D
.IF P1
	0				;BRANCH WILL GO HERE
.IFF
.IIF EQ ITAB'DISKN'DOUBLE&4,	BEQ REST'DISKN'DOUBLE		;NOT YET
.IIF NE ITAB'DISKN'DOUBLE&4,	BNE REST'DISKN'DOUBLE		;NOT YET
.ENDC
.ENDC
	JSR A,1$
	.ASCIZ /Error
/
.EVEN

1$:	TSTB @#TPS
	BPL 1$
	MOVB (A)+,@#TPB
	BNE 1$
	BR REST'DISKN'DOUBLE

.IFNZ DOUBLE
ITAB'DISKN'DOUBLE:	128.			;NOTE THAT THIS IS .BYTE 128.,0
	.BYTE 0,27.		;26 SECTORS/TRACK, GO AROUND TWICE
	512.			;AND THIS IS .BYTE 0,1
	.BYTE 6,17.		;8 SECTORS/TRACK, GO AROUND TWICE, GETTING 4 SECTORS
.ENDC
LEN==.-GO'DISKN'DOUBLE
.REPT 128.
	.IIF LT .-GO'DISKN'DOUBLE-254.,-1	;FILL OUT WITH -1'S
	.IIF Z  .-GO'DISKN'DOUBLE-254.,.IIF Z DOUBLE,<DISKN*2>!177771
	.IIF Z  .-GO'DISKN'DOUBLE-254.,.IIF NZ DOUBLE,<DISKN*2>!177761
.ENDR

.MACRO PRAD A,B
.PRINT /A'B
/
.ENDM
.IF2
PRAD <END=>,\.
PRAD <LENGTH=>,\LEN
.IIF GT LEN-256.,.ERROR TOO LONG!!!!!
.ENDC
.ENDM
BOOT 1,1	;DISK ONE, DUAL DENSITY
BOOT 0,1	;DISK ZERO, DUAL DENSITY
BOOT 1,0	;DISK ONE, SINGLE DENSITY
BOOT 0,0	;DISK ZERO, SINGLE DENSITY

.END GO00
